
&НаКлиенте
Процедура Загрузить(Команда)

	СтруктураНастроек = ПолучитьНастройкуЗагрузки_Сервер();
	
	Если СтруктураНастроек = Неопределено тогда
		Возврат;
	КонецЕсли;
	
	ДвоичныеДанные = СтруктураНастроек.ОбработкаЗагрузки.ХранилищеВнешнейОбработки.Получить();
	
	Если ДвоичныеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТабДокумент = Неопределено;
	
	ИмяФайла = ПолучитьИмяВременногоФайла("epf");
	Попытка
		ДвоичныеДанные.Записать(ИмяФайла);
		Обработка = ВнешниеОбработки.Создать(ИмяФайла);
		Обработка.КаталогЗагрузки 	= СтруктураНастроек.КаталогЗагрузки;
		Обработка.Организация 		= СтруктураНастроек.Организация;
		Обработка.Контрагент 		= СтруктураНастроек.Контрагент;
		
		// Передать внешней обработке дополнительные параметры
		Если СтруктураНастроек.ПараметрыЗагрузки <> Неопределено Тогда
			
			// Если у внешней обработки есть реквизит для дополнительных параметров, присвоить ему значение
			Если НЕ Обработка.Метаданные().Реквизиты.Найти("ДополнительныеПараметры") = Неопределено Тогда
				Обработка.ДополнительныеПараметры = СтруктураНастроек.ПараметрыЗагрузки;
			КонецЕсли;
			
		КонецЕсли;
		
		мЗаказы = Обработка.Загрузить();
		
		Для Каждого Заказ Из мЗаказы Цикл
			СтрокаТЧ = Объект.ЗагруженныеДокументы.Добавить();
			СтрокаТЧ.ЗаказПокупателя = Заказ;
		КонецЦикла;
		
		//обработаем ТЧ заказов - пересчитаем НДС и общую сумму
		ОбработатьТЧ_Сервер(мЗаказы);
		
		УдалитьФайлы(ИмяФайла);
	Исключение
		ОбщегоНазначения.СообщитьОбОшибке(ОписаниеОшибки(),, "Не удалось загрузить заказы покупателя!");
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьНастройкуЗагрузки_Сервер()
	
	обОбработка = РеквизитФормыВЗначение("Объект");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	0 КАК Приоритет,
		|	абс_НастройкиЗагрузкиЗаказовПокупателейСрезПоследних.ОбработкаЗагрузки,
		|	абс_НастройкиЗагрузкиЗаказовПокупателейСрезПоследних.ПараметрыЗагрузки,
		|	абс_НастройкиЗагрузкиЗаказовПокупателейСрезПоследних.КаталогЗагрузки
		|ИЗ
		|	РегистрСведений.абс_НастройкиЗагрузкиЗаказовПокупателей.СрезПоследних(
		|			,
		|			Организация = &Организация
		|				И Контрагент = &Контрагент
		|				И Использовать) КАК абс_НастройкиЗагрузкиЗаказовПокупателейСрезПоследних
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	1,
		|	абс_НастройкиЗагрузкиЗаказовПокупателейСрезПоследних.ОбработкаЗагрузки,
		|	абс_НастройкиЗагрузкиЗаказовПокупателейСрезПоследних.ПараметрыЗагрузки,
		|	абс_НастройкиЗагрузкиЗаказовПокупателейСрезПоследних.КаталогЗагрузки
		|ИЗ
		|	РегистрСведений.абс_НастройкиЗагрузкиЗаказовПокупателей.СрезПоследних(
		|			,
		|			Организация = &Организация
		|				И Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|				И Использовать) КАК абс_НастройкиЗагрузкиЗаказовПокупателейСрезПоследних
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет";

	Запрос.УстановитьПараметр("Организация", обОбработка.Организация);
 	Запрос.УстановитьПараметр("Контрагент", обОбработка.Контрагент);

	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		СтруктураПараметров = Новый Структура("ОбработкаЗагрузки,ПараметрыЗагрузки,КаталогЗагрузки,Организация,Контрагент");		
		ЗаполнитьЗначенияСвойств(СтруктураПараметров, ВыборкаДетальныеЗаписи);
		СтруктураПараметров.Организация = обОбработка.Организация;
		СтруктураПараметров.Контрагент = обОбработка.Контрагент;
		Возврат СтруктураПараметров;
	Иначе		
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ОбработатьТЧ_Сервер(мЗаказы)
	
	Для Каждого ссЗаказ Из мЗаказы Цикл
		обЗаказ = ссЗаказ.ПолучитьОбъект();
		Для каждого СтрокаТЧ Из обЗаказ.Товары Цикл
			ОбработкаТабличныхЧастей.РассчитатьСуммуТабЧасти(СтрокаТЧ, обЗаказ);
			ОбработкаТабличныхЧастей.РассчитатьСуммуНДСТабЧасти(СтрокаТЧ, обЗаказ);
		КонецЦикла;
		Попытка
			обЗаказ.Записать(РежимЗаписиДокумента.Запись);
		Исключение
			Сообщить("Ошибка при создании заказа покупателя");
			ЗаписьЖурналаРегистрации("ЗагрузкаЗаказов.ОшибкаЗагрузки",УровеньЖурналаРегистрации.Ошибка,,,"Ошибка при создании заказа покупателя ");
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры