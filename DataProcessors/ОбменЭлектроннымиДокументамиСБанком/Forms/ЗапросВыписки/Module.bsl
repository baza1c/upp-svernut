////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
		
	СоглашениеЭД = Параметры.СоглашениеЭД;
	
	Если Не ЗначениеЗаполнено(СоглашениеЭД) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ЭД = Неопределено;
	
	ЭлектронныеДокументыВнутренний.СформироватьЭДЗаказВыписки(
										Параметры.ДатаНачала,
										Параметры.ДатаОкончания,
										СоглашениеЭД,
										ЭД,
										Параметры.НомерСчета);
	Если Не ЗначениеЗаполнено(ЭД) Тогда
		Отказ = Истина;
	КонецЕсли;
	
	ВыполнятьКриптооперацииНаСервере = ЭлектронныеДокументыСлужебныйВызовСервера.ВыполнятьКриптооперацииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Перем ЗапросОтправлен, ВыпискаБанка;
	
	Попытка
		Если ВыполнятьКриптооперацииНаСервере Тогда
			МассивСтруктурСертификатов = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьМассивСтруктурСертификатов(Истина);
		Иначе
			МассивСтруктурСертификатов = ЭлектроннаяЦифроваяПодписьКлиент.ПолучитьМассивСтруктурСертификатов(Истина);
		КонецЕсли;
		МассивСтруктурДоступныхСертификатов = МассивСтруктурДоступныхДляПодписиСертификатов(МассивСтруктурСертификатов);
	Исключение
		МассивСтруктурДоступныхСертификатов = Новый Массив;
	КонецПопытки;
	
	НастройкиОбмена = ОпределитьНастройкиОбменаЭДПоИсточнику(МассивСтруктурДоступныхСертификатов, ЭД, СоглашениеЭД);
	
	Если НастройкиОбмена = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(НастройкиОбмена.СертификатОрганизацииДляПодписи)
			ИЛИ НЕ НастройкиОбмена.СертификатДоступен Тогда
		ТекстСообщения = НСтр("ru = 'Не найден подходящий сертификат для подписи документа Запрос выписки'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		Возврат;
	КонецЕсли;
	
	МассивЭД = Новый Массив;
	МассивЭД.Добавить(ЭД);
	ПараметрыСертификата = ЭлектронныеДокументыСлужебныйВызовСервера.РеквизитыСертификата(
		НастройкиОбмена.СертификатОрганизацииДляПодписи);
	Если ЭлектронныеДокументыСлужебныйКлиент.ПолучитьПарольКСертификату(НастройкиОбмена.СертификатОрганизацииДляПодписи,
			ПараметрыСертификата, НСтр("ru = 'Подписание электронных документов'"), МассивЭД) Тогда
		
		КолПодписанных = ЭлектронныеДокументыСлужебныйКлиент.ПодписатьЭДОпределеннымСертификатом(
			МассивЭД,
			НастройкиОбмена.СертификатОрганизацииДляПодписи,
			ПараметрыСертификата);
	КонецЕсли;
	
	Если КолПодписанных = 0 тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	КолОтправленных = 0;
	КолПолученных = 0;
	
	ОперацияВыполнена = ОтправитьЗапросНаСервере(
							ЭД,
							СоглашениеЭД,
							АдресХранилища,
							УникальныйИдентификатор,
							ИдентификаторЗадания);
	
	Если ОперацияВыполнена Тогда
		ОбработатьРезультатЗапроса()
	КонецЕсли;
	
	// Операция еще не завершена, выполняется с помощью фонового задания (асинхронно).
	ПараметрыОбработчикаОжидания = Новый Структура(
												"МинимальныйИнтервал, МаксимальныйИнтервал, ТекущийИнтервал, КоэффициентУвеличенияИнтервала",
												1,
												15,
												1,
												1.4);
	ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	ПриЗакрытииНаСервере(ИдентификаторЗадания);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервереБезКонтекста
Функция ОпределитьНастройкиОбменаЭДПоИсточнику(МассивСтруктурДоступныхСертификатов, Знач ЭД, Знач СоглашениеЭД)
	
	Возврат ЭлектронныеДокументыСлужебный.ОпределитьНастройкиОбменаЭДПоИсточнику(
																				СоглашениеЭД,
																				Истина,
																				МассивСтруктурДоступныхСертификатов,
																				ЭД);
	
КонецФункции

&НаСервереБезКонтекста
Функция ОтправитьЗапросНаСервере(Знач ЭД, Знач СоглашениеЭД, АдресХранилища, УникальныйИдентификатор, ИдентификаторЗадания)
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ЭД",           ЭД);
	СтруктураПараметров.Вставить("СоглашениеЭД", СоглашениеЭД);
	
	ИБФайловая = ОбщегоНазначения.ИнформационнаяБазаФайловая();
	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	
	Если ИБФайловая Тогда
		ЭлектронныеДокументыСлужебный.ОтправитьЗапросВыпискиВБанк(СтруктураПараметров, АдресХранилища);
		Возврат Истина;
	КонецЕсли;
	
	ЗаданиеВыполнено = Ложь;
		
	// В клиент-серверном режиме работы выполняем операцию в фоновом задании (асинхронно).
	НаименованиеЗадания = НСтр("ru = 'Отправка запроса выписки в банк'");
	ПараметрыВыполнения = Новый Массив;
	ПараметрыВыполнения.Добавить(СтруктураПараметров);
	ПараметрыВыполнения.Добавить(АдресХранилища);
	
	Если ПолучитьСкоростьКлиентскогоСоединения() = СкоростьКлиентскогоСоединения.Низкая Тогда
		ВремяОжидания = 4;
	Иначе
		ВремяОжидания = 2;
	КонецЕсли;
	
	Задание = ФоновыеЗадания.Выполнить(
					"ЭлектронныеДокументыСлужебный.ОтправитьЗапросВыпискиВБанк",
					ПараметрыВыполнения,
					,
					НаименованиеЗадания);
	Попытка
		Задание.ОжидатьЗавершения(ВремяОжидания);
	Исключение
		// Специальная обработка не требуется. Предположительно, исключение вызвано истечением времени ожидания.
	КонецПопытки;

	ИдентификаторЗадания = Задание.УникальныйИдентификатор;
	// Если операция уже завершилась, то сразу обрабатываем результат.
	Если ДлительныеОперации.ЗаданиеВыполнено(Задание.УникальныйИдентификатор) Тогда
		ЗаданиеВыполнено = Истина;
	КонецЕсли;
	Возврат ЗаданиеВыполнено;
	
КонецФункции

&НаСервереБезКонтекста
Функция МассивСтруктурДоступныхДляПодписиСертификатов(Знач МассивСтруктурСертификатов)
	
	Возврат ЭлектронныеДокументыСлужебный.МассивСтруктурДоступныхДляПодписиСертификатов(МассивСтруктурСертификатов);
	
КонецФункции

&НаСервереБезКонтекста
Процедура ПриЗакрытииНаСервере(Знач ИдентификаторЗадания)
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗадания()

	Попытка
		Если ЗаданиеВыполнено(ИдентификаторЗадания) Тогда
			ОбработатьРезультатЗапроса();
		КонецЕсли;
	Исключение
		ВызватьИсключение;
	КонецПопытки;

	ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.ТекущийИнтервал
													* ПараметрыОбработчикаОжидания.КоэффициентУвеличенияИнтервала;
	Если ПараметрыОбработчикаОжидания.ТекущийИнтервал > ПараметрыОбработчикаОжидания.МаксимальныйИнтервал Тогда
		ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.МаксимальныйИнтервал;
	КонецЕсли;
	ПодключитьОбработчикОжидания(
							"Подключаемый_ПроверитьВыполнениеЗадания",
							ПараметрыОбработчикаОжидания.ТекущийИнтервал,
							Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатЗапроса()

	КолОтправленных = 0;
	КолПолученных = 0;
	
	СтруктураВозврата = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	Если СтруктураВозврата.ЗапросОтправлен Тогда
		КолОтправленных = 1;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтруктураВозврата.ВыпискаБанка) Тогда
		КолПолученных = 1;
		
		Если НЕ ВыполнятьКриптооперацииНаСервере И СтруктураВозврата.Подписи.Количество() > 0 Тогда
			
			ДобавитьПодписиВЭлектронныйДокумент(СтруктураВозврата.ВыпискаБанка, СтруктураВозврата.Подписи);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаголовокОповещения = НСтр("ru = 'Обмен электронными документами'");
	ТекстОповещения = НСтр("ru = 'Отправленных пакетов нет'");
	
	Если КолОтправленных > 0 Тогда
		ТекстОповещения = НСтр("ru = 'Отправлено документов: (%1)'");
		ТекстОповещения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОповещения, КолОтправленных);
	КонецЕсли;
	
	Если КолПолученных = 0 Тогда
		ТекстОповещения = ТекстОповещения + НСтр("ru = ', полученных документов нет'");
	Иначе
		ТекстОповещения = ТекстОповещения
		+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = ', получено документов: (%1)'"), КолПолученных);
	КонецЕсли;
	
	Оповестить("ОбновитьСостояниеЭД");
	
	ПоказатьОповещениеПользователя(ЗаголовокОповещения, , ТекстОповещения);
	
	ОповеститьОВыборе(СтруктураВозврата.ВыпискаБанка);

КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаданиеВыполнено(Знач ИдентификаторЗадания)
	
	Возврат ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания);
	
КонецФункции

&НаКлиенте
Процедура ДобавитьПодписиВЭлектронныйДокумент(ЭД, Подписи)
	
	Попытка
		НастройкиКриптографии = ЭлектроннаяЦифроваяПодписьКлиент.ПолучитьПерсональныеНастройкиРаботыСЭЦП();
		ПровайдерЭЦП = НастройкиКриптографии.ПровайдерЭЦП;
		ПутьМодуляКриптографии = НастройкиКриптографии.ПутьМодуляКриптографии;
		ТипПровайдераЭЦП = НастройкиКриптографии.ТипПровайдераЭЦП;
			
		МенеджерКриптографии = Новый МенеджерКриптографии(ПровайдерЭЦП, ПутьМодуляКриптографии, ТипПровайдераЭЦП);
		МенеджерКриптографии.АлгоритмПодписи     = НастройкиКриптографии.АлгоритмПодписи;
		МенеджерКриптографии.АлгоритмХеширования = НастройкиКриптографии.АлгоритмХеширования;
		МенеджерКриптографии.АлгоритмШифрования  = НастройкиКриптографии.АлгоритмШифрования;
	Исключение
		ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("100");
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Операция = НСтр("ru = 'Инициализация криптосредства на компьютере'");
		ЭлектронныеДокументыСлужебныйВызовСервера.ОбработатьИсключениеПоЭДНаСервере(Операция,
																					ТекстОшибки,
																					ТекстСообщения);
		Возврат;
	КонецПопытки;
	
	Для Каждого Подпись ИЗ Подписи Цикл
		СертификатыПодписи = МенеджерКриптографии.ПолучитьСертификатыИзПодписи(Подпись);
		Если СертификатыПодписи.Количество() > 0 Тогда
			Сертификат = СертификатыПодписи[0];
		Иначе
			Продолжить;
		КонецЕсли;
		ПредставлениеПользователя = ЭлектроннаяЦифроваяПодписьКлиентСервер.ПолучитьПредставлениеПользователя(
																							Сертификат.Субъект);
		Отпечаток = Base64Строка(Сертификат.Отпечаток);
		ДвоичныеДанныеСертификата = Сертификат.Выгрузить();
		ЗанестиИнформациюОПодписи(ЭД, Подпись, Отпечаток, ПредставлениеПользователя, ДвоичныеДанныеСертификата);
	КонецЦикла;
	
	ЭлектронныеДокументыСлужебныйКлиент.ОпределитьСтатусыПодписей(ЭД);
	
КонецПроцедуры
	
&НаСервереБезКонтекста
Процедура ЗанестиИнформациюОПодписи(Знач ЭД,
									Знач Подпись,
									Знач Отпечаток,
									Знач ПредставлениеПользователя,
									Знач ДвоичныеДанныеСертификата)
	
	ЭлектроннаяЦифроваяПодпись.ЗанестиИнформациюОПодписи(
								ЭД,
								Подпись,
								Отпечаток,
								ТекущаяДатаСеанса(),
								"",
								,
								ПредставлениеПользователя,
								ДвоичныеДанныеСертификата);

КонецПроцедуры