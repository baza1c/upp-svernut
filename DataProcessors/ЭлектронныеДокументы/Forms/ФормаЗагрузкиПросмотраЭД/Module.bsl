////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервереБезКонтекста
Функция СоздатьОбъектыИБ(АдресВременногоХранилища, ОшибкаЗаписи)
	
	Перем ДеревоРазбора;
	
	СтруктураРазбора = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	
	Если СтруктураРазбора <> Неопределено И СтруктураРазбора.Свойство("ДеревоРазбора", ДеревоРазбора) Тогда
		// Заполним ссылки на объекты из дерева соответствий, если ссылок нет,
		// тогда будем создавать объекты
		ЭлектронныеДокументыВнутренний.ЗаполнитьСсылкиНаОбъектыВДереве(ДеревоРазбора, ОшибкаЗаписи);
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция СопоставитьНоменклатуру(ДанныеФормы = Неопределено)
	
	ЗначениеВозврата = Неопределено;
	СтруктураЭД = Новый Структура;
	СтруктураЭД.Вставить("ВидЭД", ВидЭД);
	СтруктураЭД.Вставить("СпособОбменаЭД", ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.БыстрыйОбмен"));
	СтруктураЭД.Вставить("ПолноеИмяФайла", ИмяФайла);
	СтруктураЭД.Вставить("Контрагент", Контрагент);
	СтруктураЭД.Вставить("НаправлениеЭД", ПредопределенноеЗначение("Перечисление.НаправленияЭД.Входящий"));
	СтруктураЭД.Вставить("ВладелецФайла", ДокументИБ);
	
	СтруктураПараметров = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьПараметрыФормыСопоставленияНоменклатуры(СтруктураЭД);
	Если ЗначениеЗаполнено(СтруктураПараметров) Тогда
		ЗначениеВозврата = ОткрытьФормуМодально(СтруктураПараметров.ИмяФормы, СтруктураПараметров.ПараметрыОткрытияФормы);
	КонецЕсли;
	
	Возврат ЗначениеВозврата;
	
КонецФункции

&НаКлиенте
Процедура ИзменитьВидимостьДоступность()
	
	Если ЗагрузкаЭД Тогда
		Элементы.ДокументИБ.Доступность = (СпособЗагрузкиДокумента = 1);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьВидимостьДоступностьПриСозданииНаСервере()
	
	Элементы.ГруппаСодержимоеДокумента.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	
	Если ЗагрузкаЭД Тогда
		ЭтаФорма.Заголовок = Нстр("ru = 'Загрузка документа из файла'");
		Элементы.ГруппаНастроек.Видимость = Истина;
		Элементы.ГруппаКнопок.Видимость = Истина;
		Элементы.ГруппаГиперссылка.Видимость = Ложь;
	Иначе
		Текст = Нстр("ru = 'Электронный документ'");
		ЭтаФорма.Заголовок = Текст;
		Элементы.ГруппаНастроек.Видимость = Ложь;
		Элементы.ГруппаКнопок.Видимость = Ложь;
		Элементы.ГруппаГиперссылка.Видимость = Истина;
	КонецЕсли;
	
	Если ВидЭД = Перечисления.ВидыЭД.КаталогТоваров Тогда
		Элементы.Загрузка.Видимость = Ложь;
		Элементы.СписокТиповДокументов.Заголовок = "Загрузить";
		СписокТиповДокументов = "Каталог товаров";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьПросмотрЭДСервер(СтруктураЭД)
	
	Перем ПерезаполняемыйДокумент, ДеревоРазбора, СтрокаОбъекта;
	
	ФайлДопДанных = Неопределено;
	Если ЗагрузкаЭД Тогда
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(СтруктураЭД.АдресВХранилище);
		
		Если СтруктураЭД.ФайлАрхива Тогда
			ПапкаДляРаспаковки = ЭлектронныеДокументыСлужебный.РабочийКаталог("Ext", СтруктураЭД.УникальныйИдентификатор);
			ИмяФайлаАрхива = ЭлектронныеДокументыСлужебный.ТекущееИмяВременногоФайла("zip");
			ДвоичныеДанные.Записать(ИмяФайлаАрхива);
			
			УдалитьФайлы(ПапкаДляРаспаковки, "*");
			
			ЧтениеЗИП = Новый ЧтениеZIPФайла(ИмяФайлаАрхива);
			Попытка
				ЧтениеЗИП.ИзвлечьВсе(ПапкаДляРаспаковки);
			Исключение
				ТекстОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
				Если НЕ ЭлектронныеДокументыСлужебный.ВозможноИзвлечьФайлы(ЧтениеЗИП, ПапкаДляРаспаковки) Тогда
					ТекстСообщения = ЭлектронныеДокументыПовтИсп.ПолучитьСообщениеОбОшибке("006");
					
					СпособИсправления = ЭлектронныеДокументыПовтИсп.ИсправлениеОшибки006();
					ТекстОшибки = ТекстОшибки + Символы.ПС + СпособИсправления;
					
				КонецЕсли;
				ЭлектронныеДокументыСлужебныйВызовСервера.ОбработатьИсключениеПоЭДНаСервере(НСтр("ru = 'Распаковка архива ЭД'"),
					ТекстОшибки, ТекстСообщения);
					
				УдалитьФайлы(ИмяФайлаАрхива);
				УдалитьФайлы(ПапкаДляРаспаковки);
				Возврат ;
			КонецПопытки;
			
			// Расшифровать файл с данными
			МассивФайлИнформации = НайтиФайлы(ПапкаДляРаспаковки, "meta*.xml", Истина);
			Если МассивФайлИнформации.Количество() > 0 Тогда
				ФайлИнформации = МассивФайлИнформации[0];
			Иначе
				УдалитьФайлы(ИмяФайлаАрхива);
				УдалитьФайлы(ПапкаДляРаспаковки);
				Возврат;
			КонецЕсли;
			
			МассивФайлКарточки = НайтиФайлы(ПапкаДляРаспаковки, "card*.xml", Истина);
			Если МассивФайлКарточки.Количество() > 0 Тогда
				ФайлКарточки = МассивФайлКарточки[0];
			Иначе
				УдалитьФайлы(ИмяФайлаАрхива);
				УдалитьФайлы(ПапкаДляРаспаковки);
				Возврат;
			КонецЕсли;
			
			СоответствиеФайлПараметры = ЭлектронныеДокументыВнутренний.ПолучитьСоответствиеФайлПараметры(ФайлИнформации, ФайлКарточки);
			Если СоответствиеФайлПараметры.Количество() = 0 Тогда
				УдалитьФайлы(ПапкаДляРаспаковки);
				УдалитьФайлы(ИмяФайлаАрхива);
				Возврат;
			КонецЕсли;
			
			Для Каждого ЭлементСоответствия Из СоответствиеФайлПараметры Цикл
				
				МассивФайловИсточник = НайтиФайлы(ПапкаДляРаспаковки, ЭлементСоответствия.Ключ, Истина);
				Если МассивФайловИсточник.Количество() > 0 Тогда
					ИмяФайла = ЭлектронныеДокументыСлужебный.ТекущееИмяВременногоФайла("xml");
					КопироватьФайл(МассивФайловИсточник[0].ПолноеИмя, ИмяФайла);
					ДопДанные = "";
					Если ТипЗнч(ЭлементСоответствия.Значение) = Тип("Структура")
						И ЭлементСоответствия.Значение.Свойство("ДопДанные", ДопДанные) И ТипЗнч(ДопДанные) = Тип("Структура")
						И ДопДанные.Свойство("ФайлДопДанных", ФайлДопДанных) И ЗначениеЗаполнено(ФайлДопДанных) Тогда
						МассивФайловИсточник = НайтиФайлы(ПапкаДляРаспаковки, ФайлДопДанных, Истина);
						Если МассивФайловИсточник.Количество() > 0 Тогда
							ФайлДопДанных = ЭлектронныеДокументыСлужебный.ТекущееИмяВременногоФайла("xml");
							КопироватьФайл(МассивФайловИсточник[0].ПолноеИмя, ФайлДопДанных);
						КонецЕсли;
					КонецЕсли;
				Иначе
					УдалитьФайлы(ПапкаДляРаспаковки);
					УдалитьФайлы(ИмяФайлаАрхива);
					Возврат;
				КонецЕсли;
			КонецЦикла;
			
			УдалитьФайлы(ИмяФайлаАрхива);
			УдалитьФайлы(ПапкаДляРаспаковки);
		Иначе
			ИмяФайла = ЭлектронныеДокументыСлужебный.ТекущееИмяВременногоФайла("xml");
			ДвоичныеДанные.Записать(ИмяФайла);
		КонецЕсли;
			
		СтруктураЭД.Свойство("СсылкаНаДокумент", ПерезаполняемыйДокумент);
	Иначе
		ИмяФайла = СтруктураЭД.ПолноеИмяФайла;
		ИмяФайлаДД = "";
		Если СтруктураЭД.Свойство("ПолноеИмяДопФайла", ИмяФайлаДД) И ЗначениеЗаполнено(ИмяФайлаДД) Тогда
			ФайлДопДанных = ИмяФайлаДД;
		КонецЕсли;
	КонецЕсли;
	
	СтруктураРазбора = ЭлектронныеДокументыВнутренний.СформироватьДеревоРазбора(ИмяФайла, Перечисления.НаправленияЭД.Входящий, ФайлДопДанных);
	Если ТипЗнч(СтруктураРазбора) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	АдресСтруктурыРазбораЭД = ПоместитьВоВременноеХранилище(СтруктураРазбора, УникальныйИдентификатор);
	ДанныеЭД = ЭлектронныеДокументыВнутренний.ПечатнаяФормаЭД(СтруктураРазбора, СтруктураЭД.НаправлениеЭД,
		СтруктураЭД.УникальныйИдентификатор, , , ВидЭД);
	
	Если ДанныеЭД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЭД) = Тип("ТабличныйДокумент") Тогда
		Если ЗагрузкаЭД Тогда
			Если (НЕ ЗначениеЗаполнено(ДокументИБ) ИЛИ СпособЗагрузкиДокумента = 0) И СтруктураРазбора <> Неопределено
					И СтруктураРазбора.Свойство("ДеревоРазбора", ДеревоРазбора)
					И СтруктураРазбора.Свойство("СтрокаОбъекта", СтрокаОбъекта) Тогда
				ОшибкаЗаписи = Ложь;
				СтрокаДерева = НайтиСтрокуВДереве(ДеревоРазбора, СтрокаОбъекта, "Контрагент");
				Если СтрокаДерева <> Неопределено Тогда
					Контрагент = СтрокаДерева.СсылкаНаОбъект;
				КонецЕсли;
			КонецЕсли;
			СписокТипов = Новый СписокЗначений;
			ЭлектронныеДокументыПереопределяемый.СписокТиповДокументовПоВидуЭД(ВидЭД, СписокТипов);
			Для Каждого ТекЗначение Из СписокТипов Цикл
				ТекЭлемент = Элементы.СписокТиповДокументов.СписокВыбора.Добавить();
				ЗаполнитьЗначенияСвойств(ТекЭлемент, ТекЗначение);
				// Если реквизит ДокументИБ еще не заполнен и прочитано первое по списку значение, то заполним имеющимися данными:
				Если НЕ ЗначениеЗаполнено(ДокументИБ) И СписокТипов.Индекс(ТекЗначение) = 0 Тогда
					СписокТиповДокументов = ТекЗначение.Представление;
					ДокументИБ = ТекЗначение.Значение;
					ИмяОбъектаМетаданных = ТекЗначение.Значение.Метаданные().ПолноеИмя();
				КонецЕсли;
				// Если в структуре параметров есть ссылка на (перезаполняемый) документ ИБ и его тип совпал с типом одного из значений
				// списка типов, то заполним этими данными соответствующие реквизиты формы.
				// Данное условие необходимо для корректной обработки ситуации, когда в качестве перезаполняемого документа, выбран
				// документ с типом не совпадающим ни с одним из доступных в списке или не совпадает с типом первого элемента списка.
				Если ЗначениеЗаполнено(ПерезаполняемыйДокумент) И ТипЗнч(ПерезаполняемыйДокумент) = ТипЗнч(ТекЗначение.Значение) Тогда
					СписокТиповДокументов = ТекЗначение.Представление;
					ДокументИБ = ПерезаполняемыйДокумент;
					ИмяОбъектаМетаданных = ТекЗначение.Значение.Метаданные().ПолноеИмя();
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Контрагент) И ЗначениеЗаполнено(ПерезаполняемыйДокумент) Тогда
			Контрагент = ПерезаполняемыйДокумент.Контрагент;
		КонецЕсли;
		
		ТабличныйДокументФормы = ДанныеЭД;
		Элементы.ГруппаСодержимоеДокумента.ТекущаяСтраница = Элементы.СтраницаТабличныйДокумент;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НайтиСтрокуВДереве(ДеревоРазбора, СтрокаОбъекта, ИмяОбъектаПоиска)
	
	ВозвращаемоеЗначение = Неопределено;
	
	СтруктураПоиска = Новый Структура("Реквизит", ИмяОбъектаПоиска);
	МассивСтрок = СтрокаОбъекта.Строки.НайтиСтроки(СтруктураПоиска);
	Если МассивСтрок.Количество() > 0 Тогда
		ИндексСтрокиКонтрагента = МассивСтрок[0].ЗначениеРеквизита;
		СтруктураПоиска = Новый Структура("ИндексСтроки", ИндексСтрокиКонтрагента);
		МассивСтрок = ДеревоРазбора.Строки.НайтиСтроки(СтруктураПоиска, Истина);
		Если МассивСтрок.Количество() > 0 Тогда
			СтрокаДерева = МассивСтрок[0];
			ВозвращаемоеЗначение = СтрокаДерева;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

&НаСервере
Функция СформироватьДокументИБ(ДанныеФормы, ТекстСообщения)
	
	Перем СтрокаОбъекта, ДеревоРазбора;
	ОшибкаЗаписи = Истина;
	Если ЗначениеЗаполнено(АдресСтруктурыРазбораЭД) И ЭтоАдресВременногоХранилища(АдресСтруктурыРазбораЭД) Тогда
		СтруктураРазбора = ПолучитьИзВременногоХранилища(АдресСтруктурыРазбораЭД);
	Иначе
		СтруктураРазбора = ЭлектронныеДокументыВнутренний.СформироватьДеревоРазбора(ИмяФайла,
																					Перечисления.НаправленияЭД.Входящий);
	КонецЕсли;
	Если СтруктураРазбора <> Неопределено И СтруктураРазбора.Свойство("ДеревоРазбора", ДеревоРазбора)
		И СтруктураРазбора.Свойство("СтрокаОбъекта", СтрокаОбъекта) Тогда
		// Если на форме указан контрагент, не совпадающий с контрагентом в дереве разбора (найденный по реквизитам из ЭД),
		// то заменим контрагента в дереве на контрагента с формы.
		СтрокаДерева = НайтиСтрокуВДереве(ДеревоРазбора, СтрокаОбъекта, "Контрагент");
		Если СтрокаДерева.СсылкаНаОбъект <> Контрагент Тогда
			СтрокаДерева.СсылкаНаОбъект = Контрагент;
		КонецЕсли;
		ДокументСсылка = ?(СпособЗагрузкиДокумента = 1, ДокументИБ, Неопределено);
		Попытка
			ДокументОбъект = ЭлектронныеДокументыПереопределяемый.СохранитьДанныеОбъектаВБД(СтрокаОбъекта,
																							ДеревоРазбора,
																							ДокументСсылка,
																							Ложь);
			Если ДанныеФормы <> Неопределено Тогда
				ЗначениеВДанныеФормы(ДокументОбъект, ДанныеФормы);
			Иначе
				ДанныеФормы = ДокументОбъект;
			КонецЕсли;
			ОшибкаЗаписи = Ложь;
		Исключение
			ТекстСообщения = ИнформацияОбОшибке();
		КонецПопытки;
	КонецЕсли;

	Возврат ОшибкаЗаписи;
	
КонецФункции

&НаСервереБезКонтекста
Функция МожноЗагрузитьЭДВида(Знач ВидЭД)
	
	МожноЗагрузить = Истина;
	МассивАктуальныхВидовЭД = ЭлектронныеДокументыПовтИсп.ПолучитьАктуальныеВидыЭД();
	Если МассивАктуальныхВидовЭД.Найти(ВидЭД) = Неопределено Тогда
		МожноЗагрузить = Ложь;
	КонецЕсли;
	
	Возврат МожноЗагрузить;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ВыполнитьДействие(Команда)
	
	Если ВидЭД = ПредопределенноеЗначение("Перечисление.ВидыЭД.КаталогТоваров") Тогда
		
		СоздатьОбъектыИБ(АдресСтруктурыРазбораЭД, Ложь);
		СопоставитьНоменклатуру();
		
	Иначе
		Если ЗагрузкаЭД И ЗначениеЗаполнено(ИмяОбъектаМетаданных) Тогда
			ОчиститьСообщения();
			
			Отказ = Ложь;
			ТекстСообщения = "";
			Если НЕ МожноЗагрузитьЭДВида(ВидЭД) Тогда
				ТекстСообщения = НСтр("ru = 'Не поддерживается загрузка электронных документов вида ""%1"".'");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", СписокТиповДокументов);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				Отказ = Истина;
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(Контрагент) Тогда
				ТекстСообщения = НСтр("ru = 'Не указан контрагент.'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				Отказ = Истина;
			КонецЕсли;
			Если СпособЗагрузкиДокумента = 1 И НЕ ЗначениеЗаполнено(ДокументИБ) Тогда
				ТекстСообщения = НСтр("ru = 'Не указан документ для перезаполнения.'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				Отказ = Истина;
			КонецЕсли;
			
			СоздатьОбъектыИБ(АдресСтруктурыРазбораЭД, Отказ);
			
			Если Отказ Тогда
				Возврат;
			КонецЕсли;
			
			СопоставлятьНоменклатуруПередЗаполнениемДокумента = Ложь;
			ЭлектронныеДокументыКлиентПереопределяемый.СопоставлятьНоменклатуруПередЗаполнениемДокумента(СопоставлятьНоменклатуруПередЗаполнениемДокумента);
			
			Если СопоставлятьНоменклатуруПередЗаполнениемДокумента Тогда
				СопоставитьНоменклатуру();
			КонецЕсли;
			
			Попытка
				Если СпособЗагрузкиДокумента = 1 И ЗначениеЗаполнено(ДокументИБ) Тогда
					ФормаДокумента = ОткрытьФорму(ИмяОбъектаМетаданных + ".ФормаОбъекта", Новый Структура("Ключ", ДокументИБ));
				Иначе
					ФормаДокумента = ОткрытьФорму(ИмяОбъектаМетаданных + ".ФормаОбъекта");
				КонецЕсли;
				
				Если ТипЗнч(ФормаДокумента) = Тип("УправляемаяФорма") Тогда
					ДанныеФормы = ФормаДокумента.Объект;
				Иначе
					ДанныеФормы = Неопределено;
				КонецЕсли;
				
				Отказ = СформироватьДокументИБ(ДанныеФормы, ТекстСообщения);
				
				Если Отказ Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				Иначе
					МассивОповещения = Новый Массив;
					МассивОповещения.Добавить(ДокументИБ);
					Оповестить("ОбновитьДокументИБПослеЗаполнения", МассивОповещения);
					Если Не СопоставлятьНоменклатуруПередЗаполнениемДокумента Тогда
						ЗначениеВозврата = СопоставитьНоменклатуру(ДанныеФормы);
						Если ЗначениеЗаполнено(ЗначениеВозврата) Тогда
							ЭлектронныеДокументыСлужебныйВызовСервера.ЗаполнитьИсточник(ДанныеФормы, ЗначениеВозврата);
						КонецЕсли;
					КонецЕсли;
					Если ТипЗнч(ФормаДокумента) = Тип("УправляемаяФорма") Тогда
						КопироватьДанныеФормы(ДанныеФормы, ФормаДокумента.Объект);
					Иначе
						ФормаДокумента.ДокументОбъект = ДанныеФормы;
					КонецЕсли;
					ФормаДокумента.Модифицированность = Истина;
					ЭтаФорма.Закрыть();
				КонецЕсли;
			Исключение
				ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , , Отказ);
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЕЙ ФОРМЫ

&НаКлиенте
Процедура СписокТиповДокументовНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекЭлемент = Неопределено;
	Если ЗначениеЗаполнено(СписокТиповДокументов) Тогда
		Для Каждого ЭлементСписка Из Элементы.СписокТиповДокументов.СписокВыбора Цикл
			Если СписокТиповДокументов = ЭлементСписка.Представление Тогда
				ТекЭлемент = ЭлементСписка;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	ВыбранныйЭлемент = ВыбратьИзСписка(Элементы.СписокТиповДокументов.СписокВыбора, Элемент, ТекЭлемент);
	Если ВыбранныйЭлемент <> Неопределено И ВыбранныйЭлемент <> ТекЭлемент Тогда
		СписокТиповДокументов = ВыбранныйЭлемент.Представление;
		Если НЕ ЗначениеЗаполнено(ДокументИБ) ИЛИ ТипЗнч(ДокументИБ) <> ТипЗнч(ВыбранныйЭлемент.Значение) Тогда
			ДокументИБ = ВыбранныйЭлемент.Значение;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СпособЗагрузкиДокументаПриИзменении(Элемент)
	
	ИзменитьВидимостьДоступность();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураЭД = "";
	НаправлениеЭД = "";
	Если Параметры.Свойство("СтруктураЭД", СтруктураЭД) И ТипЗнч(СтруктураЭД) = Тип("Структура")
		И СтруктураЭД.Свойство("НаправлениеЭД", НаправлениеЭД) Тогда
		
		ЗагрузкаЭД = (НаправлениеЭД = Перечисления.НаправленияЭД.Входящий);
		
		СтруктураЭД.Свойство("ВладелецЭД", ДокументИБ);
		Если ЗагрузкаЭД Тогда
			
			СсылкаНаЗаполняемыйДокумент = "";
			Если СтруктураЭД.Свойство("СсылкаНаДокумент", СсылкаНаЗаполняемыйДокумент)
				И ЗначениеЗаполнено(СсылкаНаЗаполняемыйДокумент) Тогда
				
				СпособЗагрузкиДокумента = 1;
			КонецЕсли;
		КонецЕсли;
		ВыполнитьПросмотрЭДСервер(СтруктураЭД);
	КонецЕсли;
	
	ЭлектронныйДокумент = Неопределено;
	Если Параметры.Свойство("ЭлектронныйДокумент", ЭлектронныйДокумент) Тогда
		ЗагрузкаЭД = Ложь;
		Параметры.Свойство("ВладелецЭД", ДокументИБ);
		ДанныеЭД = ФайлДанныхЭД(ЭлектронныйДокумент);
		Если ДанныеЭД = Неопределено Тогда
			Возврат;
		КонецЕсли;
		Если ТипЗнч(ДанныеЭД) = Тип("ТабличныйДокумент") Тогда
			ТабличныйДокументФормы = ДанныеЭД;
		КонецЕсли;
	КонецЕсли;
	
	ИзменитьВидимостьДоступностьПриСозданииНаСервере();
	
КонецПроцедуры

&НаСервере
Функция ФайлДанныхЭД(СсылкаНаЭД, Знач ИмяФайлаПодчиненногоЭД = Неопределено)
	
	ДопИнформацияПоЭД = ПрисоединенныеФайлы.ПолучитьДанныеФайла(СсылкаНаЭД, СсылкаНаЭД.УникальныйИдентификатор(), Истина);
	
	Если ДопИнформацияПоЭД.Свойство("СсылкаНаДвоичныеДанныеФайла")
		И ЗначениеЗаполнено(ДопИнформацияПоЭД.СсылкаНаДвоичныеДанныеФайла) Тогда
		
		ДанныеЭД = ПолучитьИзВременногоХранилища(ДопИнформацияПоЭД.СсылкаНаДвоичныеДанныеФайла);
		
		Если ЗначениеЗаполнено(ДопИнформацияПоЭД.Расширение) Тогда
			ИмяФайла = ЭлектронныеДокументыСлужебный.ТекущееИмяВременногоФайла(ДопИнформацияПоЭД.Расширение);
		Иначе
			ИмяФайла = ЭлектронныеДокументыСлужебный.ТекущееИмяВременногоФайла("xml");
		КонецЕсли;
		
		Если ИмяФайла = Неопределено Тогда
			ТекстОшибки = НСтр("ru = 'Не удалось просмотреть электронный документ. Проверьте настройку рабочего каталога'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			Возврат Неопределено;
		КонецЕсли;
		
		ВыборкаЭДДопДанных = ЭлектронныеДокументыСлужебный.ВыборкаДопДанныеЭД(СсылкаНаЭД);
		Если ВыборкаЭДДопДанных.Следующий() Тогда
			ДопДанныеЭД = ПрисоединенныеФайлы.ПолучитьДанныеФайла(ВыборкаЭДДопДанных.Ссылка,
				ВыборкаЭДДопДанных.Ссылка.УникальныйИдентификатор(), Истина);
			СсылкаНаДДДопДанныхЭД = "";
			Если ДопДанныеЭД.Свойство("СсылкаНаДвоичныеДанныеФайла", СсылкаНаДДДопДанныхЭД)
				И ЗначениеЗаполнено(СсылкаНаДДДопДанныхЭД) Тогда
				ДанныеДопФайла = ПолучитьИзВременногоХранилища(СсылкаНаДДДопДанныхЭД);
			
				Если ЗначениеЗаполнено(ДопДанныеЭД.Расширение) Тогда
					ИмяФайлаДопДанных = ЭлектронныеДокументыСлужебный.ТекущееИмяВременногоФайла(ДопДанныеЭД.Расширение);
				Иначе
					ИмяФайлаДопДанных = ЭлектронныеДокументыСлужебный.ТекущееИмяВременногоФайла("xml");
				КонецЕсли;
			
				Если ИмяФайлаДопДанных = Неопределено Тогда
					ТекстОшибки = НСтр("ru = 'Не удалось получить доп. данные электронного документа. Проверьте настройку рабочего каталога'");
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
					Возврат Неопределено;
				КонецЕсли;
				ДанныеДопФайла.Записать(ИмяФайлаДопДанных);
			КонецЕсли;
		КонецЕсли;
		
		ДанныеЭД.Записать(ИмяФайла);
		
		Если СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Покупатель
			ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.АктЗаказчик
			ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиПолучатель Тогда
			
			ТабличныйДокумент = ФайлДанныхЭД(СсылкаНаЭД.ЭлектронныйДокументВладелец, ИмяФайла);
			Возврат ТабличныйДокумент;
		ИначеЕсли Найти(ДопИнформацияПоЭД.Расширение, "zip") > 0 Тогда
			
			ЗИПЧтение = Новый ЧтениеZipФайла(ИмяФайла);
			ПапкаДляРаспаковки = ЭлектронныеДокументыСлужебный.РабочийКаталог(,СсылкаНаЭД.УникальныйИдентификатор());
			
			Если ПапкаДляРаспаковки = Неопределено Тогда
				ТекстОшибки = НСтр("ru = 'Не удалось просмотреть электронный документ. Проверьте настройку рабочего каталога'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
				Возврат Неопределено;
			КонецЕсли;
			
			Попытка
				ЗипЧтение.ИзвлечьВсе(ПапкаДляРаспаковки);
			Исключение
				ТекстОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
				Если НЕ ЭлектронныеДокументыСлужебный.ВозможноИзвлечьФайлы(ЗипЧтение, ПапкаДляРаспаковки) Тогда
					ТекстСообщения = ЭлектронныеДокументыПовтИсп.ПолучитьСообщениеОбОшибке("006");
					
					СпособИсправления = ЭлектронныеДокументыПовтИсп.ИсправлениеОшибки006();
					ТекстОшибки = ТекстОшибки + Символы.ПС + СпособИсправления;
					
				КонецЕсли;
				ЭлектронныеДокументыСлужебныйВызовСервера.ОбработатьИсключениеПоЭДНаСервере(НСтр("ru = 'Распаковка пакета ЭД'"),
					ТекстОшибки, ТекстСообщения);
				Возврат Неопределено;
			КонецПопытки;
			
			ФлагПросмотра = Ложь;
			
			Если СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.АктНаПередачуПрав Тогда
				ФайлыАрхиваPDF = НайтиФайлы(ПапкаДляРаспаковки, "*.pdf");
				Для Каждого РаспакованныйФайл Из ФайлыАрхиваPDF Цикл
					Возврат РаспакованныйФайл;
				КонецЦикла;
			КонецЕсли;
			
			ФайлыАрхиваXML = НайтиФайлы(ПапкаДляРаспаковки, "*.xml");
			
			Для Каждого РаспакованныйФайл Из ФайлыАрхиваXML Цикл
				
				ИмяФайлаДанных = РаспакованныйФайл.ПолноеИмя;
				Если Найти(РаспакованныйФайл.Имя,"packageDescription") Тогда
					Возврат ИмяФайла;
				КонецЕсли;
				
				ТабличныйДокумент = ЭлектронныеДокументыВнутренний.СформироватьПечатнуюФормуЭД(РаспакованныйФайл.ПолноеИмя,
																								СсылкаНаЭД.НаправлениеЭД,
																								СсылкаНаЭД.УникальныйИдентификатор(),
																								,
																								СсылкаНаЭД.Наименование);
					
				Если ТипЗнч(ТабличныйДокумент) = Тип("ТабличныйДокумент") Тогда
					Возврат ТабличныйДокумент;
				КонецЕсли;
				
			КонецЦикла;
			
			ФайлыАрхиваMXL = НайтиФайлы(ПапкаДляРаспаковки, "*.mxl");
			Для Каждого РаспакованныйФайл Из ФайлыАрхиваMXL Цикл
				ИмяФайлаДанных = РаспакованныйФайл.ПолноеИмя;
				ТабличныйДокумент = Новый ТабличныйДокумент;
				ТабличныйДокумент.Прочитать(ИмяФайлаДанных);
				УдалитьФайлы(ПапкаДляРаспаковки);
				Возврат ТабличныйДокумент;
			КонецЦикла;
			
			ФайлыАрхиваHTML = НайтиФайлы(ПапкаДляРаспаковки, "*.html");
			Для Каждого РаспакованныйФайл Из ФайлыАрхиваHTML Цикл
				Возврат РаспакованныйФайл;
			КонецЦикла;
			
			ФайлыАрхиваDOCX = НайтиФайлы(ПапкаДляРаспаковки, "*.docx");
			Для Каждого РаспакованныйФайл Из ФайлыАрхиваDOCX Цикл
				Возврат РаспакованныйФайл;
			КонецЦикла;
			
			ФайлыАрхиваXLS = НайтиФайлы(ПапкаДляРаспаковки, "*.xls");
			Для Каждого РаспакованныйФайл Из ФайлыАрхиваXLS Цикл
				Возврат РаспакованныйФайл;
			КонецЦикла;
			
			ФайлыАрхиваXML = НайтиФайлы(ПапкаДляРаспаковки, "*.xml");
			Для Каждого РаспакованныйФайл Из ФайлыАрхиваXML Цикл
				Возврат РаспакованныйФайл;
			КонецЦикла;
			
		ИначеЕсли Найти(ДопИнформацияПоЭД.Расширение, "xml") > 0 Тогда
			Если СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.Подтверждение
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ИзвещениеОПолучении
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.УведомлениеОбУточнении
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Продавец
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.АктИсполнитель
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.СчетФактура
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.КорректировочныйСчетФактура Тогда
			
				ИмяФайлаДанных = ИмяФайла;
				ТабличныйДокумент = ЭлектронныеДокументыВнутренний.СформироватьПечатнуюФормуЭД(
																			ИмяФайла,
																			СсылкаНаЭД.НаправлениеЭД,
																			СсылкаНаЭД.УникальныйИдентификатор(),
																			ИмяФайлаПодчиненногоЭД,
																			СсылкаНаЭД.Наименование,
																			ИмяФайлаДопДанных);
			
				Если ТипЗнч(ТабличныйДокумент) = Тип("ТабличныйДокумент") Тогда
					Возврат ТабличныйДокумент;
				КонецЕсли;
			
			ИначеЕсли СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ПлатежноеПоручение
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ЗапросВыписки
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ВыпискаБанка
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.Квитанция Тогда
			
				ИмяФайлаДанных = ИмяФайла;
				ТабличныйДокумент = ЭлектронныеДокументыВнутренний.СформироватьПечатнуюФормуЭД(
																			ИмяФайла,
																			СсылкаНаЭД.НаправлениеЭД,
																			СсылкаНаЭД.УникальныйИдентификатор(),
																			ИмяФайлаПодчиненногоЭД,
																			СсылкаНаЭД.УникальныйИдентификатор());
			
				Если ТипЗнч(ТабличныйДокумент)=Тип("ТабличныйДокумент") Тогда
					Возврат ТабличныйДокумент;
				КонецЕсли;

			КонецЕсли;
		Иначе
			
			Возврат ИмяФайла;
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИзменитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументИБНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ДокументИБ = Неопределено Тогда
		ТекЭлемент = Неопределено;
		Если ЗначениеЗаполнено(СписокТиповДокументов) Тогда
			Для Каждого ЭлементСписка Из Элементы.СписокТиповДокументов.СписокВыбора Цикл
				Если СписокТиповДокументов = ЭлементСписка.Представление Тогда
					ДокументИБ = ЭлементСписка.Значение;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
