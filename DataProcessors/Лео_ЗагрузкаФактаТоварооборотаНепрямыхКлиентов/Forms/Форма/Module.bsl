
&НаКлиенте
Процедура ИмяФайлаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Фильтр = "Документ Exel(*.xls)|*.xls|Документ Exel 2007(*.xmlx)|*.xlsx";
	Если Диалог.Выбрать() Тогда
		Объект.ИмяФайла = Диалог.ПолноеИмяФайла; 
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаОткрытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЗапуститьПриложение(Объект.ИмяФайла);
КонецПроцедуры

&НаСервере
Процедура ПолучитьФайлНаСервере(ДвоичныйФайл);
	Объект.ДанныеДляЗагрузки.Очистить();

	ДанныеДокумента = ПолучитьИзВременногоХранилища(ДвоичныйФайл);
	ИмяФайлаДляЗагрузки = КаталогВременныхФайлов()+"temp" + Объект.ID + Объект.РасширениеФайла;

	ДанныеДокумента.Записать(ИмяФайлаДляЗагрузки);
	
	ФайлEXCEL = ИмяФайлаДляЗагрузки ;
	ИмяЛиста = Объект.ИмяЛиста;
	КолвоСтрокExcel = 1;
	ДанныеИзФайла = ЗагрузитьМетодом_MSADODB(ФайлEXCEL,ИмяЛиста,1,,, КолвоСтрокExcel,"MicrosoftACEOLEDB12");
	
	Если Объект.КолонкаСумма > 0 Тогда
		КолонкаСумма = Объект.КолонкаСумма - 1;
	КонецЕсли;
	
    Если Объект.КолонкаКоличество > 0 Тогда
		КолонкаКоличество = Объект.КолонкаКоличество - 1;
	КонецЕсли;
	
	
	Для каждого СтрокаДанных из ДанныеИзФайла Цикл
		СтрокаДанныеДляЗагрузки = Объект.ДанныеДляЗагрузки.Добавить();

		//Номенклатура
		Номенклатура = Справочники.Номенклатура.ПустаяСсылка();
		
		Если Объект.КолонкаКодНоменклатурыКлиента > 0 Тогда
			КолонкаКодНоменклатурыКлиента = Объект.КолонкаКодНоменклатурыКлиента - 1;
			СтрокаДанныеДляЗагрузки.КодНоменклатурыКлиента = СокрЛП(СтрокаДанных[КолонкаКодНоменклатурыКлиента]);
			Номенклатура = Справочники.Номенклатура.НайтиПоРеквизиту("Артикул",СокрЛП(СтрокаДанных[КолонкаКодНоменклатурыКлиента]));
			
			Если Номенклатура =  Справочники.Номенклатура.ПустаяСсылка() Тогда	
				Номенклатура = ПоискСоответствияНоменклатурыПоКоду (Объект.СетьТорговаяТочка,СтрокаДанныеДляЗагрузки.КодНоменклатурыКлиента);
			КонецЕсли;
		КонецЕсли;
		
		Если Объект.КолонкаНаименованиеНоменклатурыКлиента > 0  и Номенклатура =  Справочники.Номенклатура.ПустаяСсылка() Тогда
			КолонкаНаименованиеНоменклатурыКлиента = Объект.КолонкаНаименованиеНоменклатурыКлиента - 1;
			СтрокаДанныеДляЗагрузки.НаименованиеНоменклатурыКлиента = СокрЛП(СтрокаДанных[КолонкаНаименованиеНоменклатурыКлиента]); 
			Если Номенклатура =  Справочники.Номенклатура.ПустаяСсылка() Тогда
				Номенклатура = ПоискСоответствияНоменклатурыПоНаименованию (Объект.СетьТорговаяТочка,СтрокаДанныеДляЗагрузки.НаименованиеНоменклатурыКлиента);
			КонецЕсли;	
		КонецЕсли;
		
		Если Номенклатура =  Справочники.Номенклатура.ПустаяСсылка() Тогда
			Сообщить("Не найдена номенклатура. " + ?(Объект.КолонкаКодНоменклатурыКлиента > 0,"Код:"+СтрокаДанныеДляЗагрузки.КодНоменклатурыКлиента+ " ","") + ?(Объект.КолонкаНаименованиеНоменклатурыКлиента > 0,"Наименование:"+СтрокаДанныеДляЗагрузки.НаименованиеНоменклатурыКлиента,"") );
		Иначе
			СтрокаДанныеДляЗагрузки.Номенклатура = Номенклатура;
		КонецЕсли;
		
		//ТорговаяТочка
		Если Объект.КолонкаIDТорговойТочки > 0 Тогда
			КолонкаIDТорговойТочки = Объект.КолонкаIDТорговойТочки - 1;
			СтрокаДанныеДляЗагрузки.IDТорговойТочки = СокрЛП(СтрокаДанных[КолонкаIDТорговойТочки]); 
		КонецЕсли;
	
	    Если Объект.КолонкаНаименованиеТорговойТочки > 0 Тогда
			КолонкаНаименованиеТорговойТочки = Объект.КолонкаНаименованиеТорговойТочки - 1;
			СтрокаДанныеДляЗагрузки.НаименованиеТорговойТочки = СокрЛП(СтрокаДанных[КолонкаНаименованиеТорговойТочки]); 
		КонецЕсли;

		Если Объект.КолонкаАдресТорговойТочки > 0 Тогда
			КолонкаАдресТорговойТочки = Объект.КолонкаАдресТорговойТочки - 1;
			СтрокаДанныеДляЗагрузки.АдресТорговойТочки = СокрЛП(СтрокаДанных[КолонкаАдресТорговойТочки]); 
		КонецЕсли;
		
		Если Объект.КолонкаКодСети > 0 Тогда
			КолонкаКодСети = Объект.КолонкаКодСети - 1;
			КодКонтрагента = СокрЛП(СтрокаДанных[КолонкаКодСети]);
			СтрокаДанныеДляЗагрузки.Сеть = Справочники.Контрагенты.НайтиПоКоду(КодКонтрагента);  
		Иначе
			СтрокаДанныеДляЗагрузки.Сеть = Объект.СетьТорговаяТочка;
		КонецЕсли;
		
		Если НЕ Объект.СетьТорговаяТочка = справочники.Лео_ТорговыеТочки.ПустаяСсылка()Тогда
			СтрокаДанныеДляЗагрузки.ТорговаяТочка = ПоискТорговойТочки(Объект.СетьТорговаяТочка,СтрокаДанныеДляЗагрузки.IDТорговойТочки,СтрокаДанныеДляЗагрузки.НаименованиеТорговойТочки);
		КонецЕсли;
		
		Если Объект.КолонкаКодКонтрагента > 0 Тогда
			КолонкаКодКонтрагента = Объект.КолонкаКодКонтрагента - 1;
			КодКонтрагента = СокрЛП(СтрокаДанных[КолонкаКодКонтрагента]);
			СтрокаДанныеДляЗагрузки.Контрагент = Справочники.Контрагенты.НайтиПоКоду(КодКонтрагента);  
		Иначе
			СтрокаДанныеДляЗагрузки.Контрагент = Объект.Контрагент;
		КонецЕсли;
		
		Если Объект.КолонкаНачалоПериода > 0 Тогда
			КолонкаНачалоПериода = Объект.КолонкаНачалоПериода - 1;
			СтрокаДанныеДляЗагрузки.НачалоПериода = Дата(Сред(СтрокаДанных[КолонкаНачалоПериода] ,7,4)+Сред(СтрокаДанных[КолонкаНачалоПериода] ,4,2)+Сред(СтрокаДанных[КолонкаНачалоПериода] ,0,2));
		Иначе
			СтрокаДанныеДляЗагрузки.НачалоПериода = Объект.Дата;
		КонецЕсли;

		Если Объект.КолонкаСумма > 0 Тогда
			КолонкаСумма = Объект.КолонкаСумма - 1;
			СтрокаДанныеДляЗагрузки.Сумма = Формат(СтрокаДанных[КолонкаСумма],"ЧЦ=10; ЧДЦ=2");
		КонецЕсли;
		
	    Если Объект.КолонкаКоличество > 0 Тогда
			КолонкаКоличество = Объект.КолонкаКоличество - 1;
			СтрокаДанныеДляЗагрузки.Количество = Формат(СтрокаДанных[КолонкаКоличество],"ЧЦ=10");
		КонецЕсли;
		
	КонецЦикла;	

КонецПроцедуры

&НаСервере
Функция ПоискСоответствияНоменклатурыПоКоду (Сеть,КодНоменклатуры)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Лео_СоответствиеНоменклатурыНепрямыхКлиентовСрезПоследних.Контрагент,
		|	Лео_СоответствиеНоменклатурыНепрямыхКлиентовСрезПоследних.СетьТорговаТочка,
		|	Лео_СоответствиеНоменклатурыНепрямыхКлиентовСрезПоследних.Номенклатура
		|ИЗ
		|	РегистрСведений.Лео_СоответствиеНоменклатурыНепрямыхКлиентов.СрезПоследних КАК Лео_СоответствиеНоменклатурыНепрямыхКлиентовСрезПоследних
		|ГДЕ
		|	Лео_СоответствиеНоменклатурыНепрямыхКлиентовСрезПоследних.АртикулВСистемеКлиента = &КодНоменклатурыКлиента
		|	И Лео_СоответствиеНоменклатурыНепрямыхКлиентовСрезПоследних.Регистратор.Дата >= &НачалоРегистрации
		|	И Лео_СоответствиеНоменклатурыНепрямыхКлиентовСрезПоследних.СетьТорговаТочка = &СетьТорговаяТочка
		|
		|СГРУППИРОВАТЬ ПО
		|	Лео_СоответствиеНоменклатурыНепрямыхКлиентовСрезПоследних.Номенклатура,
		|	Лео_СоответствиеНоменклатурыНепрямыхКлиентовСрезПоследних.Контрагент,
		|	Лео_СоответствиеНоменклатурыНепрямыхКлиентовСрезПоследних.СетьТорговаТочка";

	Запрос.УстановитьПараметр("КодНоменклатурыКлиента", КодНоменклатуры);
	Запрос.УстановитьПараметр("НачалоРегистрации", началогода(Объект.Дата));     ///по ТЗ Коваль К. и Ципулиной П. ограичиваем началом года
	Запрос.УстановитьПараметр("СетьТорговаяТочка", Сеть);


	Результат = Запрос.Выполнить();

	ТзНоменклатура = Результат.Выгрузить();

	Если ТзНоменклатура.Количество() > 1 Тогда
		Сообщить ("Найдено две или более номенклатур для кода " + КодНоменклатуры );
		Возврат Справочники.Номенклатура.ПустаяСсылка();
	ИначеЕсли ТзНоменклатура.Количество() = 1 Тогда
    	Возврат ТзНоменклатура[0].Номенклатура;
	Иначе	
	    Возврат Справочники.Номенклатура.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ПоискСоответствияНоменклатурыПоНаименованию (Сеть,НаименованиеНоменклатуры)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Лео_СоответствиеНоменклатурыНепрямыхКлиентовСрезПоследних.Контрагент,
		|	Лео_СоответствиеНоменклатурыНепрямыхКлиентовСрезПоследних.СетьТорговаТочка,
		|	Лео_СоответствиеНоменклатурыНепрямыхКлиентовСрезПоследних.Номенклатура,
		|	Лео_СоответствиеНоменклатурыНепрямыхКлиентовСрезПоследних.Период КАК Период
		|ИЗ
		|	РегистрСведений.Лео_СоответствиеНоменклатурыНепрямыхКлиентов.СрезПоследних КАК Лео_СоответствиеНоменклатурыНепрямыхКлиентовСрезПоследних
		|ГДЕ
		|	Лео_СоответствиеНоменклатурыНепрямыхКлиентовСрезПоследних.НаименованиеВСистемеКлиента = &НаименованиеНоменклатуры
		|	И Лео_СоответствиеНоменклатурыНепрямыхКлиентовСрезПоследних.СетьТорговаТочка = &СетьТорговаяТочка
		|
		|СГРУППИРОВАТЬ ПО
		|	Лео_СоответствиеНоменклатурыНепрямыхКлиентовСрезПоследних.Номенклатура,
		|	Лео_СоответствиеНоменклатурыНепрямыхКлиентовСрезПоследних.Контрагент,
		|	Лео_СоответствиеНоменклатурыНепрямыхКлиентовСрезПоследних.СетьТорговаТочка,
		|	Лео_СоответствиеНоменклатурыНепрямыхКлиентовСрезПоследних.Период
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период УБЫВ";

	Запрос.УстановитьПараметр("НаименованиеНоменклатуры", НаименованиеНоменклатуры);
	Запрос.УстановитьПараметр("СетьТорговаяТочка", Сеть);


	Результат = Запрос.Выполнить();

	ТзНоменклатура = Результат.Выгрузить();

	Если ТзНоменклатура.Количество() > 1 Тогда
		Сообщить ("Найдено две или более номенклатур по наименованию " + НаименованиеНоменклатуры );
		Возврат Справочники.Номенклатура.ПустаяСсылка();
	ИначеЕсли ТзНоменклатура.Количество() = 1 Тогда
    	Возврат ТзНоменклатура[0].Номенклатура;
	Иначе	
	    Возврат Справочники.Номенклатура.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ПоискТорговойТочки(Сеть,IDТорговойТочки,НаименованиеТорговойТочки);
    ТорговаяТочка = Справочники.Лео_ТорговыеТочки.ПустаяСсылка();
	Если IDТорговойТочки <> "" Тогда
		ТорговаяТочка = Справочники.Лео_ТорговыеТочки.НайтиПоРеквизиту("ID",IDТорговойТочки,Сеть);
	КонецЕсли;
	
	Если НаименованиеТорговойТочки <> "" и  ТорговаяТочка = Справочники.Лео_ТорговыеТочки.ПустаяСсылка()Тогда
		ТорговаяТочка = Справочники.Лео_ТорговыеТочки.НайтиПоРеквизиту("НаименованиеПолное",НаименованиеТорговойТочки,Сеть);	
	КонецЕсли;
	
	Возврат ТорговаяТочка
КонецФункции

&НаСервере
Функция ЗагрузитьМетодом_MSADODB(Знач ФайлEXCEL, Знач ИмяЛиста, Знач СтрокаЗаголовка = 3, НачСтрока = 0, КонСтрока = 0, КолвоСтрокExcel, 
	Знач ПодключениеADODB = "MicrosoftJetOLEDB40") Экспорт
	Перем СonnectionString, ADODBConnection, ADODBRecordset, ТекстЗапроса;
	Перем КолвоКолонокExcel, Поле, Колонка, ИмяКолонки;
	Перем НоваяСтрока, НомерСтроки;
	Перем ТаблицаРезультат;
	
	// Нумерация MS ADODB начинается с 1.
	
	// Переменная "СтрокаЗаголовка", не используется, т.к. HDR=YES, а не HDR=NO.
	// HDR=YES:
	// 1. Считывание заголовков колонок с 1-ой строки.
	// 2. Считываемые данные со 2-ой и последующих строк типизированы. Для варианта HDR=NO: считываемые данные - строка.
	
	// Строка соединения - определение драйвера, который будет использован для подключения к файлу EXCEL.
	Если ПодключениеADODB = "MicrosoftACEOLEDB12" Тогда
		
		// ACE.OLEDB.12.0 - Для использования данного подключения необходимо дополнительное ПО:
		// Microsoft Access Database Engine 2010 Redistributable 32/64 bit.
		//Для корректной работы также установил:
		//http://www.microsoft.com/en-us/download/details.aspx?id=13255
		//http://www.microsoft.com/en-us/download/details.aspx?id=23734		
		
		СonnectionString = "Provider=Microsoft.ACE.OLEDB.12.0;Data Source= " + СокрЛП(ФайлEXCEL) + ";Extended Properties=""Excel 12.0;HDR=NO;IMEX=1;""";
		
		// Еще один вариант.
		//СтрокаСоединения = "Driver={Microsoft Excel Driver (*.xls, *.xlsx, *.xlsm, *.xlsb)};Dbq=" + СокрЛП(ФайлEXCEL) + ";";
		
	Иначе
		
		// Jet.OLEDB.4.0 - Стандартное подключение, как правило, не требующее установки дополнительного ПО. 
		// Рекомендуется установить последний Service Pack Windows.
		СonnectionString = "Provider=Microsoft.Jet.OLEDB.4.0;Data Source= "  + СокрЛП(ФайлEXCEL) + ";Extended Properties=""Excel 8.0;HDR=NO;IMEX=1;""";
		
		// Еще один вариант.
		//СтрокаСоединения = "Driver={Microsoft Excel Driver (*.xls)};Dbq=" + СокрЛП(ФайлEXCEL) + ";";
		
	КонецЕсли;
	
	Попытка
		// Инициализация основного объекта ADODB.Connection. Открытие соединения.
		ADODBConnection = Новый COMОбъект("ADODB.Connection");
		ADODBConnection.ConnectionString =  СonnectionString;
		ADODBConnection.Open();
		// Импирически определенный параметр для правильного определения количества строк листа.
		ADODBConnection.CursorLocation = 3;    // По-умолчанию 2.
	Исключение
		Сообщить(НСтр("ru = '"+ОписаниеОшибки()+"'"), СтатусСообщения.Внимание);
		Возврат Новый ТаблицаЗначений;    // В случае ошибки возвращаем пустую таблицу значений.
	КонецПопытки;
	
	ТекстЗапроса = "SELECT * FROM [" + ИмяЛиста + "$]";
	
	// Создание Recordset. Дочерний объект ADODBConnection. Набор записей по запросу.
	Попытка
		ADODBRecordset = Новый COMОбъект("ADODB.Recordset");
		ADODBRecordset.Open(ТекстЗапроса, ADODBConnection);
		
		// Проверка заполненности листа.
		Если (ADODBRecordset.EOF ИЛИ ADODBRecordset.BOF) Тогда
			КолвоСтрокExcel = 0;
			Сообщить(НСтр("ru = '" + ИмяЛиста + ": не содержит данных.'"), СтатусСообщения.Внимание);
			
			// Завершение работы.
			// Закрытие Объектов.
			ADODBRecordset.Close();
			ADODBConnection.Close();
			ADODBRecordset   = Неопределено;
			ADODBConnection = Неопределено;
			
			Возврат Новый ТаблицаЗначений;    // В случае ошибки возвращаем пустую таблицу значений.
		КонецЕсли;
		
		// Импирически определенные параметры для правильного определения количества строк листа.
		ADODBRecordset.AbsolutePage     = 1;
		ADODBRecordset.AbsolutePosition = 1;
	Исключение
		Сообщить(НСтр("ru = '"+ОписаниеОшибки()+"'"), СтатусСообщения.Внимание);
		Возврат Новый ТаблицаЗначений;    // В случае ошибки возвращаем пустую таблицу значений.
	КонецПопытки;
	
	// Параметр, возвращаемый в вызывающую процедуру.
	КолвоСтрокExcel = ADODBRecordset.RecordCount + 1;    // (+1) - учет Строки-Заголовока, которая "съедается".
	КолвоКолонокExcel = ADODBRecordset.Fields.Count;
	
	// Проверка заполненности листа.
	Если КолвоСтрокExcel <= 2 Тогда
		КолвоСтрокExcel = 0;
		Сообщить(НСтр("ru = '" + ИмяЛиста + ": не содержит данных.'"), СтатусСообщения.Внимание);
		
		// Завершение работы.
		// Закрытие Объектов.
		ADODBRecordset.Close();
		ADODBConnection.Close();
		ADODBRecordset   = Неопределено;
		ADODBConnection = Неопределено;
		
		Возврат Новый ТаблицаЗначений;    // В случае ошибки возвращаем пустую таблицу значений.
	КонецЕсли;
	
	// Создание результирующей таблицы, в которую будут записываться считанные из EXCEL данные.
	ТаблицаРезультат = Новый ТаблицаЗначений;
	
	// Формирование колонок результирующей таблицы.
	
	// "НомерСтроки" - для наглядности и удобства.
	// В зависимости от разрабатываемой обработки.
	// "Сопоставлено" - может быть другим.
	// Здесь же могут быть добавлены другие колонки, не формируемые из содержимого файла EXCEL.
	
	//ТаблицаРезультат.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"), "№", 4);
	//ТаблицаРезультат.Колонки.Добавить("Сопоставлено", Новый ОписаниеТипов("Булево"), "Сопоставлено", 1);
	
	Для ит = 1 ПО КолвоКолонокExcel Цикл
		
		Поле = ADODBRecordset.Fields.Item(ит - 1);
		ИмяКолонки = "К_" + ит;
		//ИмяКолонки = Поле.Value;
		//Колонка = ТаблицаРезультат.Колонки.Добавить(ИмяКолонки, , СокрЛП(СтрЗаменить(Поле.Name, "#", ".")));
		ИмяКолонки = ЗаменитьСимволы(ИмяКолонки);
		Колонка = ТаблицаРезультат.Колонки.Добавить(ИмяКолонки, ,ИмяКолонки);
		
		// Замена "#" на ".", т.к. при считывании ADODB "." в имени колонки заменяется на "#".
	КонецЦикла;
	
	// ТаблицаРезультат: 1-я строка - Строка-Заголовок.
	
	// Добавление этой строки обусловлено исключительно из соображений идентичности содержимого файла EXCEL и ТаблицыЗначений,
	// выводимой на форме Обработки, и дальнейшей обработки строки заголовка
	// с целью сопоставления колонок EXCEL и реквизитов 1С: для Справочников, ПВХ, Регистров, Документов.
	
	// Если в Вашей обработке в результирующей таблице в качестве 1-ой строки не нужна Строка-Заголовок, то
	// следует закомментировать следующий цикл:
	//НоваяСтрока = ТаблицаРезультат.Добавить();
	//НоваяСтрока.НомерСтроки = 1;
	//Для ит = 1 ПО КолвоКолонокExcel Цикл
	//	
	//	ИмяКолонки = "К_" + ит;
	//	Колонка = ТаблицаРезультат.Колонки.Найти(ИмяКолонки);
	//	НоваяСтрока[ИмяКолонки] = Колонка.Заголовок;
	//	
	//КонецЦикла;
	
	// ТаблицаРезультат: Формирование строк по указанному диапазону: НачСтрока - КонСтрока.
	
	НомерСтроки = 1;
	НачСтрока =  СтрокаЗаголовка+1;
	Пока ADODBRecordset.EOF() = 0 Цикл
		
		//НомерСтроки = НомерСтроки + 1;
		
		Если НомерСтроки < НачСтрока Тогда    // Номер строки вне диапазона считываемых строк.
			ADODBRecordset.MoveNext();             // Следующая строка.
			//Продолжить;
			НомерСтроки = НомерСтроки + 1;
			Продолжить;
		КонецЕсли;
		
		Если КонСтрока > 0 И НомерСтроки > КонСтрока Тогда    // Номер строки вне диапазона считываемых строк.
			Прервать;
		КонецЕсли;
		
		НомерСтроки = НомерСтроки + 1;
		
		НоваяСтрока = ТаблицаРезультат.Добавить();
		//НоваяСтрока.НомерСтроки = НомерСтроки;
		
		Для ит = 1 ПО КолвоКолонокExcel Цикл
			
			Поле = ADODBRecordset.Fields.Item(ит - 1);
			
			Если Поле.ActualSize = 0 Тогда        // Пустое поле EXCEL.
				Продолжить;
			КонецЕсли;
			
			ЗначениеЯчейки = Поле.Value;        // Учитывая параметр HDR=YES в строке соединения, данные считываются в соответствии с их типом.
			
			ИмяКолонки = ит-1;
			НоваяСтрока[ИмяКолонки] = ЗначениеЯчейки;
			
			// Используется при формировании таблицы на форме обработки.
			//ШиринаКолонки = ТаблицаРезультат.Колонки[ИмяКолонки].Ширина;
			//ДлинаСтроки      = СтрДлина(СокрЛП(ЗначениеЯчейки));
			//ТаблицаРезультат.Колонки[ИмяКолонки].Ширина = ?(ШиринаКолонки < ДлинаСтроки, ДлинаСтроки, ШиринаКолонки);
			
		КонецЦикла;
		
		ADODBRecordset.MoveNext();   // Следующая строка.
		
	КонецЦикла;
	
	//УдалитьКолонкиСНулевойШириной(ТаблицаРезультат);
	
	// Завершение работы.
	// Закрытие Объектов.
	ADODBRecordset.Close();
	ADODBConnection.Close();
	ADODBRecordset   = Неопределено;
	ADODBConnection = Неопределено;
	
	Возврат ТаблицаРезультат;
	
КонецФункции

&НаСервере
Функция ЗаменитьСимволы(Стр)
	Стр = СтрЗаменить(Стр, " ", "");
	Стр = СтрЗаменить(Стр, "-", "");
	Стр = СтрЗаменить(Стр, "/", "");
	Стр = СтрЗаменить(Стр, ":", "");
	Стр = СтрЗаменить(Стр, ".", "");
	Стр = СтрЗаменить(Стр, "№", "");
	Стр = СтрЗаменить(Стр, "(", "");
	Стр = СтрЗаменить(Стр, ")", "");
	Возврат Стр;
КонецФункции

&НаКлиенте
Процедура ЗагрузитьДанные(Команда)
	Ф=Новый Файл(СокрЛП(Объект.ИмяФайла));
	Если Ф.Существует()=0 Тогда
		Сообщить("Указанный файл не существует!");
		Возврат;
	КонецЕсли;
	ВыбФайл = Новый Файл(Объект.ИмяФайла);
	Если ВыбФайл.Существует() Тогда
		Объект.РасширениеФайла = ВыбФайл.Расширение;
		Объект.ID = ВыбФайл.ИмяБезРасширения;
	КонецЕсли;
	
	ДвоичныйФайл = ПоместитьВоВременноеХранилище ( Новый ДвоичныеДанные(Объект.ИмяФайла),ЭтаФорма.УникальныйИдентификатор); 
	ПолучитьФайлНаСервере(ДвоичныйФайл);

КонецПроцедуры

&НаКлиенте
Процедура СоздатьТорговыеТочки(Команда)
	
	СоздатьТорговыеТочкиНаСервере ();	
	
КонецПроцедуры

&НаСервере
Процедура СоздатьТорговыеТочкиНаСервере ();

	Для Каждого СтрокаТч из  Объект.ДанныеДляЗагрузки Цикл
		СтрокаТч.ТорговаяТочка = ПоискТорговойТочки(Объект.СетьТорговаяТочка,СтрокаТч.IDТорговойТочки,СтрокаТч.НаименованиеТорговойТочки);
		
		Если СтрокаТч.ТорговаяТочка	= Справочники.Лео_ТорговыеТочки.ПустаяСсылка()и СтрокаТч.НаименованиеТорговойТочки <> "" Тогда
			НовыйЭлементСправочника	= Справочники.Лео_ТорговыеТочки.СоздатьЭлемент();
			НовыйЭлементСправочника.ID = СтрокаТч.IDТорговойТочки ;
			НовыйЭлементСправочника.Наименование = СтрокаТч.НаименованиеТорговойТочки ;
            НовыйЭлементСправочника.НаименованиеПолное = СтрокаТч.НаименованиеТорговойТочки ;
			НовыйЭлементСправочника.Адрес = СтрокаТч.АдресТорговойТочки ;
			НовыйЭлементСправочника.Владелец = Объект.Контрагент ;
			НовыйЭлементСправочника.Родитель = Объект.СетьТорговаяТочка ;
			НовыйЭлементСправочника.ФармФуд  = Объект.СетьТорговаяТочка.ФармФуд ;
			НовыйЭлементСправочника.БизнесРегион = Объект.СетьТорговаяТочка.БизнесРегион ;
            Попытка 
                НовыйЭлементСправочника.Записать();
				СтрокаТч.ТорговаяТочка = НовыйЭлементСправочника.Ссылка;
			Исключение
				Сообщить(НСтр("ru = '"+ОписаниеОшибки()+"'"), СтатусСообщения.Внимание);	
			КонецПопытки
		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументыВводФактаТоварооборотаНепрямыхКлиентов(Команда)
	СоздатьДокументыВводФактаТоварооборотаНепрямыхКлиентовНаСервере()
КонецПроцедуры

&НаСервере
Процедура СоздатьДокументыВводФактаТоварооборотаНепрямыхКлиентовНаСервере()
	
	ТчКонтрагентДата = Объект.ДанныеДляЗагрузки.Выгрузить(,"НачалоПериода,Контрагент,Сеть");	
	ТчКонтрагентДата.Свернуть("НачалоПериода,Контрагент,Сеть");	
	
	Для каждого СтрокаКонтрагентДата из ТчКонтрагентДата Цикл
	
		НовыйДокументВВыдФакта = Документы.Лео_ВводФактаТоварооборотаНепрямыхКлиентов.СоздатьДокумент();
		НовыйДокументВВыдФакта.Контрагент = СтрокаКонтрагентДата.Контрагент;
		НовыйДокументВВыдФакта.Сеть = СтрокаКонтрагентДата.Сеть;
		НовыйДокументВВыдФакта.Период = Объект.Период;
		НовыйДокументВВыдФакта.Ответственный = ПараметрыСеанса.ТекущийПользователь;
		НовыйДокументВВыдФакта.НачалоПериода = НачалоМесяца(СтрокаКонтрагентДата.НачалоПериода);
		НовыйДокументВВыдФакта.Дата = СтрокаКонтрагентДата.НачалоПериода; 
		СтруктураПараметров =  ПолучитьПараметрыРасчета(СтрокаКонтрагентДата.НачалоПериода,Объект.СетьТорговаяТочка);
		
		ПараметрыОтбора = Новый Структура();
		ПараметрыОтбора.Вставить("НачалоПериода" , СтрокаКонтрагентДата.НачалоПериода);
		ПараметрыОтбора.Вставить("Контрагент" , СтрокаКонтрагентДата.Контрагент);
		ПараметрыОтбора.Вставить("Сеть" , СтрокаКонтрагентДата.Сеть);
		
		ТчЗагрузкаФакта = Объект.ДанныеДляЗагрузки.Выгрузить(ПараметрыОтбора,);
		ТчФакт = НовыйДокументВВыдФакта.Факт;
		ТчЗагрузкаФакта.Свернуть("ТорговаяТочка,IDТорговойТочки,НаименованиеТорговойТочки,АдресТорговойТочки,Номенклатура,КодНоменклатурыКлиента,НаименованиеНоменклатурыКлиента,Контрагент,НачалоПериода,Сеть","Количество,Сумма");
		Для каждого СтрокаТч из ТчЗагрузкаФакта Цикл
			СтрокаФакт = ТчФакт.Добавить();
			СтрокаФакт.ТорговаяТочка	= СтрокаТч.ТорговаяТочка;
			СтрокаФакт.Номенклатура	= СтрокаТч.Номенклатура;
			СтрокаФакт.Количество	= СтрокаТч.Количество;
			СтрокаФакт.Товарооборот	= СтрокаТч.Сумма;
			Если НЕ СтруктураПараметров = Неопределено Тогда
				СтрокаФакт.ПроцентПремии = СтруктураПараметров.ПроцентПремии;
			КонецЕсли;

			Если  СтрокаФакт.Товарооборот > 0 Тогда
				Если СтрокаФакт.ПроцентПремии  > 0 Тогда
					СтрокаФакт.Премия =  СтрокаФакт.Товарооборот / 100 *  СтрокаФакт.ПроцентПремии +  СтрокаФакт.ФиксированнаяСуммаПремии;
				Иначе
					СтрокаФакт.Премия = СтрокаФакт.ФиксированнаяСуммаПремии;
				КонецЕсли;
			КонецЕсли;
			//СтрокаФакт.ФиксированнаяСуммаПремии	= СтрокаТч.;

		КонецЦикла;
		
		Попытка
			НовыйДокументВВыдФакта.Записать(РежимЗаписиДокумента.Проведение);
			СтрокаСозданныеДокументы = Объект.СозданныеДокументы.Добавить();
			СтрокаСозданныеДокументы.ДокументВводФакта = НовыйДокументВВыдФакта.Ссылка;
		Исключение
			Сообщить(НСтр("ru = '"+ОписаниеОшибки()+"'"), СтатусСообщения.Внимание);	
		КонецПопытки

	КонецЦикла;

КонецПроцедуры

&НаСервере
Функция ПолучитьПараметрыРасчета(ДатаСреза,Сеть)
	
	СтруктураПараметров  = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Лео_ПареметрыРасчетаПремииКлиентамСрезПоследних.Контрагент,
		|	Лео_ПареметрыРасчетаПремииКлиентамСрезПоследних.Сеть,
		|	Лео_ПареметрыРасчетаПремииКлиентамСрезПоследних.Периодичность,
		|	Лео_ПареметрыРасчетаПремииКлиентамСрезПоследних.Договор,
		|	Лео_ПареметрыРасчетаПремииКлиентамСрезПоследних.ПроцентПремии,
		|	Лео_ПареметрыРасчетаПремииКлиентамСрезПоследних.ФиксированнаяСумма,
		|	Лео_ПареметрыРасчетаПремииКлиентамСрезПоследних.ГодовойТоварооборот,
		|	Лео_ПареметрыРасчетаПремииКлиентамСрезПоследних.Действует
		|ИЗ
		|	РегистрСведений.Лео_ПареметрыРасчетаПремииКлиентам.СрезПоследних(
		|			&ДатаСреза,
		|			Сеть = &Сеть
		|				И Действует = ИСТИНА) КАК Лео_ПареметрыРасчетаПремииКлиентамСрезПоследних";

	Запрос.УстановитьПараметр("ДатаСреза", ДатаСреза);
	Запрос.УстановитьПараметр("Сеть", Сеть);

	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СтруктураПараметров = Новый Структура("ПроцентПремии, ФиксированнаяСумма", ВыборкаДетальныеЗаписи.ПроцентПремии ,ВыборкаДетальныеЗаписи.ФиксированнаяСумма);

	КонецЦикла;

	Возврат СтруктураПараметров 	
КонецФункции
