
&НаКлиенте
Процедура КомандаЗаполнитьДокументы(Команда)
	
	ЗаполнитьДокументыНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДокументыНаСервере()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КонтрагентыСвязанныеСОрганизациямиСрезПоследних.Организация,
	|	КонтрагентыСвязанныеСОрганизациямиСрезПоследних.Контрагент
	|ПОМЕСТИТЬ ВТ_КонтрагентыСвязанныеСОрганизацией
	|ИЗ
	|	РегистрСведений.КонтрагентыСвязанныеСОрганизациями.СрезПоследних КАК КонтрагентыСвязанныеСОрганизациямиСрезПоследних
	|
	|СГРУППИРОВАТЬ ПО
	|	КонтрагентыСвязанныеСОрганизациямиСрезПоследних.Организация,
	|	КонтрагентыСвязанныеСОрганизациямиСрезПоследних.Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Ссылка КАК Поступление,
	|	ПоступлениеТоваровУслуг.Организация КАК ОрганизацияПокупатель,
	|	ПоступлениеТоваровУслуг.Контрагент КАК КонтрагентПродавец,
	|	ПоступлениеТоваровУслуг.СуммаДокумента КАК СуммаПоступления,
	|	ВТ_КонтрагентыСвязанныеСОрганизацией.Организация КАК ОрганизацияПродавец,
	|	НАЧАЛОПЕРИОДА(ПоступлениеТоваровУслуг.Дата, ДЕНЬ) КАК ДатаПоступления
	|ПОМЕСТИТЬ ВТ_Поступления
	|ИЗ
	|	ВТ_КонтрагентыСвязанныеСОрганизацией КАК ВТ_КонтрагентыСвязанныеСОрганизацией
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|		ПО ВТ_КонтрагентыСвязанныеСОрганизацией.Контрагент = ПоступлениеТоваровУслуг.Контрагент
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Проведен
	|	И ПоступлениеТоваровУслуг.Дата МЕЖДУ &ПериодНач И &ПериодКон
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Реализация,
	|	РеализацияТоваровУслуг.Организация КАК ОрганизацияПродавец,
	|	РеализацияТоваровУслуг.Контрагент КАК КонтрагентПокупателдь,
	|	РеализацияТоваровУслуг.СуммаДокумента КАК СуммаРеализации,
	|	ВТ_КонтрагентыСвязанныеСОрганизацией.Организация КАК ОрганизацияПокупатель,
	|	НАЧАЛОПЕРИОДА(РеализацияТоваровУслуг.Дата, ДЕНЬ) КАК ДатаРеализации
	|ПОМЕСТИТЬ ВТ_Реализации
	|ИЗ
	|	ВТ_КонтрагентыСвязанныеСОрганизацией КАК ВТ_КонтрагентыСвязанныеСОрганизацией
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ПО ВТ_КонтрагентыСвязанныеСОрганизацией.Контрагент = РеализацияТоваровУслуг.Контрагент
	|ГДЕ
	|	РеализацияТоваровУслуг.Дата МЕЖДУ &ПериодНач И &ПериодКон
	|	И РеализацияТоваровУслуг.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Поступление,
	|	ВложенныйЗапрос.Реализация,
	|	ВложенныйЗапрос.СуммаРеализации КАК СуммаРеализацииДо,
	|	ВложенныйЗапрос.СуммаПоступления КАК СуммаПоступленияДо,
	|	ВложенныйЗапрос.Обработать,
	|	ВложенныйЗапрос.СуммаПоступленияПосле,
	|	ВложенныйЗапрос.СуммаРеализацииПосле
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТ_Поступления.Поступление КАК Поступление,
	|		ВТ_Реализации.Реализация КАК Реализация,
	|		ВТ_Реализации.СуммаРеализации КАК СуммаРеализации,
	|		ВТ_Поступления.СуммаПоступления КАК СуммаПоступления,
	|		ИСТИНА КАК Обработать,
	|		ВТ_Поступления.СуммаПоступления КАК СуммаПоступленияПосле,
	|		ВТ_Поступления.ОрганизацияПродавец КАК ОрганизацияПродавец,
	|		ВТ_Поступления.ОрганизацияПокупатель КАК ОрганизацияПокупатель,
	|		ВТ_Реализации.СуммаРеализации КАК СуммаРеализацииПосле
	|	ИЗ
	|		ВТ_Поступления КАК ВТ_Поступления
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Реализации КАК ВТ_Реализации
	|			ПО ВТ_Поступления.ОрганизацияПокупатель = ВТ_Реализации.ОрганизацияПокупатель
	|				И ВТ_Поступления.ОрганизацияПродавец = ВТ_Реализации.ОрганизацияПродавец
	|				И ВТ_Поступления.СуммаПоступления = ВТ_Реализации.СуммаРеализации
	|				И ВТ_Поступления.ДатаПоступления = ВТ_Реализации.ДатаРеализации) КАК ВложенныйЗапрос
	|ГДЕ
	|	ВложенныйЗапрос.ОрганизацияПокупатель = &ОрганизацияПокеупатель");
	
	Запрос.УстановитьПараметр("ОрганизацияПокеупатель", Объект.Организация);
	Запрос.УстановитьПараметр("ПериодНач", Объект.ПериодНач);
	Запрос.УстановитьПараметр("ПериодКон", КонецДня(Объект.ПериодКон));
	
	Объект.ТЧДокументы.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры


&НаКлиенте
Процедура КомандаВыполнитьИзменениеЦен(Команда)
	
	ВыполнитьИзменениеЦенНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьИзменениеЦенНаСервере()
	
	СписокДокументов = Объект.ТЧДокументы.НайтиСтроки(Новый Структура("Обработать", Истина));
	
	КоэффициентНаценки = (100 + Объект.ПроцентИзмененияЦены) / 100;
	
	Для Каждого СтрДокументы Из СписокДокументов Цикл
		
		НачатьТранзакцию();
		
		ОбъектРТУ = СтрДокументы.Реализация.ПолучитьОбъект();
		
		Для Каждого СтрТовары Из ОбъектРТУ.Товары Цикл 
			//+ ЛЕО Зароднюк В	
			Если Объект.НеОбрабатыватьТоварМаркировкаЧЗ Тогда 
			//Проверка на принадлежность товара к ЧЗ
				ЗначениеОКПД2	= лео_ДопФункции.Получить_ЗначенияСвойствОбъектов(СтрТовары.Номенклатура, ПланыВидовХарактеристик.СвойстваОбъектов.НайтиПоКоду("00000000066"));
				Если ЗначениеЗаполнено(ЗначениеОКПД2) И ТипЗнч(ЗначениеОКПД2)=Тип("СправочникСсылка.Лео_КлассификаторОКПД2") Тогда 
					Если ЗначениеОКПД2.ЧестныйЗнак Тогда
	                	Продолжить;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			//- ЛЕО Зароднюк В	
			СтрТовары.Цена = Окр(СтрТовары.Цена * КоэффициентНаценки, 2);
			
			ОбработкаТабличныхЧастей.РассчитатьСуммуТабЧасти(СтрТовары, ОбъектРТУ);
			ОбработкаТабличныхЧастей.РассчитатьСуммуНДСТабЧасти(СтрТовары, ОбъектРТУ);			
			
		КонецЦикла;
		
		Попытка
			ОбъектРТУ.Записать(РежимЗаписиДокумента.Проведение);
			Сообщить("Обработан документ " + ОбъектРТУ.Ссылка + ", новая сумма документа: " + УчетНДС.ПолучитьСуммуДокументаСНДС(ОбъектРТУ, "Товары"));
			СтрДокументы.СуммаРеализацииПосле = УчетНДС.ПолучитьСуммуДокументаСНДС(ОбъектРТУ, "Товары");
		Исключение
			ОбщегоНазначения.СообщитьОбОшибке("Ошибка при записи документа: " + ОбъектРТУ.Ссылка + Символы.ПС + ОписаниеОшибки());
			ОтменитьТранзакцию();
			Продолжить;
		КонецПопытки;
		
		ОбъектПТУ = СтрДокументы.Поступление.ПолучитьОбъект();
		
		Для Каждого СтрТовары Из ОбъектПТУ.Товары Цикл
			//+ ЛЕО Зароднюк В	
			Если Объект.НеОбрабатыватьТоварМаркировкаЧЗ Тогда 
			//Проверка на принадлежность товара к ЧЗ
				ЗначениеОКПД2	= лео_ДопФункции.Получить_ЗначенияСвойствОбъектов(СтрТовары.Номенклатура, ПланыВидовХарактеристик.СвойстваОбъектов.НайтиПоКоду("00000000066"));
				Если ЗначениеЗаполнено(ЗначениеОКПД2) И ТипЗнч(ЗначениеОКПД2)=Тип("СправочникСсылка.Лео_КлассификаторОКПД2") Тогда 
					Если ЗначениеОКПД2.ЧестныйЗнак Тогда
	                	Продолжить;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			//- ЛЕО Зароднюк В	
			СтрТовары.Цена = Окр(СтрТовары.Цена * КоэффициентНаценки, 2);
			
			ОбработкаТабличныхЧастей.РассчитатьСуммуТабЧасти(СтрТовары, ОбъектПТУ);
			ОбработкаТабличныхЧастей.РассчитатьСуммуНДСТабЧасти(СтрТовары, ОбъектПТУ);			
			
		КонецЦикла;
		
		Попытка
			ОбъектПТУ.Записать(РежимЗаписиДокумента.Проведение);		
			Сообщить("Обработан документ " + ОбъектПТУ.Ссылка + ", новая сумма документа: " + УчетНДС.ПолучитьСуммуДокументаСНДС(ОбъектПТУ, "Товары"));
			СтрДокументы.СуммаПоступленияПосле = УчетНДС.ПолучитьСуммуДокументаСНДС(ОбъектПТУ, "Товары");
		Исключение
			ОбщегоНазначения.СообщитьОбОшибке("Ошибка при записи документа: " + ОбъектРТУ.Ссылка + Символы.ПС + ОписаниеОшибки());
			ОтменитьТранзакцию();
			Продолжить;			
		КонецПопытки;
		
		ЗафиксироватьТранзакцию();
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	//Вставить содержимое обработчика    
	Объект.НеОбрабатыватьТоварМаркировкаЧЗ	= Истина;
КонецПроцедуры




