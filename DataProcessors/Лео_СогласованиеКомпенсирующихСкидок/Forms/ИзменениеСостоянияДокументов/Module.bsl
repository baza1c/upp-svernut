////////////////////////////////////////////////////////////////////////////////
// АНАЛИЗ ДОКУМЕНТОВ

&НаСервере
Процедура СформироватьОтчеты()

	РезультатАнализа.Очистить();
	
	Если Объект.ПроизвольныйОтчет.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	ПроизвольныйОтчетОбъект = Отчеты.ПроизвольныйОтчет.Создать();
	ПроизвольныйОтчетОбъект.УстановитьПроизвольныйОтчет(Объект.ПроизвольныйОтчет, Объект.СохраненнаяНастройка);
	
	// Установим параметр "СписокДокументов"
	ПараметрСписокДокументов = ПроизвольныйОтчетОбъект.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("СписокДокументов");
	Если ПараметрСписокДокументов <> Неопределено Тогда
		СписокДокументов = Новый СписокЗначений;
		Для каждого ЭлКоллекции Из Объект.ДокументыДляСогласования Цикл
			Если ЭлКоллекции.Пометка Тогда
				СписокДокументов.Добавить(ЭлКоллекции.ДокументДляСогласования);
			КонецЕсли; 
		КонецЦикла; 
		ПараметрСписокДокументов.Значение = СписокДокументов;
		ПараметрСписокДокументов.Использование = Истина;
	КонецЕсли; 
	
	// Установим параметр "НовоеСостояниеДокументов"
	ПараметрНовоеСостояниеДокументов = ПроизвольныйОтчетОбъект.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("НовоеСостояниеДокументов");
	Если ПараметрНовоеСостояниеДокументов <> Неопределено Тогда
		ПараметрНовоеСостояниеДокументов.Значение = НовоеСостояние;
		ПараметрНовоеСостояниеДокументов.Использование = Истина;
	КонецЕсли; 
	
	ПроизвольныйОтчетОбъект.СформироватьОтчет(РезультатАнализа,, Ложь);
	
КонецПроцедуры //

&НаСервере
Процедура УстановитьВидимостьОтчетов()

	Если Объект.ПроизвольныйОтчет.Пустая() Тогда
		Элементы.АнализДокументов.Видимость = Ложь;
	Иначе
		Элементы.АнализДокументов.Видимость = Истина;
		
		ЗаголовокОтчета = Объект.ПроизвольныйОтчет.Наименование;
		Если Объект.СохраненнаяНастройка.Пустая() Тогда
			ТекстСохраненнаяНастройка = "";
		Иначе
			ТекстСохраненнаяНастройка = " [" + Объект.СохраненнаяНастройка + "]";
		КонецЕсли;
		
		ЗаголовокОтчета = ЗаголовокОтчета + ТекстСохраненнаяНастройка;
		Элементы.АнализДокументов.Заголовок = ЗаголовокОтчета;
	КонецЕсли; 

КонецПроцедуры //

&НаКлиенте
Процедура РезультатАнализаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// Документы для согласования

&НаСервере
Функция ОбработатьСогласованиеДокументов()

	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	РезультатОбработки = ОбработкаОбъект.ОбработатьСогласованиеДокументов();
	
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
	Возврат РезультатОбработки;
	
КонецФункции //

&НаКлиенте
Процедура ДокументыДляСогласованияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометкиВСписке(НоваяПометка)

	ВыделенныеСтроки = Элементы.ДокументыДляСогласования.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() < 2 Тогда
		Для Каждого ДанныеСтроки ИЗ Объект.ДокументыДляСогласования Цикл
			ДанныеСтроки.Пометка = НоваяПометка;
		КонецЦикла;
	Иначе
		Для Каждого ИндексСтроки ИЗ ВыделенныеСтроки Цикл
			ДанныеСтроки = Объект.ДокументыДляСогласования.НайтиПоИдентификатору (ИндексСтроки);
			ДанныеСтроки.Пометка = НоваяПометка;
		КонецЦикла;
	КонецЕсли; 
	
КонецПроцедуры //
 

////////////////////////////////////////////////////////////////////////////////
// КОМАНДЫ

&НаКлиенте
Процедура ИзменитьСостояние(Команда)
	
	ВыбранныеДокументы = Объект.ДокументыДляСогласования.НайтиСтроки(Новый Структура("Пометка", Истина));
	Если ВыбранныеДокументы.Количество() = 0 Тогда
		Предупреждение(НСтр("ru = 'Не выбрано ни одного документа"));
		Возврат;
	КонецЕсли; 
	
	ТекстВопроса = НСтр("ru = 'Изменить состояние выбранных документов?'");
	Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		 Возврат;
	КонецЕсли;
	
	Если ОбработатьСогласованиеДокументов() Тогда
		СтатусДокументовБылИзменен = Истина;
	КонецЕсли;
	
	Если Объект.ДокументыДляСогласования.Количество() = 0 Тогда
		Закрыть();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда)
	
	СформироватьОтчеты();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометки(Команда)
	
	УстановитьПометкиВСписке(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьПометки(Команда)
	
	УстановитьПометкиВСписке(Ложь);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ФОРМА

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(Объект, Параметры.СвойстваОбъекта);
	
	// Заполним список документов
	Объект.ДокументыДляСогласования.Очистить();
	Для Каждого ДанныеСтроки ИЗ Параметры.ДокументыДляСогласования Цикл
		СтрокаТабличнойЧасти = Объект.ДокументыДляСогласования.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ДанныеСтроки);
		СтрокаТабличнойЧасти.Состояние = Параметры.НовоеСостояние;
	КонецЦикла;
	
	НовоеСостояние = Параметры.НовоеСостояние;
	
	// Установим заголовок кнопки для изменения документов
	Если НовоеСостояние = Перечисления.СостоянияОбъектов.Согласован Тогда
		ЗаголовокКнопки = НСтр("ru = 'Согласовать документы'");
	ИначеЕсли НовоеСостояние = Перечисления.СостоянияОбъектов.Отклонен Тогда
		ЗаголовокКнопки = НСтр("ru = 'Отклонить документы'");
	ИначеЕсли НовоеСостояние = Перечисления.СостоянияОбъектов.Отложен Тогда
		ЗаголовокКнопки = НСтр("ru = 'Отложить согласование'");
	КонецЕсли; 
	Элементы.ИзменитьСостояние.Заголовок = ЗаголовокКнопки;
	
	УстановитьВидимостьОтчетов();
	СформироватьОтчеты();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если СтатусДокументовБылИзменен Тогда
		Оповестить("СтатусДокументовБылИзменен");
	КонецЕсли; 
	
КонецПроцедуры


