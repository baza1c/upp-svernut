Процедура ЗаполнитьНачальныеЗначенияСвойств() Экспорт
	
	ПроцентСвежестиНачЗначение = 0;
	//АБС ИЗМЕНЕНИЕ   24.03.2014 10:46:28  Балашенко
	ПроцентСвежестиКонЗначение = 100;
	//АБС ИЗМЕНЕНИЕ  КОНЕЦ	
	
	ОстаточныйСрокГодностиНачЗначение = 0;
	ОстаточныйСрокГодностиКонЗначение = 0;
	
КонецПроцедуры

Функция ПолучитьЗначениеСвойства(Объект, Свойство) Экспорт
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Объект",Объект);
	СтруктураОтбора.Вставить("Свойство",Свойство);
	
	ЗначениеСвойства = РегистрыСведений.ЗначенияСвойствОбъектов.Получить(СтруктураОтбора).Значение;
	Если ЗначениеСвойства = Неопределено Тогда
		ЗначениеСвойства = ОбщегоНазначения.ПустоеЗначениеТипа(Тип(Свойство.ТипЗначения));
	КонецЕсли;
	
	Возврат ЗначениеСвойства;
	
КонецФункции

Процедура ОбновитьТаблицуТоваров() Экспорт
	
	Товары.Очистить();
	
	Для Каждого СтрокаТаб Из ДокументДвижения.Товары Цикл
		НоваяСтрока = Товары.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаб);
		
		//параметры срока годности
		Если ЗначениеЗаполнено(СтрокаТаб.СерияНоменклатуры) Тогда
			НоваяСтрока.ДатаОкончанияСрокаГодности = СтрокаТаб.СерияНоменклатуры.СрокГодности;
			
			ОстаточныйСрокГодности = НоваяСтрока.ДатаОкончанияСрокаГодности - ТекущаяДата();
			
			НоваяСтрока.ОстаточныйСрокГодности = Макс(Цел(ОстаточныйСрокГодности/86400),0);
			
			//ПолныйСрокГодностиТовара = ПолучитьЗначениеСвойства(НоваяСтрока.Номенклатура,ПланыВидовХарактеристик.СвойстваОбъектов.абс_ПолныйСрокГодностиТовара);
			РазницаДат = НоваяСтрока.ДатаОкончанияСрокаГодности - НоваяСтрока.СерияНоменклатуры.абс_ДатаПроизводства;
			НоваяСтрока.ПроцентСвежестиТовара = ?(РазницаДат = 0, 0 , 100 * ОстаточныйСрокГодности / РазницаДат);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновитьТаблицуСерий() Экспорт
	
	ОбновитьТаблицуТоваров();
	
	мРезервироватьПоСериям = Константы.ИспользоватьУказаниеСерийНоменклатурыПриРезервировании.Получить();
	ПустаяСерия = Справочники.СерииНоменклатуры.ПустаяСсылка();
	СтратегияАвторезервированияПоЗаказам = Перечисления.СтратегииАвторезервированияНоменклатуры.СначалаНаСкладахПотомВЗаказахПоставщикам;
	
	//очистить текущее размещение
	Для Каждого СтрокаТаб Из Товары Цикл
		Если Не ЗначениеЗаполнено(СтрокаТаб.СерияНоменклатуры) Тогда
			СтрокаТаб.Размещение = Неопределено;
			СтрокаТаб.СерияНоменклатуры = ПустаяСерия;
		КонецЕсли;
	КонецЦикла;
	
	//заполним размещение по складам
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ВидимостьСерии",     Истина);
	СтруктураПараметров.Вставить("Авторезервирование", Истина);
	СтруктураПараметров.Вставить("Авторазмещение",     Истина);
	СтруктураПараметров.Вставить("СтратегияАвторезервированияПоЗаказам", СтратегияАвторезервированияПоЗаказам);

	СтруктураПараметров.Вставить("Заказ"                ,ДокументДвижения);
	СтруктураПараметров.Вставить("РезервироватьПоСериям",Истина);
	СтруктураПараметров.Вставить("ОбособленныйУчет"     ,Истина);
	
	ЗаполнитьТоварыВозможнымРазмещением(СтруктураПараметров, Товары);

КонецПроцедуры


//Выполняет авторезервирование и авторазмещение в документах ЗаказПокупателя, ВнутреннийЗаказ, 
//	КорректировкаЗаказаПокупателя, КорректировкаВнутреннегоЗаказа, РезервированиеТоваров
Процедура ЗаполнитьТоварыВозможнымРазмещением(Параметры, Товары) Экспорт
	
	Авторезервирование = Параметры.Авторезервирование;
	Авторазмещение     = Параметры.Авторазмещение;
	
	Если НЕ Авторезервирование И НЕ Авторазмещение Тогда
		Возврат;
	КонецЕсли;
	
    Заказ = Параметры.Заказ;
	ЗаказСсылка = Заказ.Ссылка;
	ЕстьСкладЗаказчик = Ложь;
	ЕстьПредпочтительноеРазмещение = Ложь;
	ЕстьМассивСтатусовПартий = Ложь;
    МассивСтатусовПартий = Новый Массив;
    ЗаполнятьСуммы = ложь;
	СкладПоУмолчанию = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнойСклад");
    СтратегияАвторезервирования = Параметры.СтратегияАвторезервированияПоЗаказам;
	ОбособленныйУчет = Параметры.Свойство("ОбособленныйУчет") И Параметры.ОбособленныйУчет;
    Если НЕ ЗначениеЗаполнено(СтратегияАвторезервирования) Тогда
		СтратегияАвторезервирования = Перечисления.СтратегииАвторезервированияНоменклатуры.СначалаВЗаказахПоставщикамПотомНаСкладах;
	КонецЕсли;
	
	ЗапросСвободныйОстаток = Новый Запрос;

	Если ТипЗнч(ЗаказСсылка)=Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		СкладГруппа = Заказ.СкладГруппа;
        ЗапросСвободныйОстаток.УстановитьПараметр("ПредпочтительноеРазмещение", ?(ТипЗнч(СкладГруппа) = Тип("СправочникСсылка.Склады"), СкладГруппа, СкладПоУмолчанию));
        ЕстьПредпочтительноеРазмещение = Истина;
		ЗапросСвободныйОстаток.УстановитьПараметр("СкладЗаказчик", Неопределено);
		Если ЗначениеЗаполнено(СкладГруппа)
			И ТипЗнч(СкладГруппа) = Тип("СправочникСсылка.ГруппыДоступностиСкладов") Тогда
			ГруппаДоступностиСкладов = СкладГруппа;
		Иначе
			ГруппаДоступностиСкладов = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ГруппаДоступностиСкладов");
		КонецЕсли;
		ЗаполнятьСуммы = истина;
		ДатаОтгрузки = Заказ.ДатаОтгрузки;
	Иначе
		Возврат;
	КонецЕсли;
	ЗапросСвободныйОстаток.УстановитьПараметр("ДатаОтгрузки", ?(НЕ ЗначениеЗаполнено(ДатаОтгрузки), Заказ.Дата, ДатаОтгрузки));
	ЗапросСвободныйОстаток.УстановитьПараметр("ДокументСсылка", Заказ);
	ЗапросСвободныйОстаток.УстановитьПараметр("Организация",    Заказ.Организация);
    ЗапросСвободныйОстаток.УстановитьПараметр("СтатусПартии",  МассивСтатусовПартий);
    флРезервированиеТоваров = ложь;
	Если Параметры.Свойство("РезервированиеТоваров") Тогда
		ЗаполнятьСуммы = ложь;
		флРезервированиеТоваров = истина;

		ИмяРеквизитаРазмещение = "НовоеРазмещение";
		Если ТипЗнч(ЗаказСсылка) = Тип("ДокументСсылка.ВнутреннийЗаказ") Тогда
			ТекстЗапросаПоНоменклатуре = "
			|	ВЫБРАТЬ
			|		РегистрВнутренниеЗаказыОстатки.Номенклатура
			|	ИЗ 
			|		РегистрНакопления.ВнутренниеЗаказы.Остатки(, ВнутреннийЗаказ = &СсылкаЗаказ
			|		) КАК РегистрВнутренниеЗаказыОстатки";
        ИначеЕсли ТипЗнч(ЗаказСсылка)=Тип("ДокументСсылка.ЗаказПокупателя") Тогда
			ТекстЗапросаПоНоменклатуре = "
			|	ВЫБРАТЬ
			|		РегистрЗаказыПокупателейОстатки.Номенклатура
			|	ИЗ 
			|		РегистрНакопления.ЗаказыПокупателей.Остатки(, ЗаказПокупателя = &СсылкаЗаказ
			|		) КАК РегистрЗаказыПокупателейОстатки
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|	ВЫБРАТЬ
			|	КомплектующиеНоменклатуры.Комплектующая
			|	ИЗ 
			|		РегистрСведений.КомплектующиеНоменклатуры КАК КомплектующиеНоменклатуры
			|ГДЕ
			|	КомплектующиеНоменклатуры.Номенклатура В(
			|	ВЫБРАТЬ
			|		РегистрЗаказыПокупателейОстатки.Номенклатура
			|	ИЗ
			|		РегистрНакопления.ЗаказыПокупателей.Остатки(, ЗаказПокупателя = &СсылкаЗаказ
			|		) КАК РегистрЗаказыПокупателейОстатки)
			|";
		Иначе
			ТекстЗапросаПоНоменклатуре = "
			|	ВЫБРАТЬ
			|		РегистрПотребностиЗаказовНаПроизводствоОстатки.Номенклатура
			|	ИЗ 
			|		РегистрНакопления.ПотребностиЗаказовНаПроизводство.Остатки(, ЗаказНаПроизводство = &СсылкаЗаказ
			|		) КАК РегистрПотребностиЗаказовНаПроизводствоОстатки";

		КонецЕсли;
		ЗапросСписокНоменклатуры = новый Запрос;
		ЗапросСписокНоменклатуры.Текст = ТекстЗапросаПоНоменклатуре;
		ЗапросСписокНоменклатуры.УстановитьПараметр("СсылкаЗаказ",Заказ);
		Результат = ЗапросСписокНоменклатуры.Выполнить();
		Если Результат.Пустой() Тогда
			Возврат;
		КонецЕсли;
		Выборка = Результат.Выбрать();
		МассивНоменклатуры = новый Массив;
		Пока Выборка.Следующий() цикл
			МассивНоменклатуры.Добавить(Выборка.Номенклатура);
		КонецЦикла;
		
	Иначе
		ИмяРеквизитаРазмещение = "Размещение";
		КопияТовары = Товары.Выгрузить();
		КопияТовары.Свернуть("Номенклатура",);
		//КопияВозвратнаяТара = ВозвратнаяТара.Выгрузить();
		//КопияВозвратнаяТара.Свернуть("Номенклатура",);

		// Сформируем массив номенклатуры по товарам и таре для фильтров запросов.
		МассивНоменклатуры = КопияТовары.ВыгрузитьКолонку("Номенклатура");
		//МассивНоменклатурыТара = КопияВозвратнаяТара.ВыгрузитьКолонку("Номенклатура");
		//Для Каждого ЭлементТара Из МассивНоменклатурыТара Цикл
		//	МассивНоменклатуры.Добавить(ЭлементТара);
		//КонецЦикла; 
	КонецЕсли;
	
	ТекстФильтраПоСкладам = "
	|(
	|	ВЫБРАТЬ
	|		ГруппыДоступности.Склад
	|	ИЗ
	|		РегистрСведений.СоставГруппДоступностиСкладов КАК ГруппыДоступности
	|	ГДЕ ГруппыДоступности.ГруппаДоступности = &ГруппаДоступностиСкладов)";
	
	
	ЗапросСвободныйОстаток.УстановитьПараметр("МассивНоменклатуры",       МассивНоменклатуры);
	ЗапросСвободныйОстаток.УстановитьПараметр("ГруппаДоступностиСкладов", ГруппаДоступностиСкладов);
	ЗапросСвободныйОстаток.УстановитьПараметр( "Новый",       Справочники.Качество.Новый);

	Если Авторезервирование Тогда
		УсловиеСклад = "";
		Если ЗначениеЗаполнено(ГруппаДоступностиСкладов) Тогда
			УсловиеСклад = " Склад В" + ТекстФильтраПоСкладам;
			Если ЕстьПредпочтительноеРазмещение Тогда
				УсловиеСклад = УсловиеСклад+" ИЛИ Склад = &ПредпочтительноеРазмещение";
			КонецЕсли;
		КонецЕсли;
		Если ЕстьСкладЗаказчик Тогда
			Если УсловиеСклад = "" Тогда
				УсловиеСклад = "Склад <> &СкладЗаказчик";
			Иначе
				УсловиеСклад = "("+УсловиеСклад+") И Склад <> &СкладЗаказчик"
			КонецЕсли;
		КонецЕсли;
		
		ЗапросСвободныйОстаток.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТоварыНаСкладахОстатки.Номенклатура,
		|	ТоварыНаСкладахОстатки.ХарактеристикаНоменклатуры,
		|	ТоварыНаСкладахОстатки.Склад                      КАК Размещение,
		|	NULL                                              КАК ТоварТара,
		|	NULL                                              КАК ДоговорКонтрагента,
		|	NULL                                              КАК СтатусПартии,
		|	ТоварыНаСкладахОстатки.КоличествоОстаток          КАК Количество,
		|	ТоварыОрганизацийОстатки.КоличествоОстаток        КАК ОстатокПоОрганизации,
		|	ТоварыВРезервеНаСкладахОстатки.КоличествоОстаток  КАК КоличествоРазмещенное,
		|	ТоварыКПередачеСоСкладовОстатки.КоличествоОстаток КАК КоличествоКПередаче,
		|	1                                                 КАК СортировкаРазмещение,"
		+?(ЕстьПредпочтительноеРазмещение, "
		|	ВЫБОР
		|		КОГДА ТоварыНаСкладахОстатки.Склад = &ПредпочтительноеРазмещение ТОГДА
		|			0
		|		ИНАЧЕ
		|			1
		|	КОНЕЦ"
		,"0")+" 											  КАК СортировкаСклад,
		|	NULL                                              КАК ДатаПоступления
		|ИЗ
		|	РегистрНакопления.ТоварыНаСкладах.Остатки(, 
		|		Номенклатура В (&МассивНоменклатуры) И Качество = &Новый
		|" + ?(УсловиеСклад<>""," И ("+УсловиеСклад+")","") + ") КАК ТоварыНаСкладахОстатки
	
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|   РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки(, Номенклатура В (&МассивНоменклатуры)) КАК ТоварыВРезервеНаСкладахОстатки
		|ПО ТоварыНаСкладахОстатки.Номенклатура                 = ТоварыВРезервеНаСкладахОстатки.Номенклатура
		|   И ТоварыНаСкладахОстатки.ХарактеристикаНоменклатуры = ТоварыВРезервеНаСкладахОстатки.ХарактеристикаНоменклатуры
		|   И ТоварыНаСкладахОстатки.Склад                      = ТоварыВРезервеНаСкладахОстатки.Склад
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|   РегистрНакопления.ТоварыКПередачеСоСкладов.Остатки(, Номенклатура В (&МассивНоменклатуры) И Качество = &Новый ) КАК ТоварыКПередачеСоСкладовОстатки
		|ПО ТоварыНаСкладахОстатки.Номенклатура                 = ТоварыКПередачеСоСкладовОстатки.Номенклатура
		|   И ТоварыНаСкладахОстатки.ХарактеристикаНоменклатуры = ТоварыКПередачеСоСкладовОстатки.ХарактеристикаНоменклатуры
		|   И ТоварыНаСкладахОстатки.Склад                      = ТоварыКПередачеСоСкладовОстатки.Склад
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрНакопления.ТоварыОрганизаций.Остатки(, Организация = &Организация И Номенклатура В (&МассивНоменклатуры) И Качество = &Новый) КАК ТоварыОрганизацийОстатки
		|ПО   ТоварыНаСкладахОстатки.Номенклатура               = ТоварыОрганизацийОстатки.Номенклатура
		|   И ТоварыНаСкладахОстатки.ХарактеристикаНоменклатуры = ТоварыОрганизацийОстатки.ХарактеристикаНоменклатуры
		|";

	КонецЕсли; 
	Если Авторезервирование И Авторазмещение Тогда
		ЗапросСвободныйОстаток.Текст = ЗапросСвободныйОстаток.Текст +
		"
		|ОБЪЕДИНИТЬ ВСЕ
		|";
	КонецЕсли;
	Если Авторазмещение Тогда
		УсловиеСклад = "";
		Если ЗначениеЗаполнено(ГруппаДоступностиСкладов) Тогда
			УсловиеСклад = " ЗаказПоставщику.Склад В" + ТекстФильтраПоСкладам;
			Если ЕстьПредпочтительноеРазмещение Тогда
				УсловиеСклад = УсловиеСклад+" ИЛИ ЗаказПоставщику.Склад = &ПредпочтительноеРазмещение";
			КонецЕсли;
		КонецЕсли;
		Если ЕстьСкладЗаказчик Тогда
			Если УсловиеСклад = "" Тогда
				УсловиеСклад = "ЗаказПоставщику.Склад <> &СкладЗаказчик";
			Иначе
				УсловиеСклад = "("+УсловиеСклад+") И ЗаказПоставщику.Склад <> &СкладЗаказчик"
			КонецЕсли;
		КонецЕсли;

		ЗапросСвободныйОстаток.Текст = ЗапросСвободныйОстаток.Текст +
		"ВЫБРАТЬ " + ?(ЗапросСвободныйОстаток.Текст = "","РАЗРЕШЕННЫЕ","") + "
		|	ЗаказыПоставщикамОстатки.Номенклатура,
		|	ЗаказыПоставщикамОстатки.ХарактеристикаНоменклатуры,
		|	ЗаказыПоставщикамОстатки.ЗаказПоставщику                 КАК Размещение,
		|	ОстаткиРазмещенных.ТоварТара,
		|	ЗаказыПоставщикамОстатки.ДоговорКонтрагента,
		|	ЗаказыПоставщикамОстатки.СтатусПартии,
		|	ЗаказыПоставщикамОстатки.КоличествоОстаток               КАК Количество,
		|	ЗаказыПоставщикамОстатки.КоличествоОстаток               КАК ОстатокПоОрганизации,
		|	ОстаткиРазмещенных.КоличествоОстаток                     КАК КоличествоРазмещенное,
		|	0                                                        КАК КоличествоКПередаче,
		|	0                                                        КАК СортировкаРазмещение,"+
		?(ЕстьПредпочтительноеРазмещение,"
		|	ВЫБОР
		|		КОГДА ЗаказыПоставщикамОстатки.ЗаказПоставщику.Склад = &ПредпочтительноеРазмещение ТОГДА
		|			0
		|		ИНАЧЕ
		|			1
		|	КОНЕЦ","0")+"                                            КАК СортировкаСклад,
		|	ЗаказыПоставщикамОстатки.ЗаказПоставщику.ДатаПоступления КАК ДатаПоступления
		|ИЗ
		|	РегистрНакопления.ЗаказыПоставщикам.Остатки(, 
		|		ЗаказПоставщику.ДатаПоступления <= &ДатаОтгрузки
		|		И ЗаказПоставщику.Организация = &Организация
		|		И Номенклатура В (&МассивНоменклатуры)
		|" + ?(УсловиеСклад<>""," И ("+УсловиеСклад+")","") + "
		|" + ?(ЕстьМассивСтатусовПартий," И СтатусПартии В (&СтатусПартии)","")+"
		|       ) КАК ЗаказыПоставщикамОстатки
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|   РегистрНакопления.РазмещениеЗаказовПокупателей.Остатки(, Номенклатура В (&МассивНоменклатуры)) КАК ОстаткиРазмещенных
		|ПО ЗаказыПоставщикамОстатки.Номенклатура                 = ОстаткиРазмещенных.Номенклатура
		|   И ЗаказыПоставщикамОстатки.ХарактеристикаНоменклатуры = ОстаткиРазмещенных.ХарактеристикаНоменклатуры
		|   И ЗаказыПоставщикамОстатки.ЗаказПоставщику            = ОстаткиРазмещенных.ЗаказПоставщику
		|";
	КонецЕсли;
	Если Авторезервирование И Авторазмещение Тогда
		Если СтратегияАвторезервирования = Перечисления.СтратегииАвторезервированияНоменклатуры.СначалаВЗаказахПоставщикамПотомНаСкладах Тогда
			// Стратегия авторезервирования: Сначала в заказах поставщикам потом на складах
			ЗапросСвободныйОстаток.Текст = ЗапросСвободныйОстаток.Текст + "
			|УПОРЯДОЧИТЬ ПО                 // Определяет стратегию авторезервирования и авторазмещения
			|	СортировкаРазмещение,       // Сначала в заказах, потом на складах
			|	СортировкаСклад,            // Сначала предпочтительный склад, затем остальные
			|	ДатаПоступления УБЫВ,       // В порядке убывания дат отгрузки
			|	Размещение УБЫВ             // Заказы в обратном порядке";
		Иначе
			// Стратегия авторезервирования: Сначала на складах потом в заказах поставщикам
			ЗапросСвободныйОстаток.Текст = ЗапросСвободныйОстаток.Текст + "
			|УПОРЯДОЧИТЬ ПО                 // Определяет стратегию авторезервирования и авторазмещения
			|	СортировкаРазмещение УБЫВ,  // Сначала на складах, потом в заказах
			|	СортировкаСклад,            // Сначала предпочтительный склад, затем остальные
			|	ДатаПоступления,            // По порядку возрастания дат отгрузки
			|	Размещение                  // По порядку следования заказов";
		КонецЕсли;
	ИначеЕсли Авторезервирование Тогда
		ЗапросСвободныйОстаток.Текст = ЗапросСвободныйОстаток.Текст + "
		|УПОРЯДОЧИТЬ ПО                 // Определяет стратегию авторезервирования и авторазмещения
		|	СортировкаСклад             // Сначала предпочтительный склад, затем остальные ";
	ИначеЕсли Авторазмещение Тогда
		ЗапросСвободныйОстаток.Текст = ЗапросСвободныйОстаток.Текст + "
		|УПОРЯДОЧИТЬ ПО                 // Определяет стратегию авторезервирования и авторазмещения
		|	СортировкаСклад,            // Сначала предпочтительный склад поступления по заказу поставщику, затем остальные
		|	ДатаПоступления УБЫВ,       // В порядке убывания дат отгрузки
		|	Размещение УБЫВ             // В порядке убывания дат заказов";
	КонецЕсли;

	Таблица = ЗапросСвободныйОстаток.Выполнить().Выгрузить();
	
	// Получим права пользователя на превышение остатков по организации.
	ПраваНаПревышениеОстатковПоОрганизации = УправлениеДопПравамиПользователей.РазрешеноПревышениеОстаткаТоваровОрганизации(Заказ.Организация);
	
	
	// Вычислим по каждой строке количества, которые можно разместить
	Для Каждого Строка Из Таблица Цикл
		
		Количество            = ?(Строка.Количество = NULL, 0, Строка.Количество);
		КоличествоРазмещенное = ?(Строка.КоличествоРазмещенное = NULL, 0, Макс(Строка.КоличествоРазмещенное,0));
		КоличествоКПередаче   = ?(Строка.КоличествоКПередаче = NULL, 0, Макс(Строка.КоличествоКПередаче,0));
		ОстатокПоОрганизации  = ?(Строка.ОстатокПоОрганизации = NULL, 0, Макс(Строка.ОстатокПоОрганизации,0));
		
		//для тары остатков в регистре ТоварыОрганизаций быть не может
		//ЭтоТара = ВозвратнаяТара.Найти(Строка.Номенклатура,"Номенклатура")<>неопределено;
		ЭтоТара = Ложь;

		Если ПраваНаПревышениеОстатковПоОрганизации или ЭтоТара Тогда
			Строка.Количество = Количество - КоличествоРазмещенное - КоличествоКПередаче; 
		Иначе
			Строка.Количество = Мин(Количество - КоличествоРазмещенное - КоличествоКПередаче, ОстатокПоОрганизации); 
		КонецЕсли;	
						  
	КонецЦикла;
	
	Сч = 0;
	Пока Сч < Таблица.Количество() Цикл
		СтрокаТаблицы = Таблица.Получить(Сч);
		Если СтрокаТаблицы.Количество <= 0 Тогда
			Таблица.Удалить(СтрокаТаблицы);
			Продолжить; 
		Иначе 
			Сч = Сч + 1;
		КонецЕсли; 
	КонецЦикла;

	ПустаяХарактеристикаНоменклатуры = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();

	// Вычтем из найденных остатков количество, размещение по которому явно указано
	// в табличных частях документа. Для этого последовательно обойдем табличные части 
	// Товары и Возвратная тара.
	Для Сч = 0 По 1 Цикл

		Если Сч = 0 Тогда
			ТабличнаяЧасть = Товары;
			ЭтоТовары = Истина;
		//Иначе
		//	ТабличнаяЧасть = ВозвратнаяТара;
		//	ЭтоТовары = Ложь;
		КонецЕсли;

		Для Каждого СтрокаТЧ Из ТабличнаяЧасть Цикл
            // Если количество меньше и равно нулю, пропускаем строку.
			Если СтрокаТЧ.Количество <= 0 Тогда
				Продолжить;
			КонецЕсли; 

			// Сначала обходим все строки с размещением и если размещение не указано, пропускаем строку.
			Если НЕ ЗначениеЗаполнено(СтрокаТЧ[ИмяРеквизитаРазмещение]) Тогда
				Продолжить;
			КонецЕсли; 

			// Если не указано авторазмещение в заказах, то пропускаем все строки, в которых указан заказ.
			Если НЕ Авторазмещение И ТипЗнч(СтрокаТЧ[ИмяРеквизитаРазмещение]) <> Тип("СправочникСсылка.Склады") Тогда
				Продолжить;
			КонецЕсли; 

			// Если не указано авторезервирование на складах, то пропускаем все строки, в которых указан склад.
			Если НЕ Авторезервирование И ТипЗнч(СтрокаТЧ[ИмяРеквизитаРазмещение]) = Тип("СправочникСсылка.Склады") Тогда
				Продолжить;
			КонецЕсли; 

			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("Номенклатура", СтрокаТЧ.Номенклатура);
			СтруктураПоиска.Вставить("Размещение", СтрокаТЧ[ИмяРеквизитаРазмещение]);

			Если ЭтоТовары Тогда
				СтруктураПоиска.Вставить("ХарактеристикаНоменклатуры", СтрокаТЧ.ХарактеристикаНоменклатуры);
			Иначе
				СтруктураПоиска.Вставить("ХарактеристикаНоменклатуры", ПустаяХарактеристикаНоменклатуры);
			КонецЕсли; 

			НайденныеСтроки = Таблица.НайтиСтроки(СтруктураПоиска);

			// Погашаем количество в таблице. Пересчитаем количество в ТЧ в единицы хранения.
			Если ЭтоТовары Тогда
				// Пересчитаем количество в ТЧ в единицы хранения.
				КоличествоОсталосьПогасить = СтрокаТЧ.Количество * СтрокаТЧ.Коэффициент 
				                           / СтрокаТЧ.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент;
			Иначе
				КоличествоОсталосьПогасить = СтрокаТЧ.Количество;
			КонецЕсли; 

			Для Каждого Строка Из НайденныеСтроки Цикл

				Если КоличествоОсталосьПогасить <= 0 Тогда
					Прервать;
				КонецЕсли;

				Если Строка.Количество <= 0 Тогда
					Продолжить;
				КонецЕсли;
				
				// Если это остаток из заказа, то дополнительно проверяем на тару/товар
				Если ТипЗнч(Строка.Размещение)=Тип("ДокументСсылка.ЗаказПоставщику") Тогда
					Если ЭтоТовары И Строка.ТоварТара <> Перечисления.ТоварТара.Товар Тогда
						Продолжить;
					ИначеЕсли НЕ ЭтоТовары И Строка.ТоварТара <> Перечисления.ТоварТара.Тара Тогда
						Продолжить;
					КонецЕсли;
				КонецЕсли; 

				Если Строка.Количество >= КоличествоОсталосьПогасить Тогда
					КоэффСписания = КоличествоОсталосьПогасить/Строка.Количество;
				Иначе
					// Это ошибка: списываемое количество не должно быть больше остатка.
					// Такой документ не проведется оперативно.
					КоэффСписания = 1;
				КонецЕсли;

				СписанноеКоличество = Окр(Строка.Количество * КоэффСписания, 3, РежимОкругления.Окр15как20);

				КоличествоОсталосьПогасить = КоличествоОсталосьПогасить - СписанноеКоличество;

				Строка.Количество = Строка.Количество - СписанноеКоличество;

			КонецЦикла; 

			Если КоличествоОсталосьПогасить > 0 Тогда
				// Значит в документе неверно задано размещение. Заполнять не будем
				Возврат;
			КонецЕсли; 

		КонецЦикла;

	КонецЦикла;

	// Заполним массив, хранящий две таблицы значений, соответствующих авторазмещению товаров и тары
	МассивТаблицСтрок = Новый Массив(2);

	Для Сч = 0 По 1 Цикл

		Если Сч = 0 Тогда
			ТабличнаяЧасть = Товары;
			ЭтоТовары = Истина;
		//Иначе
		//	ТабличнаяЧасть = ВозвратнаяТара;
		//	ЭтоТовары = Ложь;
		КонецЕсли;

		ТаблицаТЧ = ТабличнаяЧасть.Выгрузить();
		ТаблицаТЧ.Колонки.Добавить("КоличествоЕдиницХранения");

		МассивТаблицСтрок[Сч] = ТабличнаяЧасть.Выгрузить();
		МассивТаблицСтрок[Сч].Очистить();
		МассивТаблицСтрок[Сч].Колонки.Добавить("ИндексИсходнойСтроки");

		Для Каждого СтрокаТЧ Из ТаблицаТЧ Цикл
            Если СтрокаТЧ.Количество <= 0 Тогда
				Продолжить;
			КонецЕсли; 

			Если ЗначениеЗаполнено(СтрокаТЧ[ИмяРеквизитаРазмещение]) Тогда
				Продолжить;
			КонецЕсли;
			Если флРезервированиеТоваров Тогда
	            Если ЗначениеЗаполнено(СтрокаТЧ.ИсходноеРазмещение) Тогда
					//нет смысла указывать одинаковыми исходное и новое размещение
					Если СтрокаТЧ.ИсходноеРазмещение = СтрокаТЧ.НовоеРазмещение Тогда
						Продолжить;
					КонецЕсли;
				КонецЕсли; 
			КонецЕсли;
			
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("Номенклатура", СтрокаТЧ.Номенклатура);

			Если ЭтоТовары Тогда
				// Пересчитаем в единицы хранения
				СтрокаТЧ.КоличествоЕдиницХранения = СтрокаТЧ.Количество * СтрокаТЧ.Коэффициент 
				                                  / СтрокаТЧ.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент;
				СтруктураПоиска.Вставить("ХарактеристикаНоменклатуры", СтрокаТЧ.ХарактеристикаНоменклатуры);
			//Иначе
			//	СтруктураПоиска.Вставить("ХарактеристикаНоменклатуры", ПустаяХарактеристикаНоменклатуры);
			//	СтрокаТЧ.КоличествоЕдиницХранения = СтрокаТЧ.Количество;
			КонецЕсли; 

			НайденныеСтроки = Таблица.НайтиСтроки(СтруктураПоиска);

			// Погашаем количество в таблице
			КоличествоОсталосьПогасить = СтрокаТЧ.КоличествоЕдиницХранения;
			Если ЗаполнятьСуммы Тогда
				СуммаОсталосьПогасить      = СтрокаТЧ.Сумма;
            КонецЕсли;
			// Погашаем количество в таблице, записывая размещение
			Для Каждого Строка Из НайденныеСтроки Цикл

				Если КоличествоОсталосьПогасить <= 0 Тогда
					Прервать;
				КонецЕсли;

				Если Строка.Количество <= 0 Тогда
					Продолжить;
				КонецЕсли;

				// Если это остаток по заказу поставщику, отберем товар и тару
				Если ТипЗнч(Строка.Размещение)=Тип("ДокументСсылка.ЗаказПоставщику") Тогда
					Если ЭтоТовары И Строка.ТоварТара = Перечисления.ТоварТара.Тара  Тогда
						Продолжить;
					ИначеЕсли ЭтоТовары И Строка.СтатусПартии = Перечисления.СтатусыПартийТоваров.ВозвратнаяТара Тогда
						Продолжить;
					ИначеЕсли НЕ ЭтоТовары И Строка.ТоварТара = Перечисления.ТоварТара.Товар Тогда
						Продолжить;
					ИначеЕсли НЕ ЭтоТовары И Строка.СтатусПартии = Перечисления.СтатусыПартийТоваров.Купленный Тогда
						Продолжить;
					КонецЕсли;
				ИначеЕсли не ЭтоТовары и ОбособленныйУчет И ТипЗнч(Строка.Размещение)=Тип("СправочникСсылка.Склады") Тогда
					//авторезервирование тары под заказ с обособленным учетом не выполняется
	                 Продолжить;
                КонецЕсли;
				Если Строка.Количество >= КоличествоОсталосьПогасить Тогда
					КоэффСписания = КоличествоОсталосьПогасить / Строка.Количество;
				Иначе
					КоэффСписания = 1;
				КонецЕсли;

				СписанноеКоличество = Окр(Строка.Количество * КоэффСписания, 3, РежимОкругления.Окр15как20);
				Если ЭтоТовары Тогда
					СписанноеКоличествоВДок = Окр( (Строка.Количество * СтрокаТЧ.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент / СтрокаТЧ.ЕдиницаИзмерения.Коэффициент)
						* КоэффСписания, 3, РежимОкругления.Окр15как20);
				Иначе
					СписанноеКоличествоВДок = Окр( Строка.Количество * КоэффСписания, 3, РежимОкругления.Окр15как20);
				КонецЕсли;

				// Добавляем строку с данными о размещенном количестве
				НоваяСтрока = МассивТаблицСтрок[Сч].Добавить();
				НоваяСтрока.ИндексИсходнойСтроки = ТаблицаТЧ.Индекс(СтрокаТЧ);

				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ);

				НоваяСтрока[ИмяРеквизитаРазмещение]  = Строка.Размещение;
				НоваяСтрока.Количество  = СписанноеКоличествоВДок;
				
				КоэффПогашения = СписанноеКоличество / КоличествоОсталосьПогасить;

				КоличествоОсталосьПогасить = КоличествоОсталосьПогасить - СписанноеКоличество;
				Если ЗаполнятьСуммы Тогда
					НоваяСтрока.Сумма                    = Окр(СуммаОсталосьПогасить * КоэффПогашения, 2 ,1);
					СуммаОсталосьПогасить = СуммаОсталосьПогасить - НоваяСтрока.Сумма;
                КонецЕсли;
				// Уменьшаем количество в исходной строке
				СтрокаТЧ.Количество = СтрокаТЧ.Количество - СписанноеКоличествоВДок;

				// Уменьшаем количество в строке остатков
				Строка.Количество   = Строка.Количество - СписанноеКоличество;

			КонецЦикла;
			Если КоличествоОсталосьПогасить > 0 Тогда
				// Добавляем строку с данными о размещенном количестве
				НоваяСтрока = МассивТаблицСтрок[Сч].Добавить();
				НоваяСтрока.ИндексИсходнойСтроки = ТаблицаТЧ.Индекс(СтрокаТЧ);
				
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ);

				Если ЭтоТовары Тогда
					НоваяСтрока.Количество = Окр( (КоличествоОсталосьПогасить * СтрокаТЧ.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент / СтрокаТЧ.ЕдиницаИзмерения.Коэффициент), 3, РежимОкругления.Окр15как20);
				Иначе
					НоваяСтрока.Количество = КоличествоОсталосьПогасить;
				КонецЕсли;
                Если ЗаполнятьСуммы Тогда
					НоваяСтрока.Сумма                    = Окр(СуммаОсталосьПогасить, 2, 1);
                КонецЕсли;
			КонецЕсли;

		КонецЦикла;
	КонецЦикла;

	// Изменяем табличную часть
	ТабличнаяЧасть = Товары;
	ЭтоТовары = Истина;
	
	ТекИндексИсходнойСтроки = Неопределено;
	Для Каждого Строка Из МассивТаблицСтрок[0] Цикл
		
		Если Строка.Количество <= 0 Тогда
			Продолжить;
		КонецЕсли;
		
		// Если строка с таким индексом уже обрабатывалась, то добавляем новую
		Если ТекИндексИсходнойСтроки = Строка.ИндексИсходнойСтроки Тогда
			Стр = ТабличнаяЧасть[Строка.ИндексИсходнойСтроки];
			РедактируемаяСтрока = ТабличнаяЧасть.Добавить();
			РедактируемаяСтрока.Номенклатура = Стр.Номенклатура;
			Если флРезервированиеТоваров Тогда
				РедактируемаяСтрока.ИсходноеРазмещение = Стр.ИсходноеРазмещение;
			КонецЕсли;
			
			Если ЭтоТовары Тогда
				РедактируемаяСтрока.ХарактеристикаНоменклатуры = Стр.ХарактеристикаНоменклатуры;
				Если ЗаполнятьСуммы Тогда
					РедактируемаяСтрока.СтавкаНДС = Стр.СтавкаНДС;
				КонецЕсли;	
			КонецЕсли;
		Иначе
			РедактируемаяСтрока = ТабличнаяЧасть[Строка.ИндексИсходнойСтроки];
		КонецЕсли; 
		
		РедактируемаяСтрока[ИмяРеквизитаРазмещение] = Строка[ИмяРеквизитаРазмещение];
		
		Если РедактируемаяСтрока.Количество = Строка.Количество Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(РедактируемаяСтрока, Строка);
		Если ЭтоТовары Тогда
			//ОбработкаТабличныхЧастей.РассчитатьКоличествоМестТабЧасти(РедактируемаяСтрока, Заказ);
		КонецЕсли;
		
		Если ЗаполнятьСуммы Тогда
			Если ЭтоТовары Тогда
				РассчитатьСуммуНДСТабЧасти(РедактируемаяСтрока);
			Иначе
				РедактируемаяСтрока.Цена = ?(РедактируемаяСтрока.Количество <> 0, РедактируемаяСтрока.Сумма/РедактируемаяСтрока.Количество, 0);
			КонецЕсли;
		КонецЕсли;
		
		ТекИндексИсходнойСтроки = Строка.ИндексИсходнойСтроки;
		
	КонецЦикла; 
	
	Если Параметры.РезервироватьПоСериям Тогда
		ЗаполнитьПоСериям(Заказ, Товары, ИмяРеквизитаРазмещение, Параметры.Свойство("РезервированиеТоваров"));
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПоСериям(ЗаказПокупателя, Товары, ИмяРеквизитаРазмещение = "Размещение", РезервированиеТоваров = ложь) 
	
	ИспользоватьСерии = глЗначениеПеременной("ИспользоватьСерииНоменклатуры");
	Если не ИспользоватьСерии Тогда
		Возврат;
	КонецЕсли;
	
	ТоварыТабличнойЧасти = Товары.Выгрузить();
	ТоварыТабличнойЧасти.Свернуть("Номенклатура");
	СкладыТабличнойЧасти = Товары.Выгрузить();
	Сч = 0;
	Пока Сч < СкладыТабличнойЧасти.Количество() Цикл
		СтрокаТаблицы = СкладыТабличнойЧасти.Получить(Сч);
		Если ТипЗнч(СтрокаТаблицы[ИмяРеквизитаРазмещение]) <> Тип("СправочникСсылка.Склады") Тогда
			СкладыТабличнойЧасти.Удалить(СтрокаТаблицы);
		Иначе
			Сч = Сч + 1;
		КонецЕсли;
	КонецЦикла;
	СкладыТабличнойЧасти.Колонки[ИмяРеквизитаРазмещение].Имя = "Склад";
	СкладыТабличнойЧасти.Свернуть("Склад");
	
	////АБС ВСТАВКА   21.07.2014 15:17:23  Балашенко
	//Если ЗначениеЗаполнено(ЗаказПокупателя.Ссылка) Тогда
	//	ДатаОстатков = ЗаказПокупателя.Дата;
	//Иначе
	//	ДатаОстатков = ТекущаяДата();
	//КонецЕсли;
	////АБС ВСТАВКА  КОНЕЦ

	МассивНоменклатуры   = ТоварыТабличнойЧасти.ВыгрузитьКолонку("Номенклатура");
	МассивСклады         = СкладыТабличнойЧасти.ВыгрузитьКолонку("Склад");
	ТаблицаСерий         = ПолучитьТаблицуСвободныхОстатковПоСериямСВыборомПараметраОтбора(МассивСклады, ЗаказПокупателя.Организация, МассивНоменклатуры, (ПоПроцентуСвежести=0) );
	ВременнаяТаблица     = Товары.Выгрузить();
	
	Товары.Очистить();
	
	Для Каждого СтрокаВременнаяТаблица Из ВременнаяТаблица Цикл
		
		РазмещениеСклад = истина;
		Если РезервированиеТоваров Тогда
			РазмещениеСклад = (ТипЗнч(СтрокаВременнаяТаблица.НовоеРазмещение) = Тип("СправочникСсылка.Склады") и НЕ ЗначениеЗаполнено(СтрокаВременнаяТаблица.ИсходноеРазмещение) и ЗначениеЗаполнено(СтрокаВременнаяТаблица.новоеРазмещение)) 
				или (ТипЗнч(СтрокаВременнаяТаблица.ИсходноеРазмещение) = Тип("СправочникСсылка.Склады") и НЕ ЗначениеЗаполнено(СтрокаВременнаяТаблица.НовоеРазмещение) и ЗначениеЗаполнено(СтрокаВременнаяТаблица.ИсходноеРазмещение));
		Иначе
			РазмещениеСклад = ТипЗнч(СтрокаВременнаяТаблица[ИмяРеквизитаРазмещение]) = Тип("СправочникСсылка.Склады")
		КонецЕсли;
		
		Если не РазмещениеСклад Тогда
			ЗаполнитьЗначенияСвойств(Товары.Добавить(), СтрокаВременнаяТаблица);
			Продолжить;
		КонецЕсли;	
		
		Если Не(СтрокаВременнаяТаблица.Номенклатура.ВестиУчетПоСериям) Тогда
			ЗаполнитьЗначенияСвойств(Товары.Добавить(), СтрокаВременнаяТаблица);
			Продолжить;
		КонецЕсли;	
		
		Если СтрокаВременнаяТаблица.Количество <= 0 Тогда
			ЗаполнитьЗначенияСвойств(Товары.Добавить(), СтрокаВременнаяТаблица);
			Продолжить;
		КонецЕсли;	

		СтруктураОтбора = Новый Структура();
		СтруктураОтбора.Вставить("Номенклатура"              , СтрокаВременнаяТаблица.Номенклатура);
		СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры", СтрокаВременнаяТаблица.ХарактеристикаНоменклатуры);
		СтруктураОтбора.Вставить("Склад"                     , СтрокаВременнаяТаблица[ИмяРеквизитаРазмещение]);
		
		НайденныеСтроки           = ТаблицаСерий.НайтиСтроки(СтруктураОтбора);
		
		КоличествоНераспределено  = СтрокаВременнаяТаблица.Количество; 
		
		
		Для Каждого СтрокаОстаток Из НайденныеСтроки Цикл
			Если КоличествоНераспределено <= 0 Тогда
				Продолжить;
			КонецЕсли;	
			
			Если СтрокаОстаток.Остаток <= 0 Тогда
				Продолжить;
			КонецЕсли;	

			
			ЕдиницаХраненияОстатков             = СтрокаВременнаяТаблица.Номенклатура.ЕдиницаХраненияОстатков;
			КоличествоОстатокВЕдиницахДокумента = СтрокаОстаток.Остаток * ЕдиницаХраненияОстатков.Коэффициент / СтрокаВременнаяТаблица.Коэффициент;
			КоличествоКСписанию                 = Мин(КоличествоНераспределено, КоличествоОстатокВЕдиницахДокумента);
			
			Если КоличествоКСписанию <= 0 Тогда
				Продолжить;
			КонецЕсли;	
			
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаВременнаяТаблица);
			НоваяСтрока.Количество = КоличествоКСписанию;
			НоваяСтрока.СерияНоменклатуры = СтрокаОстаток.СерияНоменклатуры;
			
			//параметры срока годности
			Если ЗначениеЗаполнено(НоваяСтрока.СерияНоменклатуры) Тогда
				НоваяСтрока.ДатаОкончанияСрокаГодности = СтрокаОстаток.СрокГодности;
				НоваяСтрока.ОстаточныйСрокГодности     = СтрокаОстаток.ОстаточныйСрокГодности;
				НоваяСтрока.ПроцентСвежестиТовара      = СтрокаОстаток.ПроцентСвежести;
			КонецЕсли;
			
			//ОбработкаТабличныхЧастей.РассчитатьКоличествоМестТабЧасти(НоваяСтрока, ЗаказПокупателя);
			Если не РезервированиеТоваров Тогда
				РассчитатьСуммуТабЧасти(НоваяСтрока);
				РассчитатьСуммуНДСТабЧасти(НоваяСтрока);
			КонецЕсли;
			КоличествоНераспределено = КоличествоНераспределено - КоличествоКСписанию;
			СтрокаОстаток.Остаток    = СтрокаОстаток.Остаток    - (КоличествоКСписанию * СтрокаВременнаяТаблица.Коэффициент /  ЕдиницаХраненияОстатков.Коэффициент); 
		КонецЦикла;	
		
		Если КоличествоНераспределено > 0 Тогда
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаВременнаяТаблица);
			НоваяСтрока.Количество = КоличествоНераспределено;
			//ОбработкаТабличныхЧастей.РассчитатьКоличествоМестТабЧасти(НоваяСтрока, ЗаказПокупателя);
			Если не РезервированиеТоваров Тогда
				РассчитатьСуммуТабЧасти(НоваяСтрока);
				РассчитатьСуммуНДСТабЧасти(НоваяСтрока);
			КонецЕсли;	
		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры //ЗаполнитьПоСериям()

// Рассчитывает сумму в строке табличной части документа
//
// Параметры:
//  СтрокаТабличнойЧасти - строка табличной части документа,
//  ДокументОбъект       - объект редактируемого документа,
//  ВидРасчета           - "Со всеми скидками", сумма минус скидки;
//                         "Без учета ручной скидки", сумма минус автоматические скидки;
//                         "Без учета скидок", сумма (Цена * Количество);
//
Процедура РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти)

	Сумма = СтрокаТабличнойЧасти.Цена * СтрокаТабличнойЧасти.Количество;
	СуммаСкидки = 0;

	СуммаСкидки = Сумма * СтрокаТабличнойЧасти.ПроцентАвтоматическихСкидок / 100;
	СуммаСкидки = СуммаСкидки + (Сумма * СтрокаТабличнойЧасти.ПроцентСкидкиНаценки / 100);
	
	СтрокаТабличнойЧасти.Сумма = Сумма - СуммаСкидки;

КонецПроцедуры // РассчитатьСуммуТабЧасти()

// Расчет, исходя из постоянной суммы
//
// Параметры:
//  СтрокаТабличнойЧасти - строка табличной части документа,
//  ДокументОбъект       - объект редактируемого документа.
//
Процедура РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти)

	// Если в документе нет флагов учета НДС, то в конфигурации считается, что суммы включают НДС.
	УчитыватьНДС     = ДокументДвижения.УчитыватьНДС;
	СуммаВключаетНДС = ДокументДвижения.СуммаВключаетНДС;

	СтрокаТабличнойЧасти.СуммаНДС = УчетНДС.РассчитатьСуммуНДС(СтрокаТабличнойЧасти.Сумма,
	                                                   УчитыватьНДС, СуммаВключаетНДС,
	                                                   УчетНДС.ПолучитьСтавкуНДС(СтрокаТабличнойЧасти.СтавкаНДС));

КонецПроцедуры // РассчитатьСуммуНДСТабЧасти()

// Возвращает таблицу остатков по сериям на заданом складе в заданной организации.
// Используется при заполении по кнопке "Заполнить и провести" в документах, списывающих товары.
//
// Параметры:
//  МассивСклады       - массив, склады, на котором получаются остатки.
//  Организация        - организация, по которой получаются остатки.
//  МассивНоменклатуры - массив, содержащий номенклатуру, по которой необходимо получить остатки.
//
Функция ПолучитьТаблицуСвободныхОстатковПоСериям(МассивСклады, Организация, МассивНоменклатуры) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТоварыНаСкладахОстатки.Номенклатура,
	|	ТоварыНаСкладахОстатки.СерияНоменклатуры,
	|	ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности КАК СрокГодности,
	
	|	РАЗНОСТЬДАТ(&ТекущаяДата, ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности, ДЕНЬ) КАК ОстаточныйСрокГодности,
	|	100* РАЗНОСТЬДАТ(&ТекущаяДата, ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности, ДЕНЬ) / РАЗНОСТЬДАТ(ТоварыНаСкладахОстатки.СерияНоменклатуры.абс_ДатаПроизводства, ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности, ДЕНЬ) КАК ПроцентСвежести,
	
	|	ТоварыНаСкладахОстатки.СерияНоменклатуры.Представление КАК СерияНоменклатурыПредставление,
	|	ТоварыНаСкладахОстатки.Склад,
	|	ТоварыНаСкладахОстатки.ХарактеристикаНоменклатуры,
	|	ТоварыНаСкладахОстатки.КоличествоОстаток - ЕСТЬNULL(ТоварыВРезервеНаСкладахОстатки.КоличествоОстаток, 0) - ЕСТЬNULL(ТоварыКПередачеСоСкладовОстатки.КоличествоОстаток, 0) КАК Остаток
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(
	|		,
	|		Номенклатура В (&Номенклатура)
	|			И Склад В (&Склад)
	|			И Качество = &НовоеКачество) КАК ТоварыНаСкладахОстатки
	|ЛЕВОЕ СОЕДИНЕНИЕ 
	|	РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки(,
	|		Номенклатура В (&Номенклатура)
	|		И Склад В (&Склад)) КАК ТоварыВРезервеНаСкладахОстатки
	|ПО 
	|	ТоварыНаСкладахОстатки.Номенклатура = ТоварыВРезервеНаСкладахОстатки.Номенклатура
	|	И ТоварыНаСкладахОстатки.СерияНоменклатуры = ТоварыВРезервеНаСкладахОстатки.СерияНоменклатуры
	|	И ТоварыНаСкладахОстатки.ХарактеристикаНоменклатуры = ТоварыВРезервеНаСкладахОстатки.ХарактеристикаНоменклатуры
	|	И ТоварыНаСкладахОстатки.Склад = ТоварыВРезервеНаСкладахОстатки.Склад
	|ЛЕВОЕ СОЕДИНЕНИЕ 
	|	РегистрНакопления.ТоварыКПередачеСоСкладов.Остатки(,
	|		Номенклатура В (&Номенклатура)
	|		И Склад В (&Склад)
	|		И Качество = &НовоеКачество) КАК ТоварыКПередачеСоСкладовОстатки
	|ПО 
	|	ТоварыНаСкладахОстатки.Номенклатура = ТоварыКПередачеСоСкладовОстатки.Номенклатура
	|	И ТоварыНаСкладахОстатки.СерияНоменклатуры = ТоварыКПередачеСоСкладовОстатки.СерияНоменклатуры
	|	И ТоварыНаСкладахОстатки.ХарактеристикаНоменклатуры = ТоварыКПередачеСоСкладовОстатки.ХарактеристикаНоменклатуры
	|	И ТоварыНаСкладахОстатки.Склад = ТоварыКПередачеСоСкладовОстатки.Склад
	
	|ГДЕ
	|	РАЗНОСТЬДАТ(&ТекущаяДата, ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности, ДЕНЬ) >= &ОстСтоимостьНачЗначение
	|	И РАЗНОСТЬДАТ(&ТекущаяДата, ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности, ДЕНЬ) <= &ОстСтоимостьКонЗначение
	|	И 100* РАЗНОСТЬДАТ(&ТекущаяДата, ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности, ДЕНЬ) / РАЗНОСТЬДАТ(ТоварыНаСкладахОстатки.СерияНоменклатуры.абс_ДатаПроизводства, ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности, ДЕНЬ) >= &ПроцентСвежестиНачЗначение
	|	И 100* РАЗНОСТЬДАТ(&ТекущаяДата, ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности, ДЕНЬ) / РАЗНОСТЬДАТ(ТоварыНаСкладахОстатки.СерияНоменклатуры.абс_ДатаПроизводства, ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности, ДЕНЬ) <= &ПроцентСвежестиКонЗначение
	
	|УПОРЯДОЧИТЬ ПО 
	|	ТоварыНаСкладахОстатки.Номенклатура,
	//|	СрокГодности,
	//сортируем по проценту свежести и по остаточному сроку годности
	|	100* РАЗНОСТЬДАТ(&ТекущаяДата, ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности, ДЕНЬ) / РАЗНОСТЬДАТ(ТоварыНаСкладахОстатки.СерияНоменклатуры.абс_ДатаПроизводства, ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности, ДЕНЬ),
	|	РАЗНОСТЬДАТ(&ТекущаяДата, ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности, ДЕНЬ),
	
	|	СерияНоменклатурыПредставление
	|";
	
	Запрос.УстановитьПараметр("ТекущаяДата",ТекущаяДата());
	Запрос.УстановитьПараметр("ОстСтоимостьНачЗначение",ОстаточныйСрокГодностиНачЗначение);
	Запрос.УстановитьПараметр("ОстСтоимостьКонЗначение",ОстаточныйСрокГодностиКонЗначение);
	Запрос.УстановитьПараметр("ПроцентСвежестиНачЗначение",ПроцентСвежестиНачЗначение);
	Запрос.УстановитьПараметр("ПроцентСвежестиКонЗначение",ПроцентСвежестиКонЗначение);
	
	Запрос.УстановитьПараметр("Склад"        , МассивСклады);
	Запрос.УстановитьПараметр("Номенклатура" , МассивНоменклатуры);
	Запрос.УстановитьПараметр("НовоеКачество", Справочники.Качество.Новый);
	
	Возврат Запрос.Выполнить().Выгрузить();	

КонецФункции // ПолучитьТаблицуСвободныхОстатковПоСериям()

Функция ПолучитьТаблицуСвободныхОстатковПоСериямСВыборомПараметраОтбора(МассивСклады, Организация, МассивНоменклатуры, ОтборПоПроцентуСвежести = Истина) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТоварыНаСкладахОстатки.Номенклатура,
	               |	ТоварыНаСкладахОстатки.СерияНоменклатуры,
	               |	ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности КАК СрокГодности,
	               |	РАЗНОСТЬДАТ(&ТекущаяДата, ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности, ДЕНЬ) КАК ОстаточныйСрокГодности,
	               |	ВЫБОР
	               |		КОГДА РАЗНОСТЬДАТ(ТоварыНаСкладахОстатки.СерияНоменклатуры.абс_ДатаПроизводства, ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности, ДЕНЬ) = 0
	               |			ТОГДА 0
	               |		ИНАЧЕ 100 * РАЗНОСТЬДАТ(&ТекущаяДата, ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности, ДЕНЬ) / РАЗНОСТЬДАТ(ТоварыНаСкладахОстатки.СерияНоменклатуры.абс_ДатаПроизводства, ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности, ДЕНЬ)
	               |	КОНЕЦ КАК ПроцентСвежести,
	               |	ТоварыНаСкладахОстатки.СерияНоменклатуры.Представление КАК СерияНоменклатурыПредставление,
	               |	ТоварыНаСкладахОстатки.Склад,
	               |	ТоварыНаСкладахОстатки.ХарактеристикаНоменклатуры,
	               |	ТоварыНаСкладахОстатки.КоличествоОстаток - ЕСТЬNULL(ТоварыВРезервеНаСкладахОстатки.КоличествоОстаток, 0) - ЕСТЬNULL(ТоварыКПередачеСоСкладовОстатки.КоличествоОстаток, 0) КАК Остаток
	               |ИЗ
	               |	РегистрНакопления.ТоварыНаСкладах.Остатки(
	               |			,
	               |			Номенклатура В (&Номенклатура)
	               |				И Склад В (&Склад)
	               |				И Качество = &НовоеКачество) КАК ТоварыНаСкладахОстатки
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки(
	               |				,
	               |				Номенклатура В (&Номенклатура)
	               |					И Склад В (&Склад)) КАК ТоварыВРезервеНаСкладахОстатки
	               |		ПО ТоварыНаСкладахОстатки.Номенклатура = ТоварыВРезервеНаСкладахОстатки.Номенклатура
	               |			И ТоварыНаСкладахОстатки.СерияНоменклатуры = ТоварыВРезервеНаСкладахОстатки.СерияНоменклатуры
	               |			И ТоварыНаСкладахОстатки.ХарактеристикаНоменклатуры = ТоварыВРезервеНаСкладахОстатки.ХарактеристикаНоменклатуры
	               |			И ТоварыНаСкладахОстатки.Склад = ТоварыВРезервеНаСкладахОстатки.Склад
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыКПередачеСоСкладов.Остатки(
	               |				,
	               |				Номенклатура В (&Номенклатура)
	               |					И Склад В (&Склад)
	               |					И Качество = &НовоеКачество) КАК ТоварыКПередачеСоСкладовОстатки
	               |		ПО ТоварыНаСкладахОстатки.Номенклатура = ТоварыКПередачеСоСкладовОстатки.Номенклатура
	               |			И ТоварыНаСкладахОстатки.СерияНоменклатуры = ТоварыКПередачеСоСкладовОстатки.СерияНоменклатуры
	               |			И ТоварыНаСкладахОстатки.ХарактеристикаНоменклатуры = ТоварыКПередачеСоСкладовОстатки.ХарактеристикаНоменклатуры
	               |			И ТоварыНаСкладахОстатки.Склад = ТоварыКПередачеСоСкладовОстатки.Склад
	               |ГДЕ
	               |	ВЫБОР
	               |			КОГДА &ОтборПоПроцентуСвежести
	               |				ТОГДА ВЫБОР
	               |							КОГДА РАЗНОСТЬДАТ(ТоварыНаСкладахОстатки.СерияНоменклатуры.абс_ДатаПроизводства, ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности, ДЕНЬ) = 0
	               |								ТОГДА ЛОЖЬ
	               |							ИНАЧЕ 100 * РАЗНОСТЬДАТ(&ТекущаяДата, ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности, ДЕНЬ) / РАЗНОСТЬДАТ(ТоварыНаСкладахОстатки.СерияНоменклатуры.абс_ДатаПроизводства, ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности, ДЕНЬ) >= &ПроцентСвежестиНачЗначение
	               |						КОНЕЦ
	               |						И ВЫБОР
	               |							КОГДА РАЗНОСТЬДАТ(ТоварыНаСкладахОстатки.СерияНоменклатуры.абс_ДатаПроизводства, ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности, ДЕНЬ) = 0
	               |								ТОГДА ЛОЖЬ
	               |							ИНАЧЕ 100 * РАЗНОСТЬДАТ(&ТекущаяДата, ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности, ДЕНЬ) / РАЗНОСТЬДАТ(ТоварыНаСкладахОстатки.СерияНоменклатуры.абс_ДатаПроизводства, ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности, ДЕНЬ) <= &ПроцентСвежестиКонЗначение
	               |						КОНЕЦ
	               |			ИНАЧЕ РАЗНОСТЬДАТ(&ТекущаяДата, ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности, ДЕНЬ) >= &ОстСтоимостьНачЗначение
	               |					И РАЗНОСТЬДАТ(&ТекущаяДата, ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности, ДЕНЬ) <= &ОстСтоимостьКонЗначение
	               |		КОНЕЦ
	               |	И ВЫБОР
	               |			КОГДА ТоварыНаСкладахОстатки.КоличествоОстаток - ЕСТЬNULL(ТоварыВРезервеНаСкладахОстатки.КоличествоОстаток, 0) - ЕСТЬNULL(ТоварыКПередачеСоСкладовОстатки.КоличествоОстаток, 0) = 0
	               |				ТОГДА ЛОЖЬ
	               |			ИНАЧЕ ИСТИНА
	               |		КОНЕЦ
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ТоварыНаСкладахОстатки.Номенклатура,
	               |	100 * РАЗНОСТЬДАТ(&ТекущаяДата, ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности, ДЕНЬ) / РАЗНОСТЬДАТ(ТоварыНаСкладахОстатки.СерияНоменклатуры.абс_ДатаПроизводства, ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности, ДЕНЬ),
	               |	РАЗНОСТЬДАТ(&ТекущаяДата, ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности, ДЕНЬ),
	               |	СерияНоменклатурыПредставление";
	
	Запрос.УстановитьПараметр("ТекущаяДата",ТекущаяДата());
	Запрос.УстановитьПараметр("ОстСтоимостьНачЗначение",ОстаточныйСрокГодностиНачЗначение);
	Запрос.УстановитьПараметр("ОстСтоимостьКонЗначение",ОстаточныйСрокГодностиКонЗначение);
	Запрос.УстановитьПараметр("ПроцентСвежестиНачЗначение",ПроцентСвежестиНачЗначение);
	Запрос.УстановитьПараметр("ПроцентСвежестиКонЗначение",ПроцентСвежестиКонЗначение);
	Запрос.УстановитьПараметр("ОтборПоПроцентуСвежести", ОтборПоПроцентуСвежести);
	
	Запрос.УстановитьПараметр("Склад"        , МассивСклады);
	Запрос.УстановитьПараметр("Номенклатура" , МассивНоменклатуры);
	Запрос.УстановитьПараметр("НовоеКачество", Справочники.Качество.Новый);
	
	Возврат Запрос.Выполнить().Выгрузить();	

КонецФункции // ПолучитьТаблицуСвободныхОстатковПоСериям()

Функция ПолучитьТаблицуСвободныхОстатковПоСериямБезОтбораПоПараметрам(МассивСклады, Организация, МассивНоменклатуры, ОтборПоПроцентуСвежести = Истина) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТоварыНаСкладахОстатки.Номенклатура,
	               |	ТоварыНаСкладахОстатки.СерияНоменклатуры,
	               |	ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности КАК ДатаОкончанияСрокаГодности,
	               |	РАЗНОСТЬДАТ(&ТекущаяДата, ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности, ДЕНЬ) КАК ОстаточныйСрокГодности,
	               |	ВЫБОР
	               |		КОГДА РАЗНОСТЬДАТ(ТоварыНаСкладахОстатки.СерияНоменклатуры.абс_ДатаПроизводства, ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности, ДЕНЬ) = 0
	               |			ТОГДА 0
	               |		ИНАЧЕ 100 * РАЗНОСТЬДАТ(&ТекущаяДата, ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности, ДЕНЬ) / РАЗНОСТЬДАТ(ТоварыНаСкладахОстатки.СерияНоменклатуры.абс_ДатаПроизводства, ТоварыНаСкладахОстатки.СерияНоменклатуры.СрокГодности, ДЕНЬ)
	               |	КОНЕЦ КАК ПроцентСвежестиТовара,
	               |	ТоварыНаСкладахОстатки.СерияНоменклатуры.Представление КАК СерияНоменклатурыПредставление,
	               |	ТоварыНаСкладахОстатки.Склад,
	               |	ТоварыНаСкладахОстатки.ХарактеристикаНоменклатуры,
	               |	ТоварыНаСкладахОстатки.КоличествоОстаток - ЕСТЬNULL(ТоварыВРезервеНаСкладахОстатки.КоличествоОстаток, 0) - ЕСТЬNULL(ТоварыКПередачеСоСкладовОстатки.КоличествоОстаток, 0) КАК Остаток,
	               |	ЕСТЬNULL(ТоварыОрганизацийОстатки.КоличествоОстаток, 0) КАК ОстатокПоОрганизации
	               |ИЗ
	               |	РегистрНакопления.ТоварыНаСкладах.Остатки(
	               |			,
	               |			Номенклатура В (&Номенклатура)
	               |				И Склад В (&Склад)
	               |				И Качество = &НовоеКачество) КАК ТоварыНаСкладахОстатки
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки(
	               |				,
	               |				Номенклатура В (&Номенклатура)
	               |					И Склад В (&Склад)) КАК ТоварыВРезервеНаСкладахОстатки
	               |		ПО ТоварыНаСкладахОстатки.Номенклатура = ТоварыВРезервеНаСкладахОстатки.Номенклатура
	               |			И ТоварыНаСкладахОстатки.СерияНоменклатуры = ТоварыВРезервеНаСкладахОстатки.СерияНоменклатуры
	               |			И ТоварыНаСкладахОстатки.ХарактеристикаНоменклатуры = ТоварыВРезервеНаСкладахОстатки.ХарактеристикаНоменклатуры
	               |			И ТоварыНаСкладахОстатки.Склад = ТоварыВРезервеНаСкладахОстатки.Склад
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыКПередачеСоСкладов.Остатки(
	               |				,
	               |				Номенклатура В (&Номенклатура)
	               |					И Склад В (&Склад)
	               |					И Качество = &НовоеКачество) КАК ТоварыКПередачеСоСкладовОстатки
	               |		ПО ТоварыНаСкладахОстатки.Номенклатура = ТоварыКПередачеСоСкладовОстатки.Номенклатура
	               |			И ТоварыНаСкладахОстатки.СерияНоменклатуры = ТоварыКПередачеСоСкладовОстатки.СерияНоменклатуры
	               |			И ТоварыНаСкладахОстатки.ХарактеристикаНоменклатуры = ТоварыКПередачеСоСкладовОстатки.ХарактеристикаНоменклатуры
	               |			И ТоварыНаСкладахОстатки.Склад = ТоварыКПередачеСоСкладовОстатки.Склад
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыОрганизаций.Остатки(
	               |				,
	               |				Номенклатура В (&Номенклатура)
	               |					И Склад В (&Склад)
	               |					И Качество = &НовоеКачество
	               |					И организация = &организация) КАК ТоварыОрганизацийОстатки
	               |		ПО ТоварыНаСкладахОстатки.Номенклатура = ТоварыОрганизацийОстатки.Номенклатура
	               |			И ТоварыНаСкладахОстатки.СерияНоменклатуры = ТоварыОрганизацийОстатки.СерияНоменклатуры
	               |			И ТоварыНаСкладахОстатки.ХарактеристикаНоменклатуры = ТоварыОрганизацийОстатки.ХарактеристикаНоменклатуры
	               |			И ТоварыНаСкладахОстатки.Склад = ТоварыОрганизацийОстатки.Склад
	               |ГДЕ
	               |	ВЫБОР
	               |			КОГДА ТоварыНаСкладахОстатки.КоличествоОстаток - ЕСТЬNULL(ТоварыВРезервеНаСкладахОстатки.КоличествоОстаток, 0) - ЕСТЬNULL(ТоварыКПередачеСоСкладовОстатки.КоличествоОстаток, 0) = 0
	               |				ТОГДА ЛОЖЬ
	               |			ИНАЧЕ ИСТИНА
	               |		КОНЕЦ
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ТоварыНаСкладахОстатки.Номенклатура,
	               |	ОстаточныйСрокГодности,
	               |	ПроцентСвежестиТовара,
	               |	СерияНоменклатурыПредставление";
	
	Запрос.УстановитьПараметр("ТекущаяДата",   ТекущаяДата());	
	Запрос.УстановитьПараметр("Склад"        , МассивСклады);
	Запрос.УстановитьПараметр("Номенклатура" , МассивНоменклатуры);
	Запрос.УстановитьПараметр("НовоеКачество", Справочники.Качество.Новый);
	 Запрос.УстановитьПараметр("организация" , организация);

	Возврат Запрос.Выполнить().Выгрузить();	

КонецФункции // ПолучитьТаблицуСвободныхОстатковПоСериям()


