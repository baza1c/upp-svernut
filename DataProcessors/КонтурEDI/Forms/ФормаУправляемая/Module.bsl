Перем РежимОтладки;//вывод диагностических сообщений
Перем РазбиватьПакетыЗапросов;//так проще отлаживать скорость выполнения пакетных запросов

Перем Сообщение_До;

&НаСервере
Перем МодульОбъекта;

&НаКлиенте
Перем МодульКлиентПМ;

&НаКлиенте
Перем ФормаМеркурий_НастройкаУправляемая;//МУФ

&НаКлиенте
Перем КМетрика;

&НаКлиенте
Перем МодульОбъектаКлиент;

//&НаКлиенте
//Перем РаботаССерверомДиадок;

&НаКлиенте
Перем МодульОбменКлиент Экспорт;  //API Диадока

//&НаКлиенте
//Перем МеркурийКлиентскийМодульEDI Экспорт;

// СЛУЖЕБНЫЕ {

&НаСервере
Функция ИмяФормыКраткое(Знач ИмяФормыПолное) Экспорт
	
	Возврат СтрЗаменить(ИмяФормыПолное, Объект.ПараметрыКлиентСервер.ПутьКФормам, "");
	
КонецФункции

&НаКлиенте
Процедура ИнициализироватьМетрикуКлиент()
	
	КМетрика = МодульОбъектаКлиент().ПолучитьФорму_КонтурEDI("КонтурМетрикаУФ", , , ЭтаФорма);
	КМетрика.ВычислитьРазницуЧасовыхПоясов();
	КМетрика.ИнициализироватьКонтекстПриложенияКлиент();

КонецПроцедуры

&НаКлиенте
Функция МодульМетрика() Экспорт
	
	Если ТипЗнч(КМетрика) <> Тип("УправляемаяФорма") Тогда
		ИнициализироватьМетрикуКлиент();
	КонецЕсли;
	
	Возврат КМетрика;
	
КонецФункции

&НаКлиенте
Функция НоваяМетрика() Экспорт
	
	Возврат МодульМетрика().НоваяМетрика();
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция Метрика_ОтметкаВремени()
	
	ОтметкаВремени = Неопределено;
	
	Попытка
		Выполнить("ОтметкаВремени = ТекущаяУниверсальнаяДатаВМиллисекундах()");
	Исключение
		ОтметкаВремени = ТекущаяДата();
	КонецПопытки;
	
	Возврат ОтметкаВремени;
	
КонецФункции

// Инициализация модуля и его экспортных функций
&НаСервере
Функция МодульОбъекта()
	
	Если МодульОбъекта = Неопределено Тогда
		
		КэшироватьОбработкуОбъект = Ложь; //при старте модуля ПараметрыКлиентСервер могут быть еще не инициализированы
		Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда 
			КэшироватьОбработкуОбъект = Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект = Истина;
		КонецЕсли;
		Если КэшироватьОбработкуОбъект Тогда
			МодульОбъекта = МодульОбъектаИзКэша();
		КонецЕсли;
		
		Если МодульОбъекта = Неопределено Тогда
			МодульОбъекта = РеквизитФормыВЗначение("Объект");
			МодульОбъекта.ИнициализироватьПодключаемыеМодули(); // TODO Проверить что из этого используется на формах и перенести в ПараметрыКлиентСервер
			МодульОбъекта.ОбновитьКэшМодуляОбъекта();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат МодульОбъекта;
	
КонецФункции

&НаСервере
Функция МодульОбъектаБезИнициализации()

	Возврат РеквизитФормыВЗначение("Объект");

КонецФункции // МодульОбъектаБезИнициализации()

&НаСервере
Процедура ПереинициализироватьКэшМодуляОбъектаСервер()
	
	МодульОбъекта().ФинализироватьПодключаемыеМодули();
	МодульОбъекта().ИнициализироватьПодключаемыеМодули();
	МодульОбъекта().ОбновитьКэшМодуляОбъекта();		
	
	// Используется в УстановитьЗаголовокФормы
	ПредставлениеОсобенностейМодуля = МодульОбъекта().ПредставлениеОсобенностейМодуля();
	УстановитьВидимостьЭлементовПриИзмененииНастроекПользователя();

КонецПроцедуры
	
&НаСервере
Функция МодульОбъектаИзКэша()
	
	Результат = Неопределено;
	
	Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда
		АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект;
 		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Структура = ПолучитьИзВременногоХранилища(АдресВХ);
			Если ТипЗнч(Структура) = Тип("Структура") и Структура.Свойство("ОбработкаОбъект") Тогда
			 	Результат = Структура.ОбработкаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульОбменКлиент() Экспорт
	
	Если ТипЗнч(МодульОбменКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбменКлиент;
	КонецЕсли;
	
		
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		
		Если Не ТипЗнч(ОсновнаяФорма.МодульОбменКлиент) = Тип("УправляемаяФорма") Тогда
			ИнициализироватьМодульОбменКлиент();
		КонецЕсли;
		
		Возврат ОсновнаяФорма.МодульОбменКлиент;
		
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

&НаКлиенте
Процедура ИнициализироватьМодульОбменКлиент()
	
	МодульОбменКлиент = МодульОбъектаКлиент().ПолучитьФорму_КонтурEDI("Сервис_ПодписаниеДДУправляемая", , , ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Функция МодульОбъектаКлиент() Экспорт
	
	Если Не ТипЗнч(МодульОбъектаКлиент) = Тип("УправляемаяФорма") Тогда
		ИнициализироватьМодульОбъектаКлиент();
	КонецЕсли;
	
	Возврат МодульОбъектаКлиент;
	          	
КонецФункции

&НаКлиенте
Процедура ИнициализироватьМодульОбъектаКлиент()
		
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
	ПараметрыФормы.Вставить("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	ПолныйПутьКФорме = Объект.ПараметрыКлиентСервер.ПутьКФормам + "Сервис_МодульОбъектаКлиент_Управляемая";
	
	МодульОбъектаКлиент = ПолучитьФорму(ПолныйПутьКФорме, ПараметрыФормы, ЭтаФорма);
	МодульОбъектаКлиент.Инициализировать();
		
КонецПроцедуры

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
    
	Возврат ЭтаФорма;
    	
КонецФункции

&НаКлиенте
Процедура ПоместитьВоВХ(Имя,Значение)
	
	ПоместитьВоВременноеХранилище(Значение, Объект.ПараметрыКлиентСервер.АдресаВХ[Имя]);
				
КонецПроцедуры

&НаКлиенте
Функция ИзвлечьИзВХ(Имя)
	
	АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ[Имя];
	
	Если Не ЭтоАдресВременногоХранилища(АдресВХ) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ПолучитьИзВременногоХранилища(АдресВХ);
			
КонецФункции

&НаКлиенте
Процедура ПриОткрытииФормы(Форма, ИмяКлиентскойФормы) Экспорт                                                   
	
	НоваяМетрика().Категория(ИмяКлиентскойФормы).ОткрытаФорма(Форма).ПоместитьВОчередь();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытииФормы(Форма, ИмяКлиентскойФормы) Экспорт
	
	НоваяМетрика().Категория(ИмяКлиентскойФормы).ЗакрытаФорма(Форма).ПоместитьВОчередь();
	
КонецПроцедуры

// } СЛУЖЕБНЫЕ

&НаКлиенте
Процедура МодальныйВопрос(ТекстВопроса, СписокКнопок, ДопПараметрДляПередачиВОбработчик, ИмяОбработчика)
	
	Если Параметры.МодальностьЗапрещена Тогда 
		Выполнить("ПоказатьВопрос(Новый ОписаниеОповещения("""+ИмяОбработчика+""", ЭтаФорма, ДопПараметрДляПередачиВОбработчик), ТекстВопроса, СписокКнопок,,,""Контур.EDI"")");
	Иначе
		РезультатВопроса = Вопрос(ТекстВопроса, СписокКнопок,,,"Контур.EDI");
		Выполнить(ИмяОбработчика+"(РезультатВопроса,ДопПараметрДляПередачиВОбработчик)");
	КонецЕсли;
	
КонецПроцедуры

//конец блока сервисных процедур

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Метрика_ВремяНачала = Метрика_ОтметкаВремени();
	
	Объект.ПараметрыКлиентСервер = МодульОбъектаБезИнициализации().ИнициализироватьПараметрыКлиентСервер(ЭтаФорма);
	
	КоличествоДнейВОсновномСпискеПриОткрытии = МодульОбъекта().ПолучитьКонстантуEDI("КоличествоДнейВОсновномСпискеПриОткрытии");
	Объект.ПараметрыКлиентСервер.КоличествоДнейВОсновномСпискеПриОткрытии	= ?(ТипЗнч(КоличествоДнейВОсновномСпискеПриОткрытии) = Тип("Число"), КоличествоДнейВОсновномСпискеПриОткрытии, 5);
	
	Объект.ПараметрыКлиентСерверМетрика = МодульОбъекта().Метрика_ИнициализироватьПараметрыКлиентСервер(ЭтаФорма);
	
	МодульОбъекта().ОбновитьКэшМодуляОбъекта();
	
	МодульОбъекта().Метрика_НачалоСеанса();
	МодульОбъекта().Метрика_КонтекстМодуля("session");
	МодульОбъекта().Метрика_НачатьЗамер("Замер_ЗапускМодуля", "ОсновнаяФорма", Метрика_ВремяНачала, ИмяФормыКраткое(ИмяФормы));
	
	//этим реквизитом будем запускать обмен в фоне     //хорошо бы еще точки доставки сетей проверять в фоне
	Если Параметры.Свойство("ДополнительнаяОбработкаСсылка") Тогда 
		ДополнительнаяОбработкаСсылка = Параметры.ДополнительнаяОбработкаСсылка;
	Иначе
		ДополнительнаяОбработкаСсылка = Справочники.ДополнительныеОтчетыИОбработки.НайтиПоНаименованию("Модуль Контур.EDI");
	КонецЕсли;
	ВерсияБСП = ПолучитьВерсиюБСПСервер();
	Параметры.МодальностьЗапрещена=МодульОбъекта().МодальностьЗапрещена();
	
	//Проверяем целостность наших метаданных
	ПроверкаМетаданныхПройдена = ПроверитьМетаданныеEDI();
	Если Не ПроверкаМетаданныхПройдена Тогда
		Возврат;
	КонецЕсли;
	
	АвтообменСервер();   //для автообмена не интересен список сообщений - просто выполняем обмен.
	Если Параметры.АвтообменВыполнен Тогда 
		Возврат; 
	КонецЕсли;
	
	ПредставлениеОсобенностейМодуля = МодульОбъекта().ПредставлениеОсобенностейМодуля();
	
	// дополнительно выведем описание изменений в текущей версии модуля
	Если МодульОбъекта().НужноПоказатьЧтоНовогоВРелизе() Тогда
		// При Открытии посмотрим на этот реквизит
		ПоказатьИсториюВерсий = Истина;   
	КонецЕсли;
	
	НеПоказыватьПомощник = (МодульОбъекта().ПолучитьКонстантуEDI("НеПоказыватьПомощникаПриЗапуске") = Истина);
	ЭтоПервыйЗапуск = (НеПоказыватьПомощник = Ложь);
	
	РезультатПроверкиЗапускНовойВерсииМодуля = МодульОбъекта().ПроверитьЗапускНовойВерсииМодуля();
	
	ЗаполнитьРеквизитыФормы();
	
	Параметры.РольПользователяEDI = МодульОбъекта().ПараметрыПользователяEDI.РольПользователяEDI;
	
	Если НЕ МодульОбъекта().ЕстьНеобходимыеМетаданные("РегистрыСведений.КонтурEDI_Статистика") Тогда 
		Элементы.Отчетность_ПересчитатьСтатичтикуSL.Доступность = Ложь;
	КонецЕсли;
	
	ОпределитьРежимРаботы();
	
	ЗаполнитьНастройкиОтборов();
	УстановитьВидимостьЭлементовПриИзмененииНастроекПользователя();
	
	ПроставитьСрокДействияСертификатов = (МодульОбъекта().ПолучитьКонстантуEDI("СрокДействияСертификатовПроставлен") <> Истина);
	
	ПредупредитьОбИстекающихСертификатах = ПроверитьНаличиеИстекающихСертификатовСервер(); 
	
	// предложим подключиться к другим торговым сетям
	ПредложитьПодключитьсяКДругимСетямСервер();
	
	ИнтерфейсныеПравкиДляПлатформы83();
	
	ПроверятьПомощников = Истина;
	НастроитьПомощниковEDI_Сервер();
			
	СтандартнаяОбработка = Истина;
	МодульОбъекта().ОбработкаСобытияПодключаемогоМодуля("ПриОткрытииОсновнойФормы",СтандартнаяОбработка, Новый Структура("Форма", ЭтаФорма));
       
	ИспользуетсяМеркурий = МодульОбъекта().ИспользуетсяМеркурий;
	
	Если ИспользуетсяМеркурий Тогда
		ДобавитьКнопкуНастройкиМеркурий();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьНаличиеИстекающихСертификатовСервер()
	
	Если ЗначениеЗаполнено(Параметры.ПараметрыАвтотестирования) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	МассивИстекающихСертификатов = ПодготовитьМассивИстекающихСертификатовСервер();
	
	Если МассивИстекающихСертификатов.Количество() > 0 Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции // ПредупредитьОбИстекающихСертификатахСервер()

&НаСервере
Функция ПодготовитьМассивИстекающихСертификатовСервер()

	Возврат МодульОбъекта().ПодготовитьМассивИстекающихСертификатов();

КонецФункции

&НаСервере
Функция ИнициализироватьКлиентскийМодульПМ()
	
	Возврат МодульОбъекта().ПодключитьВнешнийКлиентскийПодключаемыйМодуль();
	 
КонецФункции

&НаСервере
Процедура АвтообменСервер()
	Параметры.АвтообменВыполнен=Ложь;
	Если МодульОбъекта().ПараметрыПользователяEDI.ЭтоАвтообмен = Истина Тогда
		
		СтандартнаяОбработкаEDI = Истина;
		МодульОбъекта().ОбработкаСобытияПодключаемогоМодуля("ВыполнитьАвтообмен",СтандартнаяОбработкаEDI);
		
		Если СтандартнаяОбработкаEDI Тогда
			МодульОбъекта().ВыполнитьОбменССервером(); //без параметров обмена т.к. это автообмен
			ПроверитьНеобходимостьПодписанияДокументовВДиадок();
			ПроверитьНеобходимостьОтправкиИзвещенийОПолучении();
		КонецЕсли;
		
		Параметры.АвтообменВыполнен=Истина;
	КонецЕсли;
	
КонецПроцедуры // АвтообменСервер()

&НаСервере
Процедура ЗаполнитьРеквизитыФормы()

	ОбновлятьВесьСписокПриРаботеСЗаказами = МодульОбъекта().ПолучитьКонстантуEDI("ОбновлятьВесьСписокПриРаботеСЗаказами");
	НужноПроверитьИзмененностьМодуляEDI = МодульОбъекта().НужноПроверитьИзмененностьМодуляEDI();
	ОткрыватьВебИнтерфейсДляПодписанияЕдиничнойПачки = МодульОбъекта().ПолучитьКонстантуEDI("ОткрыватьВебИнтерфейсДляПодписанияЕдиничнойПачки")=Истина;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементовПриИзмененииНастроекПользователя()  //рефакторинг
	
	Если МодульОбъекта().ПараметрыПользователяEDI.ВариантОбмена <> "Ручной" Тогда
		Элементы.ВыполнитьОбмен.Доступность = Ложь;
	КонецЕсли;
	
	Если МодульОбъекта().ПараметрыПользователяEDI.РольПользователяEDI <> "ПолныеПрава" Тогда
		
		// запретим открытие настроек и т.д.
		Элементы.ОткрытьСтруктуруКомпании.Доступность		= Ложь;
		Элементы.ОткрытьСписокПартнеров.Доступность			= Ложь;
		Элементы.ОткрытьНастройки.Доступность				= Ложь;
		Элементы.СтартовыйПомощник.Доступность				= Ложь;
		Элементы.ОтправитьПроизвольноеСообщение.Доступность	= Ложь;
		
		Элементы.ОткрытьСписокУчетныхЗаписей.Доступность	= Ложь;
		Элементы.ОткрытьСписокПользователей.Доступность		= Ложь;
		Элементы.ОткрытьСписокНашихОрганизаций.Доступность	= Ложь;
		
		//Элементы.ДеревоФильтровИзменить.Видимость 			= Ложь;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьНастройкиФормыКлиент()
	
	СделатьАктивнойНастройкуСохраненногоФильтра(ТекущаяНастройкаФильтраСохранение);
	УстановитьЗаголовокФормы_КонтурEDI();
	
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьСохраненноеЗначениеПоказыватьЗавершенные()
	
	ПоказыватьЗавершенные = ПоказыватьЗавершенныеСохранение;
	
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьПометкиИПодсчитанныеКоличества()
	
	Если ТаблицаДеревоФильтровСохранение.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПометкиИПодсчитанныеКоличестваРекурсивно(ДеревоФильтров, ТаблицаДеревоФильтровСохранение);
	
КонецПроцедуры 

&НаКлиенте
Процедура ЗаполнитьПометкиИПодсчитанныеКоличестваРекурсивно(ТекДеревоФильтров, СохраненноеДеревоФильтров)
	
	Для каждого Строка Из ТекДеревоФильтров.ПолучитьЭлементы() Цикл
		НайденныеСтроки = СохраненноеДеревоФильтров.НайтиСтроки(Новый Структура("Значение,ГруппаНастроек", Строка.Значение, Строка.ГруппаНастроек));
		Если НайденныеСтроки.Количество() = 1 Тогда
			ЗаполнитьЗначенияСвойств(Строка, НайденныеСтроки[0], "Пометка,Количество");
		Иначе
			ЗаполнитьЗначенияСвойств(Строка, Новый Структура("Пометка,Количество", Ложь, ""), "Пометка,Количество");
		КонецЕсли;
	    ЗаполнитьПометкиИПодсчитанныеКоличестваРекурсивно(Строка, СохраненноеДеревоФильтров);
	КонецЦикла;	

КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройкиГлавнойФормы()
	
	ПоказыватьЗавершенныеСохранение = ПоказыватьЗавершенные;
	ТаблицаДеревоФильтровСохранение.Очистить();
    СохранитьТекущееДеревоФильтров(ДеревоФильтров);
	
	ТекНастройка = Элементы.СохраненныеФильтры.ТекущиеДанные;
	Если НЕ ТекНастройка = Неопределено Тогда
		ТекущаяНастройкаФильтраСохранение = ТекНастройка.Настройка;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СохранитьТекущееДеревоФильтров(ТекДеревоФильтров)
	
	Если ТекДеревоФильтров = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого Строка Из ТекДеревоФильтров.ПолучитьЭлементы() Цикл
		НоваяСтрока = ТаблицаДеревоФильтровСохранение.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		СохранитьТекущееДеревоФильтров(Строка);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ИнтерфейсныеПравкиДляПлатформы83()
	
	ВерсияПлатформы = МодульОбъекта().ВерсияПлатформы1С_КонтурEDI();
	
	Пользователь = ПользователиИнформационнойБазы.ТекущийПользователь();
	Настройка = ХранилищеСистемныхНастроек.Загрузить("Общее/НастройкиКлиентскогоПриложения", "",, Пользователь.Имя);
	Если Не ТипЗнч(Настройка) = Тип("НастройкиКлиентскогоПриложения") Тогда
		Настройка = Новый НастройкиКлиентскогоПриложения;
	КонецЕсли;
	
	СейчасВыбранТакси = Ложь;  
	
	// >= 8.3.4
	Если ((ВерсияПлатформы.Младшая = 3 И ВерсияПлатформы.Релиз >= 4) 
		ИЛИ ВерсияПлатформы.Младшая > 3) Тогда
		
		//придется завернуть без компиляции т.к. 8.2 не знает про эти штуки
		Выполнить("СейчасВыбранТакси = Настройка.ВариантИнтерфейсаКлиентскогоПриложения = ВариантИнтерфейсаКлиентскогоПриложения.Такси");
		
		Если СейчасВыбранТакси Тогда
			
			ТекущееСостояниеИнтерфейса = Неопределено;
			Выполнить("ТекущееСостояниеИнтерфейса = Настройка.ВариантМасштабаФормКлиентскогоПриложения");
			СейчасВыбранКомпактный = Ложь;
			Выполнить("СейчасВыбранКомпактный = ТекущееСостояниеИнтерфейса = ВариантМасштабаФормКлиентскогоПриложения.Компактный");
			
			Если НЕ СейчасВыбранКомпактный Тогда
				
				//Эти правки нужны только для Такси с вариантом масштаба авто или обычный!
				//слегка изменяем размеры и шрифты для того чтобы было аккуратнее
				Элементы.НадписьПодробно.ЦветТекста = Новый Цвет(51, 153, 102);
				Элементы.СвернутьНастройки.ЦветТекста = Новый Цвет(51, 153, 102); 
				Элементы.НадписьПодробно.Шрифт = Новый Шрифт(,12);
				Элементы.СвернутьНастройки.Шрифт = Новый Шрифт(,12);
				
			КонецЕсли;
		Иначе //переключение варианта актуально только для Такси
			Элементы.ПереключитьКомпактность.Доступность = Ложь;
			Элементы.ПереключитьКомпактность.Заголовок = Элементы.ПереключитьКомпактность.Заголовок+" доступно в режиме Такси";
		КонецЕсли;
	Иначе //переключение варианта актуально только 8.3+
		Элементы.ПереключитьКомпактность.Доступность = Ложь;
		Элементы.ПереключитьКомпактность.Заголовок = Элементы.ПереключитьКомпактность.Заголовок+" доступно на платформе 8.3.4 и старше";
	КонецЕсли;
	
	Если СейчасВыбранТакси Тогда
		
		// Включен режим совместимости или < 8.3.7
		Если Найти(Строка(Метаданные.РежимСовместимости), "Версия8_2_") > 0
			ИЛИ (Найти(Строка(Метаданные.РежимСовместимости), "Версия8_3_") > 0
			И МодульОбъекта().ТолькоЦифрыВСтроке_КонтурEDI(СтрЗаменить(Строка(Метаданные.РежимСовместимости), "Версия8_3_", ""))
			И Число(СтрЗаменить(Строка(Метаданные.РежимСовместимости), "Версия8_3_", "")) < 7)
			ИЛИ (ВерсияПлатформы.Младшая = 3 И ВерсияПлатформы.Релиз < 7) Тогда
			
			Элементы.ВыполнитьОбмен.Ширина = 54;
			Элементы.ВыполнитьВыбранныеЗадачи.Ширина = 16;
			Элементы.ВыбратьПериод.Ширина = 48;
			Элементы.ОбновитьСписок.Ширина = 16;
			Элементы.КнопкаЕще.Ширина = 16;
			
			//Элементы.ГруппаТПЛевая.Ширина = 34;
			//Элементы.ГруппаТППравая.Ширина = 34;
			//Элементы.ГруппаНастройки.Ширина = 46;
			
		// >= 8.3.7
		ИначеЕсли (ВерсияПлатформы.Младшая = 3 И ВерсияПлатформы.Релиз >= 7)
			ИЛИ ВерсияПлатформы.Младшая > 3 Тогда
			
			Элементы.ВыполнитьОбмен.Ширина = 0;
			
			Элементы.ВыполнитьОбмен.Высота = 3;
			Элементы.ВыполнитьВыбранныеЗадачи.Высота = 3;
			Элементы.ВыбратьПериод.Высота = 3;
			Элементы.ОбновитьСписок.Высота = 3;
			Элементы.КнопкаЕще.Высота = 3;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// <Описание процедуры>
//
// Параметры
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаСервере
Процедура ПредложитьПодключитьсяКДругимСетямСервер()
	
 	Если ЗначениеЗаполнено(Параметры.ПараметрыАвтотестирования) Тогда
		Параметры.ПредлагатьПодключиться = Ложь;
	Иначе
		Параметры.ПредлагатьПодключиться = Истина;
		
		ДатаПредложенияПодключенияТС = ХранилищеСистемныхНастроек.Загрузить("КонтурEDI_ДатаПредложенияПодключенияТС");	
		
		Если ТипЗнч(ДатаПредложенияПодключенияТС) = Тип("Структура") 
			и ДатаПредложенияПодключенияТС.Свойство("КонтурEDI_ДатаПредложенияПодключенияТС")
			и ТипЗнч(ДатаПредложенияПодключенияТС.КонтурEDI_ДатаПредложенияПодключенияТС) = Тип("Дата") Тогда
			Если ТекущаяДата() < ДатаПредложенияПодключенияТС.КонтурEDI_ДатаПредложенияПодключенияТС Тогда
				Параметры.ПредлагатьПодключиться = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // ПредложитьПодключитьсяКДругимСетям()

&НаСервере
Функция ЭтоЗапускОбъединенногоМодуляНаСтаромУФ()
	//наличие в конфигурации справочника КонтурEDI_Сообщения с отсутствующим реквизитом "Архив" и "ТребуемоеДействие
	Если  
		Метаданные.ОсновнойРежимЗапуска = РежимЗапускаКлиентскогоПриложения.УправляемоеПриложение
		И Метаданные.Справочники.Найти("КонтурEDI_Сообщения")<>Неопределено
		И (Метаданные.Справочники.КонтурEDI_Сообщения.Реквизиты.Найти("Архив")=неопределено
		ИЛИ Метаданные.Справочники.КонтурEDI_Сообщения.Реквизиты.Найти("ТребуемоеДействие")=неопределено
		ИЛИ Метаданные.Справочники.КонтурEDI_Сообщения.Реквизиты.Найти("ФайлXML")=неопределено)
		Тогда
		Возврат Истина;
	иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

//проверка самого первого бездумного запуска - нет объектов
&НаСервере
Функция ПроверитьМетаданныеEDI()//вернем ПроверкаПройдена
	
	ПроверкаМетаданныхПройдена=истина;
	
	Если ЭтоЗапускОбъединенногоМодуляНаСтаромУФ() Тогда
		Если  МодульОбъекта().ПолучитьКонстантуEDI("МыСеть")=истина Тогда
			СписокОшибок.Добавить("Внимание! Новая редакция модуля на текущем этапе не обладает функционалом торговой сети! Переход невозможен!");
			ПроверкаМетаданныхПройдена=Ложь;
			Возврат ПроверкаМетаданныхПройдена;

		иначе
			СписокОшибок.Добавить("Вы собираетесь запустить новую редакцию модуля КонтурEDI."+Символы.ПС+"Переход на новую редакцию возможен только после создания резервной копии базы данных"+Символы.ПС+
			"Рекомендуется сначала выполнить пробный переход на копии базы!!!");
			ПроверкаМетаданныхПройдена=Ложь;
			Параметры.ПереходСоСтаройРедакции = Истина;
		КонецЕсли;
	КонецЕсли;
	
	РезультатПроверкиМетаданных = МодульОбъекта().ПроверитьКорректностьИДоступностьМетаданных();//проверка конфигурации, необходимых реквизитов наших справочников, прав доступа
	
	Если НЕ РезультатПроверкиМетаданных.Успешно Тогда
		
		СписокОшибок.Добавить(РезультатПроверкиМетаданных.ТекстОшибки);
		Возврат Ложь;
		
	КонецЕсли;
		
	ИмяКонфигурации1С = МодульОбъекта().ОпределитьКонфигурацию();
	
	Если Не ЗначениеЗаполнено(ИмяКонфигурации1С) Тогда
		
		СписокОшибок.Добавить(
		"Модуль Контур EDI (УФ) поддерживает конфигурации:
		|- Управление торговлей, редакция 11
		|- Управление торговлей и взаимоотношениями с клиентами (CRM), редакция 2
		|- Комплексная автоматизация, редакция 2
		|- Управление предприятием, редакция 2
		|- Агропромышленный комплекс, редакция 2
		|- Бухгалтерия предприятия, редакция 3
		|- Бухгалтерия предприятия КОРП, редакция 3
		|- Бухгалтерия Универсал, редакция 3
		|- Бухгалтерия сельскохозяйственного предприятия, редакция 3
		|- Управление корпоративными финансами, редакция 3
		|- Трактиръ: Back-Office, редакция 3
		|- Управление небольшой (нашей) фирмой, редакция 1.4, 1.5, 1.6
		|- Розница, редакция 2
		|
		|
		|Для получения информации о поддержке Вашей конфигурации: """+Метаданные.Синоним+""" обратитесь к представителю Ритейл.Контур"
		);
		
		Возврат Ложь;

	КонецЕсли;
	
	Если Метаданные.Справочники.КонтурEDI_Сообщения.Реквизиты.Найти("ТребуемоеДействие")=Неопределено Тогда
		СписокОшибок.Добавить("Для работы этой версии модуля Контур.EDI необходимо обновить конфигурацию.");
		ПроверкаМетаданныхПройдена=Ложь;
	КонецЕсли;
	
	//Проверим типы данных, необходимые для реквизита "Документ" в справочнике "Сообщения". 		
	//Определим, какие типы документов у нас вообще используются
	МодульОбъекта().ОбновитьТаблицуТиповЗначенияОбъектов();//возможно, избыточно

	ТекстПредупреждения = МодульОбъекта().ПроверитьРеквизитДокументСправочникаСообщений();
	
	Если ЗначениеЗаполнено(ТекстПредупреждения) Тогда
		СписокОшибок.Добавить(ТекстПредупреждения);

		ПроверкаМетаданныхПройдена=ложь;
		Возврат ПроверкаМетаданныхПройдена;

	КонецЕсли;	

	Возврат ПроверкаМетаданныхПройдена;

КонецФункции // ()

Функция ПроверитьЗапускНовойВерсииМодуляМодульОбработки()       
	
	РезультатПроверки = МодульОбъекта().ПроверитьЗапускНовойВерсииМодуля(ЭтоПервыйЗапуск);
	
	Возврат РезультатПроверки;
	
КонецФункции

//Область Дерево отборов
&НаСервере
Функция ПолучитьРодителяГруппыНастроек(ИмяГруппыНастроек,_Дерево)
	
	Возврат _Дерево.Строки.Найти(ИмяГруппыНастроек,"ГруппаНастроек");
	
КонецФункции

&НаСервере
Функция ПолучитьРодителяГруппыНастроекИзДанныхФормы(ИмяГруппыНастроек)
	ЭлементРодитель=Неопределено;
	
	 КоллекцияРодителей=ДеревоФильтров.ПолучитьЭлементы();
	 Для Каждого ЭлементКоллекции Из КоллекцияРодителей Цикл
		 Если ЭлементКоллекции.ГруппаНастроек = ИмяГруппыНастроек Тогда 
			 ЭлементРодитель= ЭлементКоллекции;
			 Прервать;
		 КонецЕсли;
	 КонецЦикла;
	 
	Возврат ЭлементРодитель;
КонецФункции

&НаКлиенте
Функция ПолучитьРодителяГруппыНастроекИзДанныхФормыКлиент(ИмяГруппыНастроек)
	ЭлементРодитель=Неопределено;
	
	 КоллекцияРодителей=ДеревоФильтров.ПолучитьЭлементы();
	 Для Каждого ЭлементКоллекции Из КоллекцияРодителей Цикл
		 Если ЭлементКоллекции.ГруппаНастроек = ИмяГруппыНастроек Тогда 
			 ЭлементРодитель= ЭлементКоллекции;
			 Прервать;
		 КонецЕсли;
	 КонецЦикла;
	 
	Возврат ЭлементРодитель;
КонецФункции

&НаСервере
Процедура ЗаполнитьНастройкиОтборов(ПерезаполнятьТипыСообщений=Истина,
									ПерезаполнятьПартнеров=Истина,
									ПерезаполнятьТребуемыеДействия=Истина,
									ПерезаполнятьОрганизации = Истина)
									
	//будем все делать на сервере - т.к. все равно туда ходить за настройками
	_ДеревоФильтров								= РеквизитФормыВЗначение("ДеревоФильтров",							Тип("ДеревоЗначений"));
	_НастройкаОформленияТребуемогоДействия		= РеквизитФормыВЗначение("НастройкаОформленияТребуемогоДействия",	Тип("ТаблицаЗначений"));
	
	НастройкиПерезаполнения = Новый Структура;
	НастройкиПерезаполнения.Вставить("ПерезаполнятьТипыСообщений",		ПерезаполнятьТипыСообщений);
	НастройкиПерезаполнения.Вставить("ПерезаполнятьПартнеров",			ПерезаполнятьПартнеров);
	НастройкиПерезаполнения.Вставить("ПерезаполнятьТребуемыеДействия",	ПерезаполнятьТребуемыеДействия);
	НастройкиПерезаполнения.Вставить("ПерезаполнятьОрганизации",		ПерезаполнятьОрганизации);

	МодульОбъекта().ЗаполнитьДеревоОтборовОсновнойФормы(_ДеревоФильтров, НастройкиПерезаполнения, Параметры.РежимРаботы, ПоказыватьЗавершенные, _НастройкаОформленияТребуемогоДействия);

	ЗначениеВРеквизитФормы(_ДеревоФильтров,							"ДеревоФильтров");
	ЗначениеВРеквизитФормы(_НастройкаОформленияТребуемогоДействия,	"НастройкаОформленияТребуемогоДействия");
	
	ДобавитьУсловноеОформлениеСсылочныхТиповДереваОтборов();
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьУсловноеОформлениеСсылочныхТиповДереваОтборов()
	
	ИмяПоляОформления 	= "ДеревоФильтровЗначение";
	ИмяПоляОтбора 		= "ДеревоФильтров.Значение";
	
	МассивУдаляемыхЭлементовОформления = Новый Массив;
	
	// Очистим старые значения
	Для каждого ЭлементОформления Из УсловноеОформление.Элементы Цикл
		
		Для каждого ОформляемоеПоле Из ЭлементОформления.Поля.Элементы Цикл
			Если ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(ИмяПоляОформления) Тогда 
				МассивУдаляемыхЭлементовОформления.Добавить(ЭлементОформления);
				Продолжить;
			КонецЕсли;
		КонецЦикла;
			
	КонецЦикла;
	
	Для каждого ЭлементОформления Из МассивУдаляемыхЭлементовОформления Цикл
		УсловноеОформление.Элементы.Удалить(ЭлементОформления);		
	КонецЦикла;
	
	Для каждого СтрокаУровеньГрупп Из ДеревоФильтров.ПолучитьЭлементы() Цикл
		
		Если СтрокаУровеньГрупп.ГруппаНастроек = "Партнеры" 
			ИЛИ СтрокаУровеньГрупп.ГруппаНастроек = "Организации" 
			ИЛИ СтрокаУровеньГрупп.ГруппаНастроек = "ТипыСообщений" Тогда
			
			Для каждого СтрокаДереваОтборов Из СтрокаУровеньГрупп.ПолучитьЭлементы() Цикл
				
				ЭлементОформления = УсловноеОформление.Элементы.Добавить();
				ПолеЭлементаОформления = ЭлементОформления.Поля.Элементы.Добавить();
				ПолеЭлементаОформления.Поле = Новый ПолеКомпоновкиДанных(ИмяПоляОформления);
				
				ОтборЭлементаОформления = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				ОтборЭлементаОформления.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоляОтбора);
				ОтборЭлементаОформления.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
				ОтборЭлементаОформления.ПравоеЗначение = СтрокаДереваОтборов.Значение;
				
				ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", СтрокаДереваОтборов.Представление);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// жирность для заголовков
	СписокЗначений = Новый СписокЗначений;
	СписокЗначений.Добавить("Партнеры");
	СписокЗначений.Добавить("Организации");
	СписокЗначений.Добавить("Типы сообщений");
	СписокЗначений.Добавить("Требуемые действия");
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ПолеЭлементаОформления = ЭлементОформления.Поля.Элементы.Добавить();
	ПолеЭлементаОформления.Поле = Новый ПолеКомпоновкиДанных(ИмяПоляОформления);
	
	ОтборЭлементаОформления = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлементаОформления.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоляОтбора);
	ОтборЭлементаОформления.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	ОтборЭлементаОформления.ПравоеЗначение = СписокЗначений;
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(, , Истина));
	
КонецПроцедуры

 
&НаСервере
Процедура ЗаполнитьОтборТиповСообщений()  //Устарела, следует использовать ЗаполнитьНастройкиОтборов()
	
	ЗаполнитьНастройкиОтборов(Истина,Ложь,Ложь,Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокПартнеров()  //Устарела, следует использовать ЗаполнитьНастройкиОтборов()
	
	ЗаполнитьНастройкиОтборов(Ложь,Истина,Ложь,Ложь);
	
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьСписокТребуемыхДействий()  //Устарела, следует использовать ЗаполнитьНастройкиОтборов()
	
	ЗаполнитьНастройкиОтборов(Ложь,Ложь,Истина,Ложь);
	
КонецПроцедуры
                                                              
&НаКлиенте
Процедура РазвернутьДеревоОтбораТребуемоеДействиеКлиент()
	
	//развернуть дерево отборов требуемого действия
	КоллекцияЭлементовДерева=ОтборТребуемыеДействия.ПолучитьЭлементы();
    Для Каждого Строка Из КоллекцияЭлементовДерева Цикл    
        ИдентификаторСтроки = Строка.ПолучитьИдентификатор();
        Элементы.ОтборТребуемыеДействия.Развернуть(ИдентификаторСтроки,ИСТИНА);
	КонецЦикла;
	 
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьДеревоФильтровКлиент()
	
	//развернуть дерево отборов требуемого действия
	КоллекцияЭлементовДерева=ДеревоФильтров.ПолучитьЭлементы();
    Для Каждого Строка Из КоллекцияЭлементовДерева Цикл    
        ИдентификаторСтроки = Строка.ПолучитьИдентификатор();
        Элементы.ДеревоФильтров.Развернуть(ИдентификаторСтроки,ИСТИНА);
	КонецЦикла;
	 
КонецПроцедуры

// Считаем что если есть старое свойство ОсновнойКодGLN то надо конвертировать, если его нет - не надо конвертировать
&НаСервере
Функция ЕстьНесконвертированныеНаНовыйРелизДанные()

	Если  МодульОбъекта().ПолучитьКонстантуEDI("ОсновнойКодGLN")=Неопределено Тогда 
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;

КонецФункции // ЕстьНесконвертированныеНаНовыйРелизДанные()

&НаКлиенте
Процедура ОбработчикЗакрытияПомощникаПереходаНаНовыйРелиз(Параметры=Неопределено,ДопПараметры=Неопределено) Экспорт 
	Если ЭтаФорма.Открыта() Тогда 
		ЭтаФорма.Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикРешенияПользователяОПродолженииПереходаНаНовуюРедакцию(РезультатВопросаОПродолжении,ДопПараметры=неопределено) Экспорт
	
	Если РезультатВопросаОПродолжении = "Я создал резервную копию, продолжить" Тогда 
		ВывестиОшибкиМетаданныхEDI(истина);
	Иначе
		 ДопПараметры=Истина;
		 
		 ТекстПредупреждения="Создайте резервную копию базы данных и запустите модуль вновь.";
		 ДопПараметрДляПередачиВОбработчик=Неопределено;
		 Если Параметры.МодальностьЗапрещена Тогда 
		 	Выполнить("ПоказатьПредупреждение(Новый ОписаниеОповещения(""ОбработчикОзнакомленияСТребованиемСоздатьРезервнуюКопию"", ЭтаФорма, ДопПараметрДляПередачиВОбработчик),ТекстПредупреждения,,""Контур.EDI"")");
		 Иначе
		 	Предупреждение(ТекстПредупреждения,,"Контур.EDI");
		 	ОбработчикОзнакомленияСТребованиемСоздатьРезервнуюКопию(ДопПараметрДляПередачиВОбработчик);
		 КонецЕсли;
		 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОзнакомленияСТребованиемСоздатьРезервнуюКопию(ДопПараметрПереданныйВОбработчик=Неопределено) Экспорт

//Действия, связанные с обработкой ознакомления с предупреждением	

КонецПроцедуры 


&НаКлиенте
Процедура ВывестиОшибкиМетаданныхEDI(Отказ,ДопПараметры=неопределено) Экспорт
	
	Если СписокОшибок.Количество()>0 Тогда
	//Если истина Тогда 
		БылиОшибкиОбъектов= ложь;
		ТекстПредупреждения="";
		Для Каждого Ошибка из СписокОшибок ЦИкл
			ТекстПредупреждения=ТекстПредупреждения+""+Ошибка.Значение+Символы.ПС;  
			Если Найти(Ошибка.Значение,"В конфигурации отсутствует")<>0 ИЛИ Найти(Ошибка.Значение,"необходимо обновить")<>0 Тогда 
				БылиОшибкиОбъектов=Истина;
			КонецЕсли;
		КонецЦикла;
		
		Если БылиОшибкиОбъектов или Параметры.ПереходСоСтаройРедакции=истина Тогда 
		//Если истина Тогда 

			ТекстПредупреждения=ТекстПредупреждения+Символы.ПС+"Обновите вашу конфигурацию с использованием файла КонтурEDI_upd. ";
			
			РезультатВопросаСохраненияФайла = Неопределено;
			
			Если Параметры.МодальностьЗапрещена Тогда
				Выполнить("ПоказатьВопрос(Новый ОписаниеОповещения(""ПриОткрытииВопросОбобновленииЗавершение"", ЭтаФорма), ТекстПредупреждения+Символы.ПС+Символы.ПС+""Сохранить файл обновления на диск?"", РежимДиалогаВопрос.ДаНет)");
			иначе
				РезультатВопросаСохраненияФайла = Вопрос(ТекстПредупреждения+Символы.ПС+Символы.ПС+"Сохранить файл обновления на диск?", РежимДиалогаВопрос.ДаНет);
				ПриОткрытииВопросОбОбновленииЗавершение(РезультатВопросаСохраненияФайла, Неопределено)
			КонецЕсли;
			
			Отказ=Истина;
			
			Возврат;
		
		КонецЕсли;
		
		ТекстПредупреждения=ТекстПредупреждения+Символы.ПС+"Устраните указанные проблемы и запустите обработку вновь!";
		Если Параметры.МодальностьЗапрещена Тогда 
			Выполнить("ПоказатьПредупреждение(Новый ОписаниеОповещения(""ОбработчикОтказаОткрытияЭтойФормы"", ЭтаФорма,),ТекстПредупреждения,,""Контур.EDI"")");
		Иначе
			Предупреждение(ТекстПредупреждения,,"Обнаружены проблемы запуска модуля");
			ОбработчикОтказаОткрытияЭтойФормы();
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
КонецПроцедуры

//Здесь сохраняем файлы обновления и инструкций
&НаКлиенте
Процедура ПриОткрытииВопросОбОбновленииЗавершение(РезультатВопроса1, ДополнительныеПараметры) Экспорт
	
	РезультатВопросаСохраненияФайла = РезультатВопроса1;
	
	Если РезультатВопросаСохраненияФайла  = КодВозвратаДиалога.Да Тогда
		
		Режим = РежимДиалогаВыбораФайла.ВыборКаталога;
		Заголовок 	= "Укажите каталог, в который нужно сохранить файлы для обновления вашей конфигурации";
		Фильтр		= "Файл конфигурации 1С (*.cf)|*.cf|Файл инструкции PDF(*.pdf)|*.pdf";      
		
		ПолучаемыеФайлы = Новый Массив;
		
		АдресCF			= ПолучитьАдресВременногоХранилищаОбновленияВызовСервера();		
		ФайлCF			= Новый ОписаниеПередаваемогоФайла("КонтурEDI_upd.cf", АдресCF);
		
		АдресИнструкция = ПолучитьАдресВременногоХранилищаИнструкциияВызовСервера();		
		ФайлИнструкция	= Новый ОписаниеПередаваемогоФайла("КонтурEDI_upd_ИнструкцияПоОбновлению.pdf", АдресИнструкция);
		
		ПолучаемыеФайлы.Добавить(ФайлCF);
		ПолучаемыеФайлы.Добавить(ФайлИнструкция);

		МодульОбъектаКлиент().ОткрытьДиалогВыбораФайла_КонтурEDI("ПослеСохраненияФайлаОбновлений", ЭтаФорма,ДополнительныеПараметры,, Фильтр, Заголовок, Режим, ПолучаемыеФайлы);

	Иначе
		ТекстПредупреждения="Без обновления базы дальнейшая работа с Контур.EDI невозможна.";
		Если Параметры.МодальностьЗапрещена Тогда 
			Выполнить("ПоказатьПредупреждение(Новый ОписаниеОповещения(""ОбработчикОтказаОткрытияЭтойФормы"", ЭтаФорма,),ТекстПредупреждения,,""Контур.EDI"")");
		Иначе
			Предупреждение(ТекстПредупреждения);
			ОбработчикОтказаОткрытияЭтойФормы();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДвоичныеДанныеФайлаОбновленияКонфигурацииВызовСервера()
	Возврат МодульОбъекта().ДвоичныеДанныеФайлаОбновленияКонфигурацииСервер();	
КонецФункции

&НаСервере
Функция ПолучитьАдресВременногоХранилищаОбновленияВызовСервера()
	Возврат МодульОбъекта().ПолучитьАдресВременногоХранилищаОбновленияКонфигурацииСервер();	
КонецФункции     

&НаСервере
Функция ПолучитьДвоичныеДанныеФайлаИнструкцииВызовСервера()
	
	Сервер	 = Объект.ПараметрыКлиентСервер.URLАдреса.EDIБоевойСервер;
	ИмяФайла = Объект.ПараметрыКлиентСервер.URLАдреса.ИнструкцияОбновлениеМетаданных;

	Результат = МодульОбъекта().HTTP_GetFile(Сервер, ИмяФайла);
	
	Если Результат = Неопределено Тогда 
		
		Возврат Неопределено
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ПолучитьДвоичныеДанныеФайлаИнструкцииВызовСервера()

&НаСервере
Функция ПолучитьАдресВременногоХранилищаИнструкциияВызовСервера()
	
	ДвоичныеДанные = ПолучитьДвоичныеДанныеФайлаИнструкцииВызовСервера();
	
	Адрес = ПоместитьВоВременноеХранилище(ДвоичныеДанные, Новый УникальныйИдентификатор);
	
	Возврат Адрес;
	
КонецФункции // ПолучитьДвоичныеДанныеФайлаИнструкцииВызовСервера()


&НаСервере
Функция СтрокаСоединенияПреобразованная()
	
	СтрокаСоединенияСБД = СтрокаСоединенияИнформационнойБазы();
	
	Если EDI_ИнформационнаяБазаФайловая(СтрокаСоединенияСБД) Тогда
		Возврат "/F"+Сред(СтрокаСоединенияСБД, 6, СтрДлина(СтрокаСоединенияСБД) - 6);
	КонецЕсли;
		
	// Прибавить к имени сервера имя пути информационной базы
	ПозицияПоиска = Найти(Врег(СтрокаСоединенияСБД), "SRVR=");
	Если ПозицияПоиска <> 1 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПозицияТочкиСЗапятой = Найти(СтрокаСоединенияСБД, ";");
	НачальнаяПозицияКопирования = 6 + 1;
	КонечнаяПозицияКопирования = ПозицияТочкиСЗапятой - 2; 
	
	ИмяСервера = Сред(СтрокаСоединенияСБД, НачальнаяПозицияКопирования, КонечнаяПозицияКопирования - НачальнаяПозицияКопирования + 1);
	
	СтрокаСоединенияСБД = Сред(СтрокаСоединенияСБД, ПозицияТочкиСЗапятой + 1);
	
	// Позиция имени сервера
	ПозицияПоиска = Найти(Врег(СтрокаСоединенияСБД), "REF=");
	Если ПозицияПоиска <> 1 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НачальнаяПозицияКопирования = 6;
	ПозицияТочкиСЗапятой = Найти(СтрокаСоединенияСБД, ";");
	КонечнаяПозицияКопирования = ПозицияТочкиСЗапятой - 2; 
	
	ИмяИБНаСервере = Сред(СтрокаСоединенияСБД, НачальнаяПозицияКопирования, КонечнаяПозицияКопирования - НачальнаяПозицияКопирования + 1);
	ПутьКБД = "/S" + ИмяСервера + "\" + ИмяИБНаСервере;
	Возврат ПутьКБД;
КонецФункции

&НаКлиенте
Процедура ПослеСохраненияФайлаОбновлений(Результат, ДопПараметры = Неопределено) Экспорт
	
	Если Результат = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = Новый Структура("Закрывать", Истина);
	Иначе
		Если НЕ ДопПараметры.Свойство("Закрывать") Тогда
			ДопПараметры.Вставить("Закрывать", Истина);
		КонецЕсли;
	КонецЕсли;
	
	Если ДопПараметры.ДиалогВыбораФайла.ВыбранныеФайлы.Количество() = 0 Тогда
		Если ДопПараметры.Закрывать Тогда
			ОбработчикОтказаОткрытияЭтойФормы();
		КонецЕсли;
	КонецЕсли;
	
	КнопкиВопроса = Новый СписокЗначений;
	КнопкиВопроса.Добавить("Открыть инструкцию");
	КнопкиВопроса.Добавить("Открыть инструкцию и видео");
	КнопкиВопроса.Добавить("Я знаю, что делать дальше");
	
	РезультатВопроса = Неопределено;
	АдресКаталогаДляСохраненияФайла = ДопПараметры.ДиалогВыбораФайла.Каталог;
	ТекстВопроса = "Файлы обновления сохранены в каталог: " + АдресКаталогаДляСохраненияФайла;
	
	Если Параметры.МодальностьЗапрещена Тогда
		ОписаниеОповещения = Неопределено;
		Выполнить("ОписаниеОповещения = Новый ОписаниеОповещения(""ОтветВопросОткрытиеИнструкцииЗавершение"", ЭтаФорма, ДопПараметры)");
		Выполнить("ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, КнопкиВопроса, , , ""Контур.EDI"")");
	Иначе
		РезультатВопроса = Вопрос(ТекстВопроса, КнопкиВопроса, , , "Контур.EDI");
		ОтветВопросОткрытиеИнструкцииЗавершение(РезультатВопроса, ДопПараметры);
		Если ДопПараметры.Закрывать Тогда
			ОбработчикОтказаОткрытияЭтойФормы();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветВопросОткрытиеИнструкцииЗавершение(РезультатВопроса, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если РезультатВопроса = "Открыть инструкцию" или РезультатВопроса = "Открыть инструкцию и видео" Тогда 
		Попытка
			Если Параметры.МодальностьЗапрещена Тогда
				Выполнить("НачатьЗапускПриложения(Новый ОписаниеОповещения, Объект.ПараметрыКлиентСервер.URLАдреса.ОнлайнСправка_ОбновлениеМетаданных)");
			Иначе 
				ЗапуститьПриложение(Объект.ПараметрыКлиентСервер.URLАдреса.ОнлайнСправка_ОбновлениеМетаданных);
			КонецЕсли;
			
			Если РезультатВопроса = "Открыть инструкцию и видео" Тогда 			
				
				Если Параметры.МодальностьЗапрещена Тогда
					Выполнить("НачатьЗапускПриложения(Новый ОписаниеОповещения, ""http://www.screencast.com/users/SKB_LT_Co/folders/EDI/media/b9ffc8e1-701d-4553-93ba-5171de57b759"")");
				Иначе 
					ЗапуститьПриложение("http://www.screencast.com/users/SKB_LT_Co/folders/EDI/media/b9ffc8e1-701d-4553-93ba-5171de57b759");
				КонецЕсли
				
			КонецЕсли;
		Исключение
			ТекстПредупреждения="Не удалось открыть файл по причине: "+ОписаниеОшибки();
			Если Параметры.МодальностьЗапрещена Тогда
				
				ВыражаниеКВыполнению = "";
				Если ДополнительныеПараметры.Закрывать Тогда					
					ВыражаниеКВыполнению = ВыражаниеКВыполнению + "ОписаниеОповещения = Новый ОписаниеОповещения(""ОбработчикОтказаОткрытияЭтойФормы"", ЭтаФорма);"; 
				Иначе   					
					ВыражаниеКВыполнению = ВыражаниеКВыполнению + "ОписаниеОповещения = Неопределено;";  					
				КонецЕсли;
				
				ВыражаниеКВыполнению = ВыражаниеКВыполнению + "ПоказатьПредупреждение(ОписаниеОповещения, ТекстПредупреждения,, ""Контур.EDI"")";
				
				Выполнить(ВыражаниеКВыполнению);
				
			Иначе
				Предупреждение(ТекстПредупреждения);
				Если ДополнительныеПараметры.Закрывать Тогда
					ОбработчикОтказаОткрытияЭтойФормы();
				КонецЕсли;
			КонецЕсли;
		КонецПопытки;
	КонецЕсли;
	
	Если РезультатВопроса = "Я знаю, что делать дальше" Тогда
		Если ДополнительныеПараметры.Закрывать Тогда
			ОбработчикОтказаОткрытияЭтойФормы();
		КонецЕсли; // все сюда, тут пользователь-супергерой внедряет
	Иначе
		ТекстПредупреждения="Теперь обновите конфигурацию с использованием файла КонтурEDI_upd.cf и инструкции.";
		Если Параметры.МодальностьЗапрещена Тогда
			
			ВыражаниеКВыполнению = "";
			Если ДополнительныеПараметры.Закрывать Тогда					
				ВыражаниеКВыполнению = ВыражаниеКВыполнению + "ОписаниеОповещения = Новый ОписаниеОповещения(""ОбработчикОтказаОткрытияЭтойФормы"", ЭтаФорма);"; 
			Иначе   					
				ВыражаниеКВыполнению = ВыражаниеКВыполнению + "ОписаниеОповещения = Неопределено;";  					
			КонецЕсли;
			
			ВыражаниеКВыполнению = ВыражаниеКВыполнению + "ПоказатьПредупреждение(ОписаниеОповещения, ТекстПредупреждения,, ""Контур.EDI"")";
			
			Выполнить(ВыражаниеКВыполнению);
			
		Иначе
			Предупреждение(ТекстПредупреждения);
			Если ДополнительныеПараметры.Закрывать Тогда
				ОбработчикОтказаОткрытияЭтойФормы();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
// Скопирована в модуль для совместимости со всеми без исключения базами
// Функция ИнформационнаяБазаФайловая определяет режим эксплуатации
// информационной базы файловый (Истина) или Серверный (Ложь).
//  При проверке используется СтрокаСоединенияИнформационнойБазы, которую
// можно указать явно.
//
// Параметры:
//  СтрокаСоединенияИнформационнойБазы - Строка - параметр используется, если
//                 нужно проверить строку соединения не текущей информационной базы.
//
// Возвращаемое значение:
//  Булево.
//
Функция EDI_ИнформационнаяБазаФайловая(Знач СтрокаСоединенияИнформационнойБазы = "") Экспорт
			
	Если ПустаяСтрока(СтрокаСоединенияИнформационнойБазы) Тогда
		СтрокаСоединенияИнформационнойБазы =  СтрокаСоединенияИнформационнойБазы();
	КонецЕсли;
	Возврат Найти(Врег(СтрокаСоединенияИнформационнойБазы), "FILE=") = 1;
	
КонецФункции 

//для режима запрета модальности
Процедура СохранитьБатФайлОбновленияВспомогательныхДанных(АдресКаталогаДляСохраненияФайла)
	
	Если истина тогда //("Объект").МодальностьЗапрещена() Тогда 	    //делаем это все только для 8.3.5+
		
		Текст = Новый ЗаписьТекста(АдресКаталогаДляСохраненияФайла+"\ОбновлениеИБДляПримененияПравEDI.bat", КодировкаТекста.OEM);
		
		СисИнфо = Новый СистемнаяИнформация;
		ТекВерсияПлатформы=СисИнфо.ВерсияПриложения;
		
		Текст.ЗаписатьСтроку("""C:\Program Files (x86)\1cv8\"+ТекВерсияПлатформы+"\bin\1cv8.exe"" enterprise " +СтрокаСоединенияПреобразованная()+ " /C ЗапуститьОбновлениеИнформационнойБазы");
		Текст.Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Параметры.АвтообменВыполнен = Истина Тогда
		ПодписатьДокументыВДиадок();
		ОтправитьИзвещенияОПолучении();
		ПрекратитьРаботуСистемы();
		Возврат;
	КонецЕсли;
	
	Если не ПроверкаМетаданныхПройдена и Параметры.ПереходСоСтаройРедакции=истина Тогда
		Отказ=истина;
		//выведем вопрос с предупреждением о резервной копии и если пользователь скажет что уже сделал бэкап вызовем  ВывестиОшибкиМетаданныхEDI
		ТекстВопроса ="Вы собираетесь запустить новую редакцию модуля КонтурEDI."+Символы.ПС+"Переход на новую редакцию возможен только после создания резервной копии"+Символы.ПС+
		"Рекомендуется сначала выполнить пробный переход на копии базы!!!"+Символы.ПС+Символы.ПС+"ВЫ СДЕЛАЛИ РЕЗЕРВНУЮ КОПИЮ?";
			КнопкиВопроса=новый СписокЗначений;
			КнопкиВопроса.Добавить("Я еще не создал резервную копию");
			КнопкиВопроса.Добавить("Я создал резервную копию, продолжить");
			
			РезультатВопроса = Неопределено;
			Если Параметры.МодальностьЗапрещена Тогда
				Выполнить("ПоказатьВопрос(Новый ОписаниеОповещения(""ОбработчикРешенияПользователяОПродолженииПереходаНаНовуюРедакцию"", ЭтаФорма), ТекстВопроса, КнопкиВопроса, , , ""Контур.EDI"")");
			Иначе
				РезультатВопроса = Вопрос(ТекстВопроса, КнопкиВопроса, , , "Контур.EDI");
				ОбработчикРешенияПользователяОПродолженииПереходаНаНовуюРедакцию(РезультатВопроса,Отказ);
			КонецЕсли;
		Возврат
	КонецЕсли;
	
	Если не ПроверкаМетаданныхПройдена Тогда 
		//Выведем все что нам не нравится, предложим файлик сохранить и захлопнем модуль
		//т.е. закрытие модуля уже неизбежно             ОбработчикОтказаОткрытияЭтойФормы

		ВывестиОшибкиМетаданныхEDI(Отказ);

		Возврат;
	КонецЕсли;
	
	//БыстрыеОтборыСвернуть("");        //рефакторинг фильтры
	
	УстановитьНастройкуПериодаПоУмолчанию();
	УстановитьНадписьПериода();
	УстановитьСвернутостьПодробностейФильтра();
	ЗаполнитьСписокСохраненныхОтборов();
	ВосстановитьНастройкиФормыКлиент();
	ЗаполнитьНастройкиОтборов();
	УстановитьСохраненныеНастройкиДереваФильтров();	
	
	ЭтоПервыйЗапуск = Ложь;
	
	//проверим, не переходи ли это на ЭтотМодуль со старого модуля УФ
	Если НеПоказыватьПомощник <> Истина И ЕстьНесконвертированныеНаНовыйРелизДанные() Тогда 
		//открываем специализированную форму конвертации
		ПараметрыФормы = Новый Структура("Задача","ПереходНаНовуюРедакциюМодуля");
		МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Сервис_ПомощникПереходаНаНовыйРелизУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбработчикЗакрытияПомощникаПереходаНаНовыйРелиз", ЭтаФорма);
		Отказ = Истина;//переход всегда закрывает модуль после отработки
		Возврат;
		//модуль придется перезапускать, этим займется обработчик
	КонецЕсли;
	
	Если НеПоказыватьПомощник <> Истина И НЕ ЗначениеЗаполнено(Параметры.ПараметрыАвтотестирования) Тогда
		ПодключитьОбработчикОжидания("ЗапуститьСтартовыйПомощник",0.1,Истина);
		ЭтоПервыйЗапуск = Истина;
	КонецЕсли;
	
	Если НЕ РезультатПроверкиЗапускНовойВерсииМодуля.ПродолжитьЗапуск Тогда
		Если Параметры.МодальностьЗапрещена Тогда
			Выполнить("ПоказатьПредупреждение(Новый ОписаниеОповещения(""ОбработчикОтказаОткрытияЭтойФормы"", ЭтаФорма,),РезультатПроверкиЗапускНовойВерсииМодуля.ТекстОшибки,,""Контур.EDI"")");
		Иначе
			Предупреждение(РезультатПроверкиЗапускНовойВерсииМодуля.ТекстОшибки);
			Отказ=Истина;
			Возврат;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ПараметрыАвтотестирования) Тогда
		Автотесты_УстановитьНачальныеНастройки();
	Иначе
		//опять на сервер обновлять список
		Если не ЭтоПервыйЗапуск Тогда
			ПодключитьОбработчикОжидания("ЗаполнитьСписокЗадачКлиент",0.1,Истина);
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Параметры.ПараметрыАвтотестирования) Тогда
		//нужно понимать кастомы и управлять изменениями
		ПроверитьИзмененностьМодуля(); 
	КонецЕсли;
	
	ПредложитьПодключитсяКДругимТСКлиент();
	
	Если ПроставитьСрокДействияСертификатов Тогда
		МодульОбменКлиент().ПроставитьСрокДействияСертификатов();
	КонецЕсли;
	
	Если ПредупредитьОбИстекающихСертификатах Тогда
   		ПодключитьОбработчикОжидания("ПредупредитьОбИстекающихСертификатах", 0.1, Истина);
	КонецЕсли;
	
	ИмяПМ = ИнициализироватьКлиентскийМодульПМ();
	Если ЗначениеЗаполнено(ИмяПМ) Тогда
		Попытка
			ПараметрыФормы=	Новый Структура;
			ПараметрыФормы.Вставить("АдресХранилища", 		Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект);
			ПараметрыФормы.Вставить("МодальностьЗапрещена", Объект.ПараметрыКлиентСервер.МодальностьЗапрещена);
			Выполнить("МодульКлиентПМ = ПолучитьФорму(""ВнешняяОбработка.""+ ИмяПМ +"".Форма.МодульКлиент"", ПараметрыФормы);");
		Исключение
			КомментарийЗаписи = "Не удалось открыть клиентскую форму ПМ - " + ОписаниеОшибки();
			МодульОбъектаКлиент().ЗаписьЖурналаРегистрацииНаКлиенте_КонтурEDI("Ошибка", КомментарийЗаписи);
		КонецПопытки;
	КонецЕсли;	 
	
	ПутьКФормамМеркурий = ИнициализироватьКлиентскийМодульМеркурий();

	//вывести историю  //тоже обработчиком чтобы не улетело на задник
	Если ПоказатьИсториюВерсий = Истина Тогда 
		ПодключитьОбработчикОжидания("ПоказатьНовоеВРелизе", 0.2, Истина);
	КонецЕсли;
	
	Если ПроверятьПомощников Тогда
		ПодключитьОбработчикОжидания("НастроитьПомощниковEDI", 600);
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("ВызватьСобытиеПМОткрытиеОсновнойФормыКлиент", 1, Истина);

	НоваяМетрика().Категория("ОсновнаяФорма").НаФорме(ЭтаФорма).ЗакончитьЗамер("Замер_ЗапускМодуля").ДлительностьЗамера().ПоместитьВОчередь();
	ПодключитьОбработчикОжидания("Метрика_ОтправитьСтекСобытий", 120);
	
	ПриОткрытииФормы(ЭтаФорма, "ФормаУправляемая");
	
КонецПроцедуры

// Передает управление в клиентский ПМ с целью взаимодействия с пользователем на клиенте сразу при открытии модуля
&НаКлиенте
Процедура ВызватьСобытиеПМОткрытиеОсновнойФормыКлиент()

	ОбработкаСобытияПодключаемогоМодуляКлиент("ОткрытиеОсновнойФормыМодуляEDIКлиент",,Новый Структура("ОсновнаяФорма",ЭтаФорма));

КонецПроцедуры // ВызватьСобытиеПМОткрытиеОсновнойФормыКлиент()

&НаКлиенте
Процедура УстановитьНастройкуПериодаПоУмолчанию()

	// при открытии установим период по умолчанию
	ПериодВСекундах = ТекущаяДата() - НастройкаПериода.ДатаНачала;
	МаксимальныйПериод = Объект.ПараметрыКлиентСервер.КоличествоДнейВОсновномСпискеПриОткрытии;
	
	НастройкаПериода.Вариант		= ВариантСтандартногоПериода.ПроизвольныйПериод;
	НастройкаПериода.ДатаНачала 	= НачалоДня(ТекущаяДата() - МаксимальныйПериод * 24 * 3600);
	НастройкаПериода.ДатаОкончания 	= Дата(1,1,1);

КонецПроцедуры // УстановитьНастройкуПериодаПоУмолчанию()

&НаКлиенте
Процедура Метрика_ОтправитьСтекСобытий() Экспорт

	СтекСобытийКлиент = МодульМетрика().Метрика_СобратьСтекСобытийКлиент();
	Если СтекСобытийКлиент.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	Метрика_ОтправитьСтекСобытийСервер(СтекСобытийКлиент);	

КонецПроцедуры

&НаСервере
Процедура Метрика_ОтправитьСтекСобытийСервер(СтекСобытийКлиент = Неопределено)

	МодульОбъекта().Метрика_ОтправитьВсеСобытияСтека(СтекСобытийКлиент);	

КонецПроцедуры

&НаКлиенте
Процедура ПредупредитьОбИстекающихСертификатах()
	
	МассивИстекающихСертификатов = ПодготовитьМассивИстекающихСертификатовСервер();

	Если МассивИстекающихСертификатов.Количество() > 0 Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("МассивСертификатов",	МассивИстекающихСертификатов);
		МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Сертификаты_ПредупреждениеУправляемая", , ПараметрыФормы, ЭтаФорма);
		
	КонецЕсли;

КонецПроцедуры // ПредупредитьОбИстекающихСертификатах()

&НаКлиенте
Процедура ПроверитьИзмененностьМодуля()

	Если НужноПроверитьИзмененностьМодуляEDI=Истина Тогда
		Состояние("Проверяю целостность модуля Контур.EDI"); //Эта операция будет происходить не чаще 1 раза в 60 дней или каждый запуск если добавили во внешние
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ДополнительнаяОбработкаСсылка",Параметры.ДополнительнаяОбработкаСсылка);
		ФормаПроверкиТекРелиза = МодульОбъектаКлиент().ПолучитьФорму_КонтурEDI("Сервис_ОбновлениеУправляемая", , ПараметрыФормы, ЭтаФорма);
		ФормаПроверкиТекРелиза.ПроверитьНаличиеИВозможностьОбновления("Заглушка");
		Состояние("Целостность модуля проверена!");
	КонецЕсли;

КонецПроцедуры // ПроверитьИзмененностьМодуля()

&НаКлиенте
Процедура ОбработчикСогласияНаПервыйОбмен(РезультатВопроса,ДопПараметрПереданныйВОбработчик=Неопределено) Экспорт

	Если РезультатВопроса="Да, выполнить обмен с сервером" Тогда
		ВыполнитьОбмен(ЭтаФорма.Команды.Найти("ВыполнитьОбмен"));
	КонецЕсли;

КонецПроцедуры

//предложим подключиться к другим ТС если ПриСозданииНаСервере было решено это предложить
&НаКлиенте
Процедура ПредложитьПодключитсяКДругимТСКлиент()

    Если Параметры.ПредлагатьПодключиться Тогда
   		ПодключитьОбработчикОжидания("ПредложитьПодключитьсяКДругимТорговымСетямОбработчикОжидания",0.1,Истина);
    КонецЕсли;

КонецПроцедуры // ПредложитьПодключитсяКДругимТСКлиент()


&НаКлиенте
Процедура ПредложитьПодключитьсяКДругимТорговымСетямОбработчикОжидания()
	
	СтруктураПодключенияКДругимТС =  ПолучитьСписокНеподключенныхТорговыхСетейВызовСервера();
	Если СтруктураПодключенияКДругимТС.НеподключенныеСети.Количество()>0 Тогда
		
		//ФормаСетей = ПолучитьФорму("ФормаСервис_ПредложениеПодключитьсяКСетям");
		//ФормаСетей.ОсновнойGLN = ПолучитьКонстантуEDI("GLN_Основной");
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ОсновнойGLN",СтруктураПодключенияКДругимТС.ОсновнойGLN);
		ПараметрыФормы.Вставить("НеподключенныеСети",СтруктураПодключенияКДругимТС.НеподключенныеСети);
		МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Сервис_ПредложениеПодключитьсяКСетямУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбработчикПослеЗакрытияПредложенияПодключитьсяКСетям", ЭтаФорма); 
		//сунуть в форму
		//Для Каждого Сеть Из НеподключенныеСети Цикл
		//	НоваяСтрока = ФормаСетей.ТаблицаСетей.Добавить();
		//	НоваяСтрока.ТорговаяСеть = Сеть.Наименование;
		//	НоваяСтрока.Кнопка = "Подключиться";
		//	НоваяСтрока.СсылкаНаСайте = Сеть.СсылкаНаСайте;
		//КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// На всякий случай оставим разъем
&НаКлиенте
Процедура ОбработчикПослеЗакрытияПредложенияПодключитьсяКСетям(Параметры=Неопределено,ДопПараметры=Неопределено) Экспорт

	

КонецПроцедуры // ОбработчикПослеЗакрытияПредложенияПодключитьсяКСетям()


// Возвращаемое значение:
//   Структура   - ОсновнойGLN,НеподключенныеСети
&НаСервере
Функция ПолучитьСписокНеподключенныхТорговыхСетейВызовСервера()

	СтруктураПодключенияКДругимТС= Новый Структура("ОсновнойGLN,НеподключенныеСети",МодульОбъекта().ПолучитьКонстантуEDI("GLN_Основной"),МодульОбъекта().ПолучитьСписокНеподключенныхТорговыхСетей());	

	Возврат СтруктураПодключенияКДругимТС;
	
КонецФункции // ()


&НаКлиенте
Процедура ОбработчикОтказаОткрытияЭтойФормы(Параметр1=неопределено) Экспорт
	Если ЭтаФорма.Открыта() Тогда 
		ЭтаФорма.Закрыть(); 
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВозможностьРаботыМодуля(Отказ);
	
	Если Не МетаданныеМодуляВКонфигурацииАктуальны() Тогда
		Если Параметры.МодальностьЗапрещена = истина Тогда
			Выполнить("ПоказатьПредупреждение(,""Для использования данного интерфейса необходимо обновить конфигурацию"");");
		Иначе
			Предупреждение("Для использования данного интерфейса необходимо обновить конфигурацию");
			Отказ =истина;
		КонецЕсли;
		
	КонецЕсли;
	
	//Если не Параметры.ПризнакАрхивВСообщенияхЗаполнен Тогда
	//	Если Не ЗаполнитьФлагАрхивВСообщениях() Тогда
	//		Отказ =истина;
	//	КонецЕсли;	
	//КонецЕсли;		

КонецПроцедуры

&НаСервере
Функция МетаданныеМодуляВКонфигурацииАктуальны()
	
	Если Метаданные.Справочники.КонтурEDI_Сообщения.Реквизиты.Найти("Архив")=Неопределено Тогда
		Возврат Ложь
	иначе
		Возврат Истина;
		
	КонецЕсли;
		
КонецФункции

//обновим сообщения: заполним флаг "Архив"
//и установим нужный признак
&НаКлиенте
Функция ЗаполнитьФлагАрхивВСообщениях()
	
	Если Вопрос("Необходимо сконвертировать сообщения для использования нового интерфейса. 
		|Это займет некоторое время, в зависимости от их количества.
		|Конвертацию можно прервать в любой момент нажатием Ctrl+Break и продолжить в следующий раз.
		|Продолжить?"
		,РежимДиалогаВопрос.ОКОтмена)<>КодВозвратаДиалога.ОК Тогда
		
		Возврат Ложь;
	КонецЕсли;
	
	Если ЗаполнитьФлагАрхивВСообщенияхСервер() Тогда 
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	               
КонецФункции

&НаСервере
Функция ЗаполнитьФлагАрхивВСообщенияхСервер()
	
	Попытка	
		//конвертация
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	КонтурEDI_Сообщения.Ссылка
		|ИЗ
		|	Справочник.КонтурEDI_Сообщения КАК КонтурEDI_Сообщения
		|ГДЕ
		|	(КонтурEDI_Сообщения.Архив <> ВЫБОР
		|				КОГДА КонтурEDI_Сообщения.Направление = ""Исходящее""
		|					ТОГДА ВЫБОР
		|							КОГДА КонтурEDI_Сообщения.Статус = ""Доставлен""
		|								ТОГДА ИСТИНА
		|							ИНАЧЕ ЛОЖЬ
		|						КОНЕЦ
		|				ИНАЧЕ ВЫБОР
		|						КОГДА КонтурEDI_Сообщения.Статус = ""Загружен""
		|								ИЛИ КонтурEDI_Сообщения.Статус = ""Отклонен""
		|							ТОГДА ИСТИНА
		|						ИНАЧЕ ЛОЖЬ
		|					КОНЕЦ
		|			КОНЕЦ
		|			ИЛИ КонтурEDI_Сообщения.Партнер = ЗНАЧЕНИЕ(Справочник.КонтурEDI_ДополнительныеСправочники.)
		|			ИЛИ КонтурEDI_Сообщения.Партнер = НЕОПРЕДЕЛЕНО)"
		);
		Выборка = Запрос.Выполнить().Выбрать();
		Сч = 0;
		Пока Выборка.Следующий() Цикл
			Сч=Сч+1;
			#Если Клиент Тогда
				Состояние("Сконвертировано: "+Сч+" из "+Выборка.Количество());
				ОбработкаПрерыванияПользователя();
			#КонецЕсли
			
			Выборка.Ссылка.ПолучитьОбъект().Записать();
			
		КонецЦикла;
		
		МодульОбъекта().УстановитьКонстантуEDI("ПризнакАрхивВСообщенияхЗаполнен",Истина);
		
		Возврат Истина;
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции

&НаКлиенте
Процедура ЗапуститьСтартовыйПомощник()
		
	СохранитьНастройкиГлавнойФормы();
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Сервис_СтартовыйПомощникУправляемая", , , ЭтаФорма, "ОбработчикПослеРаботыСтартовогоПомощника", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикПослеРаботыСтартовогоПомощника(РезультатРаботыСтартовогоПомощника, ДополнительныеПараметры = Неопределено) Экспорт
	
	ОбработкаПослеОткрытияФормНастроекСОбновлениемКэшаКлиент();
	
	Если ЭтоПервыйЗапуск = истина Тогда 
		ТекстВопроса ="Выполнить первый обмен с сервером?";
		КнопкиВопроса=новый СписокЗначений;
		КнопкиВопроса.Добавить("Да, выполнить обмен с сервером");
		КнопкиВопроса.Добавить("Нет, я сначала проверю настройки");
		ДопПараметрДляПередачиВОбработчик=Неопределено;
		РезультатВопроса = Неопределено;
		
		Если Параметры.МодальностьЗапрещена Тогда
			Выполнить("ПоказатьВопрос(Новый ОписаниеОповещения(""ОбработчикСогласияНаПервыйОбмен"", ЭтаФорма, ДопПараметрДляПередачиВОбработчик), ТекстВопроса, КнопкиВопроса,,,""Контур.EDI"")");
		Иначе
			РезультатВопроса = Вопрос(ТекстВопроса, КнопкиВопроса,,,"Контур.EDI");
			ОбработчикСогласияНаПервыйОбмен(РезультатВопроса,ДопПараметрДляПередачиВОбработчик);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
//Включает/выключает видимость отборов
Процедура ОткрывашкаНажатие(Команда)
	
	Элементы.ПанельОтбораСообщений.Видимость= не Элементы.ПанельОтбораСообщений.Видимость;
	Элементы.Открывашка.Заголовок=?(Элементы.ПанельОтбораСообщений.Видимость,"<",">");
	Элементы.Открывашка.Ширина=?(Элементы.ПанельОтбораСообщений.Видимость,2,5);
	Элементы.Открывашка.Отображение=?(Элементы.ПанельОтбораСообщений.Видимость,ОтображениеКнопки.Текст,ОтображениеКнопки.КартинкаИТекст);
	ТекущийЭлемент = Элементы.ТабСообщения; // уберем рамку выделения у картинки
	Элементы.РазделительКартинка1.Видимость = Элементы.ПанельОтбораСообщений.Видимость;
	Элементы.РазделительКартинка2.Видимость = не Элементы.ПанельОтбораСообщений.Видимость;
КонецПроцедуры

&НаКлиенте
Процедура КоманднаяПанельОтборВидыСообщенийПоставщик(Команда)
	
	Параметры.РежимРаботы = "Поставщик";
	Если Не Элементы.Поставщик.Пометка Тогда
		Элементы.Поставщик.Пометка = Истина;
	КонецЕсли;
	Элементы.Сеть.Пометка = Ложь;
	ВосстановитьНастройкиФормыКлиент();
	ЗаполнитьНастройкиОтборов();
	УстановитьСохраненныеНастройкиДереваФильтров();
	ЗаполнитьСписокЗадачКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура КоманднаяПанельОтборВидыСообщенийСеть(Команда)
	
	Параметры.РежимРаботы = "Покупатель";
	Если Не Элементы.Сеть.Пометка Тогда
		Элементы.Сеть.Пометка = Истина;
	КонецЕсли;
	Элементы.Поставщик.Пометка = Ложь;
	ВосстановитьНастройкиФормыКлиент();
	ЗаполнитьНастройкиОтборов();
	УстановитьСохраненныеНастройкиДереваФильтров();
	ЗаполнитьСписокЗадачКлиент();
	
КонецПроцедуры

&НаСервере
Функция ОпределитьРежимРаботы()
	// скинем
	Элементы.Сеть.Доступность = Ложь;
	Элементы.Сеть.Пометка = Ложь;
	Элементы.Поставщик.Доступность = Ложь;
	Элементы.Поставщик.Пометка = Ложь;
		
	Параметры.РежимРаботы = "Неизвестно";
	
	//в первую очередь прикинемся поставщиком
	Если МодульОбъекта().НастройкиМодуля.МыПоставщик
		И СокрЛП(МодульОбъекта().ПолучитьКонстантуEDI("ШаблонЦепочкиДокументов_Поставщик"))<>""
	Тогда
		Параметры.РежимРаботы = "Поставщик";
		Элементы.Поставщик.Доступность = Истина;
		Элементы.Поставщик.Пометка = Истина;

	Иначе
		Элементы.Поставщик.Доступность = Ложь;
		Элементы.Поставщик.Пометка = Ложь;

	КонецЕсли;
	
	Если МодульОбъекта().НастройкиМодуля.МыТорговаяСеть
		И СокрЛП(МодульОбъекта().ПолучитьКонстантуEDI("ШаблонЦепочкиДокументов_Покупатель"))<>""
	Тогда
		//дадим возможность переключения режима работы на "Покупатель"
		Элементы.Сеть.Доступность = Истина;
		Если Параметры.РежимРаботы <> "Поставщик" Тогда
			//и если сейчас мы не поставщик, то тогда уже постараемся работать как сеть
			Параметры.РежимРаботы = "Покупатель";
			Если Не Элементы.Поставщик.Пометка Тогда
				Элементы.Сеть.Пометка = Истина;
			КонецЕсли;	
		КонецЕсли;	
		
	Иначе
		Элементы.Сеть.Доступность = Ложь;
		Элементы.Сеть.Пометка = Ложь;
	КонецЕсли;	
	
	Возврат "";
	
КонецФункции

&НаСервереБезКонтекста
Функция ВерсияПоддерживаетФоновыйОбмен(СсылкаНаСохраненныйМодуль)
	
	Попытка
		ТекВерсия =  СокрЛП(СсылкаНаСохраненныйМодуль.Версия);	
		
		ПерваяЦифраВерсии = Число(Лев(ТекВерсия,1));
		ВтороеЧислоВерсии = Число(Сред(ТекВерсия,3,2));
		ТретьеЧислоВерсии = Число(Сред(ТекВерсия,6,3));
		
		Если ПерваяЦифраВерсии=4 И ВтороеЧислоВерсии>=3 И ТретьеЧислоВерсии>=17 Тогда
			Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли
		
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции // ДополнительнаяОбработкаСсылка()

&НаКлиенте
Процедура ВыполнитьОбмен(Команда)
	
	Элементы.ВыполнитьОбмен.ЦветФона = новый Цвет(211, 242, 211);
	Элементы.ВыполнитьОбмен.Заголовок = "Обмен с сервером";
	
	Состояние("Выполняю обмен...",30,,Элементы.ВыполнитьОбмен.Картинка);
	
	ВыполнитьОбменСервер();
	ПодписатьДокументыВДиадок();
	ОтправитьИзвещенияОПолучении();
	ЗаполнитьСписокЗадачКлиент();
	#Если НЕ ВебКлиент Тогда
		Сигнал();
	#КонецЕсли
		
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьОбменВФоне(УдалосьЗапуститьВФоне)
	ТекстЗаглушки = "Обмен выполняется в фоне.
	|Вы можете продолжить работу с другими задачами или даже в другом приложении.
	|
	|По завершении обмена, мы подадим звуковой сигнал.";
	
	УдалосьЗапуститьВФоне = Истина;
	ПараметрыКоманды = Новый Структура;
	ПараметрыКоманды.Вставить("ДополнительнаяОбработкаСсылка",	ДополнительнаяОбработкаСсылка);
	ПараметрыКоманды.Вставить("СопровождающийТекст",			ТекстЗаглушки);
	ПараметрыКоманды.Вставить("Заголовок",						"Контур.EDI обмен выполняется");
	ПараметрыКоманды.Вставить("ИдентификаторКоманды",			"ИнтерактивныйФоновыйОбмен");
	
	Попытка
		
		Если Число(Лев(ВерсияБСП,1))=2 И Число(Сред(ВерсияБСП,3,1))>=3 Тогда
			
			Элементы.ВыполнитьОбмен.Доступность = Ложь;
			Элементы.ВыполнитьОбмен.ЦветФона = WebЦвета.ДымчатоБелый;
			Элементы.ВыполнитьОбмен.Заголовок = "Выполняется...";
			
			Если Число(Сред(ВерсияБСП,5,1))>=4 Тогда
				
				ИмяПроцедуры = "ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьКомандуВФоне";
				Задание = Неопределено;
				Выполнить("Задание = ДополнительныеОтчетыИОбработкиВызовСервера.ЗапуститьДлительнуюОперацию(Этаформа.УникальныйИдентификатор, ПараметрыКоманды)");
				
				ПараметрыФормы = новый Структура();
				ПараметрыФормы.Вставить("ИдентификаторЗадания", Задание.ИдентификаторЗадания);
				ПараметрыФормы.Вставить("ВыводитьОкноОжидания", Истина);
				ПараметрыФормы.Вставить("ТекстСообщения", 		ТекстЗаглушки);
				МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("ОбщаяФорма.ДлительнаяОперация", "", ПараметрыФормы, ЭтаФорма, "ВызовОбновленияСпискаПослеФоновогоОбмена", ЭтаФорма, , РежимОткрытияОкнаФормы.Независимый);
				
			ИначеЕсли Сред(ВерсияБСП,5,1)<4 Тогда
				
				ПараметрыФормыДлО = Новый Структура;
				ПараметрыФормыДлО.Вставить("ПараметрыЗапускаФоновогоЗадания", ПараметрыКоманды);
				МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("ОбщаяФорма.ДлительнаяОперацияДополнительныхОтчетовИОбработок", "", ПараметрыФормыДлО, ЭтаФорма, "ВызовОбновленияСпискаПослеФоновогоОбмена", ЭтаФорма, , РежимОткрытияОкнаФормы.Независимый);
				
			Конецесли;
			ЭтаФорма.Активизировать();
			
		Конецесли;
	Исключение
		УдалосьЗапуститьВФоне = Ложь;
		Элементы.ВыполнитьОбмен.ЦветФона = новый Цвет(211, 242, 211);
		Элементы.ВыполнитьОбмен.Доступность = Истина;
		Элементы.ВыполнитьОбмен.Заголовок = "Обмен с сервером";
	КонецПопытки;
	
	
КонецПроцедуры // ЗапуститьОбменВФоне()

&НаКлиенте
Процедура ВызовОбновленияСпискаПослеФоновогоОбмена(Параметр=Неопределено,ДопПараметр=Неопределено) Экспорт
	
	Состояние("Фоновый обмен завершен",100,,Элементы.ВыполнитьОбмен.Картинка);
	Элементы.ВыполнитьОбмен.Заголовок = "Обмен с сервером";
	Элементы.ВыполнитьОбмен.Доступность = Истина;
	Элементы.ВыполнитьОбмен.ЦветФона = новый Цвет(211, 242, 211);
	
	ПроверитьНеобходимостьПодписанияДокументовВДиадок();
	ПодписатьДокументыВДиадок();
	ПроверитьНеобходимостьОтправкиИзвещенийОПолучении();
	ОтправитьИзвещенияОПолучении();
	ЗаполнитьСписокЗадачКлиент();
	
	#Если НЕ ВебКлиент Тогда
		Сигнал();
	#КонецЕсли
	
КонецПроцедуры // ВызовОбновленияСпискаПослеФоновогоОбмена()

&НаСервере
Процедура ВыполнитьОбменСервер()
	
	ПараметрыОбмена = МодульОбъекта().ПараметрыОбменаССервером();
	ПараметрыОбмена.Вставить("ТипыСообщенийОтбор", ПолучитьТипыСообщенийОтбораСервер());
	МодульОбъекта().ВыполнитьОбменССервером(, ПараметрыОбмена);
	ПроверитьНеобходимостьПодписанияДокументовВДиадок();
	ПроверитьНеобходимостьОтправкиИзвещенийОПолучении();
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьНеобходимостьОтправкиИзвещенийОПолучении()
	
	НужноОтправитьИзвещенияОПолучении = МодульОбъекта().НужноОтправитьИзвещенияОПолучении();
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьНеобходимостьПодписанияДокументовВДиадок()
	
	НужноПодписатьДокументыВДиадок = МодульОбъекта().НужноПодписатьДокументыВДиадок(НастройкаПериода.ДатаНачала, НастройкаПериода.ДатаОкончания);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьИзвещенияОПолучении()
	
	Если НужноОтправитьИзвещенияОПолучении Тогда
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ОбъектПараметрыКлиентСервер", 	Объект.ПараметрыКлиентСервер);
		
		МодульОбменКлиент().ОтправитьИзвещенияОПолучении();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьДокументыВДиадок()

	Если НужноПодписатьДокументыВДиадок Тогда
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ОбъектПараметрыКлиентСервер", Объект.ПараметрыКлиентСервер);
		
		МодульОбменКлиент().ПодписатьПереданныеСообщения();
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ФильтрБыстрыеОтборыНажатие(Команда)
	БыстрыеОтборыСвернуть("");
	ТекущийЭлемент = Элементы.ТабСообщения;
КонецПроцедуры

&НаКлиенте
Процедура ФильтрПартнерыНажатие(Команда)
	ПартнерыСвернуть("");
	ТекущийЭлемент = Элементы.ТабСообщения;
КонецПроцедуры

&НаКлиенте
Процедура БыстрыеОтборыСвернуть(Кнопка)
	Параметры.СверткаБыстрыхОтборов = Не Параметры.СверткаБыстрыхОтборов;
	//Кнопка.Текст = ?(Кнопка.Пометка, "Типы сообщений", "Типы сообщений");
	Если Параметры.СверткаБыстрыхОтборов Тогда
		Элементы.БыстрыйОтборРазвернуть.Видимость = Истина;
		Элементы.БыстрыйОтборРазвернуть1.Видимость = Ложь;
		Элементы.ОтборГруппаБыстрыеОтборы.Видимость=Истина;
	Иначе
		Элементы.БыстрыйОтборРазвернуть.Видимость = Ложь;
		Элементы.БыстрыйОтборРазвернуть1.Видимость = Истина;
		Элементы.ОтборГруппаБыстрыеОтборы.Видимость=Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПартнерыСвернуть(Кнопка)
	Параметры.СверткаПартнеры = Не Параметры.СверткаПартнеры;
	//Кнопка.Текст = ?(Кнопка.Пометка, "Типы сообщений", "Типы сообщений");
	Если Параметры.СверткаПартнеры Тогда
		Элементы.ОтборПартнерыРазвернуть.Видимость = Истина;
		Элементы.ОтборПартнерыРазвернуть1.Видимость = Ложь;
		Элементы.ОтборПартнеры.Видимость=Истина;
	Иначе
		Элементы.ОтборПартнерыРазвернуть.Видимость = Ложь;
		Элементы.ОтборПартнерыРазвернуть1.Видимость = Истина;
		Элементы.ОтборПартнеры.Видимость=Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТипыСообщенийСвернуть(Кнопка)
	Параметры.СверткаТипыСообщений = Не Параметры.СверткаТипыСообщений;
	//Кнопка.Текст = ?(Кнопка.Пометка, "Типы сообщений", "Типы сообщений");
	Если Параметры.СверткаТипыСообщений Тогда
		Элементы.ОтборТипыСообщенийРазвернуть.Видимость = Истина;
		Элементы.ОтборТипыСообщенийРазвернуть1.Видимость = Ложь;
		Элементы.ОтборТипыСообщений.Видимость=Истина;
	Иначе
		Элементы.ОтборТипыСообщенийРазвернуть.Видимость = Ложь;
		Элементы.ОтборТипыСообщенийРазвернуть1.Видимость = Истина;
		Элементы.ОтборТипыСообщений.Видимость=Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТребуемыеДействияСвернуть(Кнопка)
	Параметры.СверткаТребуемыеДействия = Не Параметры.СверткаТребуемыеДействия;
	//Кнопка.Текст = ?(Кнопка.Пометка, "Типы сообщений", "Типы сообщений");
	Если Параметры.СверткаТребуемыеДействия Тогда
		Элементы.ОтборТребуемыеДействияРазвернуть.Видимость = Истина;
		Элементы.ОтборТребуемыеДействияРазвернуть1.Видимость = Ложь;
		Элементы.ОтборТребуемыеДействия.Видимость=Истина;
	Иначе
		Элементы.ОтборТребуемыеДействияРазвернуть.Видимость = Ложь;
		Элементы.ОтборТребуемыеДействияРазвернуть1.Видимость = Истина;
		Элементы.ОтборТребуемыеДействия.Видимость=Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтборПартнерыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СохранитьНастройкиГлавнойФормы();
	ТекДанные = Элемент.ТекущиеДанные;
	
	Если НЕ ТекДанные = Неопределено Тогда
		
		Если Поле.Имя = "ОтборПартнерыИзменить" Тогда
			
			ВвестиНового=Ложь;
			Состояние("Открываю карточку партнера",50,,Элементы.Настройки.Картинка);
			ОткрытьФормуРедактированияПартнера(ВвестиНового);	
			
		ИначеЕсли Поле.Имя = "ОтборПартнерыЗначение" Тогда
			Для Каждого Стр Из ОтборПартнеры Цикл
				Стр.Пометка = (Стр.Значение=ТекДанные.Значение);
			КонецЦикла;
			
			ПодключитьОбработчикОжидания("ЗаполнитьСписокЗадачКлиент",0.1,Истина);

		КонецЕсли;
		
	КонецЕсли;
	
	СтандартнаяОбработка=Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписокЗадачКлиент()
	
	Состояние("Строю список задач...",50,,Элементы.ОбновитьСписок.Картинка);
	
	//Запомним положение курсора
	ТекСтрока = Элементы.ТабСообщения.ТекущиеДанные;
	Если НЕ ТекСтрока = Неопределено Тогда  //м.б. открытие модуля
		Параметры.ТекСообщение=Элементы.ТабСообщения.ТекущиеДанные.СообщениеСсылка;
		Параметры.ТекДокумент=Элементы.ТабСообщения.ТекущиеДанные.Документ;
	иначе
		Параметры.ТекСообщение=Неопределено;
		Параметры.ТекДокумент=Неопределено;
	КонецЕсли;
	
    Объект.ТабСообщения.Очистить();
	
	ЗаполнитьСписокЗадачСервер();
	ПроверитьРодительскиеФлажкиДереваФильтров();
	РазвернутьДеревоФильтровКлиент();
	УстановитьЗаголовокФормы_КонтурEDI();
	
	Состояние("Обновлено!",,"Список задач актуализирован",Элементы.ВыполнитьВыбранныеЗадачи.Картинка);
	
КонецПроцедуры
 
//общая точка входа для заполнения всего и всем
//дальше разбивается на разные блоки
&НаСервере
Процедура ЗаполнитьСписокЗадачСервер()
	
	//Если НастройкаОформленияТребуемогоДействия.Количество()=0 Тогда
	//НастройкаОформленияТребуемогоДействия.Загрузить(МодульОбъекта().ПолучитьТаблицуТребуемыхДействий(Параметры.РежимРаботы,ПоказыватьЗавершенные));
	//КонецЕсли; 
	
	// НастройкаОформленияТребуемогоДействия - и так заполняется в ключевые моменты (изменения настроек модуля и режима поставщик-сеть)
	
	//Непосредственно сборка списка
	СобратьТаблицуСообщенийИЗадач();
	
	//действия после обновления списка
	ДатаПоследнегоВыполненияОбмена = МодульОбъекта().ПолучитьКонстантуEDI("ДатаПоследнегоВыполненияОбмена");
	
КонецПроцедуры

&НаСервере
Процедура СобратьТаблицуСообщенийИЗадач()
	
	//МодульОбъекта().ОбновитьКэшПартнеров(); //соответствия контрагентов EDIпартнерам и настройки цепочек
	//все равно вызовется далее в ИнициализироватьЗапросСКэшемПартнеров
	
	//поместим сюда кэш партнеров
	//ЗапросСКэшемПартнеров = ИнициализироватьЗапросСКэшемПартнеров(ложь); //ВнешнееХранилище - ложь
	//давно потеряло актуальность МО ПостроитьСписокЗадач_Сервер - вполне самодостаточна
	
	//подготовим параметры для заполнения списка задач
	НачалоПериода = НастройкаПериода.ДатаНачала;
	Если Не ЗначениеЗаполнено(НастройкаПериода.ДатаОкончания) Тогда
		КонецПериода = КонецДня(Дата("39990101"));
	Иначе	
		КонецПериода = КонецДня(НастройкаПериода.ДатаОкончания);
	КонецЕсли;	
	
	МассивПартнеров = ПолучитьПартнеровОтбораСервер();
	МассивТиповСообщений = ПолучитьТипыСообщенийОтбораСервер();
	МассивТребуемыхДействий = ПолучитьТребуемыеДействияОтбораСервер();
	МассивОрганизаций = ПолучитьОрганизацииОтбораСервер();
	
	МодульОбъекта().ОбработкаСобытияПодключаемогоМодуля("ПередОбновлениемСпискаЗадач");
	
	//заполним список задач
	Метрика_ИдЗамера = МодульОбъекта().Метрика_НовыйУИД();
	МодульОбъекта().Метрика_НачатьЗамер(Метрика_ИдЗамера, "ПостроитьСписокЗадач_Сервер", , ИмяФормыКраткое(ИмяФормы));
	ПодсчитанныеКоличестваВОтборах = МодульОбъекта().ПостроитьСписокЗадач_Сервер(Параметры.РежимРаботы,
																					НачалоПериода,
																					КонецПериода,
																					МассивПартнеров,
																					МассивТиповСообщений,
																					МассивТребуемыхДействий,
																					МассивОрганизаций,
																					ПоказыватьЗавершенные);
																					
	Метрика_Соответствие = Новый Соответствие;
	Метрика_Соответствие.Вставить("РежимРаботы", 						Параметры.РежимРаботы);
	Метрика_Соответствие.Вставить("НачалоПериода", 						НачалоПериода);
	Метрика_Соответствие.Вставить("КонецПериода", 						КонецПериода);
	Метрика_Соответствие.Вставить("МассивОрганизаций", 					МассивОрганизаций);
	Метрика_Соответствие.Вставить("МассивПартнеров", 					МассивПартнеров);
	Метрика_Соответствие.Вставить("МассивТиповСообщений", 				МассивТиповСообщений);
	Метрика_Соответствие.Вставить("МассивТребуемыхДействий", 			МассивТребуемыхДействий);
	Метрика_Соответствие.Вставить("КоличествоСтрок", 					МодульОбъекта().ТабСообщения.Количество());
	Метрика_Соответствие.Вставить("ПоказыватьЗавершенные", 				ПоказыватьЗавершенные);
	Метрика_Соответствие.Вставить("ПодсчитанныеКоличестваВОтборах",		ПодсчитанныеКоличестваВОтборах);
	
	Метрика_Переменные = МодульОбъекта().Метрика_ПодготовитьПеременныеДля("ПостроитьСписокЗадач_Сервер", Метрика_Соответствие);	
	МодульОбъекта().Метрика_ЗакончитьЗамер(Метрика_ИдЗамера, "ПостроитьСписокЗадач_Сервер", , ИмяФормыКраткое(ИмяФормы), , Метрика_Переменные);
	
	ПроставитьКоличествоВОтборах(ПодсчитанныеКоличестваВОтборах);
	
	МодульОбъекта().ОбработкаСобытияПодключаемогоМодуля("ПослеОбновленияСпискаЗадач");
	
	ЗначениеВРеквизитФормы(МодульОбъекта().ТабСообщения,"Объект.ТабСообщения");   //не забыть вывести в форму
	
	Для каждого ДанныеСтроки ИЗ Объект.ТабСообщения Цикл
		Если ЗначениеЗаполнено(ДанныеСтроки.ТребуемоеДействие) Тогда
			
			//		НастройкаОформленияТребуемогоДействия = НастройкаОтображенияСписка.ОформлениеКолонокПоЗначению.ТребуемоеДействие;
			
			НайденныеСтроки = НастройкаОформленияТребуемогоДействия.НайтиСтроки(Новый Структура("Значение",ДанныеСтроки.ТребуемоеДействие));
			Если НайденныеСтроки.Количество()=1 Тогда
				
				ДанныеСтроки.Иконка=НайденныеСтроки[0].Иконка;
				
				//еще цвет!!! и гиперссылка  но через элемент
				
			КонецЕсли;
		КонецЕсли;		
	КонецЦикла;

	ВосстановитьПоложениеТекСтрокиВСписке();
КонецПроцедуры

&НаСервере
Процедура ПроставитьКоличествоВОтборах(ПодсчитанныеКоличестваВОтборах)
	
	Если ПодсчитанныеКоличестваВОтборах<>Неопределено Тогда
		
		ТабКоличествоПартнеров = ПодсчитанныеКоличестваВОтборах.ТабКоличествоПартнеров;
		СтрокаГруппыПартнеров = ПолучитьРодителяГруппыНастроекИзДанныхФормы("Партнеры");
		Если ТипЗНЧ(СтрокаГруппыПартнеров)=Тип("ДанныеФормыЭлементДерева") Тогда
			Для Каждого СтрокаОтбора Из СтрокаГруппыПартнеров.ПолучитьЭлементы() Цикл
				СтрокаОтбора.Количество = "";//обнулим сначала
				НайденнаяСтрока = ТабКоличествоПартнеров.Найти(СтрокаОтбора.Значение);
				Если НайденнаяСтрока<>Неопределено Тогда
					СтрокаОтбора.Количество = "("+Формат(НайденнаяСтрока.Количество,"ЧДЦ=0")+")";
				КонецЕсли;	
			КонецЦикла;	
		КонецЕсли;
		
		ТабКоличествоСообщений = ПодсчитанныеКоличестваВОтборах.ТабКоличествоСообщений;
		СтрокаГруппыТиповСообщений = ПолучитьРодителяГруппыНастроекИзДанныхФормы("ТипыСообщений");
		Для Каждого СтрокаОтбора Из СтрокаГруппыТиповСообщений.ПолучитьЭлементы() Цикл
			СтрокаОтбора.Количество = "";//обнулим сначала
			НайденнаяСтрока = ТабКоличествоСообщений.Найти(СтрокаОтбора.Значение);
			Если НайденнаяСтрока<>Неопределено Тогда
				СтрокаОтбора.Количество = "("+Формат(НайденнаяСтрока.Количество,"ЧДЦ=0")+")";
			КонецЕсли;	
		КонецЦикла;	
		
		ТабКоличествоТребуемыхДействий = ПодсчитанныеКоличестваВОтборах.ТабКоличествоТребуемыхДействий;
		СтрокаГруппыДействий = ПолучитьРодителяГруппыНастроекИзДанныхФормы("ТребуемыеДействия");
		Для Каждого СтрокаОтбора1 Из СтрокаГруппыДействий.ПолучитьЭлементы() Цикл
			СтрокаОтбора1.Количество = "";
			СтрОтбора1КоличествоЧисло = 0;
			Для Каждого СтрокаОтбора2 Из СтрокаОтбора1.ПолучитьЭлементы() Цикл
				СтрокаОтбора2.Количество = "";//обнулим сначала
				НайденнаяСтрока = ТабКоличествоТребуемыхДействий.Найти(СтрокаОтбора2.Значение);
				Если НайденнаяСтрока<>Неопределено Тогда
					СтрокаОтбора2.Количество = "("+Формат(НайденнаяСтрока.Количество,"ЧДЦ=0")+")";
					СтрОтбора1КоличествоЧисло = СтрОтбора1КоличествоЧисло + НайденнаяСтрока.Количество;
					СтрокаОтбора1.Количество = "("+Формат(СтрОтбора1КоличествоЧисло,"ЧДЦ=0")+")";
				КонецЕсли;	
			КонецЦикла;
		КонецЦикла;	
		
		ТабКоличествоОрганизаций = ПодсчитанныеКоличестваВОтборах.ТабКоличествоОрганизаций;
		СтрокаГруппыОрганизаций = ПолучитьРодителяГруппыНастроекИзДанныхФормы("Организации");
		Для Каждого СтрокаОтбора Из СтрокаГруппыОрганизаций.ПолучитьЭлементы() Цикл
			СтрокаОтбора.Количество = "";//обнулим сначала
			НайденнаяСтрока = ТабКоличествоОрганизаций.Найти(СтрокаОтбора.Значение);
			Если НайденнаяСтрока<>Неопределено Тогда
				СтрокаОтбора.Количество = "("+Формат(НайденнаяСтрока.Количество,"ЧДЦ=0")+")";
			КонецЕсли;	
		КонецЦикла;	
		
	КонецЕсли;	

КонецПроцедуры // ПроставитьКоличествоВОтборах()

// Восстановим положение курсора
//тек строку установим на серверной части чтобы избежать перескоков строк
&НаСервере
Процедура ВосстановитьПоложениеТекСтрокиВСписке()
	СтараяСтрокаНайдена = Ложь;
	СтрОтбора=     Новый Структура("СообщениеСсылка", Параметры.ТекСообщение);
	НайденныеСтроки=Объект.ТабСообщения.НайтиСтроки(СтрОтбора);
	Если НайденныеСтроки.Количество()>0 Тогда 
		Идентификатор=НайденныеСтроки[0].ПолучитьИдентификатор();
		Элементы.ТабСообщения.ТекущаяСтрока=Идентификатор;
		СтараяСтрокаНайдена = Истина;
	КонецЕсли;
	
	Если Не СтараяСтрокаНайдена Тогда 
		СтрОтбора=     Новый Структура("Документ", Параметры.ТекДокумент);
		НайденныеСтроки=Объект.ТабСообщения.НайтиСтроки(СтрОтбора);
		Если НайденныеСтроки.Количество()>0 Тогда 
			Идентификатор=НайденныеСтроки[0].ПолучитьИдентификатор();
			Элементы.ТабСообщения.ТекущаяСтрока=Идентификатор;
			СтараяСтрокаНайдена = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

//Создадим запрос, в котором уже будут временные таблицы партнеров и их цепочек сообщений
&НаСервере
Функция ИнициализироватьЗапросСКэшемПартнеров(ДляВнешнегоХранилища = Ложь)
	
	//гарантированно обновится далее в момент МодульОбъекта()
	//МодульОбъекта().ОбновитьКэшПартнеров(); //соответствия контрагентов EDIпартнерам и настройки цепочек
	
	Если ДляВнешнегоХранилища Тогда
		//Запрос = СоединениеСХранилищем.NewObject("Запрос");
		//Запрос.МенеджерВременныхТаблиц  = СоединениеСХранилищем.NewObject("МенеджерВременныхТаблиц");
		//
		//СтруктураКолонокПреобразуемыхВСсылки = Новый Структура(
		//					"Партнер",
		//					"КонтурEDI_ДополнительныеСправочники"
		//					);
		//
		//Запрос.УстановитьПараметр("ТаблицаКэшПартнеров",				ПоместитьТаблицуВоВнешнююБазу(ТаблицаКэшПартнеров,СтруктураКолонокПреобразуемыхВСсылки));
		//Запрос.УстановитьПараметр("ТаблицаКэшПартнеровКонтрагентов",	ПоместитьТаблицуВоВнешнююБазу(ТаблицаКэшПартнеровКонтрагентов,СтруктураКолонокПреобразуемыхВСсылки));
		//
	Иначе
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		Запрос.УстановитьПараметр("ТаблицаКэшПартнеров",				МодульОбъекта().ТаблицаКэшПартнеров);
		Запрос.УстановитьПараметр("ТаблицаКэшПартнеровКонтрагентов",	МодульОбъекта().ТаблицаКэшПартнеровКонтрагентов);
		
	КонецЕсли;
	Запрос.УстановитьПараметр("Партнеры", ПолучитьПартнеровОтбораСервер());
	
	Запрос.Текст = 
	"Выбрать * Поместить ТаблицаКэшПартнеров из &ТаблицаКэшПартнеров как _ТаблицаКэшПартнеров 
	| Где _ТаблицаКэшПартнеров.Партнер в (&Партнеры) 
	|Индексировать по Партнер,СообщениеБлокирующееORDERS,СообщениеБлокирующееORDRSP,СообщениеБлокирующееDESADV,СообщениеБлокирующееRECADV,СообщениеБлокирующееINVOIC;
	|Выбрать * Поместить ТаблицаКэшПартнеровКонтрагентов из &ТаблицаКэшПартнеровКонтрагентов как _ТаблицаКэшПартнеровКонтрагентов
	| Где _ТаблицаКэшПартнеровКонтрагентов.Партнер в (&Партнеры) 
	| Индексировать по Контрагент, Партнер,СообщениеБлокирующееORDERS,СообщениеБлокирующееORDRSP,СообщениеБлокирующееDESADV,СообщениеБлокирующееRECADV,СообщениеБлокирующееINVOIC";
	//может, и остальные поля индексировать? Надо глянуть, что в соединениях еще участвует
	
	Если ДляВнешнегоХранилища Тогда
		//СоединениеСХранилищем.ВыполнитьЗапросСОтладкой(Запрос);
		Запрос.Выполнить();
	Иначе
		Запрос.Выполнить();
	КонецЕсли;	
	
	//здесь же заполним даты для отбора
	//ну и отборы по партнерам удобно сюда же закинуть
	Запрос.УстановитьПараметр("НачалоПериода",НастройкаПериода.ДатаНачала);
	Если Не ЗначениеЗаполнено(НастройкаПериода.ДатаОкончания) Тогда
		Запрос.УстановитьПараметр("КонецПериода",КонецДня(Дата("39990101")));
	Иначе	
		Запрос.УстановитьПараметр("КонецПериода",КонецДня(НастройкаПериода.ДатаОкончания));
	КонецЕсли;	
	
	Возврат Запрос;
	
КонецФункции	

&НаСервере
//получает массив требуемых действий, которые отмечены флажками в отборе левой панели
//верхние строки не включает
Функция ПолучитьТребуемыеДействияОтбораСервер()
	
	МассивТД = Новый Массив;
	
	СтрокаГруппыДействий = ПолучитьРодителяГруппыНастроекИзДанныхФормы("ТребуемыеДействия");
	
	Для Каждого Стр1 Из СтрокаГруппыДействий.ПолучитьЭлементы() Цикл
		Для Каждого Стр2 Из Стр1.ПолучитьЭлементы() Цикл
			Если Стр2.Пометка Тогда
				МассивТД.Добавить(Стр2.Значение);
			КонецЕсли;	
		КонецЦикла;	
	КонецЦикла;	
	
	Возврат МассивТД;
	
КонецФункции

&НаКлиенте
Функция ПолучитьТребуемыеДействияОтбораКлиент(ДляСохраненияНастроек = Ложь)
	
	МассивТД = Новый Массив;
	
	СтрокаГруппыДействий = ПолучитьРодителяГруппыНастроекИзДанныхФормыКлиент("ТребуемыеДействия");
	
	ЕстьПропуски = Ложь;
	
	Для Каждого Стр1 Из СтрокаГруппыДействий.ПолучитьЭлементы() Цикл
		Для Каждого Стр2 Из Стр1.ПолучитьЭлементы() Цикл
			Если Стр2.Пометка Тогда
				МассивТД.Добавить(Стр2.Значение);
			Иначе
				ЕстьПропуски = Истина;	
			КонецЕсли;	
		КонецЦикла;	
	КонецЦикла;	
	
	Если ДляСохраненияНастроек Тогда
		
		Если НЕ ЕстьПропуски Тогда
			МассивТД = Новый Массив;
		КонецЕсли;
		
		Возврат МассивТД;
		
	КонецЕсли;
	
	Возврат МассивТД;
	
КонецФункции

//получает массив партнеров, которые отмечены флажками в отборе левой панели
&НаСервере
Функция ПолучитьПартнеровОтбораСервер()
	
	МассивТД = Новый Массив;
	
	СтрокаГруппыПартнеров = ПолучитьРодителяГруппыНастроекИзДанныхФормы("Партнеры");
	
	Для Каждого Стр Из СтрокаГруппыПартнеров.ПолучитьЭлементы() Цикл
		Если Стр.Пометка Тогда
			МассивТД.Добавить(Стр.Значение);
		КонецЕсли;	
	КонецЦикла;	
	
	Возврат МассивТД;
	
КонецФункции

&НаКлиенте
Функция ПолучитьПартнеровОтбораКлиент(ДляСохраненияНастроек = Ложь)
	
	МассивТД = Новый Массив;
	
	СтрокаГруппыПартнеров = ПолучитьРодителяГруппыНастроекИзДанныхФормыКлиент("Партнеры");
	
	ЕстьПропуски = Ложь;
	
	Для Каждого Стр Из СтрокаГруппыПартнеров.ПолучитьЭлементы() Цикл
		Если Стр.Пометка Тогда
			МассивТД.Добавить(Стр.Значение);
		Иначе
			ЕстьПропуски = Истина;	
		КонецЕсли;	
	КонецЦикла;	
	
	Если ДляСохраненияНастроек Тогда
		
		Если НЕ ЕстьПропуски Тогда
			МассивТД = Новый Массив;
		КонецЕсли;
		
		Возврат МассивТД;
		
	КонецЕсли;
		
	Возврат МассивТД;
	
КонецФункции

//получает массив Организаций, которые отмечены флажками в отборе левой панели
&НаСервере
Функция ПолучитьОрганизацииОтбораСервер()
	
	МассивТД = Новый Массив;
	
	СтрокаГруппыПартнеров = ПолучитьРодителяГруппыНастроекИзДанныхФормы("Организации");
	
	Для Каждого Стр Из СтрокаГруппыПартнеров.ПолучитьЭлементы() Цикл
		Если Стр.Пометка Тогда
			МассивТД.Добавить(Стр.Значение);
		КонецЕсли;	
	КонецЦикла;	
	
	Возврат МассивТД;
	
КонецФункции

&НаКлиенте
Функция ПолучитьОрганизацииОтбораКлиент(ДляСохраненияНастроек = Ложь)
	
	МассивТД = Новый Массив;
	
	СтрокаГруппыПартнеров = ПолучитьРодителяГруппыНастроекИзДанныхФормыКлиент("Организации");
	
	ЕстьПропуски = Ложь;
	
	Для Каждого Стр Из СтрокаГруппыПартнеров.ПолучитьЭлементы() Цикл
		Если Стр.Пометка Тогда
			МассивТД.Добавить(Стр.Значение);
		Иначе
			ЕстьПропуски = Истина;	
		КонецЕсли;	
	КонецЦикла;	
	
	Если ДляСохраненияНастроек Тогда
		
		Если НЕ ЕстьПропуски Тогда
			МассивТД = Новый Массив;
		КонецЕсли;
		
		Возврат МассивТД;
		
	КонецЕсли;
		
	Возврат МассивТД;
	
КонецФункции

//получает массив типов сообщений, которые отмечены флажками в отборе левой панели
&НаСервере
Функция ПолучитьТипыСообщенийОтбораСервер()
	
	МассивТД = Новый Массив;
	
	СтрокаГруппыТиповСообщений = ПолучитьРодителяГруппыНастроекИзДанныхФормы("ТипыСообщений");
	
	Для Каждого Стр Из СтрокаГруппыТиповСообщений.ПолучитьЭлементы() Цикл
		Если Стр.Пометка Тогда
			МассивТД.Добавить(Стр.Значение);
		КонецЕсли;	
	КонецЦикла;	
	
	Возврат МассивТД;
	
КонецФункции

&НаКлиенте
Функция ПолучитьТипыСообщенийОтбораКлиент(ДляСохраненияНастроек = Ложь)
	
	МассивТД = Новый Массив;
	
	СтрокаГруппыТиповСообщений = ПолучитьРодителяГруппыНастроекИзДанныхФормыКлиент("ТипыСообщений");
	
	ЕстьПропуски = Ложь;
	
	Для Каждого Стр Из СтрокаГруппыТиповСообщений.ПолучитьЭлементы() Цикл
		Если Стр.Пометка Тогда
			МассивТД.Добавить(Стр.Значение);
		Иначе
			ЕстьПропуски = Истина;	
		КонецЕсли;	
	КонецЦикла;	
	
	Если ДляСохраненияНастроек Тогда
		
		Если НЕ ЕстьПропуски Тогда
			МассивТД = Новый Массив;
		КонецЕсли;
		
		Возврат МассивТД;
		
	КонецЕсли;
	
	Возврат МассивТД;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьФормуРедактированияПартнера(ВвестиНового)
	
	СохранитьНастройкиГлавнойФормы();
	ТекСтрока = Элементы.ДеревоФильтров.ТекущиеДанные;
	
	Если ВвестиНового = Истина или НЕ ТекСтрока = Неопределено Тогда
		Если ЗначениеЗаполнено(ТекСтрока.Значение) Тогда
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("СсылкаНаПартнера", ТекСтрока.Значение);
			
			МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Партнеры_ЭлементУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбработчикПослеРедактированияПартнеров", ЭтаФорма);
			
		КонецЕсли;
	иначе
		ВызватьИсключение("Что-то не так с активностью строки партнера или вводом нового");//начинаем добавлять в модуль элементы защитного программирования
	КонецЕсли;
	
КонецПроцедуры
 
&НаКлиенте
Процедура ДобавитьПартнера(Команда)
	
	СохранитьНастройкиГлавнойФормы();
	Состояние("Запрашиваю данные по сетям...",30,,Элементы.ВыполнитьОбмен.Картинка);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОткрытаКакДобавлениеПартнера", Истина);
	
	Если Параметры.РежимРаботы = "Поставщик" Тогда
		
		МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Сервис_СтартовыйПомощникУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбработчикПослеРедактированияПартнеров", ЭтаФорма);
		
	Иначе
		
		МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Партнеры_СписокУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбработчикПослеРедактированияПартнеров", ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьБыстрыеОтборы(Команда)
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура ФильтрТипыСообщенийНажатие(Команда)
	ТипыСообщенийСвернуть("");
	ТекущийЭлемент = Элементы.ТабСообщения;

КонецПроцедуры

&НаКлиенте
Процедура ОтборПартнерыПередУдалением(Элемент, Отказ)
	Отказ = истина;
КонецПроцедуры

&НаКлиенте
Процедура ОтборТипыСообщенийПередУдалением(Элемент, Отказ)
	Отказ = истина;
КонецПроцедуры

&НаКлиенте
Процедура ОтборПартнерыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ = истина;
КонецПроцедуры

&НаКлиенте
Процедура ОтборТипыСообщенийПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ = истина;
КонецПроцедуры

&НаКлиенте
Процедура ФильтрТребуемыеДействияНажатие(Команда)
	ТребуемыеДействияСвернуть("");
	ТекущийЭлемент = Элементы.ТабСообщения;
КонецПроцедуры

&НаКлиенте
Процедура ОтборТребуемыеДействияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекДанные = Элемент.ТекущиеДанные;
	
	Если НЕ ТекДанные = Неопределено Тогда
		
		Если Поле.Имя = "ОтборТребуемыеДействияЗначение" Тогда
			Для Каждого Стр Из ОтборТребуемыеДействия.ПолучитьЭлементы() Цикл
				Стр.Пометка = (Стр.Значение=ТекДанные.Значение);
				
				Для Каждого Стр2 Из Стр.ПолучитьЭлементы() Цикл
					Стр2.Пометка = ((Стр2.Значение=ТекДанные.Значение) или Стр.Пометка);     //сбросим пометку везде за исключенем когда нам тыкнули в группу
				КонецЦикла;
			КонецЦикла;
			
			ПодключитьОбработчикОжидания("ЗаполнитьСписокЗадачКлиент",0.1,Истина);
		КонецЕсли;
	КонецЕсли;
	СтандартнаяОбработка=Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ОтборТребуемыеДействияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ОтборТребуемыеДействияПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ОтборТребуемыеДействияПометкаПриИзменении(Элемент)
	
	ТекДанные = Элементы.ОтборТребуемыеДействия.ТекущиеДанные;
	УстановленныйФлажок=ТекДанные.Пометка;
	
	Для Каждого Стр Из ОтборТребуемыеДействия.ПолучитьЭлементы() Цикл
		Если Стр.Значение=ТекДанные.Значение и Стр.ПолучитьЭлементы().Количество()>0 Тогда 
			Для Каждого Стр2 Из Стр.ПолучитьЭлементы() Цикл
				Стр2.Пометка = УстановленныйФлажок;     //ставим так же как у предка
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	////ЗаполнитьСписокЗадачКлиент();   //не надо так)
КонецПроцедуры

&НаКлиенте
Процедура ОтборТипыСообщенийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекДанные = Элемент.ТекущиеДанные;
	
	Если НЕ ТекДанные = Неопределено Тогда
		
		Если Поле.Имя = "ОтборТипыСообщенийЗначение" Тогда
			Для Каждого Стр Из ОтборТипыСообщений Цикл
				Стр.Пометка = (Стр.Значение=ТекДанные.Значение);
			КонецЦикла;
			
			ПодключитьОбработчикОжидания("ЗаполнитьСписокЗадачКлиент",0.1,Истина);

		КонецЕсли;
		
	КонецЕсли;
	
	СтандартнаяОбработка=Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписок(Команда=неопределено,ДопПараметр=Неопределено) Экспорт //может выступать обработчиком
	ЗаполнитьСписокЗадачКлиент();
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериод(Команда)
	ОткрытьВыборПериода();
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВыборПериода()
	Диалог = Новый ДиалогРедактированияСтандартногоПериода();
	Диалог.Период = НастройкаПериода;
	
	Если Параметры.МодальностьЗапрещена Тогда 
		Выполнить("Диалог.Показать(Новый ОписаниеОповещения(""ОбработчикВыбораПериода"", ЭтаФорма,))");
	Иначе
		Если Диалог.Редактировать() Тогда 
			ОбработчикВыбораПериода(Диалог.Период,неопределено);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикВыбораПериода(Период,ДополнительныеПараметры=неопределено) Экспорт
	Если Период<>Неопределено Тогда
		Если Период.ДатаНачала > Период.ДатаОкончания 
			И ЗначениеЗаполнено(Период.ДатаОкончания) Тогда
			Предупреждение("Дата начала периода не должна превышать дату окончания периода.");
			Возврат;
		КонецЕсли;
		НастройкаПериода = Период;
		УстановитьНадписьПериода();
		ЗаполнитьСписокЗадачКлиент();
	КонецЕсли;
КонецПроцедуры // ОбработчикВыбораПериода()

&НаКлиенте
Процедура УстановитьНадписьПериода()
	
	Элементы.ВыбратьПериод.Заголовок = ПолучитьНадписьДляКнопкиВыбораПериода(НастройкаПериода.ДатаНачала, НастройкаПериода.ДатаОкончания);  
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьНадписьДляКнопкиВыбораПериода(ДатаНачала, ДатаОкончания) Экспорт
	
	Результат = "";
	
	ПоказыватьГод = Ложь;
	
	Если ЗначениеЗаполнено(ДатаНачала)
		И ЗначениеЗаполнено(ДатаОкончания) Тогда
		ПоказыватьГод = НЕ Год(ДатаНачала) = Год(ДатаОкончания);
	КонецЕсли;
	
	ДатаНачалаСтрока 		= ПолучитьПредставлениеДаты_КонтурEDI(ДатаНачала, ПоказыватьГод);
	ДатаОкончанияСтрока 	= ПолучитьПредставлениеДаты_КонтурEDI(ДатаОкончания, ПоказыватьГод);
	
	Если ЗначениеЗаполнено(ДатаНачала) Тогда
		ВерхняяСтрока 	= "с " + ДатаНачалаСтрока;
	Иначе
		ВерхняяСтрока 	= "";
	КонецЕсли;
	Если ЗначениеЗаполнено(ДатаОкончания) Тогда
		НижняяСтрока  	=  ?(ЗначениеЗаполнено(ДатаНачала), Символы.ПС, "") + "по " + ДатаОкончанияСтрока;
	Иначе
		НижняяСтрока 	= "";
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ДатаНачала)
		И НЕ ЗначениеЗаполнено(ДатаОкончания) Тогда
		ВерхняяСтрока 	= "за все время";
		НижняяСтрока 	= "";
	КонецЕсли;
	
	СтрокаВыравниватель = "";
	Если ЗначениеЗаполнено(ДатаНачала) 
		И ЗначениеЗаполнено(ДатаОкончания) Тогда
		РазницаДлины 	= СтрДлина(ВерхняяСтрока) - СтрДлина(НижняяСтрока);
		СтрокаВыравниватель = "";
		Если НЕ РазницаДлины = 0 Тогда
			Для Сч = 1 По Макс(РазницаДлины, -РазницаДлины) Цикл
				СтрокаВыравниватель = СтрокаВыравниватель + " ";		
			КонецЦикла;
			Если РазницаДлины > 0 Тогда
				НижняяСтрока = НижняяСтрока + СтрокаВыравниватель;	
			Иначе
				ВерхняяСтрока = ВерхняяСтрока + СтрокаВыравниватель;	
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Результат = ВерхняяСтрока + НижняяСтрока;  
	
	Возврат Результат;
	
КонецФункции	

&НаКлиенте
Функция ПолучитьПредставлениеДаты_КонтурEDI(Дата, ПоказыватьГод = Ложь)
	
	Если НЕ ЗначениеЗаполнено(Дата) Тогда
		Возврат "";
	КонецЕсли;
	
	ПредставлениеДатыПериода = "";
	
	ТекущийГод = Год(ТекущаяДата());
	
	ДеньДата 	= День(Дата);
	МесяцДата 	= Месяц(Дата);
	ГодДата 	= Год(Дата);
	
	ПредставлениеДатыПериода = Строка(ДеньДата) + " " + ПолучитьНазваниеМесяцаВРодительномПадеже(МесяцДата) 
								+ ?((ПоказыватьГод ИЛИ НЕ ГодДата = ТекущийГод), " " + Формат(ГодДата, "ЧГ="), "");
						
	Возврат ПредставлениеДатыПериода;					
						
КонецФункции
	
&НаКлиенте
Функция ПолучитьНазваниеМесяцаВРодительномПадеже(НомерМесяца)
	
	Если НомерМесяца = 1 Тогда
		Возврат "января";
	ИначеЕсли НомерМесяца = 2 Тогда
		Возврат "февраля";
	ИначеЕсли НомерМесяца = 3 Тогда
		Возврат "марта";
	ИначеЕсли НомерМесяца = 4 Тогда
		Возврат "апреля";
	ИначеЕсли НомерМесяца = 5 Тогда
		Возврат "мая";
	ИначеЕсли НомерМесяца = 6 Тогда
		Возврат "июня";
	ИначеЕсли НомерМесяца = 7 Тогда
		Возврат "июля";
	ИначеЕсли НомерМесяца = 8 Тогда
		Возврат "августа";
	ИначеЕсли НомерМесяца = 9 Тогда
		Возврат "сентября";
	ИначеЕсли НомерМесяца = 10 Тогда
		Возврат "октября";
	ИначеЕсли НомерМесяца = 11 Тогда
		Возврат "ноября";
	ИначеЕсли НомерМесяца = 12 Тогда
		Возврат "декабря";
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

//сформируем заголовок формы

&НаКлиенте
Процедура УстановитьЗаголовокФормы_КонтурEDI()
	
	_ПредставлениеПериода 	= ПредставлениеПериода(НастройкаПериода.ДатаНачала, НастройкаПериода.ДатаОкончания);
	ТекстЗаголовкаФормы 	= "Контур.EDI v" + Объект.ПараметрыКлиентСервер.НомерРелизаМодуля + ПредставлениеОсобенностейМодуля + " / ";
	ТекстПериода 			= ?(_ПредставлениеПериода = "", "<период не установлен>", _ПредставлениеПериода);
	
	ЭтаФорма.Заголовок = ТекстЗаголовкаФормы + " " + ТекстПериода + " / Последний обмен с сервером: " + ДатаПоследнегоВыполненияОбмена;
	
КонецПроцедуры	

&НаКлиенте
Процедура ПоказатьЗавершенныеПереключение(Команда)
	
	Элементы.ПоказатьЗавершенные.Пометка = не Элементы.ПоказатьЗавершенные.Пометка;
	Элементы.ПоказатьЗавершенные.Заголовок = ?(Элементы.ПоказатьЗавершенные.Пометка,"Скрыть завершенные","Показать завершенные"); 
	ПоказыватьЗавершенные = Элементы.ПоказатьЗавершенные.Пометка;
	Состояние("Переключаю режим...",15,,Элементы.Настройки.Картинка);
	
	ЗаполнитьСписокТребуемыхДействий();
	РазвернутьДеревоОтбораТребуемоеДействиеКлиент();
	
	ОбновитьСписок();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикПослередактированияУчетныхЗаписей(ПараметрыЗакрытияИнтерактивнойФормы = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПереинициализироватьКэшМодуляОбъектаСервер();   //могли поменять ключевые настройки (например транспорт)
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокУчетныхЗаписей(Команда)
	
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("УчетныеЗаписи_СписокУправляемая", , , ЭтаФорма, "ОбработчикПослередактированияУчетныхЗаписей", ЭтаФорма);
	
КонецПроцедуры                            

&НаКлиенте
Процедура ОткрытьСписокПользователей(Команда)
	
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Пользователи_СписокУправляемая", , , ЭтаФорма, "ОбработчикПослередактированияУчетныхЗаписей", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСтартовыйПомощник(Команда)
	
	ЗапуститьСтартовыйПомощник();			
	
КонецПроцедуры

&НаКлиенте
Процедура РазделительКартинкаНажатие(Элемент)
	ОткрывашкаНажатие("");
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройки(Команда)
	
	СохранитьНастройкиГлавнойФормы();
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("СервисНастройкиУправляемая", , , ЭтаФорма, "ОбработчикПослередактированияНастроек", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикПослередактированияНастроек(ПараметрыЗакрытияИнтерактивнойФормы=неопределено, ДополнительныеПараметры=неопределено) Экспорт
	
	ОбработкаПослеОткрытияФормНастроекСОбновлениемКэшаКлиент();
	// переинициализируем форму обмена, т.к. в настройках могли переключить механизм криптографии
	ИнициализироватьМодульОбменКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "КонтурEDI_НастроитьФорму" Тогда
		ОбработкаПослеОткрытияФормНастроекСОбновлениемКэшаКлиент();
	КонецЕсли;
	Если ИмяСобытия = "КонтурEDI_ОбновитьСписокЗадач" Тогда
		ЗаполнитьСписокЗадачКлиент();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПослеОткрытияФормНастроекСервер()
	
	ЗаполнитьРеквизитыФормы();
	УстановитьВидимостьЭлементовПриИзмененииНастроекПользователя();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПослеОткрытияФормНастроекСОбновлениемКэшаСервер()
	
	ПереинициализироватьКэшМодуляОбъектаСервер();
	ОпределитьРежимРаботы();
	ЗаполнитьРеквизитыФормы();
	УстановитьВидимостьЭлементовПриИзмененииНастроекПользователя();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаПослеОткрытияФормНастроекКлиент()
	
	Состояние("Обновляю кэш...",10,,Элементы.Настройки.Картинка);
	ОбработкаПослеОткрытияФормНастроекСервер();
	ОбработкаПослеОткрытияФормНастроекПродолжениеКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаПослеОткрытияФормНастроекСОбновлениемКэшаКлиент()
	
	Состояние("Обновляю кэш...",10,,Элементы.Настройки.Картинка);
	ОбработкаПослеОткрытияФормНастроекСОбновлениемКэшаСервер();
	ОбработкаПослеОткрытияФормНастроекПродолжениеКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаПослеОткрытияФормНастроекПродолжениеКлиент()
	
	Состояние("Обновляю форму...",10,,Элементы.Настройки.Картинка);
	ВосстановитьНастройкиФормыКлиент();
	Состояние("Обновляю форму...",20,,Элементы.Настройки.Картинка);
	ВосстановитьСохраненноеЗначениеПоказыватьЗавершенные();
	Состояние("Обновляю форму...",30,,Элементы.Настройки.Картинка);
	ЗаполнитьНастройкиОтборов();
	Состояние("Обновляю форму...",60,,Элементы.Настройки.Картинка);
	УстановитьСохраненныеНастройкиДереваФильтров();
	Состояние("Обновляю форму...",80,,Элементы.Настройки.Картинка);
	ВосстановитьПометкиИПодсчитанныеКоличества();
	Состояние("Обновляю форму...",100,,Элементы.Настройки.Картинка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаСобытияПодключаемогоМодуляКлиент(ИмяСобытия,СтандартнаяОбработкаEDI=Неопределено,ПараметрыПодключаемогоМодуля = Неопределено)
	
	Если ИспользуетсяМеркурий Тогда
		Попытка//если старая версия меркурия
			ПараметрыФ = Объект.ПараметрыКлиентСервер;
			ПараметрыФ.Вставить("ПутьКФормамМеркурий", ПутьКФормамМеркурий);
			КлиентскийМодуль = ПолучитьФорму(ПутьКФормамМеркурий+"КлиентскийМодульEDI_Управляемая",ПараметрыФ);
			КлиентскийМодуль.ОбработатьСобытиеОсновногоМодуля(ИмяСобытия, ПараметрыПодключаемогоМодуля, СтандартнаяОбработкаEDI);
		Исключение
			КомментарийЗаписи = "Не удалось обработать клиентское событие ПМ - " + ИмяСобытия + " - " + ОписаниеОшибки();
			МодульОбъектаКлиент().ЗаписьЖурналаРегистрацииНаКлиенте_КонтурEDI("Ошибка", КомментарийЗаписи);
		КонецПопытки;
	КонецЕсли;

	Если НЕ МодульКлиентПМ = Неопределено Тогда
		
		МодульКлиентПМ.ОбработатьСобытиеКонтурEDI(ИмяСобытия, ПараметрыПодключаемогоМодуля, СтандартнаяОбработкаEDI);
		
	КонецЕсли;
	
КонецПроцедуры
	
&НаКлиенте                                          	
Процедура ТабСообщенияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекКолонка = Поле;
	ТекСтрока = Элемент.ТекущиеДанные;
	
	// клиентский ПМ
	СтандартнаяОбработкаEDI = Истина;
	ПараметрыСобытияПМ = Новый Структура("Колонка,Строка, ОсновнаяФорма", ТекКолонка, ТекСтрока, ЭтаФорма);
	ОбработкаСобытияПодключаемогоМодуляКлиент("ВыборСтрокиОсновногоСписка", СтандартнаяОбработкаEDI, ПараметрыСобытияПМ);
	
	Если СтандартнаяОбработкаEDI = Истина Тогда
		Если ТекКолонка.Имя = "ТабСообщенияТребуемоеДействие" Тогда
			//вообще у нас тут должна быть общая логика: что в карточке сообщения, что в списке, что в групповых действиях
			//еще один момент: неплохо бы сделать возможность обновления только одной строки.
			//как вариант, добавить соответствующее условие во все запросы.
			
			ПакетнаяОбработка = Ложь;
			ВыполнитьЗадачу(ТекСтрока, ПакетнаяОбработка);
			
		ИначеЕсли ТекКолонка.Имя = "ТабСообщенияДокумент"
			И ЗначениеЗаполнено(ТекСтрока.Документ)
			Тогда
			
			МодульОбъектаКлиент().ОткрытьФормуЗначения_КонтурEDI(ТекСтрока.Документ);
			
			Возврат;
			
		Иначе	
			//откроем сообщение
			Если ЗначениеЗаполнено(ТекСтрока.СообщениеСсылка) Тогда
				Состояние("Открываю сообщение...",80,,Элементы.КнопкаЕщеОткрытьКарточкуСообщения.Картинка);
				мОткрытьКарточкуСообщения(ТекСтрока);
				Возврат;
			Иначе
				Возврат;
			КонецЕсли;	
			
		КонецЕсли;	
	КонецЕсли;
	
	Если ОбновлятьВесьСписокПриРаботеСЗаказами Тогда 
		ЗаполнитьСписокЗадачКлиент();
	Иначе
		ВычеркнутьТекущиеСтроки();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВычеркнутьТекущиеСтроки()
	
	//Вычеркнем тек строки
	МассивИДвыделенных = Элементы.ТабСообщения.ВыделенныеСтроки;
	Если МассивИДвыделенных.Количество()>0 Тогда
		Для Каждого ВыделеннаяСтрокаИД Из МассивИДвыделенных ЦИкл
			ЭлементКоллекции = Объект.ТабСообщения.НайтиПоИдентификатору(ВыделеннаяСтрокаИД);
			ЭлементКоллекции.Обработан = Истина;
			ЭлементКоллекции.Иконка = 0;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры


&НаСервереБезКонтекста
Функция ОпределитьИмяТипаДокумента(Ссылка)

Возврат "Документ."+Ссылка.Метаданные().Имя;	

КонецФункции // ОпределитьИмяТипаОбъекта(ТекСтрока.Документ)()


&НаКлиенте
Функция ПолучитьКодДействияПоПредставлению(ПредставлениеДействия)
	
	НужныеСтроки = НастройкаОформленияТребуемогоДействия.НайтиСтроки(Новый Структура("Значение",ПредставлениеДействия));
	Если НужныеСтроки.Количество() = 0 Тогда
		ВызватьИсключение "Проблемы с требуемыми действиями";
	КонецЕсли;
	Возврат НужныеСтроки[0].КодДействия;
	
КонецФункции	



////Общая точка входа для действий ниже
//Выполняет задачу в строке ВыбраннаяСтрока.
//Работает в 2 режимах - "Пакетном", когда дополнительные формы не открываются,
//	и "Основном", с открытием окон
&НаКлиенте
Процедура ВыполнитьЗадачу(ВыбраннаяСтрока, ПакетнаяОбработка = Ложь)
	
	Если ВыбраннаяСтрока.Обработан Тогда
		Возврат;//незачем еще раз делать
	КонецЕсли;	
	
	Результат = Ложь;
	КодДействия = ПолучитьКодДействияПоПредставлению(ВыбраннаяСтрока.ТребуемоеДействие);
	//здесь у нас и будет работа с кодами действий. Сюда же прокинем ПМ - продумать работу в УФ и ОФ сразу
	
	СтандартнаяОбработкаEDI = Истина;
	//возможно здесь будет когда-то ПМ
	
	Если Лев(ВыбраннаяСтрока.ТипСообщения,2) = "M_" Тогда
		Если ПакетнаяОбработка = Истина Тогда
			Возврат;//пакетная обработка осуществляется отдельно
		Иначе
			МеркурийВыполнитьЗадачу(ВыбраннаяСтрока,ПакетнаяОбработка,КодДействия);
			Если КодДействия = "РаботаЗавершена" Тогда
				Если Не ПакетнаяОбработка Тогда
					мОткрытьКарточкуСообщения(ВыбраннаяСтрока);
				КонецЕсли;
			КонецЕсли;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Не СтандартнаяОбработкаEDI Тогда
		//сработал ПМ
		
	Иначе	
		//ПМ ничего не перехватывал		
		
		//общие действия
		Если КодДействия = "ПринятьСообщение" Тогда
			
			Состояние("Выполняю задачу...",75,,Элементы.Настройки.Картинка);
			_ОбработатьСообщение(ВыбраннаяСтрока,ПакетнаяОбработка,КодДействия);
			
		ИначеЕсли КодДействия = "ОбработатьНовуюВерсию" Тогда
			
			Состояние("Выполняю задачу...",75,,Элементы.Настройки.Картинка);
			_ОбработатьСообщение(ВыбраннаяСтрока,ПакетнаяОбработка,КодДействия);
			
		ИначеЕсли КодДействия = "РаботаЗавершена"
			Или КодДействия = "РаботаЗавершенаСУточнением"
			Или КодДействия = "ОжидайтеПодписьПартнера"
			Или КодДействия = "ДождатьсяДоставки"
			Или КодДействия = "НеПрошлоВалидацию" Тогда
			
			Если Не ПакетнаяОбработка Тогда
				мОткрытьКарточкуСообщения(ВыбраннаяСтрока);
			КонецЕсли;	
			
		ИначеЕсли Лев(КодДействия,10) = "Отправить_" Тогда 
			
			_ОтправитьИсходящееСообщение(ВыбраннаяСтрока,ПакетнаяОбработка,КодДействия);
			
		ИначеЕсли КодДействия = "ИсправитьОшибки" Тогда
			
			_ПереотправитьСообщение(ВыбраннаяСтрока,ПакетнаяОбработка);
	
		ИначеЕсли Лев(КодДействия,15) = "СоздатьДокумент" Тогда
			
			_СоздатьДокументНаОснованииКлиент(ВыбраннаяСтрока,ПакетнаяОбработка);
			
		ИначеЕсли Лев(КодДействия,16) = "ПровестиДокумент" Тогда
			
			_ПровестиДокументКлиент(ВыбраннаяСтрока,ПакетнаяОбработка);
			
		КонецЕсли;
		
		//теперь действия, разделяющиеся для поставщика и для сети
		
		Если Параметры.РежимРаботы = "Поставщик" Тогда
			
			Если КодДействия = "ПодписатьВДиадок" Тогда
				
				_ПодписатьИОтправить(ВыбраннаяСтрока,ПакетнаяОбработка);
				
			ИначеЕсли КодДействия = "ОбработатьРасхождения_RECADV" Тогда
				
				_ОбработатьРезультатПриемки(ВыбраннаяСтрока,ПакетнаяОбработка,КодДействия);
				
			ИначеЕсли КодДействия = "ОбработатьРасхождения_ORDERS" Тогда
				
				_ОбработатьУточнениеОбратногоЗаказа(ВыбраннаяСтрока,ПакетнаяОбработка,КодДействия);
				
			КонецЕсли;	
			
		ИначеЕсли Параметры.РежимРаботы = "Покупатель" Тогда
			
			Если КодДействия = "ОбработатьРасхождения_ORDRSP" Тогда
				
				_ОбработатьУточнениеЗаказа(ВыбраннаяСтрока,ПакетнаяОбработка,КодДействия);     //сторона сети 
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПодготовитьИсходящееСообщениеВызовСервера(ТипСообщения, Документ)

	ТекСообщение= МодульОбъекта().ПодготовитьИсходящееСообщение(ТипСообщения, Документ);
	
   возврат Истина;
КонецФункции // ПодготовитьИсходящееСообщениеВызовСервера()

&НаСервере
Функция ОтправитьИсходящееСообщениеБезОткрытияФормыСервер(ТипСообщения,_Документ, ПараметрыСсылка = Неопределено)
	
	Сообщение = МодульОбъекта().ПодготовитьИсходящееСообщение(ТипСообщения, _Документ, ПараметрыСсылка);
	
	ПараметрыДействия = Новый Структура();
	ПараметрыДействия.Вставить("ОтправитьСообщениеИзФормы",	Истина);
	ПараметрыДействия.Вставить("Сообщение",					Сообщение);
	
	Если ТипСообщения="DESADV" Тогда
		//DESADV может отправляться вместе с INVOIC
		ОтправлятьINVOICсDESADV = МодульОбъекта().ПолучитьЗначениеСвойстваОбъектаEDI(Сообщение.Получатель1С, "ОтправлятьINVOICсDESADV");
		
		Если ОтправлятьINVOICсDESADV = Истина Тогда
			СчетФактура = МодульОбъекта().ПолучитьСчетФактуруНакладной(_Документ);
			Если Не ЗначениеЗаполнено(СчетФактура) Тогда
				Сообщить("Не удалось найти счет-фактуру для отправки по документу "+_Документ);
				//если в партнере стоит флаг "Отправлять INVOIC вместе с DESADV", и мы не нашли документ для отправки INVOIC, то не будем отправлять и DESADV.
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
		
		Если МодульОбъекта().НужноПропуститьСообщениеСУслугами_DESADV(Сообщение) Тогда
			
			Если НЕ Сообщение.СодержитОшибки Тогда
				
				Если ОтправлятьINVOICсDESADV = Истина Тогда
					СообщениеINVOIC = МодульОбъекта().ПодготовитьИсходящееСообщение("INVOIC", СчетФактура);
					Если СообщениеINVOIC.СодержитОшибки Тогда
						МодульОбъекта().Сообщить_КонтурEDI("Ошибки проверки связанного сообщения INVOIC по документу: " + СчетФактура);
						Возврат Ложь;
					Иначе
						ПараметрыДействия.Вставить("Сообщение",	СообщениеINVOIC);
						МодульОбъекта().ОтправитьСообщение("INVOIC", СчетФактура, ПараметрыДействия);
					КонецЕсли;
				КонецЕсли;
				
				МодульОбъекта().СохранитьСообщение(Сообщение, "Пропущен");
				МодульОбъекта().Сообщить_КонтурEDI("Сообщение DESADV по документу с услугами: " + _Документ + " было автоматически пропущено.");
				Возврат Истина;
			Иначе
				Возврат Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
		//DESADV может отправляться вместе с ALCRPT
		ОтправлятьALCRPTсDESADV = МодульОбъекта().ПолучитьЗначениеСвойстваОбъектаEDI(Сообщение.Получатель1С, "ОтправлятьALCRPT");
		ОтправлятьALCRPTсINVOIC = МодульОбъекта().ПолучитьЗначениеСвойстваОбъектаEDI(Сообщение.Получатель1С, "ОтправлятьALCRPTсINVOIC");
		Если ОтправлятьALCRPTсDESADV Или ОтправлятьALCRPTсINVOIC Тогда 
			//следует проверить валидность ALCRPT
			Сообщение = МодульОбъекта().ПодготовитьИсходящееСообщение("ALCRPT", _Документ, неопределено);
			Если Сообщение.СодержитОшибки Тогда
				МодульОбъекта().Сообщить_КонтурEDI("Ошибки проверки связанного сообщения ALCRPT по документу: "+_Документ);
				Возврат Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	МодульОбъекта().ОтправитьСообщение(ТипСообщения,_Документ,ПараметрыДействия);
	
КонецФункции


&НаКлиенте
Функция _ОтправитьИсходящееСообщение(ВыбраннаяСтрока,НеОткрыватьФормы = Ложь,КодДействия="")
	
	ТипСообщения = ВыбраннаяСтрока.ТипСообщения;
	
	//сообщение собирается непостредственно в самой карточке   (goto мОткрытьКарточкуСообщения())
	
	Если НеОткрыватьФормы Тогда
		
		//Если это DESADV или INVOIC, то на форме есть кнопка Сохранить. Возможно, нужно убрать кнопку, если не используется SSCC. 
		//Если сохранили, нужно передать параметр СообщениеСсылка для проверки в СохранитьСообщение.
		Если ЗначениеЗаполнено(ВыбраннаяСтрока.СообщениеСсылка) Тогда
			ПараметрыСсылка = Новый Структура;
			ПараметрыСсылка.Вставить("СообщениеСсылка", ВыбраннаяСтрока.СообщениеСсылка);
		КонецЕсли;
		//обработаем и вычеркнем если все ок
		Результат = ОтправитьИсходящееСообщениеБезОткрытияФормыСервер(ВыбраннаяСтрока.ТипСообщения, ВыбраннаяСтрока.Документ, ПараметрыСсылка);
		Возврат Результат;
	
	Иначе
		//с открытием формы
		//Сообщение_До = СтатусСсылкиИПризнакАрхивУСсылкиСообщения(ВыбраннаяСтрока.СообщениеСсылка);//более не анализируем для исходящих
		
		мОткрытьКарточкуСообщения(ВыбраннаяСтрока,КодДействия);
		
		Если не Параметры.МодальностьЗапрещена Тогда
			
			Возврат Истина;             //Рефакторинг - придумать хороший механизм действий после выполнения действия
			//теперь проверим, что произошло с сообщением после закрытия его формы
			//Сообщение_После = СтатусСсылкиИПризнакАрхивУСсылкиСообщения(ВыбраннаяСтрока.СообщениеСсылка);
			//
			//Если Сообщение_После<>Неопределено
			//	И (Сообщение_До.СтатусСсылки<>Сообщение_После.СтатусСсылки
			//	Или Сообщение_До.Архив<>Сообщение_После.Архив)
			//	Тогда
			//	Возврат Истина;//что-то произошло, вычеркиваем эту строку
			//Иначе
			//	
			//	Возврат Ложь;
			//	
			//КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция _ПереотправитьСообщение(ВыбраннаяСтрока,НеОткрыватьФормы = Ложь,КодДействия="")
	
	ТипСообщения = ВыбраннаяСтрока.ТипСообщения;
	
	//В УФ мы не сможем вернуться на клиент для открытия формы с собраннымсообщением
	//т.к. там есть несериализуемая ТЗ!
	
	//////попробуем щас через реквизит формы его протолкать      скорее всего не получится, но пробуем!
	//// Резалт= ПодготовитьИсходящееСообщениеВызовСервера(ТипСообщения, ВыбраннаяСтрока.Документ);
	//ОТ ЭТОГО ВАРИАНТА ОТКАЗЫВАЕМСЯ Т.К. Сообщение через клиент (а карточку открыть мона ток там) передавать очень печально 
	//просто позволим собирать сообщение непостредственно самой карточке   (иди в мОткрытьКарточкуСообщения())
	
	Если НеОткрыватьФормы Тогда
		
		//обработаем и вычеркнем если все ок
		ПараметрыОтправки = Новый Структура;
		ПараметрыОтправки.Вставить("ПереотправляемоеСообщениеСсылка", ВыбраннаяСтрока.СообщениеСсылка);

		Результат = ОтправитьИсходящееСообщениеБезОткрытияФормыСервер(ВыбраннаяСтрока.ТипСообщения, ВыбраннаяСтрока.Документ, ПараметрыОтправки);
		Возврат Результат;
	
	Иначе
		//с открытием формы
		//Сообщение_До = СтатусСсылкиИПризнакАрхивУСсылкиСообщения(ВыбраннаяСтрока.СообщениеСсылка);//более не анализируем для исходящих
		
		мОткрытьКарточкуСообщения(ВыбраннаяСтрока,КодДействия);
		
		Если не Параметры.МодальностьЗапрещена Тогда
			
			Возврат Истина;             //Рефакторинг - придумать хороший механизм действий после выполнения действия
			//теперь проверим, что произошло с сообщением после закрытия его формы
			//Сообщение_После = СтатусСсылкиИПризнакАрхивУСсылкиСообщения(ВыбраннаяСтрока.СообщениеСсылка);
			//
			//Если Сообщение_После<>Неопределено
			//	И (Сообщение_До.СтатусСсылки<>Сообщение_После.СтатусСсылки
			//	Или Сообщение_До.Архив<>Сообщение_После.Архив)
			//	Тогда
			//	Возврат Истина;//что-то произошло, вычеркиваем эту строку
			//Иначе
			//	
			//	Возврат Ложь;
			//	
			//КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

//подпишем текущий СФ в Диадоке
&НаКлиенте
Процедура _ПодписатьИОтправить(ВыбраннаяСтрока, НеОткрыватьФормы = Ложь)
	
	Если НеОткрыватьФормы = Истина Тогда
		
		НовСтрока = СчетаФактурыНаПодписание.Добавить();
		НовСтрока.Ссылка = ВыбраннаяСтрока.СообщениеСсылка;
		
	ИначеЕсли ОткрыватьВебИнтерфейсДляПодписанияЕдиничнойПачки = Истина Тогда
		
		ДокументСсылка = ВыбраннаяСтрока.Документ;
		СообщениеСсылка = ВыбраннаяСтрока.СообщениеСсылка; 
		ДокументыНеПодписаны = Истина;
		
		ОткрытьЭлектронныеДокументыВВебДиадок(СообщениеСсылка, ДокументСсылка, ДокументыНеПодписаны);
		
	Иначе

		МассивСообщений = Новый Массив;
		МассивСообщений.Добавить(ВыбраннаяСтрока.СообщениеСсылка);
		ЗапроситьСертификатИПодписатьСФ(МассивСообщений);
		ОбработчикПослеЗакрытияКарточкиСообщения();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция _ОбработатьСообщение(ВыбраннаяСтрока,НеОткрыватьФормы = Ложь,КодДействия="")    //На ОФ ОбработатьВходящееСообщение()
	
	Если НеОткрыватьФормы Тогда
		
		//обработаем и вычеркнем если все ок
		Результат = ОбработкаСообщенияБезОткрытияФормыСервер(ВыбраннаяСтрока.ТипСообщения, ВыбраннаяСтрока.СообщениеСсылка);

		Возврат Результат;
	
	Иначе
		//с открытием формы
		Сообщение_До = СтатусСсылкиИПризнакАрхивУСсылкиСообщения(ВыбраннаяСтрока.СообщениеСсылка);
		
		мОткрытьКарточкуСообщения(ВыбраннаяСтрока,КодДействия);
		
		Если не Параметры.МодальностьЗапрещена Тогда 
			//теперь проверим, что произошло с сообщением после закрытия его формы
			Сообщение_После = СтатусСсылкиИПризнакАрхивУСсылкиСообщения(ВыбраннаяСтрока.СообщениеСсылка);
			
			Если Сообщение_После<>Неопределено
				И (Сообщение_До.СтатусСсылки<>Сообщение_После.СтатусСсылки
				Или Сообщение_До.Архив<>Сообщение_После.Архив)
				Тогда
				Возврат Истина;//что-то произошло, вычеркиваем эту строку
			Иначе
				
				Возврат Ложь;
				
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура _ОбработатьРезультатПриемки(ВыбраннаяСтрока,НеОткрыватьФормы = Ложь,КодДействия="")
	
	ОбработатьРасхожденияСообщенияИДокумента(ВыбраннаяСтрока,"Приемка");

КонецПроцедуры

&НаКлиенте
Процедура _ОбработатьУточнениеОбратногоЗаказа(ВыбраннаяСтрока,НеОткрыватьФормы = Ложь,КодДействия="")
	
	ОбработатьРасхожденияСообщенияИДокумента(ВыбраннаяСтрока,"ОбратныйЗаказ");
	
КонецПроцедуры

// откроем форму обработки расхождения сообщения и документа (Orders в ответ на porders или recadv в ответ на desadv) 
//
&НаКлиенте
Процедура ОбработатьРасхожденияСообщенияИДокумента(ВыбраннаяСтрока,ИсточникРасхождений)

	ПараметрыФормы = Новый Структура; 
	ПараметрыФормы.Вставить("Документ",						ВыбраннаяСтрока.Документ);
	ПараметрыФормы.Вставить("ИсточникРасхождений",			ИсточникРасхождений);
	ПараметрыФормы.Вставить("ПараметрыАвтотестирования",	Параметры.ПараметрыАвтотестирования);
	ПараметрыФормы.Вставить("СообщениеСсылка",				ВыбраннаяСтрока.СообщениеСсылка);
	ПараметрыФормы.Вставить("Партнер",						ВыбраннаяСтрока.Партнер);
	                                            
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Сервис_ОбработкаРасхожденийПриемкиУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбработчикПослеЗакрытияКарточкиСообщения", ЭтаФорма, ВыбраннаяСтрока.СообщениеСсылка);

КонецПроцедуры // ОбработатьРасхожденияСообщенияИДокумента()

&НаКлиенте
Процедура _ОбработатьУточнениеЗаказа(ВыбраннаяСтрока,НеОткрыватьФормы = Ложь,КодДействия="")
	
	//откроем форму обработки расхождений
	ПараметрыФормы = Новый Структура; 
	ПараметрыФормы.Вставить("Документ",						ВыбраннаяСтрока.Документ);
	ПараметрыФормы.Вставить("ИсточникРасхождений",			"ОбработкаУточненияЗаказа");
	ПараметрыФормы.Вставить("ПараметрыАвтотестирования",	Параметры.ПараметрыАвтотестирования);
	ПараметрыФормы.Вставить("СообщениеСсылка",				ВыбраннаяСтрока.СообщениеСсылка);
	                                            
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Сервис_ОбработкаУточненногоЗаказаУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбработчикПослеЗакрытияКарточкиСообщения", ЭтаФорма, ВыбраннаяСтрока.СообщениеСсылка);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьВидОперацииРТУБП30()
	Возврат Перечисления.ВидыОперацийРеализацияТоваров.Товары;
КонецФункции // Перечисления.ВидыОперацийРеализацияТоваров.Товары()

&НаКлиенте
Функция _СоздатьДокументНаОснованииКлиент(ВыбраннаяСтрока,НеОткрыватьФормы = Ложь)  

	СозданныйДокумент = Неопределено;
	
	КодДействия = ПолучитьКодДействияПоПредставлению(ВыбраннаяСтрока.ТребуемоеДействие);
	ИмяСоздаваемогоДокумента = СтрЗаменить(Сред(КодДействия, 17), "_ПоКорректировке", "");
	
	ПредставлениеДокументаНаОснованииТекущего = ПредставлениеДокументаНаОснованииТекущегоСервер(ВыбраннаяСтрока.Документ, Параметры.РежимРаботы, ИмяСоздаваемогоДокумента);
	
	Если НЕ ПредставлениеДокументаНаОснованииТекущего = Неопределено Тогда
		
		ТекстПредупреждения = "Документ на основании " + Строка(ВыбраннаяСтрока.Документ) + " уже был создан: " + ПредставлениеДокументаНаОснованииТекущего;
		ДопПараметрДляПередачиВОбработчик = Неопределено;
		Если Параметры.МодальностьЗапрещена Тогда 
			Выполнить("ПоказатьПредупреждение(,ТекстПредупреждения,,""Контур.EDI"")");
		Иначе
			Предупреждение(ТекстПредупреждения,,"Контур.EDI");
		КонецЕсли;
		
		Возврат Ложь;
		
	КонецЕсли;
		
	Если НеОткрыватьФормы = Истина Тогда
		
		СозданныйДокументСсылка = СоздатьДальнейшийДокумент1ССервер(ВыбраннаяСтрока.Документ, ИмяСоздаваемогоДокумента, ВыбраннаяСтрока.ТипСообщения);
		Если ЗначениеЗаполнено(СозданныйДокументСсылка) Тогда 
			Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;
		
	КонецЕсли;
		
	//определить конфу
	ИмяКонфигурации = Объект.ПараметрыКлиентСервер.ИмяКонфигурации1С;
	
	//только для поставщика
	Если Параметры.РежимРаботы <> "Поставщик" Тогда 
		Возврат Ложь; 
	КонецЕсли;
	//рефакторинг реализовать для сети
	
	Если ВыбраннаяСтрока.ТипСообщения = "INVOIC" ИЛИ ВыбраннаяСтрока.ТипСообщения = "COINVOIC" Тогда
		////////////////////////////////будем создавать СЧФ////////////////////////////////
		Если ИмяКонфигурации = "УФ_УТ" 
			ИЛИ ИмяКонфигурации = "УФ_Розница" Тогда
			
			ДанныеСчетаФактуры = ПолучитьДанныеДляСозданияСчетаФактуры_УФ_УТ_Розница_Сервер(ВыбраннаяСтрока.Документ);
			
			ПараметрыФормы = 	Новый Структура("Основание, ДокументОснование, ВозвращатьПараметрыПредставления",
												ДанныеСчетаФактуры, 
												ВыбраннаяСтрока.Документ, 
												Ложь);
			
		ИначеЕсли ИмяКонфигурации = "УФ_БП"
			ИЛИ ИмяКонфигурации = "УФ_УНФ" Тогда
			
			ПараметрыФормы = Новый Структура("Основание", ВыбраннаяСтрока.Документ);
			
		КонецЕсли;
		
		ИмяФормыДокумента = "Документ." + ИмяСоздаваемогоДокумента + ".ФормаОбъекта";									
		МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI(ИмяФормыДокумента, "", ПараметрыФормы, ЭтаФорма, "ОбработчикПослеЗакрытияКарточкиСообщения", ЭтаФорма);
		
	Иначе
		////////////////////////////////будем создавать РТУ////////////////////////////////
		
		СтруктураЗначенияЗаполнения = Новый Структура("ДокументОснование", ВыбраннаяСтрока.Документ);
		
		// кастомные структуры
		Если ИмяКонфигурации = "УФ_БП" Тогда
			
			СтруктураЗначенияЗаполнения.Вставить("ВидОперации", ПолучитьВидОперацииРТУБП30());
			СтруктураЗначенияЗаполнения.Вставить("Основание", 	ВыбраннаяСтрока.Документ);
			
		ИначеЕсли ИмяКонфигурации = "УФ_УТ" Тогда
			
			СтруктураЗначенияЗаполнения.Вставить("ДатаОтгрузки", ?(ВыбраннаяСтрока.ДатаПоставки = Дата(1,1,1), ТекущаяДата(), ВыбраннаяСтрока.ДатаПоставки));  
			
		ИначеЕсли ИмяКонфигурации = "УФ_УНФ" Тогда
			
			МассивЗаказов = Новый Массив();
			МассивЗаказов.Добавить(ВыбраннаяСтрока.Документ);
			СтруктураЗначенияЗаполнения.Вставить("МассивЗаказовПокупателей", 	МассивЗаказов);
			СтруктураЗначенияЗаполнения.Вставить("ОснованиеПечатиСсылка", 		ВыбраннаяСтрока.Документ);
			
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", СтруктураЗначенияЗаполнения);
		ИмяФормыДокумента = "Документ." + ИмяСоздаваемогоДокумента + ".ФормаОбъекта";
		
		Если Параметры.МодальностьЗапрещена 
			ИЛИ ИмяКонфигурации = "УФ_БП" Тогда
			
			Если ИмяКонфигурации = "УФ_УТ"
				ИЛИ ИмяКонфигурации = "УФ_БП" Тогда
				
				МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI(ИмяФормыДокумента, "", ПараметрыФормы, ЭтаФорма, "ОбработчикПослеЗакрытияКарточкиСообщения", ЭтаФорма);
				
			ИначеЕсли ИмяКонфигурации = "УФ_УНФ" Тогда 
				
				ФормаДокумента = ПолучитьФорму(ИмяФормыДокумента, СтруктураЗначенияЗаполнения);
				ФормаДокумента.Объект.Заказ = ВыбраннаяСтрока.Документ;
				ФормаДокумента.ЗаполнитьПоЗаказуЗавершение(КодВозвратаДиалога.Да, "");
				ФормаДокумента.Модифицированность =	Истина;
				ФормаДокумента.Объект.Дата = ВыбраннаяСтрока.ДатаПоставки;
				ФормаДокумента.РежимОткрытияОкна  =	РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс;
				Выполнить("ФормаДокумента.ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения(""ОбработчикПослеЗакрытияКарточкиСообщения"",ЭтаФорма)");
				
				ФормаДокумента.Открыть();
				
			КонецЕсли;
		Иначе
			
			ПараметрыФормы = Новый Структура("Основание", ВыбраннаяСтрока.Документ);
			ФормаДокумента = ПолучитьФорму(ИмяФормыДокумента, ПараметрыФормы, ЭтаФорма);
			Если ИмяКонфигурации = "УФ_УНФ" Тогда  //не поддерживаем 1РТУ на несколько заказов (т.е. всегда только 1 заказ для рту)
				ФормаДокумента.Объект.Заказ = ВыбраннаяСтрока.Документ;
			КонецЕсли;
			ФормаДокумента.ОткрытьМодально();
			
			ОбработчикПослеЗакрытияКарточкиСообщения();
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Ложь;
		
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеДляСозданияСчетаФактуры_УФ_УТ_Розница_Сервер(ДокументОснование)
	
	ДанныеСчетаФактуры = Новый Структура;
	ДанныеСчетаФактуры.Вставить("ДокументОснование", 	ДокументОснование);
	ДанныеСчетаФактуры.Вставить("Организация",			ДокументОснование.Организация);
	
	ИсправлениеСчетаФактуры 	= Ложь;
    КорректировкаСчетаФактуры	= Ложь;

	Если НЕ Метаданные.Документы.Найти("КорректировкаРеализации") = Неопределено
		И ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
		
		Если НЕ ДокументОснование.Метаданные().Реквизиты.Найти("ВидКорректировки") = Неопределено Тогда
			
			ХозяйственнаяОперация = ДокументОснование.ВидКорректировки;
			
		ИначеЕсли НЕ ДокументОснование.Метаданные().Реквизиты.Найти("ХозяйственнаяОперация") = Неопределено Тогда
			
			ХозяйственнаяОперация = ДокументОснование.ХозяйственнаяОперация;
			
		Иначе
			
			ВызватьИсключение("При получении данных для создания счета-фактуры не удалось определить хозяйственную операцию");
			
		КонецЕсли;
			
		ИсправлениеСчетаФактуры 	= (ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ИсправлениеОшибок); 
	    КорректировкаСчетаФактуры	= (ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КорректировкаПоСогласованиюСторон);
		
	КонецЕсли;
		
	ДанныеСчетаФактуры.Вставить("Исправление",			ИсправлениеСчетаФактуры);
	ДанныеСчетаФактуры.Вставить("Корректировочный",		КорректировкаСчетаФактуры);
	
	Возврат ДанныеСчетаФактуры;

КонецФункции // ПолучитьДанныеДляСозданияСчетаФактуры_УФ_УТ_Розница_Сервер()

&НаСервере
Процедура УНФ_ЗаполнитьРТУНаСервере(НовыйОбъект, Заказ)

	НовДок=Документы.РасходнаяНакладная.СоздатьДокумент();
	НовДок.Заполнить(Заказ);
	
	ЗначениеВДанныеФормы(НовДок,НовыйОбъект);
	
	//НовыйОбъект.Заказ = Заказ;

КонецПроцедуры


// Создает дальнейший документ 1С в цепочке сообщений 
//
// Параметры
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
// Возвращаемое значение:
//   <Тип.Вид>   - <описание возвращаемого значения>
//
&НаСервере
Функция СоздатьДальнейшийДокумент1ССервер(ДокументОснование, ИмяСоздаваемогоДокумента, ТипСообщения)
	
	СтандартнаяОбработкаEDI = Истина;
	Результат = МодульОбъекта().ОбработкаСобытияПодключаемогоМодуля("СоздатьДокументНаОсновании", СтандартнаяОбработкаEDI, Новый Структура("ДокументОснование,ИмяСоздаваемогоДокумента", ДокументОснование, ИмяСоздаваемогоДокумента));
	
	Если НЕ СтандартнаяОбработкаEDI Тогда
		Возврат Результат;
	КонецЕсли;
	
	//рефакторинг - затолкать в МО
	НовДок = Документы[ИмяСоздаваемогоДокумента].СоздатьДокумент();
	
	ИмяКонфигурации = Объект.ПараметрыКлиентСервер.ИмяКонфигурации1С;
	
	Если Параметры.РежимРаботы = "Поставщик" 
		И (ИмяКонфигурации = "УФ_УТ" ИЛИ ИмяКонфигурации = "УФ_Розница")
		И ИмяСоздаваемогоДокумента = "СчетФактураВыданный" Тогда
		
		Попытка
			
			ДанныеСчетаФактуры = ПолучитьДанныеДляСозданияСчетаФактуры_УФ_УТ_Розница_Сервер(ДокументОснование);

			НовДок.Заполнить(ДанныеСчетаФактуры);	
			
		Исключение
			
			НовДок.Заполнить(ДокументОснование);
			
		КонецПопытки;
		
	Иначе
		НовДок.Заполнить(ДокументОснование);
	КонецЕсли;
	
	//теперь кастомные заполнялки
	Если ИмяСоздаваемогоДокумента = "СчетФактураВыданный" Тогда
		
		Если МодульОбъекта().ЕстьРеквизитОбъекта_Контур(НовДок, "ВидСчетаФактуры")
			И МодульОбъекта().ЕстьНеобходимыеМетаданные("Перечисления.ВидСчетаФактурыВыставленного.НаРеализацию") Тогда
			
			НовДок.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаРеализацию;
			
		КонецЕсли;	
		
		Попытка
			//в некоторых конфигурациях еще надо заполнить табличную часть "ДатаНомерДокументовОплаты"
			Выполнить("
			|Если НовДок.ДатаНомерДокументовОплаты.Количество() = 0 Тогда
			|	НовДок.ДатаНомерДокументовОплаты.Добавить();
			|	НовДок.ДатаНомерДокументовОплаты[0].ДатаПлатежноРасчетногоДокумента = НовДок.ДатаПлатежноРасчетногоДокумента;
			|	НовДок.ДатаНомерДокументовОплаты[0].НомерПлатежноРасчетногоДокумента = НовДок.НомерПлатежноРасчетногоДокумента;
			|КонецЕсли;
			|");
		Исключение
			КомментарийЗаписи = "Не удалось заполнить таблицу документов оплаты - " + ОписаниеОшибки();
			МодульОбъекта().ЗаписьЖурналаРегистрации_КонтурEDI(УровеньЖурналаРегистрации.Ошибка, КомментарийЗаписи);	
		КонецПопытки;	
	КонецЕсли;	
	//конец заполнялок
	
	НовДок.Дата = ТекущаяДата();
	НовДок.УстановитьНовыйНомер();
	
	//еще кастомные проверки. Сюда тоже прокинуть ПМ
	Если ИмяСоздаваемогоДокумента = "РеализацияТоваровУслуг"
		Или ИмяСоздаваемогоДокумента = "схРеализацияТоваровУслуг"
		Тогда
		
		ИмяКонфигурации1С	= МодульОбъекта().ОпределитьКонфигурацию();
		ИмяТабЧастиТовары 	= ?(ИмяКонфигурации1С = "УФ_УНФ", "Запасы", "Товары");
		НетТоваров 			= НовДок[ИмяТабЧастиТовары].Количество() = 0;
		ЕстьТабЧастьУслуги 	= НЕ (НовДок.Метаданные().ТабличныеЧасти.Найти("Услуги") = Неопределено);
		НетУслуг 			= НЕ ЕстьТабЧастьУслуги ИЛИ (ЕстьТабЧастьУслуги И НовДок.Услуги.Количество() = 0);
		
		Если НетТоваров 
			И НетУслуг
			Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;	
	
	НовДок.Записать(РежимЗаписиДокумента.Запись);
	
	Возврат НовДок.Ссылка;
	
КонецФункции // СоздатьДальнейшийДокумент1ССервер()


// Проверяет, был ли создан дальнейший документ 1С в цепочке 
//
// Параметры
//  Документ  		-	<Документ.Ссылка> - текущий документ, из которого предполагается дальнейшее создание документа
//  РежимРаботы  	-	<Строка> - текущий вариант работы - сеть/поставщик
//
// Возвращаемое значение:
//   Булево   - Ложь: документ 1С не создан (не удалось найти последующий документ 1С), Истина: нашелся хотя бы 1 последующий документ 1С
//
&НаСервере
Функция ПредставлениеДокументаНаОснованииТекущегоСервер(Документ, РежимРаботы, ИмяСоздаваемогоДокумента)
	
	ПредставлениеДокумента = Неопределено;
	
	ДокументНаОснованииТекущего = МодульОбъекта().НайтиДокументНаОснованииТекущего(Документ, РежимРаботы, ИмяСоздаваемогоДокумента);
	
	Если НЕ ДокументНаОснованииТекущего = Неопределено Тогда
		ПредставлениеДокумента = Строка(ДокументНаОснованииТекущего); 
	КонецЕсли;
	
	Возврат ПредставлениеДокумента;
	
КонецФункции // ПредставлениеДокументаНаОснованииТекущегоСервер()

&НаКлиенте
//если документ сегодняшний, то пытаемся провести оперативно
Функция _ПровестиДокументКлиент(ВыбраннаяСтрока,НеОткрыватьФормы = Ложь)  
	
	Если ДокументПроведен(ВыбраннаяСтрока.Документ) Тогда
		//уже проведен
		Возврат Истина;
	КонецЕсли;
	
	Если НеОткрыватьФормы Тогда
		Возврат _ПровестиДокументСервер(ВыбраннаяСтрока.Документ);
	Иначе
		//проверить модальность
		
		ИмяТипаДокумента = ОпределитьИмяТипаДокумента(ВыбраннаяСтрока.Документ);
		ИмяФормыДокумента = ИмяТипаДокумента + ".ФормаОбъекта";
		ПараметрыФормы = Новый Структура("Ключ", ВыбраннаяСтрока.Документ);
		
		МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI(ИмяФормыДокумента, "", ПараметрыФормы, ЭтаФорма, "ОбработчикПослеЗакрытияКарточкиСообщения", ЭтаФорма);

	КонецЕсли;
	
КонецФункции

&НаСервере
Функция _ПровестиДокументСервер(ДокСсылка)
	
	СтандартнаяОбработкаEDI = Истина;
	ДокОбъект = ДокСсылка.ПолучитьОбъект();
	
	Результат = МодульОбъекта().ОбработкаСобытияПодключаемогоМодуля("ПередПроведениемДокумента", СтандартнаяОбработкаEDI, Новый Структура("ДокументСсылка,ДокОбъект", ДокСсылка, ДокОбъект));
	
	Если НЕ СтандартнаяОбработкаEDI Тогда
		Возврат Результат;
	КонецЕсли;    	
	
	//Проводить = (ДокОбъект.Метаданные().Проведение = Метаданные.СвойстваОбъектов.Проведение.Разрешить);
	ПроводитьОперативно = 
	(ДокОбъект.Метаданные().ОперативноеПроведение = Метаданные.СвойстваОбъектов.ОперативноеПроведение.Разрешить)
	//проверим, проводится ли данный документ оперативно
	И (НачалоДня(ДокОбъект.Дата) = НачалоДня(ТекущаяДата()));
	//оперативно проводим только сегодняшние документы
	
	Попытка
		РежимПроведения  =?(ПроводитьОперативно,РежимПроведенияДокумента.Оперативный,РежимПроведенияДокумента.Неоперативный);
		ДокОбъект.Записать(РежимЗаписиДокумента.Проведение,РежимПроведения);
		Возврат Истина;
	Исключение
		Возврат Ложь;
	КонецПопытки;
		
КонецФункции // _ПровестиДокументСервер()

&НаСервереБезКонтекста
Функция ДокументПроведен(Ссылка)
	Возврат Ссылка.Проведен;
КонецФункции
// Для отклонения 
&НаСервере
Функция НайтиСообщениеДокументаСервер(ДокументСсылка, ТипСообщения, Направление)
	
	Док_ORDERS = МодульОбъекта().НайтиСвязанныйДокументПоТипуСообщения(ДокументСсылка,"ORDERS");
	СообщениеСсылка = МодульОбъекта().НайтиСообщениеДокумента(Док_ORDERS, ТипСообщения, Направление);
	
	Возврат СообщениеСсылка;
	
КонецФункции // ПолучитьДанныеДляОтклоненияЗаказаПолностью()

//в первом параметре нам передана ссылка нового сообщения
//во втором параметре передана ссылка старого сообщения или структура анализа старого сообщения
&НаКлиенте
Процедура ОбработчикПослеЗакрытияКарточкиСообщения(НоваяСсылка=неопределено, СтараяСсылка=неопределено) Экспорт
	//теперь проверим, что произошло с сообщением после закрытия его формы для случая, когда мы не обновляем весь список, а вычеркиваем отработанную строку
	
	//проверим, есть ли изменения в состояния
	
	Если ОбновлятьВесьСписокПриРаботеСЗаказами Тогда
		//Замерим производительность - если обновление идет более 10 секунд при условии - предложить сделать настройку вычеркивания строк или изменить период
		ЗаполнитьСписокЗадачКлиентСАнализомПроизводительности();
		
	Иначе
		Если СообщенияПоСсылкамИзменились(НоваяСсылка,СтараяСсылка) Тогда 
			ВычеркнутьТекущиеСтроки();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписокЗадачКлиентСАнализомПроизводительности()

		//Попытка
		//	Выполнить("ПредВремя = ТекущаяУниверсальнаяДатаВМиллисекундах()");//не на всех платформах есть
		//Исключение
			ПредВремя=ТекущаяДата();
		//КонецПопытки;
		
		ЗаполнитьСписокЗадачКлиент();
		
		//Попытка
		//	Выполнить("ВремяВыполнения = ТекущаяУниверсальнаяДатаВМиллисекундах()-ПредВремя");
		//Исключение
		ВремяВыполнения = ТекущаяДата()-ПредВремя;
		//КонецПопытки;
		
		Если ВремяВыполнения>=10 Тогда
			ДатаОтказаОтВычерков = ПолучитьКонстантуEDIВызовСервера("ОтказВычеркиватьСтрокиДо");
			Если ДатаОтказаОтВычерков=Неопределено или ДатаОтказаОтВычерков<ТекущаяДата() Тогда 
				Элементы.ТекстПереходНаВычеркивание.Заголовок ="Мы заметили, что перестроение списка происходит слишком долго ("+ВремяВыполнения+" сек.) Это может быть вызвано слишком большим периодом выборки или низкой производительностью вашей системы.
				|
				|Попробуйте уменьшить интервал дат выборки документов.
				|
				|Если есть проблемы с производительностью системы - то для таких систем мы разарботали специальный режим вычеркивания строк с требуемыми действиями.
				|
				|В режиме вычеркивания мы не перестраиваем список после каждого действия, а лишь вычеркиваем эту строку, а перестроение списка происходит по кнопке обновить";
				
				Элементы.ГруппаВсплывающийДиалог.Видимость=истина;
				Элементы.ВсплывающийДиалогПереходНаВычеркивание.Видимость=истина;
				
			КонецЕсли;
		КонецЕсли;
		
КонецПроцедуры // ЗаполнитьСписокЗадачКлиентСАнализомПроизводительности()

&НаКлиенте
Процедура ОбработчикРешенияОпереключенииРежима(РезультатВопроса=Неопределено,ДопПараметр=Неопределено) Экспорт
	
	Если РезультатВопроса="Открыть выбор периода (рекомендуется)" Тогда
		ОткрытьВыборПериода();
	ИначеЕсли РезультатВопроса="Попробовать режим вычеркивания (рекомендуется)" Тогда
		ОбновлятьВесьСписокПриРаботеСЗаказами=Ложь;
		ТекстПредупреждения="Режим изменен только для текущего сеанса.";
		Если Параметры.МодальностьЗапрещена Тогда 
			Выполнить("ПоказатьПредупреждение(,ТекстПредупреждения,,""Контур.EDI"")");
		Иначе
			Предупреждение(ТекстПредупреждения,,"Контур.EDI");
		КонецЕсли;
		
	ИначеЕсли РезультатВопроса="Переключить режим на вычеркивание" Тогда
		УстановитьКонстантуEDIВызовСервера("ОбновлятьВесьСписокПриРаботеСЗаказами",ложь);
		ОбновлятьВесьСписокПриРаботеСЗаказами=Ложь;
		ТекстПредупреждения="Режим изменен";
		Если Параметры.МодальностьЗапрещена Тогда 
			Выполнить("ПоказатьПредупреждение(,ТекстПредупреждения,,""Контур.EDI"")");
		Иначе
			Предупреждение(ТекстПредупреждения,,"Контур.EDI");
		КонецЕсли;
	ИначеЕсли РезультатВопроса="Отложить вопрос на день" Тогда
		УстановитьКонстантуEDIВызовСервера("ОтказВычеркиватьСтрокиДо",ТекущаяДата()+60*60*24);
	ИначеЕсли РезультатВопроса="Отказаться" Тогда  //на 7 дней
		УстановитьКонстантуEDIВызовСервера("ОтказВычеркиватьСтрокиДо",ТекущаяДата()+60*60*24*7);
	КонецЕсли;		

КонецПроцедуры // ОбработчикРешенияОпереключенииРежима()

&НаСервере
Функция ПолучитьКонстантуEDIВызовСервера(ИмяКонстнанты)

Возврат МодульОбъекта().ПолучитьКонстантуEDI(ИмяКонстнанты)	

КонецФункции // УстановитьКонстантуEDIВызовСервера()
&НаСервере
Процедура УстановитьКонстантуEDIВызовСервера(ИмяКонстнанты,Значение)

МодульОбъекта().УстановитьКонстантуEDI(ИмяКонстнанты,Значение)	

КонецПроцедуры // УстановитьКонстантуEDIВызовСервера()

//возвращает результат сравнения ключевых статусов ссылок на сообщения (допускается передать ссылки на разные сообщения)
//если сравнить не удается - считается что изменения были
&НаКлиенте
Функция СообщенияПоСсылкамИзменились(СсылкаНаСообщение1,СсылкаНаСообщение2)
	
		Если ТипЗнч(СсылкаНаСообщение1)=Тип("СправочникСсылка.КонтурEDI_Сообщения")
			И (ТипЗнч(СсылкаНаСообщение2)=Тип("СправочникСсылка.КонтурEDI_Сообщения")
			или ТипЗнч(СсылкаНаСообщение2)=Тип("Структура")) Тогда
			
			Сообщение_До = ?(ТипЗнч(СсылкаНаСообщение2)=Тип("Структура"),СсылкаНаСообщение2,СтатусСсылкиИПризнакАрхивУСсылкиСообщения(СсылкаНаСообщение2));
			Сообщение_После = СтатусСсылкиИПризнакАрхивУСсылкиСообщения(СсылкаНаСообщение1);
			Если Сообщение_До=Неопределено или Сообщение_После=Неопределено Тогда 
				Возврат Истина; 
			КонецЕсли;  //если не можем проанализировать - пессимистически считаем что изменение было
			
			Если (Сообщение_До.СтатусСсылки<>Сообщение_После.СтатусСсылки
				Или Сообщение_До.Архив<>Сообщение_После.Архив)
				Тогда
				Возврат Истина;//что-то произошло
			Иначе
				Возврат Ложь; //статусы полностью совпадают - походит на то что карточку сообщения просто закрыли не выполнив действия или действия выполнить не удалось
			КонецЕсли;
		Иначе
			Возврат Истина; //если не можем проанализировать - пессимистически считаем что изменение было
		КонецЕсли;
		
КонецФункции

&НаСервереБезКонтекста
Функция СтатусСсылкиИПризнакАрхивУСсылкиСообщения(СообщСсылка)
	
	Если СообщСсылка = Неопределено Тогда 
		СтруктураВозврата = Неопределено;	
	иначе
		СтруктураВозврата = Новый Структура;
		СтруктураВозврата.Вставить("СтатусСсылки",СообщСсылка.Статус);
		СтруктураВозврата.Вставить("Архив",СообщСсылка.Архив);
	КонецЕсли;
	
	Возврат СтруктураВозврата;
	
КонецФункции

&НаСервере
Функция ОбработкаСообщенияБезОткрытияФормыСервер(ТипСообщения, СообщениеСсылка)
	
	Если 	ТипСообщения = "ORDERS"
		ИЛИ ТипСообщения = "RETDES"
		ИЛИ ТипСообщения = "RETANN"
		ИЛИ ТипСообщения = "DESADV"Тогда
		
		СообщениеЗаказ = МодульОбъекта().ПрочитатьСообщение(СообщениеСсылка,,ТипСообщения,"Входящее");
		МодульОбъекта().КонвертироватьСообщениеEDIв1С(СообщениеЗаказ);
		
		Если Не ЗначениеЗаполнено(СообщениеЗаказ.Партнер) Тогда
			СообщениеЗаказ.Партнер = МодульОбъекта().ОпределитьПартнераСообщения(СообщениеЗаказ);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СообщениеЗаказ.Партнер) Тогда
			Если СообщениеЗаказ.Направление = "Входящее" Тогда
				Если НЕ ЗначениеЗаполнено(СообщениеЗаказ.Отправитель1С) Тогда
					СообщениеЗаказ.Отправитель1С = СообщениеЗаказ.Партнер;
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
		РезультатПроверкиДанных = МодульОбъекта().ПроверитьПоля1С(СообщениеЗаказ);
		
		Если РезультатПроверкиДанных.Успешно Тогда
			
			РезультатВыполненияДействия = МодульОбъекта().ПринятьВходящееСообщение(СообщениеЗаказ);
			
			Возврат РезультатВыполненияДействия.Успешно;
			
		Иначе
			
			Возврат Ложь;
			
		КонецЕсли;
		
		
	Иначе //ВыбраннаяСтрока.ТипСообщения = "ORDERS" Тогда
		//пока не обрабатываем другие сообщения
		Возврат Ложь;
		
	КонецЕсли;
КонецФункции

&НаСервере
Функция _ОтправитьИсходящееСообщениеБезОткрытияФорм(ВыбраннаяСтрока,НеОткрыватьФормы=Истина)
	
	ТипСообщения = ВыбраннаяСтрока.ТипСообщения;
	
	Сообщение = МодульОбъекта().ПодготовитьИсходящееСообщение(ТипСообщения, ВыбраннаяСтрока.Документ);
	
	//Если НеОткрыватьФормы Тогда
		
		_Параметры = Новый Структура();
		_Параметры.Вставить("ОтправитьСообщениеИзФормы",	Истина);
		_Параметры.Вставить("Сообщение",					Сообщение);
		
		Если ТипСообщения="DESADV" Тогда
			//DESADV может отправляться вместе с INVOIC
			ОтправлятьINVOICсDESADV = МодульОбъекта().ПолучитьЗначениеСвойстваОбъектаEDI(Сообщение.Получатель1С, "ОтправлятьINVOICсDESADV");
			
			Если ОтправлятьINVOICсDESADV = Истина Тогда
				СчетФактура = МодульОбъекта().ПолучитьСчетФактуруНакладной(ВыбраннаяСтрока.Документ);
				Если Не ЗначениеЗаполнено(СчетФактура) Тогда
					Сообщить("Не удалось найти счет-фактуру для отправки по документу "+ВыбраннаяСтрока.Документ);
					//если в партнере стоит флаг "Отправлять INVOIC вместе с DESADV", и мы не нашли документ для отправки INVOIC, то не будем отправлять и DESADV.
					Возврат Ложь;
				КонецЕсли;
			КонецЕсли;	
		КонецЕсли;
		
		МодульОбъекта().ОтправитьСообщение(ТипСообщения,ВыбраннаяСтрока.Документ,_Параметры);
		
	//КонецЕсли;
	
КонецФункции


////открывает карточку сообщения СообщениеСсылка
&НаКлиенте
Процедура мОткрытьКарточкуСообщения(СтрокаСообщения, КодДействия = "", ПроизвольныеПараметры = Неопределено)  
	
		
	//!!! В управляемых формах несколько иной механизм сбора исходящих сообщения.
	//Сообщение мы не передаем в форму в уже готовом виде (как в ОФ) а передаем только параметры для сбора сообщения - СБОР сообщения вызывает САМА ФОРМА СООБЩЕНИЯ!!!
	//при этом на Эту процедуру наверчено больше чем в ОФ
	//в УФ больше используем КодДействия
	
	Если Лев(СтрокаСообщения.ТипСообщения,2) = "M_" Тогда//МУФ
		ОткрытьКарточкуМеркурий(СтрокаСообщения);
		Возврат;
	КонецЕсли;

	СообщениеСсылка = СтрокаСообщения.СообщениеСсылка;
	АнализСообщения_До = СтатусСсылкиИПризнакАрхивУСсылкиСообщения(СообщениеСсылка);
	
	ТолькоПросмотр=Ложь;
	Если Лев(КодДействия,10)="Отправить_"
		Или СтрокаСообщения.ТребуемоеДействие = "Обработать входящее сообщение"
		Или СтрокаСообщения.ТребуемоеДействие = "Переотправить сообщение"
		Или СтрокаСообщения.ТребуемоеДействие = "Обработать новую версию"
		Или (СтрокаСообщения.ТребуемоеДействие = "Не прошло валидацию" И Параметры.РежимРаботы = "Поставщик")
    Тогда
		ТолькоПросмотр = Ложь;
	Иначе
		ТолькоПросмотр = Истина;
	КонецЕсли;	

	ПараметрыФормы = Новый Структура("СообщениеСсылка",		СообщениеСсылка); 
	ПараметрыФормы.Вставить("Документ1С",					СтрокаСообщения.Документ);
	ПараметрыФормы.Вставить("ТипСообщения",					СтрокаСообщения.ТипСообщения);
	ПараметрыФормы.Вставить("Направление",					СтрокаСообщения.Направление);
	ПараметрыФормы.Вставить("КодДействия",					КодДействия);
	ПараметрыФормы.Вставить("РежимРаботы",					Параметры.РежимРаботы);
	ПараметрыФормы.Вставить("АдресХранилища",				Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект);
	ПараметрыФормы.Вставить("IDОсновнойФормы",				Объект.ПараметрыКлиентСервер.IDОсновнойФормы);
	ПараметрыФормы.Вставить("ПараметрыАвтотестирования",	Параметры.ПараметрыАвтотестирования);
	
	Если СтрокаСообщения.ТребуемоеДействие = "Переотправить сообщение"
		или (СтрокаСообщения.ТребуемоеДействие = "Работа завершена" и Лев(КодДействия,10)="Отправить_") 
		или (СтрокаСообщения.ТребуемоеДействие = "Работа завершена с уточнением" и Лев(КодДействия,10)="Отправить_") 
		или (СтрокаСообщения.ТребуемоеДействие = "Ожидайте подпись партнера" и Лев(КодДействия,10)="Отправить_") 
		Тогда
		
		ПараметрыФормы.Вставить("Действие", 						"Переотправка");
		ПараметрыФормы.Вставить("ПереотправляемоеСообщениеСсылка",	СообщениеСсылка);
		ТолькоПросмотр = Ложь;
		
	ИначеЕсли ТипЗнч(ПроизвольныеПараметры) = Тип("Структура") И ПроизвольныеПараметры.Свойство("ПереотправляемоеСообщениеСсылка") Тогда
		
		ПараметрыФормы.Вставить("Действие", 						"Переотправка");
		ПараметрыФормы.Вставить("ПереотправляемоеСообщениеСсылка",	ПроизвольныеПараметры.ПереотправляемоеСообщениеСсылка);
		ТолькоПросмотр = Ложь;

	КонецЕсли;
	
	ПараметрыФормы.Вставить("ТолькоПросмотр",				ТолькоПросмотр);
	
	Если НЕ ПроизвольныеПараметры = Неопределено Тогда 
		ПараметрыФормы.Вставить("ПроизвольныеПараметры",	ПроизвольныеПараметры);
	КонецЕсли;
	
	Если КодДействия = "Отправить_ORDRSP" 
		и ТипЗНЧ(ПроизвольныеПараметры) = Тип("Структура") 
		и ПроизвольныеПараметры.Свойство("Статус") 
		и ПроизвольныеПараметры.Статус = "Отклонить" Тогда 
		ПараметрыФормы.ТипСообщения	= "ORDRSP";  
		//костылик для отправки полной отмены в ordrsp из списка
		//Еще костыль на случай, когда по архивному ORDERS отправляем отклонение
		//нужно убрать ПереотправляемоеСообщениеСсылка, потому что в нем сам ORDERS, 
		//или подставить ссылку на уже отправленный ORDRSP
		Если СтрокаСообщения.ТипСообщения = "ORDERS" 
			И ПараметрыФормы.Свойство("ПереотправляемоеСообщениеСсылка") Тогда
			ПереотправляемоеСообщениеСсылка = НайтиСообщениеДокументаСервер(СтрокаСообщения.Документ, "ORDRSP", "Исходящее");
			Если ПереотправляемоеСообщениеСсылка <> Неопределено Тогда
				ПараметрыФормы.ПереотправляемоеСообщениеСсылка = ПереотправляемоеСообщениеСсылка;
			Иначе
				ПараметрыФормы.Удалить("ПереотправляемоеСообщениеСсылка");	
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если (КодДействия = "Отправить_RETINS" ИЛИ КодДействия = "Отправить_RETREC") 
		И ТипЗНЧ(ПроизвольныеПараметры) = Тип("Структура") 
		И ПроизвольныеПараметры.Свойство("Статус") 
		И ПроизвольныеПараметры.Статус = "Отклонить" Тогда
		
		ПараметрыФормы.ТипСообщения	= СтрЗаменить(КодДействия, "Отправить_", "");
		
	КонецЕсли;
	
	ИмяКарточкиСообщения = "ФормаСообщенияУправляемая";//Единая карточка сообщения для большинства типов сообщений
	Если СтрокаСообщения.ТипСообщения = "PRICELIST" 
		ИЛИ СтрокаСообщения.ТипСообщения = "PRICAT" 
		ИЛИ СтрокаСообщения.ТипСообщения = "SLSRPT"
		ИЛИ СтрокаСообщения.ТипСообщения = "INVRPT" Тогда
		ИмяКарточкиСообщения = "ФормаНезависимогоСообщенияУправляемая";
	ИначеЕсли СтрокаСообщения.ТипСообщения = "IFTMBF" 
		ИЛИ СтрокаСообщения.ТипСообщения = "IFTMBC" Тогда	
		ИмяКарточкиСообщения = "ФормаСообщенияПеревозкиУправляемая";
	ИначеЕсли СтрокаСообщения.ТипСообщения = "DELFOR" Тогда
		ИмяКарточкиСообщения = "ФормаСообщенияDELFORУправляемая";
	КонецЕсли;
	
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI(ИмяКарточкиСообщения, , ПараметрыФормы, ЭтаФорма, "ОбработчикПослеЗакрытияКарточкиСообщения", ЭтаФорма, АнализСообщения_До);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокНашихОрганизаций(Команда)
	
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("НашиОрганизации_СписокУправляемая", , , ЭтаФорма, "ОбработчикПослередактированияУчетныхЗаписей", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТребуемоеДействиеВСообщениях(Команда)
	ЗаполнитьТребуемоеДействиеВСообщенияхСервер();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТребуемоеДействиеВСообщенияхСервер()
	
	Запрос = МодульОбъекта().ИнициализироватьЗапрос_КонтурEDI(ложь);
	Запрос.Текст = 
	"ВЫБРАТЬ первые 1
	|	КонтурEDI_Сообщения.Ссылка
	|ИЗ
	|	Справочник.КонтурEDI_Сообщения КАК КонтурEDI_Сообщения
	|ГДЕ
	|	КонтурEDI_Сообщения.ТребуемоеДействие = """""
	;
	Если Запрос.Выполнить().Пустой() Тогда
		МодульОбъекта().УстановитьКонстантуEDI("Сообщения_ЗаполненоТребуемоеДействие",Истина);
	КонецЕсли;	
	
	
	//конвертация
	
	Запрос = МодульОбъекта().ИнициализироватьЗапрос_КонтурEDI(ложь);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КонтурEDI_Сообщения.Ссылка
	|ИЗ
	|	Справочник.КонтурEDI_Сообщения КАК КонтурEDI_Сообщения
	|ГДЕ
	|	КонтурEDI_Сообщения.ТребуемоеДействие = """""
	;
	Выборка = Запрос.Выполнить().Выбрать();
	Сч = 0;
	Пока Выборка.Следующий() Цикл
		Сч=Сч+1;
		
		ОбъектСообщения = Выборка.Ссылка.ПолучитьОбъект();
		СтруктураСообщения = МодульОбъекта().ПрочитатьСообщение(Выборка.Ссылка);
		МодульОбъекта().СохранитьОбъектСообщения(ОбъектСообщения,СтруктураСообщения);//перезаполним реквизиты справочника
		
	КонецЦикла;
	
	МодульОбъекта().УстановитьКонстантуEDI("Сообщения_ЗаполненоТребуемоеДействие",Истина);//как назвать константу?
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокПартнеров(Команда)
	
	СохранитьНастройкиГлавнойФормы();
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОткрытаКакДобавлениеПартнера", Истина);
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Партнеры_СписокУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбработчикПослеРедактированияПартнеров", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикПослеРедактированияПартнеров(ПараметрыЗакрытияИнтерактивнойФормы = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	//переведем активный элемент чтобы при перезаполнении партнеров не произошла случайно активация ячейки добавления партнера
	Элементы.ДеревоФильтров.ТекущийЭлемент = Элементы.ДеревоФильтров.ПодчиненныеЭлементы.ДеревоФильтровГруппа.ПодчиненныеЭлементы.ДеревоФильтровПометка;
	ОбработкаПослеОткрытияФормНастроекКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСтруктуруКомпании(Команда)
	
 	СохранитьНастройкиГлавнойФормы();
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("СтруктураКомпании_ЭлементУправляемая", , , ЭтаФорма, "ОбработчикПослередактированияСтруктурыКомпании", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикПослередактированияСтруктурыКомпании(ПараметрыЗакрытияИнтерактивнойФормы=неопределено, ДополнительныеПараметры=неопределено) Экспорт
	
	ОбработкаПослеОткрытияФормНастроекСОбновлениемКэшаКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьБыстрыеОтборы()
			
	СтруктураОтбора = Новый Структура;
	Если ЗначениеЗаполнено(ОтборНомерЗаказа) Тогда 
		СтруктураОтбора.Вставить("НомерЗаказа",СокрЛП(ОтборНомерЗаказа));
	КонецЕсли;
	Если  ЗначениеЗаполнено(ОтборДатаПоставки) Тогда 
		СтруктураОтбора.Вставить("ДатаПоставки",ОтборДатаПоставки);
	КонецЕсли;
	Если  ЗначениеЗаполнено(ОтборДатаЗаказа) Тогда 
		СтруктураОтбора.Вставить("ДатаЗаказа",ОтборДатаЗаказа);
	КонецЕсли;
	Если  ЗначениеЗаполнено(ОтборНомерСообщения) Тогда 
		СтруктураОтбора.Вставить("НомерПервичногоДокумента",ОтборНомерСообщения);
	КонецЕсли;
	Если  ЗначениеЗаполнено(ОтборДатаСообщения) Тогда 
		СтруктураОтбора.Вставить("ДатаПервичногоДокумента",ОтборДатаСообщения);
	КонецЕсли;   
	
	Если СтруктураОтбора.Количество() > 0 Тогда
		Элементы.ТабСообщения.ОтборСтрок = Новый ФиксированнаяСтруктура(СтруктураОтбора);
	Иначе
		Элементы.ТабСообщения.ОтборСтрок = Неопределено;
	КонецЕсли;

КонецПроцедуры


//быстрые отборы: номер заказа
&НаКлиенте
Процедура ТекЗначениеВБыстрыйОтборНомерСообщения(Команда)
	
	ТекСтрока = Элементы.ТабСообщения.ТекущиеДанные;
	Если ТекСтрока<>Неопределено Тогда 
		ОтборНомерСообщения=ТекСтрока.НомерПервичногоДокумента;
		УстановитьБыстрыеОтборы();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТекЗначениеВБыстрыйОтборДатаСообщения(Команда)
	ТекСтрока = Элементы.ТабСообщения.ТекущиеДанные;
	Если ТекСтрока<>Неопределено Тогда 
		ОтборДатаСообщения=ТекСтрока.ДатаПервичногоДокумента;
		УстановитьБыстрыеОтборы();
	КонецЕсли;
КонецПроцедуры 

&НаКлиенте
Процедура ОтборНомерСообщенияПриИзменении(Элемент)
	УстановитьБыстрыеОтборы();  
КонецПроцедуры

&НаКлиенте
Процедура ОтборНомерЗаказаПриИзменении(Элемент)
	УстановитьБыстрыеОтборы();
	
	//ЭлементОтбора = Элементы.ТабСообщения.ОтборСтрок.НомерЗаказа;
	//ЭлементОтбора.Значение = ОтборНомерЗаказа;
	//ЭлементОтбора.ВидСравнения = ВидСравнения.Содержит;
	//ЭлементОтбора.Использование = ЗначениеЗаполнено(ОтборНомерЗаказа);
	
	//Пасхалочки ф студийю!!!
	Если ВРЕГ(ОтборНомерЗаказа)="     " Тогда    //5 пробелов - кул
		Предупреждение("Включено бессмертие");
		Элементы.ВыполнитьОбмен.Картинка = БиблиотекаКартинок.Константа;
		Элементы.ВыполнитьОбмен.Заголовок = "ОБМЕН!!!";
		
		Элементы.ВыполнитьОбмен.ЦветФона = WebЦвета.Красный;
		
		//ЭлементыФормы.Панель1.ЦветФона = WebЦвета.Красный;
		//ЭлементыФормы.ПанельОтбораСообщений.ЦветФона = WebЦвета.Красный;
		
		//ЭлементыФормы.ТабСообщения.ЦветФонаПоля = WebЦвета.Красный;
		
	ИначеЕсли ВРЕГ(ОтборНомерЗаказа)="контур" Тогда
		Предупреждение("Включено бесконечное оружие");
	ИначеЕсли ВРЕГ(ОтборНомерЗаказа)="12345" Тогда
		Предупреждение("Возможно, вы имели в виду ""54321""");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТекЗначениеВБыстрыйОтборНомерЗаказа(Команда)
	ТекСтрока = Элементы.ТабСообщения.ТекущиеДанные;
	Если ТекСтрока<>Неопределено Тогда 
		ОтборНомерЗаказа=ТекСтрока.НомерЗаказа;
		УстановитьБыстрыеОтборы();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТекЗначениеВБыстрыйОтбордатаЗаказа(Команда)
	ТекСтрока = Элементы.ТабСообщения.ТекущиеДанные;
	Если ТекСтрока<>Неопределено Тогда 
		ОтборДатаЗаказа=ТекСтрока.ДатаЗаказа;
		УстановитьБыстрыеОтборы();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТекЗначениеВБыстрыйОтборДатаПоставки(Команда)
	ТекСтрока = Элементы.ТабСообщения.ТекущиеДанные;
	Если ТекСтрока<>Неопределено Тогда 
		ОтборДатаПоставки=ТекСтрока.ДатаПоставки;
		УстановитьБыстрыеОтборы();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТребуемыеДействияСброситьФлажки(Команда)
	УстановитьИлиСброситьФлажкиТребуемыхДействий(Ложь);	
КонецПроцедуры

&НаКлиенте
Процедура ТребуемыеДействияУстановитьФлажки(Команда)
	УстановитьИлиСброситьФлажкиТребуемыхДействий(Истина);	
	ПодключитьОбработчикОжидания("ЗаполнитьСписокЗадачКлиент",0.1,Истина);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИлиСброситьФлажкиТребуемыхДействий(Установить)
	Для Каждого Стр Из ОтборТребуемыеДействия.ПолучитьЭлементы() Цикл
		Стр.Пометка = Установить;
		
		Для Каждого Стр2 Из Стр.ПолучитьЭлементы() Цикл
			Стр2.Пометка = Установить;     //сбросим пометку везде за исключенем когда нам тыкнули в группу
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИлиСброситьФлажкиТиповСообщений(Установить)
	Для Каждого Стр Из ОтборТипыСообщений Цикл
		Стр.Пометка = Установить;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура КнопкаЕщеСвязанныеСообщения(Команда)
	
	ТекСтрока = Элементы.ТабСообщения.ТекущиеДанные;
	
	Если ТекСтрока<>Неопределено
		И ЗначениеЗаполнено(ТекСтрока.Документ) Тогда
	
		ПараметрыФормы=	Новый Структура;
		ПараметрыФормы.Вставить("ДокументСсылка",		ТекСтрока.Документ);
		ПараметрыФормы.Вставить("СообщениеСсылка",		ТекСтрока.СообщениеСсылка);
		ПараметрыФормы.Вставить("МодальностьЗапрещена",	Параметры.МодальностьЗапрещена);
		ПараметрыФормы.Вставить("РежимРаботы",			Параметры.РежимРаботы);
		
		МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Сервис_СвязанныеСообщенияУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбновитьСписок", ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаЕщеПереотправить(Команда)
	
	МассивИДвыделенных = Элементы.ТабСообщения.ВыделенныеСтроки;
	
	Если МассивИДвыделенных.Количество()=0 Тогда
		
		Возврат;
		
	ИначеЕсли МассивИДвыделенных.Количество()=1 Тогда
		
		ВыбраннаяСтрока = Элементы.ТабСообщения.ТекущиеДанные;
		ПакетнаяОбработка = Ложь;
		
		КодДействия="";//"ИсправитьОшибки"
		Если ВыбраннаяСтрока<>Неопределено Тогда 
						
			ПроизвольныеПараметры = Новый Структура;
			ПроизвольныеПараметры.Вставить("Действие", 			"Переотправка"); 		
			ПроизвольныеПараметры.Вставить("ПереотправляемоеСообщениеСсылка", ВыбраннаяСтрока.СообщениеСсылка);
			
			мОткрытьКарточкуСообщения(ВыбраннаяСтрока, КодДействия = "Переотправить сообщение", ПроизвольныеПараметры)
		
		КонецЕсли;
		
	ИначеЕсли МассивИДвыделенных.Количество()>0 Тогда
		
		КнопкиВопроса=новый СписокЗначений;
		КнопкиВопроса.Добавить("Переотправить "+МассивИДвыделенных.Количество());
		КнопкиВопроса.Добавить("Отмена");
		
		МодальныйВопрос("Будет выполнена переотправка: "+МассивИДвыделенных.Количество()+" сообщений. Продолжить?", КнопкиВопроса, МассивИДвыделенных, "ОбработчикСогласиеНаПереотправку");	
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикСогласиеНаПереотправку(РезультатВопроса,МассивИДвыделенных) Экспорт
	
	Если РезультатВопроса<>Неопределено И Лев(РезультатВопроса,13)="Переотправить" Тогда 
		
		Переотправлено=ПереотправитьВызовСервера(МассивИДвыделенных);
		
		Если Переотправлено=0 Тогда
			Сообщить("Не удалось переотправить ни одного сообщения");
		Иначе
			Сообщить("Успешно переотправлено "+Переотправлено+" сообщений");
			Если ОбновлятьВесьСписокПриРаботеСЗаказами Тогда 
				ЗаполнитьСписокЗадачКлиент();
			Иначе
				ВычеркнутьТекущиеСтроки();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПереотправитьВызовСервера(МассивИДвыделенных)
	
        КоличествоУспешно=0;
		Для Каждого ВыделеннаяСтрокаИД Из МассивИДвыделенных Цикл
			ВыделеннаяСтрока = Объект.ТабСообщения.НайтиПоИдентификатору(ВыделеннаяСтрокаИД);
			
			Если ВыделеннаяСтрока.Направление<>"Исходящее"
				ИЛИ Лев(ВыделеннаяСтрока.ТребуемоеДействие,9)="Отправить" тогда 
				Продолжить; //не будем переотправлять входящие и те, которые и так к отправке
			КонецЕсли;
			
			ПроизвольныеПараметры = Новый Структура;
			ПроизвольныеПараметры.Вставить("Действие", 			"Переотправка"); 		
			ПроизвольныеПараметры.Вставить("ПереотправляемоеСообщениеСсылка", ВыделеннаяСтрока.СообщениеСсылка);

			Сообщение = МодульОбъекта().ПодготовитьИсходящееСообщение(ВыделеннаяСтрока.ТипСообщения, ВыделеннаяСтрока.Документ, ПроизвольныеПараметры);
			
			КопияСообщения		= МодульОбъекта().ПолучитьКопиюСообщения(Сообщение);
			РезультатПроверки	= МодульОбъекта().ПроверитьПоляСообщения(КопияСообщения);
			
			Если РезультатПроверки.Успешно = Истина Тогда
				МодульОбъекта().СохранитьСообщение(Сообщение);											
				КоличествоУспешно=КоличествоУспешно+1;
			КонецЕсли;
		КонецЦикла;
        Возврат КоличествоУспешно;
		
КонецФункции // ПереотправитьВызовСервера()


&НаКлиенте
Процедура КнопкаЕщеОткрытьКарточкуСообщения(Команда)
	ТекСтрока = Элементы.ТабСообщения.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекСтрока.СообщениеСсылка) Тогда
    	мОткрытьКарточкуСообщения(ТекСтрока);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаЕщеОтменитьЗаказ(Команда)
	
	ТекСтрока = Элементы.ТабСообщения.ТекущиеДанные;
	Документ = ТекСтрока.Документ;
	СсылкаНаИсходныйORDERS = ТекСтрока.СообщениеСсылка;
	
	КнопкаЕщеОтменитьЗаказСервер(Документ,СсылкаНаИсходныйORDERS);
	
	ОбработчикПослеЗакрытияКарточкиСообщения();
	
КонецПроцедуры

&НаСервере
Процедура КнопкаЕщеОтменитьЗаказСервер(Документ,СсылкаНаИсходныйORDERS)
	
	ПараметрыСообщения = Новый Структура();
	ПараметрыСообщения.Вставить("Статус", "Отменить");
	ПараметрыСообщения.Вставить("ПереотправляемоеСообщениеСсылка", СсылкаНаИсходныйORDERS);
	
	МодульОбъекта().ОтправитьСообщение("ORDERS", Документ, ПараметрыСообщения);	

КонецПроцедуры

&НаКлиенте
Процедура КнопкаЕщеОтклонитьЗаказПолностью(Команда)
	
	Если Элементы.ТабСообщения.ВыделенныеСтроки.Количество()=0 Тогда
		
		Возврат;
		
	ИначеЕсли Элементы.ТабСообщения.ВыделенныеСтроки[0]=Неопределено Тогда
		
		Возврат;
		
	ИначеЕсли Элементы.ТабСообщения.ВыделенныеСтроки.Количество()=1 Тогда
		
	ТекСтрока = Элементы.ТабСообщения.ТекущиеДанные;
	
	МожноОтклонитьЗаказ = МожноОтправитьСообщение(ТекСтрока.Партнер, "ORDRSP");
	Если Не МожноОтклонитьЗаказ Тогда
		
		ТекстПредупреждения = "Настройки торговой сети не позволяют отклонить заказ.";
		Если Параметры.МодальностьЗапрещена Тогда 
			Выполнить("ПоказатьПредупреждение(,ТекстПредупреждения,,""Контур.EDI"")");
		Иначе
			Предупреждение(ТекстПредупреждения,,"Контур.EDI");
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	ПроизвольныеПараметры = Новый Структура();
	ПроизвольныеПараметры.Вставить("Статус","Отклонить");
	
	Если ТекСтрока.ТипСообщения = "ORDERS" Тогда
		ВходящийЗаказСсылка = ТекСтрока.СообщениеСсылка;
	ИначеЕсли ТекСтрока.ТипСообщения = "ORDRSP" Тогда
		ВходящийЗаказСсылка = НайтиСообщениеДокументаСервер(ТекСтрока.Документ, "ORDERS", "Входящее");
	КонецЕсли;
	ПроизвольныеПараметры.Вставить("ВходящийЗаказ", ВходящийЗаказСсылка);   //теперь так можно
	
	мОткрытьКарточкуСообщения(ТекСтрока,"Отправить_ORDRSP",ПроизвольныеПараметры);
	
	Иначе
	
		//групповые действия для отклонения нескольких заказов
		
		ТекстВопроса ="Будет обработано строк: "+Элементы.ТабСообщения.ВыделенныеСтроки.Количество()+". Продолжить?";
		КнопкиВопроса=новый СписокЗначений;
		КнопкиВопроса.Добавить("Продолжить");
		КнопкиВопроса.Добавить("Отмена");
		ДопПараметрДляПередачиВОбработчик=Неопределено;
		РезультатВопроса = Неопределено;
		
		Если Параметры.МодальностьЗапрещена Тогда
			Выполнить("ПоказатьВопрос(Новый ОписаниеОповещения(""ОбработчикСогласияНаГрупповыеORDRSP"", ЭтаФорма, ДопПараметрДляПередачиВОбработчик), ТекстВопроса, КнопкиВопроса,,,""Контур.EDI"")");
		Иначе
			РезультатВопроса = Вопрос(ТекстВопроса, КнопкиВопроса,,,"Контур.EDI");
			ОбработчикСогласияНаГрупповыеORDRSP(РезультатВопроса,ДопПараметрДляПередачиВОбработчик);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаЕщеОтклонитьВозвратПолностью(Команда)
	
	Если Элементы.ТабСообщения.ВыделенныеСтроки.Количество()=0 Тогда
		
		Возврат;
		
	ИначеЕсли Элементы.ТабСообщения.ВыделенныеСтроки[0]=Неопределено Тогда
		
		Возврат;
		
	ИначеЕсли Элементы.ТабСообщения.ВыделенныеСтроки.Количество()=1 Тогда
		
		ТекСтрока = Элементы.ТабСообщения.ТекущиеДанные;
		
		ПроизвольныеПараметры = Новый Структура();
		ПроизвольныеПараметры.Вставить("Статус", "Отклонить"); 		
		
		Если ТекСтрока.ТипСообщения = "RETANN" Тогда
			ТипОтправляемогоСообщения = "RETINS";   			
		ИначеЕсли ТекСтрока.ТипСообщения = "RETDES" Тогда	
			ТипОтправляемогоСообщения = "RETREC";
		КонецЕсли;
		
		ПроизвольныеПараметры.Вставить("ВходящийВозврат", ТекСтрока.СообщениеСсылка);
		
		МожноОтклонитьВозврат = МожноОтправитьСообщение(ТекСтрока.Партнер, ТипОтправляемогоСообщения);
		
		Если Не МожноОтклонитьВозврат Тогда
			ТекстПредупреждения = "Настройки торговой сети не позволяют отклонить возврат.";
			Если Параметры.МодальностьЗапрещена Тогда 
				Выполнить("ПоказатьПредупреждение(,ТекстПредупреждения,,""Контур.EDI"")");
			Иначе
				Предупреждение(ТекстПредупреждения,,"Контур.EDI");
			КонецЕсли;
			
			Возврат;
		КонецЕсли;
		
		мОткрытьКарточкуСообщения(ТекСтрока, "Отправить_" + ТипОтправляемогоСообщения, ПроизвольныеПараметры);
		
	Иначе 		
		//групповые действия для отклонения нескольких возвратов
		
		ТекстВопроса ="Будет обработано строк: "+Элементы.ТабСообщения.ВыделенныеСтроки.Количество()+". Продолжить?";
		КнопкиВопроса=новый СписокЗначений;
		КнопкиВопроса.Добавить("Продолжить");
		КнопкиВопроса.Добавить("Отмена");
		ДопПараметрДляПередачиВОбработчик=Неопределено;
		РезультатВопроса = Неопределено;
		
		Если Параметры.МодальностьЗапрещена Тогда
			Выполнить("ПоказатьВопрос(Новый ОписаниеОповещения(""ОбработчикСогласияНаГрупповыеRETINS_RETREC"", ЭтаФорма, ДопПараметрДляПередачиВОбработчик), ТекстВопроса, КнопкиВопроса,,,""Контур.EDI"")");
		Иначе
			РезультатВопроса = Вопрос(ТекстВопроса, КнопкиВопроса,,,"Контур.EDI");
			ОбработчикСогласияНаГрупповыеRETINS_RETREC(РезультатВопроса,ДопПараметрДляПередачиВОбработчик);
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикСогласияНаГрупповыеRETINS_RETREC(РезультатВопроса,ДопПараметрДляПередачиВОбработчик) Экспорт
	Если РезультатВопроса= "Продолжить" Тогда 
	
		МассивСТрокДляОбработки = Новый Массив;
		Для Каждого ИдентификаторСтроки Из Элементы.ТабСообщения.ВыделенныеСтроки Цикл
		 	ТекСтрока = Объект.ТабСообщения.НайтиПоИдентификатору(ИдентификаторСтроки);
			МассивСТрокДляОбработки.Добавить(ТекСтрока);
		КонецЦикла;
		
		Для Каждого СтрокаДанныхФормы Из МассивСТрокДляОбработки Цикл
			 //проверим что перед нами строка RETANN
			 Если СтрокаДанныхФормы.ТипСообщения = "RETANN" ИЛИ СтрокаДанныхФормы.ТипСообщения = "RETDES" Тогда 
			 	ОтклонитьВозвратыСервер(СтрокаДанныхФормы.СообщениеСсылка,СтрокаДанныхФормы.Партнер,СтрокаДанныхФормы.Документ,СтрокаДанныхФормы.ТипСообщения);
			 КонецЕсли;
		 КонецЦикла;
		 
		 //обновить список
		 ЗаполнитьСписокЗадачКлиент();
    КонецЕсли;
КонецПроцедуры // 

&НаСервере
Процедура ОтклонитьВозвратыСервер(СообщениеСсылка,Партнер,Документ1С,ТипСообщения)
	
	ПараметрыОтмены = Новый Структура();
	ПараметрыОтмены.Вставить("Статус",				"Отклонить");
	ПараметрыОтмены.Вставить("ВходящийВозврат",		СообщениеСсылка);
	
	МожноОтклонитьВозврат = (МодульОбъекта().ПолучитьЗначениеСвойстваОбъектаEDI(Партнер,"RETINS")=Истина);
	Если Не МожноОтклонитьВозврат Тогда
		ПредставлениеПартнера = МодульОбъекта().ПолучитьПредставлениеЭлементаСправочника(Партнер);
		МодульОбъекта().ВывестиПредупреждение_КонтурEDI("Настройки торговой сети """ + ПредставлениеПартнера + """ не позволяют отклонить возврат.");
		Возврат;
	КонецЕсли;
		
	НачатьТранзакцию();
	Попытка		
		
		Если ТипСообщения = "RETANN" Тогда
			Сообщение = МодульОбъекта().ПодготовитьИсходящееСообщение("RETINS",Документ1С,ПараметрыОтмены);
		Иначе
			Сообщение = МодульОбъекта().ПодготовитьИсходящееСообщение("RETREC",Документ1С,ПараметрыОтмены);
		КонецЕсли;
		МодульОбъекта().СохранитьСообщение(Сообщение);
		
	    Если ТранзакцияАктивна() Тогда
	        ЗафиксироватьТранзакцию();
	    КонецЕсли;
		
	Исключение
		
	    Если ТранзакцияАктивна() Тогда
	        ОтменитьТранзакцию();
		КонецЕсли;
		
		ТекстОшибки = "Не удалось отклонить возврат - " + ОписаниеОшибки();
		МодульОбъекта().ЗаписьЖурналаРегистрации_КонтурEDI(УровеньЖурналаРегистрации.Ошибка, ТекстОшибки);
		ВызватьИсключение ТекстОшибки;				
		
	КонецПопытки;
		
КонецПроцедуры // 

&НаКлиенте
Процедура КнопкаЕщеРаспечататьЗаказы(Команда)

	МассивИДвыделенных = Элементы.ТабСообщения.ВыделенныеСтроки;
	МассивСообщений = Новый Массив;
	Если МассивИДвыделенных.Количество()>0 Тогда
		Для Каждого ВыделеннаяСтрокаИД Из МассивИДвыделенных Цикл
			ВыделеннаяСтрока = Объект.ТабСообщения.НайтиПоИдентификатору(ВыделеннаяСтрокаИД);
			Если ВыделеннаяСтрока.СообщениеСсылка <> Неопределено
				И ВыделеннаяСтрока.ТипСообщения = "ORDERS" Тогда
				МассивСообщений.Добавить(ВыделеннаяСтрока.СообщениеСсылка);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	Если МассивСообщений.Количество() = 0 Тогда
		ТекстПредупреждения="В выбранных строках нет заказов для печати.";
		Если Параметры.МодальностьЗапрещена Тогда 
			Выполнить("ПоказатьПредупреждение(,ТекстПредупреждения,,""Контур.EDI"")");
		Иначе
			Предупреждение(ТекстПредупреждения,,"Контур.EDI");
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ТабДокОбщий = ОбработатьМассовуюПечатьЗаказовСервер(МассивСообщений);
	
	ТабДокОбщий.Показать("Новые заявки ("+МассивСообщений.Количество()+" шт) - "+Формат(ТекущаяДата(),"ДЛФ=Д"));
	
КонецПроцедуры

&НаСервере
Функция ОбработатьМассовуюПечатьЗаказовСервер(МассивСообщений)
	
	ТабДокОбщий = МодульОбъекта().ОбработатьМассовуюПечатьЗаказов(МассивСообщений);

	Возврат ТабДокОбщий;
	
КонецФункции

&НаКлиенте
Процедура КнопкаЕщеРаспечататьПалетныеЛисты(Команда)
	
	МассивИДвыделенных = Элементы.ТабСообщения.ВыделенныеСтроки;
	МассивСообщений = Новый Массив;
	Если МассивИДвыделенных.Количество()>0 Тогда
		Для Каждого ВыделеннаяСтрокаИД Из МассивИДвыделенных Цикл
			ВыделеннаяСтрока = Объект.ТабСообщения.НайтиПоИдентификатору(ВыделеннаяСтрокаИД);
			Если (ВыделеннаяСтрока.ТипСообщения = "DESADV"
				ИЛИ ВыделеннаяСтрока.ТипСообщения = "INVOIC")
				И ЗначениеЗаполнено(ВыделеннаяСтрока.СообщениеСсылка) Тогда
				МассивСообщений.Добавить(ВыделеннаяСтрока.СообщениеСсылка);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Результат = ОбработатьМассовуюПечатьПалетныхЛистовСервер(МассивСообщений);
	
	Если Не Результат.Успешно Тогда
		ТекстПредупреждения="Не удалось подключить внешнюю компоненту для формирования картинки штрихкода.";
		Если Параметры.МодальностьЗапрещена Тогда 
			Выполнить("ПоказатьПредупреждение(,ТекстПредупреждения,,""Контур.EDI"")");
		Иначе
			Предупреждение(ТекстПредупреждения,,"Контур.EDI");
		КонецЕсли;
	Иначе
		Если Результат.КоличествоЛистов = 0 Тогда
			ТекстПредупреждения="В выбранных строках нет палетных листов для печати.";
			Если Параметры.МодальностьЗапрещена Тогда 
				Выполнить("ПоказатьПредупреждение(,ТекстПредупреждения,,""Контур.EDI"")");
			Иначе
				Предупреждение(ТекстПредупреждения,,"Контур.EDI");
			КонецЕсли;
		Иначе
			Результат.ТабДокОбщий.Показать("Палетные листы ("+Результат.КоличествоЛистов+" шт) - "+Формат(ТекущаяДата(),"ДЛФ=Д"));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОбработатьМассовуюПечатьПалетныхЛистовСервер(МассивСообщений)

	Результат = МодульОбъекта().ОбработатьМассовуюПечатьПалетныхЛистов(МассивСообщений);

	Возврат Результат;	

КонецФункции // ОбработатьМассовуюПечатьПалетныхЛистов()

&НаКлиенте
Процедура ОбработчикСогласияНаГрупповыеORDRSP(РезультатВопроса,ДопПараметрДляПередачиВОбработчик) Экспорт
	Если РезультатВопроса= "Продолжить" Тогда 
	
		МассивСТрокДляОбработки = Новый Массив;
		Для Каждого ИдентификаторСтроки Из Элементы.ТабСообщения.ВыделенныеСтроки Цикл
		 	ТекСтрока = Объект.ТабСообщения.НайтиПоИдентификатору(ИдентификаторСтроки);
			МассивСТрокДляОбработки.Добавить(ТекСтрока);
		КонецЦикла;
		
		Для Каждого СтрокаДанныхФормы Из МассивСТрокДляОбработки Цикл
			 //проверим что перед нами строка orders
			 Если СтрокаДанныхФормы.ТипСообщения = "ORDERS" и СтрокаДанныхФормы.ТребуемоеДействие<>"Обработать уточнение обратного заказа" Тогда 
			 	ОтклонитьЗаказыСервер(СтрокаДанныхФормы.СообщениеСсылка,СтрокаДанныхФормы.Партнер,СтрокаДанныхФормы.Документ);
			 КонецЕсли;
		 КонецЦикла;
		 
		 //обновить список
		 ЗаполнитьСписокЗадачКлиент();
    КонецЕсли;
КонецПроцедуры // ОбработчикСогласияНаГрупповыеORDRSP()

&НаСервере
Процедура ОтклонитьЗаказыСервер(СообщениеСсылка,Партнер,Документ1С)
	
	ПараметрыОтмены = Новый Структура();
	ПараметрыОтмены.Вставить("Статус","Отклонить");
	ПараметрыОтмены.Вставить("ВходящийЗаказ",СообщениеСсылка);
	
	МожноОтклонитьЗаказ = (МодульОбъекта().ПолучитьЗначениеСвойстваОбъектаEDI(Партнер,"ORDRSP")=Истина);
	Если Не МожноОтклонитьЗаказ Тогда
		ПредставлениеПартнера = МодульОбъекта().ПолучитьПредставлениеЭлементаСправочника(Партнер);
		МодульОбъекта().ВывестиПредупреждение_КонтурEDI("Настройки торговой сети """ + ПредставлениеПартнера + """ не позволяют отклонить заказ.");
		Возврат;
	КонецЕсли;
		
	НачатьТранзакцию();
	Попытка		
		
		Сообщение = МодульОбъекта().ПодготовитьИсходящееСообщение("ORDRSP",Документ1С,ПараметрыОтмены);
		МодульОбъекта().СохранитьСообщение(Сообщение);
		
		//исходный заказ помечаем отклоненным
		СообщениеЗаказ = МодульОбъекта().ПолучитьОбъектСообщения(СообщениеСсылка);
		СообщениеЗаказ.Статус = "Отклонен";
		СообщениеЗаказ.ТребуемоеДействие = "СообщениеОтклоненоПолучателем";
		СообщениеЗаказ.Записать();
		
	    Если ТранзакцияАктивна() Тогда
	        ЗафиксироватьТранзакцию();
		КонецЕсли;	
		
	Исключение
		
	    Если ТранзакцияАктивна() Тогда
	        ОтменитьТранзакцию();
		КонецЕсли;
		
		ТекстОшибки = "Не удалось отклонить заказ - " + ОписаниеОшибки();
		МодульОбъекта().ЗаписьЖурналаРегистрации_КонтурEDI(УровеньЖурналаРегистрации.Ошибка, ТекстОшибки);
		ВызватьИсключение ТекстОшибки;				
		
	КонецПопытки;
	
КонецПроцедуры // ОтклонитьЗаказСервер()

&НаСервере
Функция МожноОтправитьСообщение(Партнер, ТипСообщения)
	
	Возврат (МодульОбъекта().ПолучитьЗначениеСвойстваОбъектаEDI(Партнер, ТипСообщения) = Истина);
	
КонецФункции // МожноОтправитьСообщение()

&НаКлиенте
Процедура УстановитьДоступностьКнопокЕще(Элемент)
	
	Для Каждого КнопкаРазделаЕще Из Элементы.Еще.ПодчиненныеЭлементы  Цикл
		КнопкаРазделаЕще.Видимость = Ложь;
	КонецЦикла;
	
	КоличествоВыделенныхСтрок = Элементы.ТабСообщения.ВыделенныеСтроки.Количество();
	//заполним общие действия
	
	СкопироватьКнопкуЕще("ВыделитьВсе");
	СкопироватьКнопкуЕще("ПереотправитьПоРеестру");
	СкопироватьКнопкуЕще("СверкаСчетовФактур");
	СкопироватьКнопкуЕще("ПроверитьНаличиеОтказовЗапросовНаУточнение");
	СкопироватьКнопкуЕще("Разделитель");
	
	Если Параметры.РежимРаботы = "Поставщик" 
		И НЕ КоличествоВыделенныхСтрок = 0 Тогда 
		СкопироватьКнопкуЕще("РаспечататьЗаказы");
		СкопироватьКнопкуЕще("РаспечататьПалетныеЛисты");
	КонецЕсли;
	
	Если Параметры.РежимРаботы = "Покупатель" Тогда
		СкопироватьКнопкуЕще("СоздатьЗаказ");
	КонецЕсли;
	
	Если Параметры.РежимРаботы = "Поставщик" 
		И КоличествоВыделенныхСтрок > 1 Тогда
		Для Каждого ИдентификаторСтроки Из Элементы.ТабСообщения.ВыделенныеСтроки Цикл
			
			Строка = Объект.ТабСообщения.НайтиПоИдентификатору(ИдентификаторСтроки);
			
			ВывелиОтклонитьЗаказПолностью = Ложь;
			ВывелиОтклонитьВозвратПолностью = Ложь;
			
			Если Строка <> Неопределено 
				И Строка.ТипСообщения = "ORDERS" 
				И Строка.ТребуемоеДействие <> "Обработать уточнение обратного заказа" Тогда 
				// сделаем кнопку массового отклонения заказов
				СкопироватьКнопкуЕще("ОтклонитьЗаказПолностью");
				ВывелиОтклонитьЗаказПолностью = Истина;
			КонецЕсли;
			
			Если Строка <> Неопределено 
				И (Строка.ТипСообщения = "RETANN" 
				ИЛИ Строка.ТипСообщения = "RETDES")  Тогда 
				// сделаем кнопку массового отклонения возвратов
				СкопироватьКнопкуЕще("ОтклонитьВозвратПолностью");
				ВывелиОтклонитьВозвратПолностью = Истина;
			КонецЕсли;
			Если ВывелиОтклонитьЗаказПолностью И ВывелиОтклонитьВозвратПолностью Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;	
	
	//теперь действия, зависящие от конкретной строки
	
	Если КоличествоВыделенныхСтрок = 1
		И Элементы.ТабСообщения.ВыделенныеСтроки[0] <> Неопределено Тогда
		
		ВыбраннаяСтрока = Элементы.ТабСообщения.ТекущиеДанные; 
		Если ВыбраннаяСтрока = Неопределено Тогда 
			Состояние("Нет активной строки",100,,);
			Возврат;
		КонецЕсли;
		
		//установим доступность каждой кнопки
		
		//Кнопка "Открыть карточку сообщения"
		Если ЗначениеЗаполнено(ВыбраннаяСтрока.СообщениеСсылка) Тогда
			СкопироватьКнопкуЕще("ОткрытьКарточкуСообщения");
		КонецЕсли;	
		
		//Кнопка "Связанные сообщения"
		Если ЗначениеЗаполнено(ВыбраннаяСтрока.Документ) Тогда
			СкопироватьКнопкуЕще("СвязанныеСообщения");
		КонецЕсли;	
		
		//кнопки для поставщика
		
		Если Параметры.РежимРаботы = "Поставщик" Тогда
			
			СкопироватьКнопкуЕще("ПоказатьНаЯндексКартах");
			
			//Кнопки "Показать расхождения входящего заказа и заказа в 1С"
			//	"Отклонить заказ полностью"
			Если (ВыбраннаяСтрока.ТипСообщения = "ORDERS"
				Или ВыбраннаяСтрока.ТипСообщения = "ORDRSP")
				И ЗначениеЗаполнено(ВыбраннаяСтрока.СообщениеСсылка)
				И ВыбраннаяСтрока.ТребуемоеДействие <> "Ожидайте заказ" Тогда
				СкопироватьКнопкуЕще("ПоказатьРасхожденияВходящегоЗаказаИЗаказаВ1С");
				Если ВыбраннаяСтрока.ТребуемоеДействие <> "Обработать уточнение обратного заказа" Тогда
					СкопироватьКнопкуЕще("ОтклонитьЗаказПолностью");
				КонецЕсли;
			КонецЕсли;
			
			//Кнопка "Отклонить возврат полностью"
			Если (ВыбраннаяСтрока.ТипСообщения = "RETDES" 
				ИЛИ ВыбраннаяСтрока.ТипСообщения = "RETANN")
				И ЗначениеЗаполнено(ВыбраннаяСтрока.СообщениеСсылка) Тогда
				СкопироватьКнопкуЕще("ОтклонитьВозвратПолностью");
			КонецЕсли;
			
			//Кнопка "Показать расхождения приемки"
			Если ВыбраннаяСтрока.ТипСообщения = "RECADV"
				И ЗначениеЗаполнено(ВыбраннаяСтрока.СообщениеСсылка)
				И ВыбраннаяСтрока.ТребуемоеДействие <> "Ожидайте уведомление о приемке" Тогда
				СкопироватьКнопкуЕще("ПоказатьРасхожденияПриемки");
			КонецЕсли;
			
			//Кнопка "Переотправить"
			Если ВыбраннаяСтрока.Направление = "Исходящее" 
				И Найти(ВыбраннаяСтрока.ТребуемоеДействие, "Создать") = 0 
				И Найти(ВыбраннаяСтрока.ТребуемоеДействие, "Провести") = 0 
				И Найти(ВыбраннаяСтрока.ТребуемоеДействие, "Ожидается проведение") = 0 
				И Найти(ВыбраннаяСтрока.ТребуемоеДействие, "Ожидается создание") = 0 
				И ВыбраннаяСтрока.ТипСообщения <> "DELFOR" Тогда
				СкопироватьКнопкуЕще("Переотправить");
			КонецЕсли;	
			
			Если (ВыбраннаяСтрока.ТипСообщения = "INVOIC" ИЛИ ВыбраннаяСтрока.ТипСообщения = "COINVOIC")
				И ЗначениеЗаполнено(ВыбраннаяСтрока.СообщениеСсылка) 
			Тогда
				
				//как отфильтровать?
				Если ВыбраннаяСтрока.ТребуемоеДействие <> "Создать счет-фактуру" 
					И ВыбраннаяСтрока.ТребуемоеДействие <> "Отправить счет-фактуру" 
					//И ВыбраннаяСтрока.ТребуемоеДействие <> "Подписать в Диадоке"   //c 16 релиза можно открыть
				Тогда
					СкопироватьКнопкуЕще("ОткрытьЭлДокументыВДиадоке");
				КонецЕсли;	
				
			КонецЕсли;

		//кнопки для сети	
		ИначеЕсли Параметры.РежимРаботы = "Покупатель" Тогда
			
			//Кнопка "Переотправить"
			Если (ВыбраннаяСтрока.ТипСообщения = "ORDERS"
				Или ВыбраннаяСтрока.ТипСообщения = "RECADV"
				Или ВыбраннаяСтрока.ТипСообщения = "RETANN")
				
				И ЗначениеЗаполнено(ВыбраннаяСтрока.СообщениеСсылка)
				
				И (ВыбраннаяСтрока.ТребуемоеДействие = "Работа завершена"
					или ВыбраннаяСтрока.ТребуемоеДействие = "Переотправить сообщение")
			Тогда
				СкопироватьКнопкуЕще("Переотправить");
			КонецЕсли;
			
			//Кнопка "Скопировать"
			Если ЗначениеЗаполнено(ВыбраннаяСтрока.Документ)
				И (ВыбраннаяСтрока.ТипСообщения = "ORDERS" или ВыбраннаяСтрока.ТипСообщения = "ORDRSP")
			Тогда
				СкопироватьКнопкуЕще("СкопироватьЗаказ");
			КонецЕсли;
			
			//Кнопка "Отменить заказ"
			Если ВыбраннаяСтрока.ТипСообщения = "ORDERS"
				И ЗначениеЗаполнено(ВыбраннаяСтрока.Документ)
				И ЗначениеЗаполнено(ВыбраннаяСтрока.СообщениеСсылка)
				//нужна ли проверка на то, что сообщение отправлено
			Тогда
				СкопироватьКнопкуЕще("ОтменитьЗаказ");
			КонецЕсли;
			
		КонецЕсли;	
	Иначе
		СкопироватьКнопкуЕще("Переотправить");
		Состояние("Нет активной строки",100,,);
	КонецЕсли;	
	
	ДобавитьКнопкиЕщеИзПМ();
	
	//установить доступность пунктов из "Еще"

КонецПроцедуры

//в УФ просто устанавливает видимость соответствующей кнопки  название оставлено для удобства || разработки уф оф
//В обычных формах работает так:
//Копирует кнопку с заданным именем из скрытого меню "Еще" в видимое меню
//Такие навороты нужны для того, чтобы в меню "Еще" были только доступные для выбранной строки действия
&НаКлиенте
Процедура СкопироватьКнопкуЕще(ИмяКнопки)
	//Источник = ЭлементыФормы.КнопкаЕщеСкрытая.Кнопки.Найти(ИмяКнопки);
	//Нов = ЭлементыФормы.КнопкаЕще.Кнопки.Добавить(Источник.Имя, Источник.ТипКнопки,Источник.Текст, Источник.Действие);
	//Нов.Картинка = Источник.Картинка;
	//Нов.СочетаниеКлавиш = Источник.СочетаниеКлавиш;
	//Нов.Доступность = Истина;
	//
	//Нов = ЭлементыФормы.МенюСпискаДокументов.Кнопки.Добавить(Источник.Имя, Источник.ТипКнопки,Источник.Текст, Источник.Действие);
	//Нов.Картинка = Источник.Картинка;
	//Нов.СочетаниеКлавиш = Источник.СочетаниеКлавиш;
	//Нов.Доступность = Истина;
	
	//метка рефакторинг - пока не все кнопки перенесены - в попытке. 
	Попытка
		Выполнить("Элементы.КнопкаЕще"+ИмяКнопки+".Видимость=Истина");
	Исключение
		КомментарийЗаписи = "Не удалось скопировать кнопки меню еще - " + ОписаниеОшибки();
		МодульОбъектаКлиент().ЗаписьЖурналаРегистрацииНаКлиенте_КонтурEDI("Ошибка", КомментарийЗаписи);
	КонецПопытки;
	
КонецПроцедуры	

&НаКлиенте
Процедура ДобавитьКнопкиЕщеИзПМ()
	
	ПараметрыОбработчика = Новый Структура("ЭтоУФ,ПараметрыКнопок", Истина, Новый СписокЗначений);
	ОбработкаСобытияПодключаемогоМодуляКлиент("ИзменитьСоставКнопкиЕще",,ПараметрыОбработчика);
	Для Каждого ПараметрыКнопки из ПараметрыОбработчика.ПараметрыКнопок Цикл
		Попытка
			Выполнить("Элементы."+ПараметрыКнопки.Значение+".Видимость="+?(ПараметрыКнопки.Пометка, "Истина", "Ложь"));
			Выполнить("Элементы."+ПараметрыКнопки.Значение+".Заголовок="""+ПараметрыКнопки.Представление+"""");
			Выполнить("Элементы."+ПараметрыКнопки.Значение+".Картинка = ПараметрыКнопки.Картинка");
		Исключение
			КомментарийЗаписи = "Не удалось добавить кнопки меню еще из ПМ - " + ОписаниеОшибки();
			МодульОбъектаКлиент().ЗаписьЖурналаРегистрацииНаКлиенте_КонтурEDI("Ошибка", КомментарийЗаписи);
		КонецПопытки;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура КнопкаЕщеНажатие(Команда)
	
	//Состояние("Определяю доступные действия", 60,,Элементы.Настройки.Картинка);
	
	УстановитьДоступностьКнопокЕще("");
	
	СписокКнопок=Новый СписокЗначений;
	Для Каждого КнопкаРазделаЕще Из Элементы.Еще.ПодчиненныеЭлементы  Цикл
		Если КнопкаРазделаЕще.Видимость = истина Тогда 
			СписокКнопок.Добавить(КнопкаРазделаЕще.Имя, КнопкаРазделаЕще.Заголовок, , КнопкаРазделаЕще.Картинка);
		КонецЕсли;
	КонецЦикла;
	
	
	Если Параметры.МодальностьЗапрещена Тогда 
		//Выполнить("ПоказатьВыборИзМеню(Новый ОписаниеОповещения(""ОбработчикВыбораДействияИзМенюЕще"",ЭтаФорма), СписокКнопок)"); 
		Выполнить("ПоказатьВыборИзСписка(Новый ОписаниеОповещения(""ОбработчикВыбораДействияИзМенюЕще"",ЭтаФорма), СписокКнопок)");//Привет тебе платформа 8.3.7! 
	Иначе
		ВыбранноеЗначение=ВыбратьИзМеню(СписокКнопок,Элементы.КнопкаЕще);
		ОбработчикВыбораДействияИзМенюЕще(ВыбранноеЗначение);
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикВыбораДействияИзМенюЕще(ВыбранноеЗначение=неопределено,ДополнительныеПараметры=неопределено)Экспорт
	Если ВыбранноеЗначение<>Неопределено Тогда
		Состояние("Выполняю действие: " + НРег(ВыбранноеЗначение.Представление),50,,Элементы.Настройки.Картинка);
		ИмяОбработчика = ВыбранноеЗначение.Значение;
		Если Лев(ИмяОбработчика, 11) = "КнопкаЕщеПМ" Тогда//ПМ ее добавил, он и знает, что с ней делать
			
			ПараметрыВызоваПМ = Новый Структура("ИмяКнопки,ЭтоУФ,ВыделенныеСтроки, НужноОбновитьСписокЗадач", ВыбранноеЗначение.Значение, Истина, ВыделенныеСтрокиВМассив(), Ложь);
			ОбработкаСобытияПодключаемогоМодуляКлиент("НажатиеКнопкиЕщеПодключаемогоМодуля",,ПараметрыВызоваПМ);
			
			Если ПараметрыВызоваПМ.НужноОбновитьСписокЗадач = Истина Тогда 
				ЗаполнитьСписокЗадачКлиент();
			КонецЕсли;
		Иначе
			Выполнить(ИмяОбработчика+"("""")");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ВыделенныеСтрокиВМассив() Экспорт//чтобы ВыделенныеСтроки УФ использовать в механизмах для ОФ (Для Каждого Строка Из ВыделенныеСтроки)
	
	ВыделенныеСтроки = Элементы.ТабСообщения.ВыделенныеСтроки;
	МассивСтрок = Новый Массив;
	Для Каждого ТекИД из ВыделенныеСтроки Цикл
		ТекСтрока = Объект.ТабСообщения.НайтиПоИдентификатору(ТекИД);
		СтруктураСтроки = Новый Структура("СообщениеСсылка,ТипСообщения,ТребуемоеДействие,Документ,ДатаДокумента,Контрагент,НомерДокумента,Обработан,Организация,Пометка");
		ЗаполнитьЗначенияСвойств(СтруктураСтроки, ТекСтрока);
		МассивСтрок.Добавить(СтруктураСтроки);
	КонецЦикла;
	Возврат МассивСтрок;
	
КонецФункции
//////////////////////////////////////////////////////////////

&НаКлиенте
Процедура ВыполнитьВыбранныеЗадачи(Команда)
	
	Если Элементы.ТабСообщения.ВыделенныеСтроки.Количество()=0 Тогда
		
		Возврат;
		
	ИначеЕсли Элементы.ТабСообщения.ВыделенныеСтроки[0]=Неопределено Тогда
		
		Возврат;
		
	ИначеЕсли Элементы.ТабСообщения.ВыделенныеСтроки.Количество()=1 Тогда
		//одиночное действие для одной строки
		ВыполнитьЗадачу(Объект.ТабСообщения.НайтиПоИдентификатору(Элементы.ТабСообщения.ВыделенныеСтроки[0]),Ложь);
		
	Иначе
		//групповые действия для нескольких строк
		
		ТекстВопроса ="Будет выполнено действий: "+Элементы.ТабСообщения.ВыделенныеСтроки.Количество()+". Продолжить?";
		КнопкиВопроса=новый СписокЗначений;
		КнопкиВопроса.Добавить("Да, выполнить действия");
		КнопкиВопроса.Добавить("Отмена");
		ДопПараметрДляПередачиВОбработчик=Неопределено;
		РезультатВопроса = Неопределено;
		
		Если Параметры.МодальностьЗапрещена Тогда
			Выполнить("ПоказатьВопрос(Новый ОписаниеОповещения(""ОбработчикСогласияНагрупповыеДействия"", ЭтаФорма, ДопПараметрДляПередачиВОбработчик), ТекстВопроса, КнопкиВопроса,,,""Контур.EDI"")");
		Иначе
			РезультатВопроса = Вопрос(ТекстВопроса, КнопкиВопроса,,,"Контур.EDI");
			ОбработчикСогласияНаГрупповыеДействия(РезультатВопроса,ДопПараметрДляПередачиВОбработчик);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикСогласияНаГрупповыеДействия(РезультатВопроса,ДопПараметрПереданныйВОбработчик=Неопределено) Экспорт
	
	Если РезультатВопроса= "Да, выполнить действия" Тогда 
		СчетаФактурыНаПодписание.Очистить();
		
		МассивСТрокДляОбработки = Новый Массив;
		Для Каждого ИдентификаторСтроки Из Элементы.ТабСообщения.ВыделенныеСтроки Цикл
		 	ТекСтрока = Объект.ТабСообщения.НайтиПоИдентификатору(ИдентификаторСтроки);
			МассивСТрокДляОбработки.Добавить(ТекСтрока);
		КонецЦикла;
		
		Для Каждого СтрокаДанныхФормы Из МассивСТрокДляОбработки Цикл 
			ВыполнитьЗадачу(СтрокаДанныхФормы,Истина);
		КонецЦикла;
		
		//если были выбраны строки с действием "Подписать в Диадок", тогда выполним это действие здесь.
		Если СчетаФактурыНаПодписание.Количество()>0 Тогда
			МассивСообщений = Новый Массив;
			Для Каждого Стр Из СчетаФактурыНаПодписание Цикл
				МассивСообщений.Добавить(Стр.Ссылка);
			КонецЦикла;
			
			// идентификаторы ДД получим уже в самой форме подписания //СобратьТаблицуСчетовФактурНаПодписание(МассивСообщений);
			
			ЗапроситьСертификатИПодписатьСФ(МассивСообщений);
			
		КонецЕсли;
		
		Если ИспользуетсяМеркурий Тогда
			ОтправитьТранзакцииМеркурийМассово(Элементы.ТабСообщения.ВыделенныеСтроки); //если они конечно есть
		КонецЕсли;
		
		Если ОбновлятьВесьСписокПриРаботеСЗаказами <> Истина Тогда
			ВычеркнутьТекущиеСтроки();
		Иначе
			ЗаполнитьСписокЗадачКлиент();
		КонецЕсли;	
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	//ПриЗакрытииСервер();
	//немного перепалки: http://devtrainingforum.v8.1c.ru/forum/thread.jsp?id=649040
	//перенесено в форму настроек при их записи
	
	ПриЗакрытииФормы(ЭтаФорма, "ФормаУправляемая");

КонецПроцедуры

//{#Область ГрупповоеПодписаниеСчетовФактур

&НаКлиенте
Процедура ЗапроситьСертификатИПодписатьСФ(МассивСообщений)
	
	//работу будем вести через вспомогательную форму - клиентский модуль API нам будут недоступны сертификаты, установленные на сервере 1С предприятия, 
	//пока что в EDI не было кейса когда серт на сервере. но он вполне возможно произойдет, тогда потребуется делать серверный модуль API как сделано, например в сверке
	
	//положим в вх массив сообщений клиентский модуль обмена будет брать их
	ПоместитьВоВХ("Диадок_МассивСообщенийДляПодписания", МассивСообщений);
	МодульОбменКлиент().ПодписатьПереданныеСообщения();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПараметрыИнтернетСоединения_KE_ВызовСервера()
	
	СтруктураПараметровСоединения = МодульОбъекта().ПолучитьСписокСвойствEDI(
	"ИспользуетсяПрокси
	|ЛогинПрокси
	|ПарольПрокси
	|СерверПрокси
	|ПортПрокси
	|ТипПрокси
	|Сервер
	|Порт
	|АдресКаталогаОбменаEDI"
	);
	СтруктураПараметровСоединения.ИспользуетсяПрокси = (СтруктураПараметровСоединения.ИспользуетсяПрокси = Истина);

	СтруктураПараметровСоединения.Вставить("ПассивныйРежим",			Истина);//никто не снимает этот флаг, и правильно.
	
	Если Не ЗначениеЗаполнено(СтруктураПараметровСоединения.Порт) Тогда
		СтруктураПараметровСоединения.Порт = 21;
	КонецЕсли;
	
	Возврат СтруктураПараметровСоединения;
КонецФункции

&НаКлиенте
Функция ПолучитьФайлHTTP_Клиент(мСоединение,ИмяФайлаНаСервере,ИмяЛокальногоФайла) Экспорт
	Попытка
		мСоединение.Получить(ИмяФайлаНаСервере, ИмяЛокальногоФайла);
		мСоединение=Неопределено;
		Возврат Истина;
	Исключение
		Ф=новый Файл(ИмяЛокальногоФайла);
		Если Ф.Существует() Тогда
			УдалитьФайлы(ИмяЛокальногоФайла);
		КонецЕсли;	
		мСоединение=Неопределено;
		Возврат Ложь;
	КонецПопытки;
КонецФункции	

//}#КонецОбласти //ГрупповоеПодписаниеСчетовФактур

//{#Область Автотесты
	
&НаСервере
Процедура Автотесты_УстановитьНачальныеНастройкиНаСервере()
	
	МодульОбъекта = МодульОбъекта();
	МодульОбъекта.ПараметрыАвтотестирования = Истина;
	МодульОбъекта.Автотесты_ОчиститьДанныеМодуля();
	
	СтруктураПараметров = ПолучитьИзВременногоХранилища(Параметры.ПараметрыАвтотестирования);
	ВариантОбмена		= СтруктураПараметров.Настройки.ВариантОбменаДанными;
	
	МодульОбъекта().УстановитьКонстантуEDI("ПротоколОбмена_КонтурEDI",	ВариантОбмена.Вид);
	МодульОбъекта().УстановитьКонстантуEDI("Сервер",					ВариантОбмена.Сервер);
	МодульОбъекта().УстановитьКонстантуEDI("Порт",						21);
	МодульОбъекта().УстановитьКонстантуEDI("АдресКаталогаОбменаEDI",	ВариантОбмена.ПутьККаталогу);
	
	Если Не ЗначениеЗаполнено(МодульОбъекта().ПолучитьКонстантуEDI("ШаблонЦепочкиДокументов_Поставщик")) Тогда
		СписокШаблонов = МодульОбъекта().ПолучитьСписокДоступныхШаблоновЦепочки("Поставщик");
		Если СписокШаблонов.Количество()>0 Тогда
			ЗначениеШаблонаПоУмолчанию = СписокШаблонов.НайтиПоЗначению(МодульОбъекта().ОпределитьИндексШаблонаДляЦепочки("", "Поставщик"));
			МодульОбъекта().УстановитьКонстантуEDI("ШаблонЦепочкиДокументов_Поставщик", ?(ЗначениеШаблонаПоУмолчанию = Неопределено,СписокШаблонов[0].Представление,ЗначениеШаблонаПоУмолчанию.Представление));
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Автотесты_УстановитьНачальныеНастройки()
	
	Автотесты_УстановитьНачальныеНастройкиНаСервере();
	
	Элементы.Поставщик.Пометка = Истина;
	Элементы.Сеть.Пометка = Ложь;
	Параметры.РежимРаботы = "Поставщик";
	
	ПараметрыФормы = Новый Структура("ПараметрыАвтотестирования", Параметры.ПараметрыАвтотестирования);
	
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("СтруктураКомпании_ЭлементУправляемая", , ПараметрыФормы, ЭтаФорма);
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("УчетныеЗаписи_ЭлементУправляемая", , ПараметрыФормы, ЭтаФорма);
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("СервисНастройкиУправляемая", , ПараметрыФормы, ЭтаФорма);
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Партнеры_ЭлементУправляемая", , ПараметрыФормы, ЭтаФорма, "ЗапуститьАвтотесты", ЭтаФорма);
	                       
КонецПроцедуры

&НаСервере
Процедура Автотесты_ЗаменитьПеременныеПоШаблону(ТекСообщение)
	
	СтруктураПараметров = ПолучитьИзВременногоХранилища(Параметры.ПараметрыАвтотестирования);
	Переменные = СтруктураПараметров.Настройки.Переменные;
	
	Для Каждого ПолеСообщения Из ТекСообщение Цикл
		Если НЕ ПолеСообщения.Ключ = "Товары" Тогда
			Если Найти(СокрЛП(ПолеСообщения.Значение),"{**")>0 Тогда
				ШаблонПоляСообщения = СокрЛП(ПолеСообщения.Значение);
				Для Каждого ПолеПеременной Из Переменные Цикл
					ШаблонПеременной = ПолеПеременной.Значение.Шаблон;
					Если ШаблонПоляСообщения = ШаблонПеременной Тогда
						ТекСообщение[ПолеСообщения.Ключ] = ПолеПеременной.Значение.Значение;
						Прервать;
					КонецЕсли;	
				КонецЦикла;
			КонецЕсли;			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция Автотесты_ПолучитьНовыйИдентификаторСообщения() 
	
	УИД = Новый УникальныйИдентификатор();
	УИД = СтрЗаменить(УИД,"-","");
	
	Возврат УИД;
	
КонецФункции

&НаСервере
Функция Автотесты_ОтправитьТестовоеСообщение(ИмяСообщения) Экспорт
	
	СтруктураПараметров = ПолучитьИзВременногоХранилища(Параметры.ПараметрыАвтотестирования);
	ВходящиеДанные = СтруктураПараметров.Настройки.ВходящиеДанные;
	
	НайденноеСообщение = Неопределено;
	Для Каждого Стр Из ВходящиеДанные Цикл
		Если Стр.Имя = ИмяСообщения Тогда
			НайденноеСообщение = Стр;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если НайденноеСообщение = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТекСообщение = НайденноеСообщение.Значение;
	Автотесты_ЗаменитьПеременныеПоШаблону(ТекСообщение);
	
	Сообщение = МодульОбъекта().ПолучитьСтруктуруСообщения(ТекСообщение.ТипСообщения,"Исходящее");
	Сообщение.ТипСообщения		= ТекСообщение.ТипСообщения;
	Сообщение.Направление		= "Исходящее";
	Сообщение.ID = Автотесты_ПолучитьНовыйИдентификаторСообщения();
	
	Сообщение.ПокупательEDI.GLN = ТекСообщение.Покупатель;
	Сообщение.ПродавецEDI.GLN = ТекСообщение.Поставщик;
	Сообщение.ГрузополучательEDI.GLN = ТекСообщение.Грузополучатель;
	
	Сообщение.ДокументEDI.Номер = ТекСообщение.НомерДокумента;
	Сообщение.ДокументEDI.Дата = ТекСообщение.ДатаДокумента;
	
	Если Сообщение.ТипСообщения = "ORDERS" Тогда
		Сообщение.ОбратныйЗаказEDI.Номер	= ТекСообщение.НомерОбратногоЗаказа;
		Сообщение.ОбратныйЗаказEDI.Дата		= ТекСообщение.ДатаОбратногоЗаказа;
	КонецЕсли;
	
	Сообщение.ЗаказEDI.Номер = ТекСообщение.НомерЗаказа;
	Сообщение.ЗаказEDI.Дата = ТекСообщение.ДатаЗаказа;
	
	Сообщение.НакладнаяEDI.Номер = ТекСообщение.НомерНакладной;
	Сообщение.НакладнаяEDI.Дата = ТекСообщение.ДатаНакладной;
	
	Сообщение.ВалютаEDI = "RUB";
	
	Сообщение.Статус = ТекСообщение.Статус;
	
	Сообщение.ОтправительEDI.GLN = СокрЛП(СтруктураПараметров.Настройки.Партнер.GLN);
	Сообщение.ПолучательEDI.GLN = СокрЛП(СтруктураПараметров.Настройки.СтруктураКомпании.GLN);
	
	Для Каждого СтрокаТоваров Из ТекСообщение.Товары Цикл
		
		НоваяСтрока = Сообщение.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТоваров);
		НоваяСтрока.КодЕдиницыИзмеренияEDI = СтрокаТоваров.КодЕдиницыИзмерения;
		НоваяСтрока.СтавкаНДСEDI = СтрокаТоваров.СтавкаНДС;
		
	КонецЦикла;
	
	РезультатКонвертации = МодульОбъекта().КонвертироватьИсходящееСообщениеПоНовому(Сообщение);
	
	ТекПутьККаталогу = СтруктураПараметров.Настройки.ВариантОбменаДанными.ПутьККаталогу;
	Если ЗначениеЗаполнено(ТекПутьККаталогу) Тогда
		ПереместитьФайл(РезультатКонвертации.ПутьКФайлу,ТекПутьККаталогу+"\inbox\"+ИмяСообщения+"_"+СокрЛП(Сообщение.ДокументEDI.Номер)+".xml")
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура Автотесты_ЭмулироватьПолучениеСтатусногоСообщения(Документ,ТипСообщения,Этап,Статус) Экспорт
	
	СтруктураПараметров = ПолучитьИзВременногоХранилища(Параметры.ПараметрыАвтотестирования);
	
	Сообщение = МодульОбъекта().ПрочитатьСообщение(,Документ,ТипСообщения, "Исходящее", , );
	
 	ТекПутьККаталогу = СтруктураПараметров.Настройки.ВариантОбменаДанными.ПутьККаталогу;
	
	Результат = МодульОбъекта().СформироватьУведомлениеОПрочтенииСообщения(Сообщение,Этап,Статус);

	Если Результат.Успешно = Истина Тогда
		ФайлСтатусного = Новый Файл(Результат.ПутьКФайлу);
		Если ФайлСтатусного.Существует() Тогда
			ПереместитьФайл(Результат.ПутьКФайлу,ТекПутьККаталогу+"\Reports\"+ФайлСтатусного.Имя);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция Автотесты_ВыполнитьКодНаСервере(ВыполняемыйКод,ПараметрыКода)
	
	Результат = Неопределено;
	
	Выполнить(ВыполняемыйКод);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция Автотесты_ПолучитьКодСледующегоШага()
	
	Код = Неопределено;
	
	СтруктураПараметров = ПолучитьИзВременногоХранилища(Параметры.ПараметрыАвтотестирования);
	Для Каждого Стр Из СтруктураПараметров.ВыполняемыйКод Цикл
		
		Если Стр.Выполнено Тогда
			Продолжить;
		КонецЕсли;
		
		Если Стр.ФормаОбработки = "Форма" Тогда
			Код = Стр.ВыполняемыйКод;
			Прервать;
		Иначе
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Код;
	
КонецФункции

&НаКлиенте
Процедура Автотесты_ПометитьПоследнийШагКакВыполненный()
	
	Код = Неопределено;
	
	СтруктураПараметров = ПолучитьИзВременногоХранилища(Параметры.ПараметрыАвтотестирования);
	Для Каждого Стр Из СтруктураПараметров.ВыполняемыйКод Цикл
		
		Если Стр.Выполнено Тогда
			Продолжить;
		КонецЕсли;
		Если Стр.ФормаОбработки = "Форма" Тогда
			Стр.Выполнено = Истина;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(СтруктураПараметров,Параметры.ПараметрыАвтотестирования);
	
КонецПроцедуры

&НаКлиенте
Функция Автотесты_ПолучитьЗначениеПеременной(ИмяПеременной)
	
	СтруктураПараметров = ПолучитьИзВременногоХранилища(Параметры.ПараметрыАвтотестирования);
	
	Возврат СтруктураПараметров.Настройки.Переменные[ИмяПеременной].Значение;
	
КонецФункции

&НаКлиенте
Процедура Автотесты_УстановитьЗначениеПеременной(Имя,Значение,Шаблон)
	
	СтруктураПеременной = Новый Структура();
	
	СтруктураПеременной.Вставить("Имя", 	Имя);
	СтруктураПеременной.Вставить("Значение",Значение);
	СтруктураПеременной.Вставить("Шаблон",	Шаблон);

	СтруктураПараметров = ПолучитьИзВременногоХранилища(Параметры.ПараметрыАвтотестирования);
	
	СтруктураПараметров.Настройки.Переменные.Вставить(СтруктураПеременной);
	
	ПоместитьВоВременноеХранилище(СтруктураПараметров,Параметры.ПараметрыАвтотестирования);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьАвтотесты() Экспорт
	
	ОбработчикПослеРедактированияПартнеров();
	ПодключитьОбработчикОжидания("Автотесты_ВыполнитьСледующийШаг",0.1,Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Автотесты_ВыполнитьСледующийШаг()
	
	Код = Автотесты_ПолучитьКодСледующегоШага();
		
	Если Код = Неопределено Тогда
			
		Сообщить("Больше нет кода для выполнения");
			
		Возврат;
			
	Иначе
			
		Выполнить(Код);
			
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Автотесты_ВыполнитьСледующийШаг",0.1,Истина);

КонецПроцедуры

&НаКлиенте
Процедура Автотесты_ЗавершитьВыполнениеТестирования(Флаг) Экспорт
	
	СтруктураПараметров = ПолучитьИзВременногоХранилища(Параметры.ПараметрыАвтотестирования);
	
	Если Флаг = "fail" Тогда
	//	ПараметрыАвтотестирования.ВыполняемыеДействия.Очистить();
	КонецЕсли;	
		
	СтруктураВыполнения = Новый Структура;
	СтруктураВыполнения.Вставить("РезультатВыполнения",Неопределено);
	СтруктураВыполнения.Вставить("Переменные",Неопределено);
	//СтруктураВыполнения.Вставить("РезультатВыполнения",ПараметрыАвтотестирования.РезультатВыполненияСценария);
	//СтруктураВыполнения.Вставить("Переменные",ПараметрыАвтотестирования.Настройки.Переменные);
	
	ФайлРезультата = Новый ТекстовыйДокумент;
	//ФайлРезультата.УстановитьТекст(ЗначениеВСтрокуВнутр(СтруктураВыполнения));
	ФайлРезультата.Записать(СтруктураПараметров.КаталогСценария+"\out\result.txt");
	
	ПутьКФайлу = СтруктураПараметров.КаталогСценария+"\out\"+Флаг+".txt";

	Текст = Новый ТекстовыйДокумент;
	Текст.Записать(ПутьКФайлу);
	
КонецПроцедуры

//}#КонецОбласти //Автотесты

&НаКлиенте
Процедура ТабСообщенияПередУдалением(Элемент, Отказ)
	Отказ=Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТабСообщенияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ=Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТабСообщенияПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура КнопкаЕщеПоказатьРасхожденияВходящегоЗаказаИЗаказаВ1С(Команда)
	
	ТекСтрока = Элементы.ТабСообщения.ТекущиеДанные;
	
	Если ТекСтрока<>Неопределено И ЗначениеЗаполнено(ТекСтрока.Документ) Тогда 
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ВидРасхождения", 		"РасхожденияЗаказа");
		ПараметрыФормы.Вставить("Заказ",				ТекСтрока.Документ);
		ПараметрыФормы.Вставить("Параметры",			Неопределено);
		
		МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Сервис_ПросмотрРасхожденийУправляемая", , ПараметрыФормы, ЭтаФорма, , , , РежимОткрытияОкнаФормы.Независимый);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаЕщеПоказатьРасхожденияПриемки(Команда)
	
	ТекСтрока = Элементы.ТабСообщения.ТекущиеДанные;
	
	Если ТекСтрока<>Неопределено И ЗначениеЗаполнено(ТекСтрока.Документ) Тогда 
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ВидРасхождения", 		"РасхожденияПриемки");
		ПараметрыФормы.Вставить("Накладная",			ТекСтрока.Документ);
		ПараметрыФормы.Вставить("Параметры",			Неопределено);
		
		МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Сервис_ПросмотрРасхожденийУправляемая", , ПараметрыФормы, ЭтаФорма, , , , РежимОткрытияОкнаФормы.Независимый);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьФоновыйАвтообмен(Команда)
	
	//только 8.3 балин!
	//ПараметрыОбработчика = Неопределено;
	//Выполнить("ОписаниеОбработчика=	Новый ОписаниеОповещения(ОбработчикОстановкиАвтообмена, ЭтаФорма, ПараметрыОбработчика)");

	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("ФоновыйОбменУправляемая", , , ЭтаФорма, , , , РежимОткрытияОкнаФормы.Независимый, , ВариантОткрытияОкна.ОтдельноеОкно);
	
	Элементы.ВыполнитьОбмен.Доступность = Ложь;
	Элементы.ВыполнитьОбмен.Заголовок = "Запущен автообмен";
	
	ПодключитьОбработчикОжидания("ЗаполнитьСписокЗадачКлиент",600); //10 интервал обновления списка - пользователь уже успел налить чай, попить его и бросил взгляд на монитор
	
КонецПроцедуры

Процедура ОбработчикОстановкиАвтообмена(Параметр=Неопределено) Экспорт
	Элементы.ВыполнитьОбмен.Доступность=Истина;
	Элементы.ВыполнитьОбмен.Заголовок="Обмен с сервером";
	ОтключитьОбработчикОжидания("ЗаполнитьСписокЗадачКлиент");
КонецПроцедуры

&НаКлиенте
Процедура ТипыСообщенийСброситьФлажки(Команда)
	УстановитьИлиСброситьФлажкиТиповСообщений(Ложь);	
КонецПроцедуры

&НаКлиенте
Процедура ТипыСообщенийУстановитьФлажки(Команда)
	УстановитьИлиСброситьФлажкиТиповСообщений(Истина);	
	ПодключитьОбработчикОжидания("ЗаполнитьСписокЗадачКлиент",0.1,Истина);
КонецПроцедуры

&НаКлиенте
Процедура ДеревоФильтровПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ=Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоФильтровПередУдалением(Элемент, Отказ)
	Отказ=Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоФильтровВыбор(Элемент, ЗНАЧ ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыбраннаяСтрока = Элементы.ДеревоФильтров.ТекущиеДанные;
	
	Если НЕ ВыбраннаяСтрока = Неопределено Тогда
		Если ВыбраннаяСтрока.ГруппаНастроек ="Организации" Тогда 
			Если НЕ ПроверитьДоступностьОрганизацийВызовСервера() Тогда //- выпилить с релиза  4.03.008
				ПредложитьОбновитьОбъектыМетаданныхКонтурEDI("Для возможности настраивать доступ необходимо обновить объекты Контур.EDI. Сохранить файл обновления?");
				Отказ=Истина;
			КонецЕсли;
		КонецЕсли;
		ГруппаНастроек = ВыбраннаяСтрока.ГруппаНастроек;
		
		Если ГруппаНастроек = "Партнеры" И Элемент.ТекущийЭлемент.Имя = "ДеревоФильтровИзменить" Тогда
			
			СохранитьНастройкиГлавнойФормы();
			Если ВыбраннаяСтрока.ПолучитьРодителя() = Неопределено Тогда //в корень
				Если Параметры.РольПользователяEDI<>"ПолныеПрава" Тогда
					Возврат;//пользователю с ограниченными правами не разрешено добавлять партнеров
				Иначе
					ДобавитьПартнера(Неопределено);
				КонецЕсли;
			Иначе
				ВвестиНового=Ложь;
				Состояние("Открываю карточку партнера",50,,Элементы.Настройки.Картинка);
				ОткрытьФормуРедактированияПартнера(ВвестиНового);
				Возврат;
			КонецЕсли;
			
		ИначеЕсли ГруппаНастроек = "ТипыСообщений" 
			И ВыбраннаяСтрока <> Неопределено  
			И Элемент.ТекущийЭлемент.Имя = "ДеревоФильтровИзменить" Тогда 
			
			Если (ВыбраннаяСтрока.Значение = "PRICELIST"
				ИЛИ ВыбраннаяСтрока.Значение = "SLSRPT"
				ИЛИ ВыбраннаяСтрока.Значение = "INVRPT") Тогда
				
				//Нажатие на добавление нового сообщения
				ДопПараметры = Новый Структура;
				ДопПараметры.Вставить("ТипСообщения", ВыбраннаяСтрока.Значение);
				ДопПараметры.Вставить("Направление", "Исходящее");
				
				ОткрытьФормуНезависимогоСообщения(ДопПараметры);
				Возврат;
			ИначеЕсли ВыбраннаяСтрока.Значение = "DELFOR" Тогда
				ОткрытьФормуDELFOR();	
			КонецЕсли;
			
		Иначе
			
			СброситьПометкиВыбранногоФильтра();
			
			ГруппаНастроек = ПолучитьРодителяГруппыНастроекИзДанныхФормыКлиент(ВыбраннаяСтрока.ГруппаНастроек);
			
			Если НЕ ГруппаНастроек = Неопределено Тогда
				Если ВыбраннаяСтрока = ГруппаНастроек Тогда
					СнятьУстановитьФлажкиВеткиДерева(ВыбраннаяСтрока,Истина);//ткнули в чъего-то родителя
				Иначе
					ОставитьТолькоВыбраннуюСтроку(ГруппаНастроек,ВыбраннаяСтрока); //просто хотим только текущую строку оставить
				КонецЕсли;
			КонецЕсли;
			
			ПроверитьРодительскиеФлажкиДереваФильтров();
			ЗаполнитьСписокЗадачКлиент();
			
		КонецЕсли;
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНезависимогоСообщения(ПараметрыФормы = Неопределено)
	
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("ФормаНезависимогоСообщенияУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбработчикПослеОтправкиНезависимогоСообщения", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуDELFOR(ПараметрыФормы = Неопределено)//ДДД
	
	Если ПараметрыФормы = Неопределено Тогда
		ПараметрыФормы = Новый Структура;	
	КонецЕсли;
	ПараметрыФормы.Вставить("РежимРаботы", Параметры.РежимРаботы);
	ПараметрыФормы.Вставить("Направление", "Исходящее");
	
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("ФормаСообщенияDELFORУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбработчикПослеОтправкиНезависимогоСообщения", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикПослеОтправкиНезависимогоСообщения(Параметр1 = Неопределено, ДопПараметр = Неопределено) Экспорт
	
	//обновить список, ведь там появилась новая строчка.
	Состояние("Обновляю форму...",35,,Элементы.Настройки.Картинка);
	ОбновитьСписок();
	
	//еще что-то? вроде нет.
	
КонецПроцедуры

&НаКлиенте
Процедура ОставитьТолькоВыбраннуюСтроку(ВеткаДерева,ВыбраннаяСтрока)
	
	Для Каждого Стр Из ВеткаДерева.ПолучитьЭлементы() Цикл
		Если Стр = ВыбраннаяСтрока Тогда
			Стр.Пометка = Истина;
			СнятьУстановитьФлажкиВеткиДерева(Стр,Истина);
			ТекРодитель = Стр.ПолучитьРодителя();
			Пока НЕ ТекРодитель = Неопределено Цикл
				ТекРодитель.Пометка = Истина;
				ТекРодитель = ТекРодитель.ПолучитьРодителя();
			КонецЦикла;
		Иначе
			Стр.Пометка = Ложь;
			ОставитьТолькоВыбраннуюСтроку(Стр,ВыбраннаяСтрока);
		КонецЕсли;
	КонецЦикла;
			
КонецПроцедуры

//Область Введние фильтра по организациям
&НаКлиенте
Процедура ПредложитьОбновитьОбъектыМетаданныхКонтурEDI(ТекстВопроса = Неопределено) Экспорт
	Если ЗначениеЗаполнено(ТекстВопроса) Тогда
		
		КнопкиВопроса=новый СписокЗначений;
		КнопкиВопроса.Добавить("Да");
		КнопкиВопроса.Добавить("Нет");
		ДопПараметрДляПередачиВОбработчик=Неопределено;
		РезультатВопроса = Неопределено;
		
		Если Параметры.МодальностьЗапрещена Тогда
			Выполнить("ПоказатьВопрос(Новый ОписаниеОповещения(""ОбработчикСогласияСохраненияФайла"", ЭтаФорма, ДопПараметрДляПередачиВОбработчик), ТекстВопроса, КнопкиВопроса,,,""Контур.EDI"")");
		Иначе
			РезультатВопроса = Вопрос(ТекстВопроса, КнопкиВопроса,,,"Контур.EDI");
			ОбработчикСогласияСохраненияФайла(РезультатВопроса,ДопПараметрДляПередачиВОбработчик);
		КонецЕсли;
		
	Иначе
		ОбработчикСогласияСохраненияФайла("Да","Не задан вопрос");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьМакетВызовСервера(ИмяМакетаОбновления)

Возврат МодульОбъекта().ПолучитьМакет(ИмяМакетаОбновления);	

КонецФункции // ПолучитьМакет(ИмяМакетаОбновления)()

&НаКлиенте
Процедура ОбработчикСогласияСохраненияФайла(РезультатВопроса,ДопПараметр)Экспорт

	Если РезультатВопроса<>"Да" Тогда 
		Возврат;
	КонецЕсли;
	
	ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогВыбора.Заголовок		= "Укажите файл, в который нужно сохранить обновление для вашей конфигурации";
	ДиалогВыбора.Фильтр			= "Файл конфигурации 1С (*.cf)|*.cf";      
	ДиалогВыбора.ПолноеИмяФайла = "КонтурEDI_upd.cf";
	
	Если ДиалогВыбора.Выбрать() Тогда      
		
		ПутьКФайлу = ДиалогВыбора.ПолноеИмяФайла;
		
	Иначе
		
		Возврат;
		
	КонецЕсли;
	
	ИмяМакетаОбновления = "ОбновлениеДляХраненияДанных";
	
	Макет=ПолучитьМакетВызовСервера(ИмяМакетаОбновления);
	Макет.Записать(ПутьКФайлу);
	
	//еще высветим на экране текстовый документ с описанием того, что надо делать
	Чтиво=Новый ТекстовыйДокумент;
	Чтиво.УстановитьТекст(
	"ВНИМАНИЕ! 
	|При добавлении оптимизированных объектов хранения данных в окне ""Сравнение, объединение"" НЕОБХОДИМО снять флажок с раздела ""Свойства""!"
	);
	Чтиво.Показать();
	
	Предупреждение("Запустите модуль после обновления конфигурации базы из файла");

КонецПроцедуры // ОбработчикСогласияСохраненияФайла()

&НаСервере
Функция ПроверитьДоступностьОрганизацийВызовСервера()

	Возврат МодульОбъекта().ЕстьМетаданныеХраненияОрганизации;

КонецФункции // ПроверитьДоступностьНастроекВызовСервера()

//Конец Область Введние фильтра по организациям

&НаКлиенте
Процедура ДеревоФильтровПометкаПриИзменении(Элемент)
	
	ВыбраннаяСтрока = Элементы.ДеревоФильтров.ТекущиеДанные;
	
	Если ВыбраннаяСтрока<>Неопределено и ВыбраннаяСтрока.ГруппаНастроек ="Организации" Тогда 
		Если НЕ ПроверитьДоступностьОрганизацийВызовСервера() Тогда //- выпилить с релиза  4.03.008
			ПредложитьОбновитьОбъектыМетаданныхКонтурEDI("Для возможности настраивать доступ необходимо обновить объекты Контур.EDI. Сохранить файл обновления?");
			Отказ=Истина;
		КонецЕсли;
	КонецЕсли;
	
	СброситьПометкиВыбранногоФильтра();
	
	ТекСтрока = Элементы.ДеревоФильтров.ТекущиеДанные;
	СнятьУстановитьФлажкиВеткиДерева(ТекСтрока,ТекСтрока.Пометка);
	ПроверитьРодительскиеФлажкиДереваФильтров();
	ПодключитьОбработчикОжидания("ЗаполнитьСписокЗадачКлиент",0.1,Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьУстановитьФлажкиВеткиДерева(ВеткаДерева,Пометка)
	
	Для Каждого Стр Из ВеткаДерева.ПолучитьЭлементы() Цикл
		Стр.Пометка = Пометка;
		СнятьУстановитьФлажкиВеткиДерева(Стр,Пометка);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьРодительскиеФлажкиДереваФильтров()
	
	Для Каждого Стр1 ИЗ ДеревоФильтров.ПолучитьЭлементы() Цикл
		
		Для Каждого Стр2 Из Стр1.ПолучитьЭлементы() Цикл
			Если Стр2.ПолучитьЭлементы().Количество()>0 Тогда
				Стр2.Пометка = (НайтиВДереве(Стр2,Ложь,"Пометка") = Неопределено);
			КонецЕсли;
		КонецЦикла;
		
		Если Стр1.ПолучитьЭлементы().Количество()>0 Тогда
			Стр1.Пометка = (НайтиВДереве(Стр1,Ложь,"Пометка") = Неопределено);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция НайтиВДереве(ДеревоИлиЕгоЭлемент,ЧтоИщем,ВКакойКолонке)
	НайденныйЭлемент = Неопределено;
	
	Для	Каждого ЭлементДерева Из ДеревоИлиЕгоЭлемент.ПолучитьЭлементы() Цикл
		ЗначениеВКолонке = Неопределено;
		НашлиПоСвойству = ЭлементДерева.Свойство(ВКакойКолонке,ЗначениеВКолонке);
		Если НашлиПоСвойству и ЗначениеВКолонке=ЧтоИщем Тогда 
			НайденныйЭлемент = ЭлементДерева;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат НайденныйЭлемент;
КонецФункции

&НаКлиенте
Функция УДочернихЭлементовЕстьСброшенныеПометки(ЭлементРодитель)
	ЕстьСброшенные=Ложь;
	
	Для Каждого ДочернийЭлемент Из ЭлементРодитель.ПолучитьЭлементы() Цикл
		Если ДочернийЭлемент.Пометка=Ложь Тогда 
			ЕстьСброшенные=Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЕстьСброшенные;
КонецФункции 

&НаКлиенте
Процедура ОбработчикВыбораНастройкиОтбора(ВыбранноеЗначение = Неопределено, ДопПараметр = Неопределено) Экспорт

	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбранноеЗначение.Значение = "Новый отбор" Тогда
		
		ИмяНастройки = "";
		ОбработчикВводаПустогоИмениНастройкиОтбора(ИмяНастройки);

	Иначе
			
		ИмяНастройки = ВыбранноеЗначение.Значение;
		ОбработчикВыбораНастройкиОтбораДляСохранения(ИмяНастройки);	
		
	КонецЕсли;

КонецПроцедуры // ОбработчикВыбораНастройкиОтбора()

&НаКлиенте
Процедура ОбработчикВводаИмениНастройкиОтбора(ИмяНастройки, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ИмяНастройки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ИмяНастройки) Тогда
		
		ТекстПредупреждения = "Укажите название для нового отбора";
		МодульОбъектаКлиент().Предупреждение_КонтурEDI(ТекстПредупреждения, , , "ОбработчикВводаПустогоИмениНастройкиОтбора", ЭтаФорма, ИмяНастройки);
		Возврат;
		
	КонецЕсли;
	
	Если МожноИзменятьНастройкуОтбора(ИмяНастройки) = Ложь Тогда
			
		ТекстПредупреждения = "Нельзя изменять предопределенную настройку";
		МодульОбъектаКлиент().Предупреждение_КонтурEDI(ТекстПредупреждения, , , "ОбработчикВводаПустогоИмениНастройкиОтбора", ЭтаФорма, ИмяНастройки);
		Возврат;
		
	КонецЕсли;
	
	ОбработчикВыбораНастройкиОтбораДляСохранения(ИмяНастройки);
	
КонецПроцедуры

// Необходим из-за количества аргументов обработчика предупреждения - 1
//
&НаКлиенте
Процедура ОбработчикВводаПустогоИмениНастройкиОтбора(ИмяНастройки) Экспорт
	
	ПараметрыОбработчика = Неопределено;
	ТекстПодсказки = "Введите название нового отбора";
	
	МодульОбъектаКлиент().ПоказатьВводСтроки_КонтурEDI("ОбработчикВводаИмениНастройкиОтбора", ЭтаФорма, ПараметрыОбработчика, 
		ИмяНастройки, ТекстПодсказки); 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикВыбораНастройкиОтбораДляСохранения(ИмяНастройки)
	
	СохранитьНастройкуОтбораКлиент(ИмяНастройки);
	ЗаполнитьСписокСохраненныхОтборов();
	СброситьПометкиВыбранногоФильтра();
	СделатьАктивнойНастройкуСохраненногоФильтра(ИмяНастройки);
	
КонецПроцедуры
	
&НаКлиенте
Процедура СохранитьНастройкуОтбораКлиент(ТекНастройка)
	
	ДляСохраненияНастроек = Истина;
	СохранитьНастройкуОтбора(ТекНастройка, 
								ПолучитьПартнеровОтбораКлиент(ДляСохраненияНастроек), 
								ПолучитьТипыСообщенийОтбораКлиент(ДляСохраненияНастроек), 
								ПолучитьТребуемыеДействияОтбораКлиент(ДляСохраненияНастроек), 
								ПолучитьОрганизацииОтбораКлиент(ДляСохраненияНастроек), 
								ПоказыватьЗавершенные, 
								Параметры.РежимРаботы);	

КонецПроцедуры // СохранитьНастройкуОтбораВызовСервера()

&НаСервереБезКонтекста
Процедура СохранитьНастройкуОтбора(ИмяНастройки, 
									МассивОтмеченныхПартнеров, 
									МассивОтмеченныхТиповСообщений, 
									МассивОтмеченныхДействий, 
									МассивОтмеченныхОрганизаций,
									ПоказыватьЗавершенные,
									РежимРаботы) Экспорт
	
	СтруктураНастроек = Новый Структура;
	СтруктураНастроек.Вставить("МассивОтмеченныхПартнеров",			МассивОтмеченныхПартнеров);
	СтруктураНастроек.Вставить("МассивОтмеченныхТиповСообщений",	МассивОтмеченныхТиповСообщений);
	СтруктураНастроек.Вставить("МассивОтмеченныхДействий",			МассивОтмеченныхДействий);
	СтруктураНастроек.Вставить("МассивОтмеченныхОрганизаций",		МассивОтмеченныхОрганизаций);
	СтруктураНастроек.Вставить("ПоказыватьЗавершенные",				ПоказыватьЗавершенные);
	СтруктураНастроек.Вставить("РежимРаботы",						РежимРаботы);
	
	ХранилищеОбщихНастроек.Сохранить("КонтурEDI/СохраненныеНастройкиСписка", ИмяНастройки, СтруктураНастроек);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСписокНастроекОтбора() Экспорт
	
	СписокНастроек = Новый СписокЗначений;
	СписокНастроек = ХранилищеОбщихНастроек.ПолучитьСписок("КонтурEDI/СохраненныеНастройкиСписка");
	
	Возврат СписокНастроек;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьНастройкуОтбора(ИмяНастройки) Экспорт
	
	ЗначениеНастройки = Неопределено;
	ЗначениеНастройки = ХранилищеОбщихНастроек.Загрузить("КонтурEDI/СохраненныеНастройкиСписка", ИмяНастройки);
	
	Возврат ЗначениеНастройки;
	
КонецФункции

&НаКлиенте
Процедура ВосстановитьФлагФильтраПоказыватьЗавершенные()
	
	ТекНастройка = Элементы.СохраненныеФильтры.ТекущиеДанные;
	Если ТекНастройка = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	СохраненнаяНастройка = ПолучитьНастройкуОтбора(ТекНастройка.Настройка);
	Если ЗначениеЗаполнено(СохраненнаяНастройка) Тогда
		ПоказыватьЗавершенные = СохраненнаяНастройка.ПоказыватьЗавершенные;
	Иначе
		ПоказыватьЗавершенные = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьНастройкуОтбора(ИмяНастройки) Экспорт
	
	ХранилищеОбщихНастроек.Удалить("КонтурEDI/СохраненныеНастройкиСписка", ИмяНастройки, ИмяПользователя());
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписокСохраненныхОтборов()
	
	СохраненныеФильтры.Очистить();
	
	СписокНастроекОтбора = ПолучитьСписокНастроекОтбора();
	
	Для Каждого Стр Из СписокНастроекОтбора Цикл
		НоваяСтрока = СохраненныеФильтры.Добавить();
		НоваяСтрока.Настройка = Стр.Значение;
	КонецЦикла;
	
	НайденныеСтроки = СохраненныеФильтры.НайтиСтроки(Новый Структура("Настройка", "Все документы"));
	
	Если НайденныеСтроки.Количество() = 0 Тогда
		НоваяСтрока = СохраненныеФильтры.Добавить();
		НоваяСтрока.Настройка = "Все документы";
	КонецЕсли;	
	
	//установим высоту
	Элементы.СохраненныеФильтры.Высота=СохраненныеФильтры.Количество();
	Элементы.ГруппаСохраненныеОтборы.Высота = Элементы.СохраненныеФильтры.Высота+1;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройкуОтборов(Команда)
	
	СписокНастроекОтбора = ПолучитьСписокНастроекОтбора();
	
	СписокВыбораНастроек = Новый СписокЗначений();
	Для Каждого Стр Из СписокНастроекОтбора Цикл
		СписокВыбораНастроек.Добавить(Стр.Значение,,,БиблиотекаКартинок.ЗагрузитьНастройкиОтчета);
	КонецЦикла;
	
	СписокВыбораНастроек.Добавить("Новый отбор",,,БиблиотекаКартинок.НовоеОкно);
	
	Если СписокВыбораНастроек.Количество() = 1 Тогда
		
		ВыбранноеЗначение = СписокВыбораНастроек[0];
		ОбработчикВыбораНастройкиОтбора(ВыбранноеЗначение);
		
	Иначе
		
		ПараметрыОбработчика = Неопределено;
		МодульОбъектаКлиент().ВыбратьИзСписка_КонтурEDI("ОбработчикВыбораНастройкиОтбора", ЭтаФорма, ПараметрыОбработчика, 
			СписокВыбораНастроек, Элементы.СохранитьНастройку);
			
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ДеревоФильтровУстановитьВсеФлажки(Команда)
	УстановитьИлиСброситьФлажкиДереваОтборов(Истина);	
	ПодключитьОбработчикОжидания("ЗаполнитьСписокЗадачКлиент",0.1,Истина);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИлиСброситьФлажкиДереваОтборов(Установить) //ой жееесть, закидают жеж сейчас
	Для Каждого Стр Из ДеревоФильтров.ПолучитьЭлементы() Цикл
		Стр.Пометка = Установить;
		Для Каждого Стр2 Из Стр.ПолучитьЭлементы() Цикл
			Стр2.Пометка = Установить;
			Для Каждого Стр3 Из Стр2.ПолучитьЭлементы() Цикл
				Стр3.Пометка = Установить;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьЗавершенныеПриИзменении(Элемент)
	
	ПоказыватьЗавершенныеСохранение = ПоказыватьЗавершенные;
	ЗаполнитьНастройкиОтборов(Ложь,Ложь,);
	ПодключитьОбработчикОжидания("ЗаполнитьСписокЗадачКлиент",0.01,Истина);

КонецПроцедуры

&НаКлиенте
Процедура УдалитьНастройкуОтборов(Команда)
	
	ТекСтрока = Элементы.СохраненныеФильтры.ТекущиеДанные;
	
	Если НЕ ТекСтрока = Неопределено Тогда
		
		ТекНастройка = ТекСтрока.Настройка;
		
		Если МожноИзменятьНастройкуОтбора(ТекНастройка) = Ложь Тогда
			
			ТекстПредупреждения = "Нельзя изменять предопределенную настройку.";
			МодульОбъектаКлиент().Предупреждение_КонтурEDI(ТекстПредупреждения);
			
		Иначе
			УдалитьНастройкуОтбора(ТекНастройка);
			ЗаполнитьСписокСохраненныхОтборов();
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Функция МожноИзменятьНастройкуОтбора(ИмяНастройки) Экспорт
	
	Если ИмяНастройки = "Все документы" Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура СохраненныеФильтрыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Состояние("Заполняю настройки...", 65, , Элементы.Настройки.Картинка);
	
	ТекНастройка = Элемент.ТекущиеДанные.Настройка;
	
	СнятьУстановитьФлажкиВеткиДерева(ДеревоФильтров, Истина);
	СделатьАктивнойНастройкуСохраненногоФильтра(ТекНастройка);
	ЗаполнитьНастройкиОтборов(Ложь, Ложь);
	УстановитьСохраненныеНастройкиДереваФильтров();
	ПодключитьОбработчикОжидания("ЗаполнитьСписокЗадачКлиент", 0.01, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СброситьПометкиВыбранногоФильтра()
	
	Для каждого СтрокаВарианта Из СохраненныеФильтры Цикл
		
		СтрокаВарианта.Пометка=Ложь;
		
	КонецЦикла;	
	
КонецПроцедуры // СброситьПометкиВыбранногоФильтра()

&НаКлиенте
Функция ПолучитьТекНастройкуФильтровКлиент()
	
	НайденнаяНастройка = Неопределено;
	
	Для Каждого СтрокаНастройки ИЗ СохраненныеФильтры Цикл
		Если СтрокаНастройки.Пометка Тогда 
			НайденнаяНастройка = СтрокаНастройки;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат НайденнаяНастройка;
КонецФункции // ПолучитьТекНастройкуФильтровКлиент()

&НаКлиенте
Процедура УстановитьСохраненныеНастройкиДереваФильтров()
	
	ТекНастройка = ПолучитьТекНастройкуФильтровКлиент();
	
	Если Не ТекНастройка = Неопределено Тогда
		
		СохраненнаяНастройка = ПолучитьНастройкуОтбора(ТекНастройка.Настройка);
		
		Если ЗначениеЗаполнено(СохраненнаяНастройка) Тогда
			
			МассивОтмеченныхПартнеров		= СохраненнаяНастройка.МассивОтмеченныхПартнеров;
			МассивОтмеченныхТиповСообщений	= СохраненнаяНастройка.МассивОтмеченныхТиповСообщений;
			МассивОтмеченныхДействий		= СохраненнаяНастройка.МассивОтмеченныхДействий;
			МассивОтмеченныхОрганизаций		= СохраненнаяНастройка.МассивОтмеченныхОрганизаций;
			
			Если МассивОтмеченныхПартнеров.Количество()>0 Тогда
				СтрокаГруппы = ПолучитьРодителяГруппыНастроекИзДанныхФормыКлиент("Партнеры");
				Для Каждого Стр Из СтрокаГруппы.ПолучитьЭлементы() Цикл
					Если МассивОтмеченныхПартнеров.Найти(Стр.Значение) = Неопределено Тогда
						Стр.Пометка = Ложь;
					КонецЕсли;
				КонецЦикла;	
			КонецЕсли;
			
			Если МассивОтмеченныхТиповСообщений.Количество()>0 Тогда
				СтрокаГруппы = ПолучитьРодителяГруппыНастроекИзДанныхФормыКлиент("ТипыСообщений");
				Для Каждого Стр Из СтрокаГруппы.ПолучитьЭлементы() Цикл
					Если МассивОтмеченныхТиповСообщений.Найти(Стр.Значение) = Неопределено Тогда
						Стр.Пометка = Ложь;
					КонецЕсли;
				КонецЦикла;	
			КонецЕсли;
			
			Если МассивОтмеченныхДействий.Количество()>0 Тогда
				СтрокаГруппы = ПолучитьРодителяГруппыНастроекИзДанныхФормыКлиент("ТребуемыеДействия");
				Для Каждого Стр1 Из СтрокаГруппы.ПолучитьЭлементы() Цикл
					Для Каждого Стр2 Из Стр1.ПолучитьЭлементы() Цикл
						Если МассивОтмеченныхДействий.Найти(Стр2.Значение) = Неопределено Тогда
							Стр2.Пометка = Ложь;
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;	
			КонецЕсли;
			
			Если МассивОтмеченныхОрганизаций.Количество()>0 Тогда
				СтрокаГруппыОрганизации = ПолучитьРодителяГруппыНастроекИзДанныхФормыКлиент("Организации");
				Для Каждого Стр1 Из СтрокаГруппыОрганизации.ПолучитьЭлементы() Цикл
					Если МассивОтмеченныхОрганизаций.Найти(Стр1.Значение) = Неопределено Тогда
						Стр1.Пометка = Ложь;
					КонецЕсли;
				КонецЦикла;	
			КонецЕсли;  
			
		КонецЕсли;     		
	КонецЕсли;
	
	ПроверитьРодительскиеФлажкиДереваФильтров();
 	РазвернутьДеревоФильтровКлиент();

КонецПроцедуры

&НаКлиенте
Процедура СделатьАктивнойНастройкуСохраненногоФильтра(ИмяНастройки = "")

	НайденныеСтроки = СохраненныеФильтры.НайтиСтроки(Новый Структура("Настройка", ИмяНастройки));
	Если НайденныеСтроки.Количество() = 1 Тогда
		Элементы.СохраненныеФильтры.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
		Для каждого Строка Из СохраненныеФильтры Цикл
			Если Строка = НайденныеСтроки[0] Тогда
				Строка.Пометка = Истина;
			Иначе
				Строка.Пометка = Ложь;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ВосстановитьФлагФильтраПоказыватьЗавершенные();

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьНастройкуОтборов(Команда)
	СохраненныеФильтрыВыбор("", "", "", ложь);
КонецПроцедуры

&НаКлиенте
Процедура СвернутьРазвернутьПодробностиФильтра(Команда)
	
	СвернутостьДереваФильтровСохранение	= НЕ СвернутостьДереваФильтровСохранение;
	УстановитьСвернутостьПодробностейФильтра();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСвернутостьПодробностейФильтра()

	Элементы.Растяжитель.Видимость = СвернутостьДереваФильтровСохранение;
	Элементы.СохранитьНастройку.Видимость = Не СвернутостьДереваФильтровСохранение;
	Элементы.ДеревоФильтров.Видимость = Не СвернутостьДереваФильтровСохранение;
	
	Элементы.СвернутьНастройки.Заголовок = ?(Не СвернутостьДереваФильтровСохранение,"скрыть","показать");
	Элементы.СвернутьНастройки.Ширина = ?(Не СвернутостьДереваФильтровСохранение,7,9);

КонецПроцедуры // УстановитьСвернутостьПодробностейФильтра()

&НаКлиенте
Процедура КнопкаЕщеВыделитьВсе(Команда)
	Элементы.ТабСообщения.ВыделенныеСтроки.Очистить();
	Для каждого стр из Объект.ТабСообщения Цикл
        Элементы.ТабСообщения.ВыделенныеСтроки.Добавить(стр.ПолучитьИдентификатор());
    КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура КнопкаЕщеОткрытьЭлДокументыВДиадоке(Команда)
	
	ТекСтрока = Элементы.ТабСообщения.ТекущиеДанные;
	ДокументСсылка = ТекСтрока.Документ;
	СообщениеСсылка = ТекСтрока.СообщениеСсылка;
	ДокументыНеПодписаны = (ТекСтрока.ТребуемоеДействие = "Подписать в Диадоке");
	
	ОткрытьЭлектронныеДокументыВВебДиадок(СообщениеСсылка, ДокументСсылка, ДокументыНеПодписаны);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЭлектронныеДокументыВВебДиадок(СообщениеСсылка, ДокументСсылка, ДокументыНеПодписаны)
	
	СписокСсылок = ПолучитьСписокСсылокНаДокументыВВебДиадокСервер(СообщениеСсылка, ДокументСсылка, ДокументыНеПодписаны);
	
	Если СписокСсылок.Количество() = 1 Тогда
		
		ОбработчикВыбораДокументаДД(СписокСсылок[0], Неопределено);
		
	ИначеЕсли СписокСсылок.Количество() > 1 Тогда
		
		ТекстОбращенияПриВыборе = "Выберите документ для его просмотра в Диадоке";
		
		МодульОбъектаКлиент().ПоказатьВыборЭлемента_КонтурEDI("ОбработчикВыбораДокументаДД", ЭтаФорма, , СписокСсылок, , ТекстОбращенияПриВыборе);
				
	Иначе
		
		ТекстПредупреждения = "Не удалось найти ссылки на созданные документы в Диадок.
			|Попробуйте зайти в Диадок через ваш веб браузер.";
		МодульОбъектаКлиент().Предупреждение_КонтурEDI(ТекстПредупреждения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСписокСсылокНаДокументыВВебДиадокСервер(СообщениеСсылка, ДокументСсылка, ДокументыНеПодписаны)
	
	Возврат МодульОбъекта().ПолучитьСписокСсылокНаДокументыВВебДиадок(СообщениеСсылка, ДокументСсылка, ДокументыНеПодписаны);

КонецФункции

&НаКлиенте
Процедура ОбработчикВыбораДокументаДД(ВыбранноеЗначение=Неопределено,ДопПараметр=Неопределено)Экспорт
	
	СсылкаНаДокумент = "";

	Если НЕ ВыбранноеЗначение = Неопределено Тогда
		СсылкаНаДокумент = ВыбранноеЗначение.Значение;
	КонецЕсли;
	
	Если НЕ СсылкаНаДокумент = "" Тогда
		
		ЗапуститьПриложение(СсылкаНаДокумент);
		
	КонецЕсли;
	
КонецПроцедуры // ()

//ПолныйДубль ОФ-УФ
//Поведение различается в МодульОбменКлиент() - управление передастся в Обычную или управляемую форму
//код отличается в Предупреждение(ТекстПредупреждения,,"Контур.EDI")
&НаКлиенте
Процедура КнопкаЕщеПроверитьНаличиеОтказовЗапросовНаУточнение(Команда)
	
	МассивДляУточнения = ПолучитьДокументыСУточнениямиСервер(); //сервер
	МассивУточненных = новый Массив; //сложим сюда те, по которым уточнили
	ДокументыПоКоторымНеУдалосьОбновитьСтатус=новый Массив;
	
	ПодлежитУточнению=МассивДляУточнения.Количество();
	Если ПодлежитУточнению>0 Тогда
		сч=0;
		МодульОбменКлиент().СброситьПроверкиДанныхАвторизации();
		
		Для Каждого ДокументСУточнением Из МассивДляУточнения Цикл
			сч=сч+1;
			ТекПроцент = Цел((сч * 100 / ПодлежитУточнению)); 

			Состояние("Получение статусов из Диадока: "+сч+" из "+ПодлежитУточнению, ТекПроцент);
			Отправитель1С=ДокументСУточнением.Отправитель1С;
			//отказ от компоненты
			//если к оргнизации привязан сертификат в настройках, то мы можем просто сходить в ДД за статусом
			
			СтатусыЮЗДО = МодульОбменКлиент().УзнатьСостояниепоСФ(Отправитель1С,ДокументСУточнением.boxId,ДокументСУточнением.messageId);
			
			Если СтатусыЮЗДО.Успешно=Истина Тогда
				
				//Интерпретируем статусы ДД в статус сообщения INVOIC (элемента справочника)         АктуальноеСостояниеСообщенияINVOIC - имеется в виду по тек. данным Диадока
				АктуальноеСостояниеСообщенияINVOIC=МодульОбменКлиент().ПолучитьСостояниеСообщенияINVOICПоСостояниюЮЗДО(СтатусыЮЗДО);  
				
				Если (ЗначениеЗаполнено(АктуальноеСостояниеСообщенияINVOIC.ТребуемоеДействие)
						И ДокументСУточнением.ТребуемоеДействие<>АктуальноеСостояниеСообщенияINVOIC.ТребуемоеДействие)
					ИЛИ (ЗначениеЗаполнено(АктуальноеСостояниеСообщенияINVOIC.Статус)
						И ДокументСУточнением.Статус<>АктуальноеСостояниеСообщенияINVOIC.Статус)
					Тогда 
					Попытка
						ОбновитьСтатусСообщенияINVOICИзДДВызовСервера(ДокументСУточнением.СообщениеСсылка,АктуальноеСостояниеСообщенияINVOIC);
						МассивУточненных.Добавить(ДокументСУточнением.Документ);
					Исключение
						КомментарийЗаписи = "Не удалось обновить статус сообщения из Диадока - " + ОписаниеОшибки();
						МодульОбъектаКлиент().ЗаписьЖурналаРегистрацииНаКлиенте_КонтурEDI("Предупреждение", КомментарийЗаписи);
					КонецПопытки;
				КонецЕсли;
				
				//действия со статусом
			Иначе
				Если ЗначениеЗаполнено(СтатусыЮЗДО.ОписаниеОшибки) Тогда
					Сообщить(СтатусыЮЗДО.ОписаниеОшибки);
				КонецЕсли;	
				ДокументыПоКоторымНеУдалосьОбновитьСтатус.Добавить(ДокументСУточнением.Документ);
			КонецЕсли;
			
		КонецЦикла;
		
		МодульОбменКлиент().СброситьПарольКонтейнераДанныхАвторизации();
		
		//сообщить что получилось, а что нет
		ТекстПредупреждения=?(МассивУточненных.Количество()=0,"Нет обновлений статусов.","Количество обновленных статусов: "+МассивУточненных.Количество());
		Если ДокументыПоКоторымНеУдалосьОбновитьСтатус.Количество()<>0 Тогда 
			ТекстПредупреждения = ТекстПредупреждения + Символы.ПС + "Не удалось определить статус в Диадоке по " + ДокументыПоКоторымНеУдалосьОбновитьСтатус.Количество() + " документам." 
								+ Символы.ПС + "Возможно, не указаны логин и пароль или не назначен сертификат по организации.";
		КонецЕсли;
		
		Если Параметры.МодальностьЗапрещена Тогда 
			Выполнить("ПоказатьПредупреждение(,ТекстПредупреждения,,""Контур.EDI"")");
		Иначе
			Предупреждение(ТекстПредупреждения,,"Контур.EDI");
		КонецЕсли;
		
		//обновить список
		ОбновитьСписок();
		
	Иначе
		ТекстПредупреждения="По текущему списку нет доступных для проверки счетов-фактур.";
		Если Параметры.МодальностьЗапрещена Тогда 
			Выполнить("ПоказатьПредупреждение(,ТекстПредупреждения,,""Контур.EDI"")");
		Иначе
			Предупреждение(ТекстПредупреждения,,"Контур.EDI");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Параметры
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//  <АктуальноеСостояниеСообщенияINVOIC>  - <Структура> - Статус/ТребуемоеДействие/Архив
&НаСервере
Процедура ОбновитьСтатусСообщенияINVOICИзДДВызовСервера(СообщениеСсылка,АктуальноеСостояниеСообщенияINVOIC)
	
	МодульОбъекта().ОбновитьСтатусСообщенияINVOICИзДД(СообщениеСсылка,АктуальноеСостояниеСообщенияINVOIC);
	
КонецПроцедуры // ОбновитьСтатусСообщенияINVOICИзДД()

&НаСервере
Функция ПолучитьДокументыСУточнениямиСервер()
	
	Возврат МодульОбъекта().ПолучитьДокументыСУточнениями();
	
КонецФункции // ПолучитьДокументыСУточнениями()

&НаКлиенте
Функция ПолучитьКлассификаторСтатусовДД() Экспорт

	Возврат ПолучитьКлассификаторСтатусовДДВызовСервера();	

КонецФункции // ПолучитьКлассификатор()

&НаСервере
Функция ПолучитьКлассификаторСтатусовДДВызовСервера()

	Возврат МодульОбъекта().ЗагрузитьКлассификаторИзМакета("СостоянияДокументовДД");

КонецФункции // ПолучитьКлассификаторСтатусовДД()


//Переотправка по реестру

&НаКлиенте
Функция ВыбратьФайлРеестра()

	ДФ=Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДФ.Фильтр = "Файлы реестра документов из Диадока(*.csv)|*.csv";			
	ДФ.Заголовок = "Выберите файл реестра счетов-фактур из Диадока";
	Если ДФ.Выбрать() Тогда
		Состояние("Загрузка файла с сервера");
		
		Возврат ДФ.ВыбранныеФайлы[0];
	КонецЕсли;
	
    Возврат Неопределено;
КонецФункции // ВыбратьФайлРеестра()

&НаКлиенте
Функция ПрочитатьФайлРеестра(ПолноеИмяФайла)
	
	МассивДокументов = Новый Массив;
	
	Текст = Новый ТекстовыйДокумент;
	
	Попытка
		
		Текст.Прочитать(ПолноеИмяФайла);
		
	Исключение             
		
		Сообщить("Не удалось прочитать выбранный файл!");
		Сообщить(ОписаниеОшибки());
		Возврат МассивДокументов;
		
	КонецПопытки;  	
	
	КолСтрок = Текст.КоличествоСтрок();

	Если КолСтрок > 1 Тогда
		
		Для н = 2 По КолСтрок Цикл
			
			ТекСтрока = Текст.ПолучитьСтроку(н);
			
			СписокПараметров = РазложитьСтрокуКлиент(ТекСтрока,";");
					
			Если СписокПараметров.Количество()>6 Тогда
				
				СписокПараметров.Получить(3);
				
				ПредставлениеДокумента	= СписокПараметров.Получить(3).Значение;
				
				НомерДокумента			= СписокПараметров.Получить(4);
				НомерДокумента			= СтрЗаменить(НомерДокумента, "=", "");
				НомерДокумента			= СтрЗаменить(НомерДокумента, """", "");
				
				ДатаДокументаСтрокой	= СокрЛП(СписокПараметров.Получить(5));
				
				МассивДокументов.Добавить(Новый Структура("ДокументВДиадоке,Документ,НомерДокумента,ДатаДокументаСтрокой", ПредставлениеДокумента, Неопределено, НомерДокумента, ДатаДокументаСтрокой));
				
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		Сообщить("Выбранный реестр документов не содержит данных!");
		
	КонецЕсли;	
	
	Возврат МассивДокументов;
	
КонецФункции 

Функция РазложитьСтрокуКлиент(Знач Строка,Разделитель=",")
	
	СЗ=Новый СписокЗначений();
	
	Строка=СтрЗаменить(Строка,Разделитель,Символы.ПС);
	
	ЧислоСтрок = СтрЧислоСтрок(Строка);
	
	Для н=1 По ЧислоСтрок Цикл
		СЗ.Добавить(СтрПолучитьСтроку(Строка,н));
	КонецЦикла;
	
	Возврат СЗ;
	
КонецФункции

//Проставим документ1С ссылка в массиве структур
&НаСервере
Функция НайтиДокументы1ССервер(МассивДокументов)
	НайденоДокументов=0;
	
	ТипДокументаСчетФактура = МодульОбъекта().ПолучитьТипЗначенияОбъекта("ИсходящийСчетФактура",,Истина);
	
	Для каждого ТекСтрока Из МассивДокументов Цикл
		
		ДатаХорошая=Ложь;
		Попытка
			ДатаДокумента = Дата(Сред(ТекСтрока.ДатаДокументаСтрокой,7,4),Сред(ТекСтрока.ДатаДокументаСтрокой,4,2),Лев(ТекСтрока.ДатаДокументаСтрокой,2));
			ДатаХорошая = Истина;	
		Исключение
			ДатаДокумента = ТекущаяДата();
		КонецПопытки;
		
		НайденныйДокумент = Документы[ТипДокументаСчетФактура].НайтиПоНомеру(ТекСтрока.НомерДокумента,ДатаДокумента);
		
		Если НЕ ЗначениеЗаполнено(НайденныйДокумент) Тогда
			
			Запрос = Новый Запрос();
			
			ТекстУсловияДата = "";
			Если ДатаХорошая Тогда
				ТекстУсловияДата = "	И НАЧАЛОПЕРИОДА(СчетФактураВыданный.Дата, День) = &Дата";
				Запрос.УстановитьПараметр("Дата",ДатаДокумента);
			КонецЕсли;
			
			Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
			|	СчетФактураВыданный.Ссылка
			|ИЗ
			|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
			|ГДЕ
			|	СчетФактураВыданный.Номер ПОДОБНО ""%"+ТекСтрока.НомерДокумента+"""
			|"+ТекстУсловияДата+"
			|УПОРЯДОЧИТЬ ПО СчетФактураВыданный.Дата УБЫВ";
			
			Запрос.Текст = СтрЗаменить(Запрос.Текст,"СчетФактураВыданный",ТипДокументаСчетФактура);
			
			НайденныйДокумент = МодульОбъекта().ПолучитьРезультатЗапроса(Запрос);
			
			Если ЗначениеЗаполнено(НайденныйДокумент) Тогда
				ТекСтрока.Документ = НайденныйДокумент;
			КонецЕсли;

		КонецЕсли;
		
		Если ЗначениеЗаполнено(НайденныйДокумент) Тогда
			НайденоДокументов=НайденоДокументов+1;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат НайденоДокументов;
	
КонецФункции // НайтиДокументы1ССервер()

&НаКлиенте
Процедура ОбработчикСогласияПереотправки(Ответ,ТаблицаДокументовИзФайла)Экспорт
	Если Ответ= "Да, переотправить найденные" Тогда
	Состояние("Подготавливаю к отправке...",70,,Элементы.ОткрытьНастройки.Картинка);
	КоличествоУспешно = ПереотправитьПоРееструСервер(ТаблицаДокументовИзФайла);	
	ТекстПредупреждения="";
	Если КоличествоУспешно = 0 Тогда
		ТекстПредупреждения="Не было отправлено ни одного документа! 
		|Они содержат ошибки, поэтому не могут быть переотправлены.";
	Иначе
		ТекстПредупреждения="Подготовлено к переотправке счетов-фактур: "+СокрЛП(КоличествоУспешно)+"."+Символы.ПС+"Для завершения операции выполните обмен.";
	КонецЕсли;
	Если Параметры.МодальностьЗапрещена Тогда 
		Выполнить("ПоказатьПредупреждение(,ТекстПредупреждения,,""Контур.EDI"")");
	Иначе
		Предупреждение(ТекстПредупреждения,,"Контур.EDI");
	КонецЕсли;
	
	ОбновитьСписок();
	КонецЕсли;
КонецПроцедуры // ОбработчикСогласияПереотправки()

 &НаСервере
Функция ПереотправитьПоРееструСервер(ТаблицаДокументовИзФайла)
	
	КоличествоУспешно = 0;
	
	Для каждого Стр Из ТаблицаДокументовИзФайла Цикл
		
		Если ЗначениеЗаполнено(Стр.Документ) Тогда
			
			СФ = Стр.Документ;
			
			ИсходноеСообщение = МодульОбъекта().НайтиСообщениеДокумента(СФ,"INVOIC");
			Если ИсходноеСообщение = Неопределено Тогда
				Продолжить;//мы не отправляли сообщение по данному документу
			КонецЕсли;
			
			Сообщение = МодульОбъекта().ПодготовитьИсходящееСообщение("INVOIC", СФ);
			Сообщение.Вставить("ПереотправляемоеСообщениеСсылка",ИсходноеСообщение);
			
			КопияСообщения		= МодульОбъекта().ПолучитьКопиюСообщения(Сообщение);
			РезультатПроверки	= МодульОбъекта().ПроверитьПоляСообщения(КопияСообщения);
			
			Если РезультатПроверки.Успешно = Истина Тогда
				
				ПараметрыОтправки=Новый Структура;
				ПараметрыОтправки.Вставить("Сообщение",Сообщение);
				ПараметрыОтправки.Вставить("ОтправитьСообщениеИзФормы",Истина);
				
				МодульОбъекта().ОтправитьСообщение("INVOIC",СФ, ПараметрыОтправки);											
				
				КоличествоУспешно=КоличествоУспешно+1;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат КоличествоУспешно;
	
КонецФункции // ПереотправитьПоРееструСервер()


&НаКлиенте
Процедура КнопкаЕщеПереотправитьПоРеестру(Команда)
	
	ПолноеИмяФайла = ВыбратьФайлРеестра();
	Если ПолноеИмяФайла = Неопределено Тогда
		Возврат; 
	КонецЕсли;
	
	Состояние("Анализирую реестр...", 50, , Элементы.ОткрытьНастройки.Картинка);
	
	ТаблицаДокументовИзФайла = ПрочитатьФайлРеестра(ПолноеИмяФайла);//массив
	
	НайденоДокументовВ1С = НайтиДокументы1ССервер(ТаблицаДокументовИзФайла);
	Если НайденоДокументовВ1С > 0 Тогда 
		
		//спросим подтверждение отправки
		ТекстВопроса ="По этому реестру в 1С было найдено: " + НайденоДокументовВ1С + " документов. 
		|Переотправить их?";
		
		КнопкиВопроса = Новый СписокЗначений;
		КнопкиВопроса.Добавить("Да, переотправить найденные");
		КнопкиВопроса.Добавить("Отмена");
		
		ДопПараметрДляПередачиВОбработчик = ТаблицаДокументовИзФайла;
		
		РезультатВопроса = Неопределено;
		
		Если Параметры.МодальностьЗапрещена Тогда
			
			Выполнить("ПоказатьВопрос(Новый ОписаниеОповещения(""ОбработчикСогласияПереотправки"", ЭтаФорма, ДопПараметрДляПередачиВОбработчик), ТекстВопроса, КнопкиВопроса,,,""Контур.EDI"")");
			
		Иначе
			
			РезультатВопроса = Вопрос(ТекстВопроса, КнопкиВопроса, , , "Контур.EDI");
			ОбработчикСогласияПереотправки(РезультатВопроса,ДопПараметрДляПередачиВОбработчик);
			
		КонецЕсли;
		
	Иначе
		
		ТекстПредупреждения = "По реестру в 1С не найдено ни одного документа.";
		
		Если Параметры.МодальностьЗапрещена Тогда 
			Выполнить("ПоказатьПредупреждение(,ТекстПредупреждения,,""Контур.EDI"")");
		Иначе
			Предупреждение(ТекстПредупреждения,,"Контур.EDI");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
//Конец переотправка по реестру

&НаКлиенте
Процедура КнопкаЕщеПоказатьНаЯндексКартах(Команда)
	
	СодержимоеТД= Элементы.ТабСообщения.ТекущиеДанные.ТочкаДоставки;
	
	Если ЗначениеЗаполнено(СодержимоеТД) Тогда
		Если ТипЗнч(СодержимоеТД)=Тип("Строка")Тогда 
			АдресТД = СодержимоеТД;
		Иначе
			АдресТД=ПолучитьАдресТДДляЯндексКарты(СодержимоеТД);
		КонецЕсли;
		ПоказатьАдресНаКарте(АдресТД, "Яндекс.Карты");
	Иначе
		ТекстПредупреждения="В этой строке не заполнен адрес";
		ДопПараметрДляПередачиВОбработчик=Неопределено;
		Если Параметры.МодальностьЗапрещена Тогда 
			Выполнить("ПоказатьПредупреждение(,ТекстПредупреждения,,""Контур.EDI"")");
		Иначе
			Предупреждение(ТекстПредупреждения,,"Контур.EDI");
		КонецЕсли;
		//по хорошему - надо метнуться в сообщение - конвертнуть shipTo и взять адрес оттуда
		//ждем деманд
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПолучитьАдресТДДляЯндексКарты(ТДСсылка)
	Возврат МодульОбъекта().ПолучитьЭлементСправочника("ТочкиДоставкиСторонние", ТДСсылка).АдресДоставки;	
КонецФункции

&НаКлиенте
Процедура ПоказатьАдресНаКарте(Адрес, ИмяКартографическогоСервиса) Экспорт
	АдресКодированный = Адрес;
	Если ИмяКартографическогоСервиса = "GoogleMaps" Тогда
		СтрокаЗапуска = "https://maps.google.ru/?q=" + АдресКодированный;
	Иначе
		СтрокаЗапуска = "https://maps.yandex.ru/?text=" + АдресКодированный;
	КонецЕсли;
	
	ПерейтиПоНавигационнойСсылке(СтрокаЗапуска);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьТехподдержкаПочтаНажатие(Команда)
	
	НоваяМетрика().НаФорме(ЭтаФорма).Категория("Помощь").НажатаКнопка(Команда).ПоместитьВОчередь();
	Попытка
		темаОбращения = "Проблема в модуле (" + Лев(ЭтаФорма.Заголовок, 22) + ")";
		текстОбращения = "<шаблон> В форме ________ происходит_________ если__________ при этом________";
		
		ЗапуститьПриложение(Объект.ПараметрыКлиентСервер.URLАдреса.ТехподдержкаПочта 
							+ "?subject=" + темаОбращения + "&body=" + текстОбращения);
	Исключение
		Сообщить("Не получилось подключиться к вашей почтовой программе по-умолчанию." + Символы.ПС + "Мы по-прежнему хотим решить вашу проблему, звоните: 8 800 500-33-51")
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьТехподдержкаОнлайнЧатНажатие(Команда)
	
	НоваяМетрика().НаФорме(ЭтаФорма).Категория("Помощь").НажатаКнопка(Команда).ПоместитьВОчередь();
	ЗапуститьПриложение(Объект.ПараметрыКлиентСервер.URLАдреса.ТехподдержкаОнлайнЧат);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьТехподдержкаТелефонНажатие(Команда)
	
	НоваяМетрика().НаФорме(ЭтаФорма).Категория("Помощь").НажатаКнопка(Команда).ПоместитьВОчередь();
	ЗапуститьПриложение(Объект.ПараметрыКлиентСервер.URLАдреса.ТехподдержкаТелефон);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьСправкаНажатие(Команда)
	
	НоваяМетрика().НаФорме(ЭтаФорма).Категория("Помощь").НажатаКнопка(Команда).ПоместитьВОчередь();
	ЗапуститьПриложение(Объект.ПараметрыКлиентСервер.URLАдреса.ОнлайнСправка);	
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьНовоеВРелизе() Экспорт
	
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Сервис_НовоеВРелизеУправляемая", , , ЭтаФорма, , , , РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаКлиенте
Процедура НовоеВРелизе(Команда)
	
	НоваяМетрика().НаФорме(ЭтаФорма).Категория("Помощь").НажатаКнопка(Команда).ПоместитьВОчередь();
	ОткрытьФормуПомощникаEDI(Объект.ПараметрыКлиентСервер.URLАдреса.ИсторияРелизовМодуляEDI);
	
КонецПроцедуры

&НаКлиенте
Процедура ТехподдержкаТелеграмНажатие(Команда)
	
	НоваяМетрика().НаФорме(ЭтаФорма).Категория("Помощь").НажатаКнопка(Команда).ПоместитьВОчередь();
	ЗапуститьПриложение(Объект.ПараметрыКлиентСервер.URLАдреса.ТехподдержкаТелеграмЧат);
	
КонецПроцедуры

&НаСервере
Функция ОсновнойGLN()

Возврат МодульОбъекта().ПолучитьКонстантуEDI("GLN_Основной");	

КонецФункции // ОсновнойGLN()

&НаКлиенте
Процедура ПредоставитьУдаленныйДоступКонтурVNC(Команда)
	
	//все на клиенте!
	НоваяМетрика().НаФорме(ЭтаФорма).Категория("Помощь").НажатаКнопка(Команда).ПоместитьВОчередь();
	
	ПолныйПутьКФайлу = КаталогВременныхФайлов() + "Контур_VNC.exe";
	
	Состояние("Загрузка модуля удаленного доступа...");
	
	Сервер = Объект.ПараметрыКлиентСервер.URLАдреса.Диагностика;
	ИмяФайла = Объект.ПараметрыКлиентСервер.URLАдреса.ФайлКонтурVNC;
	
	Результат = ПолучитьФайл_НаСервере(Сервер, ИмяФайла);
	
	Если Результат = Неопределено Тогда 
		МодульОбъектаКлиент().Предупреждение_КонтурEDI("Не удалось загрузить модуль техподдержки с сервера. 
		|Вы можете попытаться самостоятельно скачать модуль с сайта www.help.kontur.ru/vnc 
		|или обратиться к системному администратору");
		
		Состояние("");
		Возврат;
	КонецЕсли;
	
	Результат.Записать(ПолныйПутьКФайлу);	
	
	Состояние("Запуск модуля удаленного доступа...");
	ЗапуститьПриложение(ПолныйПутьКФайлу);
	
КонецПроцедуры

&НаСервере 
Функция ПолучитьФайл_НаСервере(Сервер, ИмяФайла)
	
	  Возврат МодульОбъекта().HTTP_GetFile(Сервер, ИмяФайла);
	
КонецФункции

&НаКлиенте
Процедура ПредоставитьУдаленныйДоступTeamViewer(Команда)
	
	НоваяМетрика().НаФорме(ЭтаФорма).Категория("Помощь").НажатаКнопка(Команда).ПоместитьВОчередь();
	
	ПолныйПутьКФайлуЛокально = КаталогВременныхФайлов() + "КонтурEDI_TeamViewer.exe";
	Файл = Новый Файл(ПолныйПутьКФайлуЛокально);
	
	Если Не Файл.Существует() Тогда 
		
		Состояние("Загрузка модуля удаленного доступа...");
		Сообщить("Загрузка модуля удаленного доступа...");  
		
		Сервер = Объект.ПараметрыКлиентСервер.URLАдреса.EDIБоевойСервер;
		ИмяФайла = Объект.ПараметрыКлиентСервер.URLАдреса.ФайлTeamViewer;
		
		Результат = ПолучитьФайл_НаСервере(Сервер, ИмяФайла);
		
		Если Результат = Неопределено Тогда 
			МодульОбъектаКлиент().Предупреждение_КонтурEDI("Не удалось загрузить модуль техподдержки с сервера. 
			|Вы можете попытаться самостоятельно скачать модуль с сайта www.teamviewer.com
			|или обратиться к системному администратору");
			
			Состояние("");
			Возврат;
		КонецЕсли;
		
		Результат.Записать(ПолныйПутьКФайлуЛокально);
		
	КонецЕсли;
	
	Состояние("Запуск модуля удаленного доступа...");
	ЗапуститьПриложение(ПолныйПутьКФайлуЛокально);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьДиагностику(Команда)
	
	НоваяМетрика().НаФорме(ЭтаФорма).Категория("Помощь").НажатаКнопка(Команда).ПоместитьВОчередь();
	ЗапуститьПриложение(Объект.ПараметрыКлиентСервер.URLАдреса.ДиагностикаКонтурЭДО);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьКонфигуратор(Команда)
	
	НоваяМетрика().НаФорме(ЭтаФорма).Категория("Помощь").НажатаКнопка(Команда).ПоместитьВОчередь();
	ЗапуститьСистему("DESIGNER /AppAutoCheckMode");
	
КонецПроцедуры

//Область Вслывающие неблокирующие диалоги
&НаКлиенте
Процедура ВД_ПопробоватьВычеркивание(Команда)
	ОбновлятьВесьСписокПриРаботеСЗаказами=Ложь;
	ТекстПредупреждения="Режим изменен только для текущего сеанса.";
	Если Параметры.МодальностьЗапрещена Тогда 
		Выполнить("ПоказатьПредупреждение(,ТекстПредупреждения,,""Контур.EDI"")");
	Иначе
		Предупреждение(ТекстПредупреждения,,"Контур.EDI");
	КонецЕсли;
	Элементы.ВсплывающийДиалогПереходНаВычеркивание.Видимость=Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ВД_ОткрытьВыборПериода(Команда)
	ОткрытьВыборПериода();
	Элементы.ВсплывающийДиалогПереходНаВычеркивание.Видимость=Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ВД_ПереключитьНаВычеркивание(Команда)
	УстановитьКонстантуEDIВызовСервера("ОбновлятьВесьСписокПриРаботеСЗаказами",ложь);
	ОбновлятьВесьСписокПриРаботеСЗаказами=Ложь;
	ТекстПредупреждения="Режим изменен";
	Если Параметры.МодальностьЗапрещена Тогда 
		Выполнить("ПоказатьПредупреждение(,ТекстПредупреждения,,""Контур.EDI"")");
	Иначе
		Предупреждение(ТекстПредупреждения,,"Контур.EDI");
	КонецЕсли;
	Элементы.ВсплывающийДиалогПереходНаВычеркивание.Видимость=Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ВД_Отложить(Команда)
	УстановитьКонстантуEDIВызовСервера("ОтказВычеркиватьСтрокиДо",ТекущаяДата()+60*60*24);
	Элементы.ВсплывающийДиалогПереходНаВычеркивание.Видимость=Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ВД_ВычеркиваниеЗакрыть(Команда)
	Элементы.ВсплывающийДиалогПереходНаВычеркивание.Видимость=Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоФильтровПриАктивизацииЯчейки(Элемент)
	Если Элемент.ТекущийЭлемент <>Неопределено Тогда   
		
		Если Элемент.ТекущийЭлемент.Имя = "ДеревоФильтровИзменить"  
			И Элемент.ТекущиеДанные <> Неопределено  
			И ТипЗнч(Элемент.ТекущиеДанные.Изменить)= Тип("Картинка")
			//и Элемент.ТекущиеДанные.Изменить.Вид <> ВидКартинки.Пустая
			Тогда 
			//выполнить действие                                     
			ДеревоФильтровВыбор(Элемент, Элемент.ТекущиеДанные, Элементы.ДеревоФильтровИзменить, ложь);
		КонецЕсли;
	КонецЕсли;
	
	//переведем активный элемент чтобы при перезаполнении партнеров не произошла случайно активация ячейки добавления партнера
	Элемент.ТекущийЭлемент = Элементы.ДеревоФильтровЗначение;
КонецПроцедуры

&НаКлиенте
Процедура АнализЗаказов(Команда)
	
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Отчет_АнализЗаказовУправляемая", , , ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Функция Статистика_ПолучитьКоличествоНеотраженныхСообщений()
	
	Возврат МодульОбъекта().Отчетность_НеотраженнаяСтатистика_ПолучитьКоличествоСообщений();
	
КонецФункции

&НаСервере
Процедура Статистика_РассчитатьСтатистикуНаСервере()
	
	МодульОбъекта().Отчетность_НеотраженнаяСтатистика_Рассчитать();
				
КонецПроцедуры

//Здесь сохраняем файлы обновления и инструкций
&НаКлиенте
Процедура Статистика_Рассчитать(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса  = КодВозвратаДиалога.Да Тогда
        Статистика_РассчитатьСтатистикуНаСервере();
	КонецЕсли;
	
	ОткрытьФорму("Отчет.КонтурEDI_АнализДанных.Форма");
		
КонецПроцедуры

&НаКлиенте
Процедура Отчетность(Команда)
	
	//проверить доступность метаданных по отчетности
	//и либо вызвать отчет либо сказать обратитесь в контур
	Если ЕстьМетаданныеОтчетов() Тогда
		
		КоличествоНерасчитанных = Статистика_ПолучитьКоличествоНеотраженныхСообщений();
		Если КоличествоНерасчитанных > 0 Тогда
			
			ТекстВопроса = "Есть новые сообщения ("+КоличествоНерасчитанных+"). Рассчитать по ним статистику?";
			
			Если Параметры.МодальностьЗапрещена Тогда
				Выполнить("ПоказатьВопрос(Новый ОписаниеОповещения(""Статистика_Рассчитать"", ЭтаФорма), ТекстВопроса, РежимДиалогаВопрос.ДаНет)");
			Иначе
				РезультатВопросаРасчетаСтатистики = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
				Статистика_Рассчитать(РезультатВопросаРасчетаСтатистики, Неопределено)
			КонецЕсли;
			
			Возврат;
		
		КонецЕсли;
		
		ОткрытьФорму("Отчет.КонтурEDI_АнализДанных.Форма");
		
	Иначе
		ТекстПредупреждения="Для работы с Отчетностью Контур.EDI необходимо обновить конфигурацию!";	
		ТекстПредупреждения=ТекстПредупреждения+Символы.ПС+"Обновите вашу конфигурацию с использованием файла КонтурEDI_upd. ";
		
		РезультатВопросаСохраненияФайла = Неопределено;
		
		Если Параметры.МодальностьЗапрещена Тогда
			Выполнить("ПоказатьВопрос(Новый ОписаниеОповещения(""ПриОткрытииВопросОбобновленииЗавершение"", ЭтаФорма), ТекстПредупреждения+Символы.ПС+Символы.ПС+""Сохранить файл обновления на диск?"", РежимДиалогаВопрос.ДаНет)");
		иначе
			РезультатВопросаСохраненияФайла = Вопрос(ТекстПредупреждения+Символы.ПС+Символы.ПС+"Сохранить файл обновления на диск?", РежимДиалогаВопрос.ДаНет);
			ПриОткрытииВопросОбОбновленииЗавершение(РезультатВопросаСохраненияФайла, Неопределено)
		КонецЕсли;
		
		ЗапуститьПриложение(Объект.ПараметрыКлиентСервер.URLАдреса.ИнструкцияОтчетностьSL);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьВерсиюБСПСервер()
	Возврат МодульОбъекта().ПолучитьВерсиюБСП_КонтурEDI();
КонецФункции

&НаСервере
Функция ЕстьМетаданныеОтчетов()
	Возврат  (МодульОбъекта().ЕстьНеобходимыеМетаданные("РегистрыСведений.КонтурEDI_Статистика") 
			И МодульОбъекта().ЕстьНеобходимыеМетаданные("Отчеты.КонтурEDI_АнализДанных")); 
КонецФункции

&НаКлиенте
Процедура Отчетность_ПересчитатьСтатичтикуSL(Команда)
	
	Диалог = Новый ДиалогРедактированияСтандартногоПериода();
	Диалог.Период = НастройкаПериода;
	
	Если Параметры.МодальностьЗапрещена Тогда 
		Выполнить("Диалог.Показать(Новый ОписаниеОповещения(""Обработчик_ПересчитатьСтатичтикуSL"", ЭтаФорма,))");
	Иначе
		Если Диалог.Редактировать() Тогда 
			Обработчик_ПересчитатьСтатичтикуSL(Диалог.Период,неопределено);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Обработчик_ПересчитатьСтатичтикуSL(ПериодSL,ДопПараметр1=Неопределено) Экспорт
	
	Если ПериодSL<>Неопределено  Тогда 
		Состояние("Рассчитываю статистику сообщений за период...",50,,Элементы.Настройки.Картинка);
		
		ДатаС = ПериодSL.ДатаНачала;
		Если Не ЗначениеЗаполнено(ПериодSL.ДатаОкончания) Тогда
			ДатаПо = КонецДня(Дата("39990101"));
		Иначе	
			ДатаПо = КонецДня(ПериодSL.ДатаОкончания);
		КонецЕсли;	
		
		Отчетность_ПересчитатьСтатистикуSLВызовСервера(ДатаС,ДатаПо);
		Предупреждение("Статистика по сообщениям за период с "+Формат(ДатаС,"ДФ=dd.MM.yyyy")+" по "+Формат(ДатаПо,"ДФ=dd.MM.yyyy")+" обновлена");
		
	КонецЕсли;
	
	
КонецПроцедуры

&НаСервере
Процедура Отчетность_ПересчитатьСтатистикуSLВызовСервера(ДатаС,ДатаПо)
	
	МодульОбъекта().Отчетность_ПересчитатьСтатистикуSLСервер(ДатаС,ДатаПо);
	
КонецПроцедуры 

&НаКлиенте
Процедура УзнатьСвоюВерсиюБСП(Команда)
	Сообщить("Ваша версия БСП: "+ ПолучитьВерсиюБСПСервер());
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьКомпактность(Команда)
	
	СохранитьНастройкиГлавнойФормы();
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Сервис_УФ_ПереключениеКомпактности", , , ЭтаФорма, "ОбработчикПослеРедактированияПартнеров", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуОтправкиПроизвольногоСообщения(Команда)
	
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("ОтправкаПроизвольногоСообщенияУправляемая", , , ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуОбновленияМодуля(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДополнительнаяОбработкаСсылка",Параметры.ДополнительнаяОбработкаСсылка);
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Сервис_ОбновлениеУправляемая", , ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуУдаленияСообщений(Команда)
	
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Сервис_УдалениеСообщенийУправляемая", , , ЭтаФорма, "ОбработчикПослеУдаленияСообщений", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикПослеУдаленияСообщений(Результат = Неопределено, ДопПараметры = Неопределено) Экспорт

	ОбновитьСписок();
	
КонецПроцедуры

&НаКлиенте
Процедура ОПрограмме(Команда)
	
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Сервиc_ОПрограммеУправляемая", , , ЭтаФорма);
	
КонецПроцедуры

//Меркурий
&НаСервере
Процедура ДобавитьКнопкуНастройкиМеркурий()
	
	Если Элементы.Найти("НастройкиМеркурий") = Неопределено Тогда
		МеркурийНастройки = Элементы.Добавить("НастройкиМеркурий", Тип("КнопкаФормы"), Элементы.Настройки);
		МеркурийНастройки.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
		МеркурийНастройки.Заголовок = "Настройки ГИС Меркурий";
		МеркурийНастройки.ИмяКоманды = "ОткрытьНастройкиМеркурий";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура МодульОбъектаОбновитьПриНеобходимости()
	МодульОбъекта();	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкиМеркурий(Команда)
	
	СохранитьНастройкиГлавнойФормы();
	Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект = Истина;
	МодульОбъектаОбновитьПриНеобходимости();//обновим обработку во временном хранилище, если устарела и получим новый Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОбъектПараметрыКлиентСервер", 	Объект.ПараметрыКлиентСервер);
	ПараметрыФормы.Вставить("АдресХранилища", 				Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект);
	ПараметрыФормы.Вставить("МодальностьЗапрещена", 		Объект.ПараметрыКлиентСервер.МодальностьЗапрещена);
	ПараметрыФормы.Вставить("ПутьКФормамМеркурий", 			ПутьКФормамМеркурий);
	ПараметрыФормы.Вставить("КэшироватьМодульОбъекта", 		Истина);
	
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Меркурий_НастройкаУправляемая", ПутьКФормамМеркурий, ПараметрыФормы, ЭтаФорма, "ОбработчикПослередактированияНастроек", ЭтаФорма, ,РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаСервере
Функция ИнициализироватьКлиентскийМодульМеркурий()
	
	Возврат МодульОбъекта().ПодключитьВнешнийКлиентскийМодульМеркурий();
	 
КонецФункции

&НаКлиенте
Функция МеркурийПолучитьКлиентскийМодульEDI() Экспорт
	
	Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект = Истина;
	МодульОбъектаОбновитьПриНеобходимости();//обновим обработку во временном хранилище, если устарела и получим новый Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("IDОсновнойФормы", 			Объект.ПараметрыКлиентСервер.IDОсновнойФормы);
	ПараметрыФормы.Вставить("АдресХранилища", 			Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект);
	ПараметрыФормы.Вставить("МодальностьЗапрещена", 	Объект.ПараметрыКлиентСервер.МодальностьЗапрещена);
	ПараметрыФормы.Вставить("ПутьКФормамМеркурий", 		ПутьКФормамМеркурий);
	ПараметрыФормы.Вставить("КэшироватьМодульОбъекта", 	Истина);

	МеркурийКлиентскийМодульEDI = МодульОбъектаКлиент().ПолучитьФорму_КонтурEDI("КлиентскийМодульEDI_Управляемая", ПутьКФормамМеркурий, ПараметрыФормы, ЭтаФорма);
	Возврат МеркурийКлиентскийМодульEDI;

КонецФункции

&НаКлиенте
Процедура МеркурийВыполнитьЗадачу(ВыбраннаяСтрока,ПакетнаяОбработка=Ложь,КодДействия)
	МеркурийПолучитьКлиентскийМодульEDI().МеркурийВыполнитьЗадачу(ВыбраннаяСтрока,ПакетнаяОбработка,КодДействия)
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКарточкуМеркурий(ВыбраннаяСтрока, ПереотправкаСообщения = Ложь, Сообщение=Неопределено )
	
	МеркурийПолучитьКлиентскийМодульEDI().ОткрытьКарточкуМеркурий(ВыбраннаяСтрока, ПереотправкаСообщения, Сообщение)
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьСообщениеМеркурий(ВыбраннаяСтрока, НеОткрыватьФормы = Ложь, ПереотправкаСообщения = Ложь)
	
	МеркурийПолучитьКлиентскийМодульEDI().ОтправитьСообщениеМеркурий(ВыбраннаяСтрока, НеОткрыватьФормы, ПереотправкаСообщения)
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьТранзакцииМеркурийМассово(ВыделенныеСтроки)
	МеркурийПолучитьКлиентскийМодульEDI().ОтправитьТранзакцииМеркурийМассово(Объект.ТабСообщения, ВыделенныеСтроки);
КонецПроцедуры

&НаКлиенте
Процедура КнопкаДействияФормыНажатие(Элемент, ДопПараметры = Неопределено)
	
	Если ДопПараметры = Неопределено Тогда
		КнопкаДействияФормыНажатиеСервер(Элемент.Имя, ДопПараметры);
		ВыполнитьПродолжениеАлгоритмаРасширения(Неопределено,ДопПараметры)
	Иначе
		ВыполнитьПродолжениеАлгоритмаРасширения(Неопределено,ДопПараметры) 		
	КонецЕсли;
	  	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПродолжениеАлгоритмаРасширения(ВыбранныйЭлемент, ДопПараметры) Экспорт

	Алгоритм = "";
	
	Если ТипЗнч(ДопПараметры) = Тип("Структура") Тогда
		Если ДопПараметры.Свойство("Алгоритм") Тогда
			Алгоритм =  ДопПараметры.Алгоритм;
			Если ДопПараметры.Свойство("ПродолжениеАлгоритма") Тогда
				ДопПараметры.Вставить("Алгоритм", ДопПараметры.ПродолжениеАлгоритма);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Алгоритм <> "" Тогда
		Попытка 
			Выполнить(Алгоритм);
		Исключение
			Сообщить(ОписаниеОшибки());
		КонецПопытки;
	Иначе
		//Сообщить("Не задано продолжение процедуры");
	Конецесли;

КонецПроцедуры

&НаСервере
Процедура КнопкаДействияФормыНажатиеСервер(ИмяКоманды, ДопПараметры = Неопределено)
	
	
	Если ДопПараметры = Неопределено Тогда
    	ДопПараметры = Новый Структура("ИмяКоманды,Форма",ИмяКоманды,ЭтаФорма);
	КонецЕсли;
	
	МодульОбъекта().ОбработкаСобытияПодключаемогоМодуля("КнопкаОсновнойФормыНажатие",,ДопПараметры);
	
	ДопПараметры.Удалить("Форма");
		
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	СохранитьНастройкиГлавнойФормы();
КонецПроцедуры

&НаКлиенте
Процедура КнопкаЕщеСоздатьЗаказ(Команда)
	ТипДокументаЗаказ = ПолучитьТипЗначенияОбъекта("ИсходящийЗаказПоставщику",,Истина);
	Если ТипДокументаЗаказ<>Неопределено Тогда
		Форма = ПолучитьФорму("Документ." + ТипДокументаЗаказ + ".ФормаОбъекта");
		Форма.Открыть();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПолучитьТипЗначенияОбъекта(ИмяОбъекта, Кратко = Ложь, ВернутьИмяВМетаданных = Ложь)
	Возврат МодульОбъекта().ПолучитьТипЗначенияОбъекта(ИмяОбъекта,Кратко,ВернутьИмяВМетаданных);		
КонецФункции

&НаСервере
Процедура УдалитьЛишниеКнопкиТопПомощников()

	Если ТипЗнч(ПомощникиEDI_Структура) = Тип("Структура") Тогда
		Для Каждого ЭлементСтруктуры Из ПомощникиEDI_Структура Цикл
			
			КомандаНаУдаление = Команды.Найти(ЭлементСтруктуры.Ключ);
			Если КомандаНаУдаление <> Неопределено Тогда
				Команды.Удалить(КомандаНаУдаление);
			КонецЕсли;
						
			ЭлементНаУдаление = Элементы.Найти(ЭлементСтруктуры.Ключ);
			Если ЭлементНаУдаление <> Неопределено Тогда
				Элементы.Удалить(ЭлементНаУдаление);
			КонецЕсли;	
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры 

&НаСервере
Процедура НастроитьПомощниковEDI_Сервер()
    		
	ПомощникиEDI_Соответствие = Новый Соответствие;
	
	Если ТипЗнч(ПомощникиEDI_Структура) = Тип("Структура") Тогда
		Для Каждого Помощник Из ПомощникиEDI_Структура Цикл
			ПомощникиEDI_Соответствие.Вставить(Помощник.Значение.Имя, Помощник.Значение.Ссылка);  			
		КонецЦикла;
	КонецЕсли;
		
	РезультатПолученияПомощников	= МодульОбъекта().ПолучитьПомощниковEDI(, ПомощникиEDI_Соответствие);
	
	Если ТипЗнч(РезультатПолученияПомощников) <> Тип("Структура") ИЛИ НЕ РезультатПолученияПомощников.Свойство("Помощники") Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьЛишниеКнопкиТопПомощников();  
		
	ПомощникиEDI_Соответствие		= РезультатПолученияПомощников.Помощники;
	ОтметкаОНеПрочитанном			= Ложь;
	
	ИспользованныеПомощники_СписокЗначений = Новый СписокЗначений;
	ИспользованныеПомощники_СписокЗначений.ЗагрузитьЗначения(МодульОбъекта().ПолучитьИспользованныхПомощников()); 	
	
    ПомощникиEDI_Структура = Новый Структура;
	
    Счетчик = 0;
	ПредыдущийЭлемент = Элементы.ТопПомощников;
	Для Каждого ЭлементПомощник Из ПомощникиEDI_Соответствие Цикл 
		
		Счетчик = Счетчик + 1;
		
		ИмяПомощникаДляСтруктуры = "ТопПомощников" + Формат(Счетчик,"ЧЦ=3; ЧГ=");
		
		ПомощникиEDI_Структура.Вставить(ИмяПомощникаДляСтруктуры, Новый Структура("Имя, Ссылка",ЭлементПомощник.Ключ, ЭлементПомощник.Значение));
		
		ПометкаКнопки = Ложь;
		Если ИспользованныеПомощники_СписокЗначений.НайтиПоЗначению(ЭлементПомощник.Ключ) = Неопределено Тогда
			ОтметкаОНеПрочитанном	= Истина;
			ПометкаКнопки			= Истина;
		КонецЕсли;
		
		//
		//Добавляем новую команду
		КомандаПомощника = ЭтаФорма.Команды.Добавить(ИмяПомощникаДляСтруктуры);
		КомандаПомощника.Действие = "ТопПомощников";
		КомандаПомощника.Заголовок = ЭлементПомощник.Ключ;
		//Добавляем новую кнопку
		ПомощникКнопка = Элементы.Вставить(ИмяПомощникаДляСтруктуры, Тип("КнопкаФормы"), Элементы.ПодменюПомощь, ПредыдущийЭлемент);
		
		ПомощникКнопка.Заголовок    = ЭлементПомощник.Ключ;
		ПомощникКнопка.Вид			= ВидКнопкиФормы.ОбычнаяКнопка;
		ПомощникКнопка.ИмяКоманды	= ИмяПомощникаДляСтруктуры;
		
		Если ПометкаКнопки Тогда
			ПомощникКнопка.Картинка		= Элементы.КартинкаИнформацияСОтметкой.Картинка;
		Иначе
			ПомощникКнопка.Картинка		= Элементы.КартинкаИнформацияБезОтметки.Картинка;
		КонецЕсли;
		ПредыдущийЭлемент = ПомощникКнопка;
		//
	КонецЦикла;  
	
	ИзменитьИконкуКнопкиПомощи();
	
КонецПроцедуры  

&НаСервере
Процедура ИзменитьИконкуКнопкиПомощи()
     	
	Если ОтметкаОНеПрочитанном Тогда
		Элементы.ПодменюПомощь.Картинка = Элементы.КартинкаПомощьСОтметкой.Картинка;
	Иначе
		Элементы.ПодменюПомощь.Картинка = Элементы.КартинкаПомощьБезОтметки.Картинка; 
	КонецЕсли;

КонецПроцедуры 

&НаКлиенте
Процедура НастроитьПомощниковEDI() Экспорт
	
	Попытка
				
		НастроитьПомощниковEDI_Сервер();				
						
	Исключение
		
		ПроверятьПомощников = Ложь;
	 	ОтключитьОбработчикОжидания("НастроитьПомощниковEDI");
		Сообщить(ОписаниеОшибки());
		
	КонецПопытки;

КонецПроцедуры

&НаСервере
Процедура ОтметитьПомощник_ФормаEDI_Сервер(ИмяПомощника)

	МодульОбъекта().ОтметитьПомощникEDIИспользованным(ИмяПомощника);
	
	ОтметкаОНеПрочитанном = Ложь;
	
	Для Каждого Помощник Из ПомощникиEDI_Структура Цикл
		
		ПометкаКнопки = Ложь;
		Если ИспользованныеПомощники_СписокЗначений.НайтиПоЗначению(Помощник.Значение.Имя) = Неопределено Тогда
			ОтметкаОНеПрочитанном	= Истина;
			ПометкаКнопки			= Истина;
		КонецЕсли;
		
		ПомощникКнопка = Элементы[Помощник.Ключ];
		
		Если ПометкаКнопки Тогда
			ПомощникКнопка.Картинка		= Элементы.КартинкаИнформацияСОтметкой.Картинка;
		Иначе
			ПомощникКнопка.Картинка		= Элементы.КартинкаИнформацияБезОтметки.Картинка;
		КонецЕсли; 		
		
	КонецЦикла; 
	
	ИзменитьИконкуКнопкиПомощи();
	
КонецПроцедуры 

&НаКлиенте
Процедура ОткрытьФормуПомощникаEDI(СсылкаНаПомощника) Экспорт

	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("URL",СсылкаНаПомощника);
	
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("ФормаПомощникаEDI_Управляемая", , ПараметрыФормы, ЭтаФорма, , , , РежимОткрытияОкнаФормы.Независимый);

КонецПроцедуры

&НаКлиенте
Процедура ОбработчикВыбораПомощника(ИмяПомощника) Экспорт
		
	Если ПомощникиEDI_Структура.Свойство(ИмяПомощника) Тогда
		
		СтруктураПомощника = ПомощникиEDI_Структура[ИмяПомощника];
		
		ОткрытьФормуПомощникаEDI(СтруктураПомощника.Ссылка); 
		
		Если  ИспользованныеПомощники_СписокЗначений.НайтиПоЗначению(ИмяПомощника) = Неопределено Тогда
			ИспользованныеПомощники_СписокЗначений.Добавить(СтруктураПомощника.Имя); 
			
			ОтметитьПомощник_ФормаEDI_Сервер(СтруктураПомощника.Имя);
			
		КонецЕсли;   
		
	КонецЕсли;  	
		
КонецПроцедуры  

&НаКлиенте
Процедура ТопПомощников(Команда)
	
	НоваяМетрика().НаФорме(ЭтаФорма).Категория("Помощь").НажатаКнопка(Команда).ПоместитьВОчередь();
	ОбработчикВыбораПомощника(Команда.Имя);
	
КонецПроцедуры    

&НаКлиенте
Процедура ФайлОбновленияКонфигурации(Команда)
	
	ДополнительныеПараметры = Новый Структура("Закрывать", Ложь);
		
	ПриОткрытииВопросОбОбновленииЗавершение(КодВозвратаДиалога.Да, ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьМодульДляАнализаВКонтур(Команда)
	
	НоваяМетрика().НаФорме(ЭтаФорма).Категория("Помощь").НажатаКнопка(Команда).ПоместитьВОчередь();
	
	РезультатОтправки = Новый Структура("Успешно,ТекстОшибки,Действие,ТекстСтатуса", Истина, "", "ОпределитьВозможность", "Не выполнено.");
	
	РезультатОтправки = МодульОбъектаКлиент().ВыполнитьМетодМодуляОбъекта("Функция", "ОтправитьМодульДляАнализаВКонтур", Новый Структура("РезультатОтправки", РезультатОтправки));
	
	Если НЕ РезультатОтправки.Успешно Тогда
		
		МодульОбъектаКлиент().Предупреждение_КонтурEDI(РезультатОтправки.ТекстОшибки);
		
	ИначеЕсли РезультатОтправки.Действие = "ВыбратьОбработку" 
		И РезультатОтправки.Свойство("СписокИменОбработок")
		И ТипЗнч(РезультатОтправки.СписокИменОбработок) = Тип("СписокЗначений") Тогда
		
		МодульОбъектаКлиент().ПоказатьВыборЭлемента_КонтурEDI("ОбработчикВыбораОбработкиКонтурEDI", ЭтаФорма, Неопределено, РезультатОтправки.СписокИменОбработок, , "Выберите обработку с модулем Контур-EDI");
		
	Иначе
		
		МодульОбъектаКлиент().Предупреждение_КонтурEDI(РезультатОтправки.ТекстСтатуса);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикВыбораОбработкиКонтурEDI(Результат, ДопПараметры)  Экспорт

	Если Результат <> Неопределено Тогда
		
		РезультатОтправки = Новый Структура("Успешно,ТекстОшибки,Действие,ТекстСтатуса,ВыбраннаяСтрока", Истина, "", "ВыбранаОбработка", "Не выполнено.", Результат.Значение);
						
		РезультатОтправки = МодульОбъектаКлиент().ВыполнитьМетодМодуляОбъекта("Функция", "ОтправитьМодульДляАнализаВКонтур", Новый Структура("РезультатОтправки", РезультатОтправки));
		
	КонецЕсли;

КонецПроцедуры 

//Конец Область Вслывающие неблокирующие диалоги

РежимОтладки = Ложь;
РазбиватьПакетыЗапросов = Ложь;

//РежимОтладки = Истина;//включить для вывода скорости выполнения запросов
//РазбиватьПакетыЗапросов = Истина;
