&НаСервере
Перем МодульОбъекта;

&НаКлиенте
Перем МодульОбъектаКлиент;

&НаСервере
Перем ПараметрыПользователяEDI;

// СЛУЖЕБНЫЕ {

// Инициализация модуля и его экспортных функций
&НаСервере
Функция МодульОбъекта()
	
	Если МодульОбъекта = Неопределено Тогда
		
		Если Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект Тогда
			МодульОбъекта = МодульОбъектаИзКэша();
		КонецЕсли;
		
		Если МодульОбъекта = Неопределено Тогда
			МодульОбъекта = РеквизитФормыВЗначение("Объект");
			МодульОбъекта.ИнициализироватьПодключаемыеМодули(); // TODO Проверить что из этого используется на формах и перенести в ПараметрыКлиентСервер
			МодульОбъекта.ОбновитьКэшМодуляОбъекта();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат МодульОбъекта;
	
КонецФункции

&НаСервере
Функция МодульОбъектаИзКэша()
	
	Результат = Неопределено;
	
	Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда
		АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект;
 		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Структура = ПолучитьИзВременногоХранилища(АдресВХ);
			Если ТипЗнч(Структура) = Тип("Структура") и Структура.Свойство("ОбработкаОбъект") Тогда
			 	Результат = Структура.ОбработкаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульОбъектаКлиент()
	
	Если ТипЗнч(МодульОбъектаКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбъектаКлиент;
	КонецЕсли;
	
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		МодульОбъектаКлиент = ОсновнаяФорма.МодульОбъектаКлиент();
		Возврат МодульОбъектаКлиент;
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
	
	Если ЭтаФорма.ВладелецФормы = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ЭтаФорма.ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;
    	
КонецФункции

&НаКлиенте
Функция НоваяМетрика()
	
	Возврат ОсновнаяФорма().НоваяМетрика();
	
КонецФункции

// } СЛУЖЕБНЫЕ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
	Параметры.Свойство("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	
	ИмяПоляСообщения = Параметры.ИмяПоляСообщения;
	Сообщение = ПолучитьИзВременногоХранилища(Параметры.АдресСообщенияВВХ);
	
	ОчиститьПометкиПолейСервер();
	
	КтоМы = МодульОбъекта().ОпределитьКемМыЯвляемся(Сообщение);
	Партнер	= МодульОбъекта().ОпределитьПартнера(Новый Структура("ТипЗначения,Сообщение", "Сообщение", Сообщение));
	
	МетаданныеСообщения = МодульОбъекта().ПолучитьМетаданныеСообщения(Сообщение.ТипСообщения, Сообщение.Направление);
	СвойстваПоляСообщения = МодульОбъекта().ПолучитьСвойстваПоля(ИмяПоляСообщения, МетаданныеСообщения);	
	
	Если СвойстваПоляСообщения.Тип1С = "ЮрФизЛицоСвое" Тогда
		
		ЮрФизЛицоЗаголовок  = "Организация";
		
	ИначеЕсли СвойстваПоляСообщения.Тип1С = "ЮрФизЛицоСтороннее" Тогда
			
		ЮрФизЛицоЗаголовок  = "Контрагент";
		
	Иначе
		
		ЮрФизЛицоЗаголовок  = ИмяПоляСообщения;
		
	КонецЕсли;
	
	Элементы.ЮрФизЛицо1С.Заголовок = ЮрФизЛицоЗаголовок;
			
	Элементы.ЗаголовокКонтрагента.Заголовок = СокрЛП(Элементы.ЮрФизЛицо1С.Заголовок)+" в 1С";
	
	Если КтоМы = "Поставщик" Тогда
		Если Сообщение.Направление = "Входящее" Тогда
			Элементы.Группа2.Заголовок = "Данные, которые пришли от торговой сети";
		Иначе
			Элементы.Группа2.Заголовок = "Данные, которые будут отправлены в торговую сеть";
		КонецЕсли;
	Иначе
		Если Сообщение.Направление = "Входящее" Тогда
			Элементы.Группа2.Заголовок = "Данные, которые пришли от поставщика";
		Иначе
			Элементы.Группа2.Заголовок = "Данные, которые будут отправлены поставщику";
		КонецЕсли;
	КонецЕсли;
	
	ЮрФизЛицо = Сообщение[ИмяПоляСообщения+"EDI"];
	
	ЮрФизЛицо1С = Сообщение[ИмяПоляСообщения+"1С"];
	
	ЗаполнитьПоляФормы(ЮрФизЛицо);
	
	УстановитьВидимостьПолейЮрФизЛица();
	
	НетОшибок = Истина;
	
	Элементы.ЮрФизЛицо1С.ТолькоПросмотр = (Сообщение.Направление = "Исходящее") ИЛИ (МодульОбъекта().ПараметрыПользователяEDI.РольПользователяEDI <> "ПолныеПрава");
	
	НетОшибок = мПроверитьПоляЮрФизЛица();
	
	Если КтоМы = "Поставщик" 
		И Сообщение.Направление = "Входящее" 
		И СвойстваПоляСообщения.Тип1С = "ЮрФизЛицоСтороннее" Тогда
				
		Если ПараметрыПользователяEDI = неопределено Тогда 
			ПараметрыПользователяEDI= МодульОбъекта().ПолучитьПараметрыТекущегоПользователяEDI();
		КонецЕсли;
		
		Если НЕ ПараметрыПользователяEDI.ГрузополучательИзЮрФизЛицаТД = Истина Тогда
			
			Если МодульОбъекта().ЭтоОбратнаяОтгрузка(Сообщение.ТипСообщения) 
				ИЛИ Сообщение.ТипСообщения = "RETANN" Тогда
				ТочкаДоставкиСторонняя = Сообщение.Грузоотправитель1С;
			Иначе
				ТочкаДоставкиСторонняя = Сообщение.Грузополучатель1С;
			КонецЕсли;

			ТочкаДоставки = МодульОбъекта().ПолучитьЭлементСправочника("ТочкиДоставкиСторонние", ТочкаДоставкиСторонняя);
			
			Если ЗначениеЗаполнено(ТочкаДоставки) И ЗначениеЗаполнено(ТочкаДоставки.ЮрФизЛицо) Тогда
				
				Элементы.ЮрФизЛицо1С.Доступность = Ложь;
				Элементы.НадписьПримечаниеТочкиДоставки.Видимость = Истина;
				
			КонецЕсли;
			
		КонецЕсли;
				
	КонецЕсли;
	
	Если НетОшибок Тогда
		
		Элементы.ТаблицаОшибок.Видимость = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьПометкиПолейСервер()
	
	Элементы.GLN.ЦветФона = ЦветаСтиля.ЦветФонаПоля;
	Элементы.Банк.ЦветФона = ЦветаСтиля.ЦветФонаПоля;
	Элементы.Бик.ЦветФона = ЦветаСтиля.ЦветФонаПоля;
	Элементы.ГлавныйБухгалтер.ЦветФона = ЦветаСтиля.ЦветФонаПоля;
	Элементы.Город.ЦветФона = ЦветаСтиля.ЦветФонаПоля;
	Элементы.Дом.ЦветФона = ЦветаСтиля.ЦветФонаПоля;
	Элементы.Имя.ЦветФона = ЦветаСтиля.ЦветФонаПоля;
	Элементы.Индекс.ЦветФона = ЦветаСтиля.ЦветФонаПоля;
	Элементы.ИНН.ЦветФона = ЦветаСтиля.ЦветФонаПоля;
	Элементы.Квартира.ЦветФона = ЦветаСтиля.ЦветФонаПоля;
	Элементы.Корпус.ЦветФона = ЦветаСтиля.ЦветФонаПоля;
	Элементы.КПП.ЦветФона = ЦветаСтиля.ЦветФонаПоля;
	Элементы.Наименование.ЦветФона = ЦветаСтиля.ЦветФонаПоля;
	Элементы.НаселенныйПункт.ЦветФона = ЦветаСтиля.ЦветФонаПоля;
	Элементы.НомерСчета.ЦветФона = ЦветаСтиля.ЦветФонаПоля;
	Элементы.Район.ЦветФона = ЦветаСтиля.ЦветФонаПоля;
	Элементы.Регион.ЦветФона = ЦветаСтиля.ЦветФонаПоля;
	Элементы.Руководитель.ЦветФона = ЦветаСтиля.ЦветФонаПоля;
	Элементы.Телефон.ЦветФона = ЦветаСтиля.ЦветФонаПоля;
	Элементы.Улица.ЦветФона = ЦветаСтиля.ЦветФонаПоля;
	Элементы.Фамилия.ЦветФона = ЦветаСтиля.ЦветФонаПоля;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоляФормы(ЮрФизЛицо)
	
	ВидЮрФизЛица = ЮрФизЛицо.Вид;
	
	Наименование	= ЮрФизЛицо.Наименование;
	Фамилия			= ЮрФизЛицо.Фамилия;
	Имя				= ЮрФизЛицо.Имя;
	Отчество		= ЮрФизЛицо.Отчество;
	
	Если ЗначениеЗаполнено(ЮрФизЛицо.GLN) Тогда
		GLN = ЮрФизЛицо.GLN;
	КонецЕсли;
	ИНН = ЮрФизЛицо.ИНН;
	КПП = ЮрФизЛицо.КПП;
	
	Банк		= ЮрФизЛицо.Банк;
	БИК			= ЮрФизЛицо.БИК;
	НомерСчета	= ЮрФизЛицо.НомерСчета;
	
	Телефон = ЮрФизЛицо.Телефон;
	
	Руководитель		= ЮрФизЛицо.Руководитель;
	ГлавныйБухгалтер	= ЮрФизЛицо.ГлавныйБухгалтер;
	
	Если Не ЗначениеЗаполнено(ЮрФизЛицо.Адрес) Тогда
		
		ПереключательАдреса = "Российский";
		
	Иначе
		
		ВидАдреса = ЮрФизЛицо.Адрес.ВидАдреса;
		
		Если НЕ ЗначениеЗаполнено(ВидАдреса) Тогда
			ПереключательАдреса = "Российский";
		Иначе
			ПереключательАдреса = ВидАдреса;
		КонецЕсли;
		
		АдресЮрФизЛица = ЮрФизЛицо.Адрес;
		
		Индекс			= АдресЮрФизЛица.Индекс;
		Регион			= АдресЮрФизЛица.Регион;
		Район			= АдресЮрФизЛица.Район;
		Город			= АдресЮрФизЛица.Город;
		НаселенныйПункт = АдресЮрФизЛица.НаселенныйПункт;
		Улица			= АдресЮрФизЛица.Улица;
		Дом				= АдресЮрФизЛица.Дом;
		Корпус			= АдресЮрФизЛица.Корпус;
		Квартира		= АдресЮрФизЛица.Квартира;
		
		КодСтраны		= АдресЮрФизЛица.КодСтраны;
		Адрес			= АдресЮрФизЛица.Адрес;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьПолейЮрФизЛица()
	
	Если ВидЮрФизЛица = "ФизЛицо" Тогда
		
		Элементы.СтраницаЮрЛицо.Видимость = ложь;
		Элементы.СтраницаФизЛицо.Видимость = Истина;
		
		Элементы.КПП.Видимость			= Ложь;
		
	Иначе
		
		Элементы.СтраницаЮрЛицо.Видимость = Истина;
		Элементы.СтраницаФизЛицо.Видимость = Ложь;
		
		Элементы.КПП.Видимость			= Истина;
		
	КонецЕсли;		
	
КонецПроцедуры

&НаСервере
Функция мПроверитьПоляЮрФизЛица()
	
	Если Сообщение.Направление = "Исходящее" Тогда
		
		ОбязательныеПоля 	= МодульОбъекта().ПолучитьОбязательныеПоля(Сообщение);
		ТаблицаОшибокПоля	= МодульОбъекта().ИнициализироватьТаблицуОшибок();
		СтруктураСообщения	= МодульОбъекта().ПолучитьМетаданныеПоляСообщения("ЮрФизЛицо");
		
		МодульОбъекта().ПроверитьПоляСообщенияEDI(ЮрФизЛицо, СтруктураСообщения, ТаблицаОшибокПоля, ИмяПоляСообщения+"EDI",,, ОбязательныеПоля);
		
		БылиОшибки = ?(ТаблицаОшибок.Количество()=0, Ложь, Истина);
		
		ТаблицаОшибок.Очистить();
		
		ОчиститьПометкиПолейСервер();
		
		Если НЕ ТаблицаОшибокПоля.Количество()=0 Тогда
			
			Для Каждого Стр Из ТаблицаОшибокПоля Цикл
				
				Если Стр.ИмяПоля = "Адрес" Тогда
					Для каждого СтрАдреса ИЗ Стр.СведенияОбОшибках Цикл
						
						ВывестиСведенияОбОшибке(СтрАдреса);
						
					КонецЦикла;
					
				Иначе
					ВывестиСведенияОбОшибке(Стр);
				КонецЕсли;	
				
			КонецЦикла;
			
			ВывестиПанельИнформации("В данных по этому юр. лицу найдены ошибки, их необходимо исправить перед отправкой.","Плохо");
			
			Элементы.ТаблицаОшибок.Видимость=Истина;
			
			Возврат Ложь;
			
		Иначе
			
			Если БылиОшибки Тогда
				
				ВывестиПанельИнформации(" Поздравляем! Все ошибки исправлены!","Хорошо");
				
			Иначе
				
				Если КтоМы = "Поставщик" Тогда
					ОкончаниеИсходящее	= "в торговую сеть.";
					ОкончаниеВходящее	= "от торговой сети.";
				Иначе
					ОкончаниеИсходящее	= "поставщику.";
					ОкончаниеВходящее	= "от поставщика.";
				КонецЕсли;
				
				Если Сообщение.Направление = "Исходящее" Тогда
					
					ВывестиПанельИнформации("В этой форме вы можете проверить данные, которые будут отправлены "+ОкончаниеИсходящее);
					
				Иначе
					
					ВывестиПанельИнформации("В этом окне вы можете увидеть подробно те данные, которые были отправлены "+ОкончаниеВходящее);
					
				КонецЕсли;
				
			КонецЕсли;
			
			Элементы.ТаблицаОшибок.Видимость=ложь;
			
		КонецЕсли;
		
	Иначе
		Если КтоМы = "Поставщик" Тогда
			ОкончаниеИсходящее	= "в торговую сеть.";
			ОкончаниеВходящее	= "от торговой сети.";
		Иначе
			ОкончаниеИсходящее	= "поставщику.";
			ОкончаниеВходящее	= "от поставщика.";
		КонецЕсли;
		ВывестиПанельИнформации("В этом окне вы можете увидеть подробно те данные, которые были отправлены "+ОкончаниеВходящее,"Хорошо");
		Если НЕ ЗначениеЗаполнено(ЮрФизЛицо1С) Тогда
			
			ИмяПоля = НРег(СокрЛП(Элементы.ЮрФизЛицо1С.Заголовок));
			ИмяПоля = СтрЗаменить(ИмяПоля,"ент","ента"); 
			ИмяПоля = СтрЗаменить(ИмяПоля,"ция","цию"); 
			ИмяПоля = СтрЗаменить(ИмяПоля,"ель","еля"); 
			
			ВывестиПанельИнформации("Для загрузки сообщения выберите "+ИмяПоля+".");
			
		КонецЕсли;	
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура ВывестиСведенияОбОшибке(Стр)
	
	НоваяСтрока = ТаблицаОшибок.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока,Стр);
	
	НоваяСтрока.ТекстОшибки = Стр.СведенияОбОшибках;
	
	Если Стр.ИмяПоля = "GLN" Тогда
		
		НоваяСтрока.Действие = "Открыть форму настройки GLN";
		
	Иначе
		
		ИмяПоля = НРег(СокрЛП(Элементы.ЮрФизЛицо1С.Заголовок));
		ИмяПоля = СтрЗаменить(ИмяПоля,"ент","ента");
		ИмяПоля = СтрЗаменить(ИмяПоля,"ция","ции");
		ИмяПоля = СтрЗаменить(ИмяПоля,"ель","еля");
		
		НоваяСтрока.Действие = "Открыть форму "+ИмяПоля;
		
	КонецЕсли;
	
	НайденноеПоле = Элементы.Найти(Стр.ИмяПоля);
	
	Если НЕ НайденноеПоле = Неопределено Тогда
		
		НайденноеПоле.ЦветФона = WebЦвета.Лосось;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВывестиПанельИнформации(Текст,Вид = Неопределено)
	
	// Возможные виды:
	//	- Хорошо
	//	- Плохо
	//	- Нейтрально
	
	Если Вид = "Хорошо" Тогда
		
		Элементы.НадписьИнформация.ЦветТекста = WebЦвета.ЦианТемный;
		
	ИначеЕсли Вид = "Плохо" Тогда
		
		Элементы.НадписьИнформация.ЦветТекста = WebЦвета.Киноварь;
		
	Иначе	
		
		Элементы.НадписьИнформация.ЦветТекста = WebЦвета.ТемноОранжевый;
		
	КонецЕсли;
		
	Элементы.НадписьИнформация.Заголовок = СокрЛП(Текст);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключательАдресаПриИзменении(Элемент)
	
	Если ПереключательАдреса = "Российский" Тогда
		
		Элементы.АдресРоссийский.Видимость = Истина;
		Элементы.АдресИностранный.Видимость = Ложь;
		
	Иначе
		
		Элементы.АдресРоссийский.Видимость = Ложь;
		Элементы.АдресИностранный.Видимость = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидЮрФизЛицаПриИзменении(Элемент)
	
	УстановитьВидимостьПолейЮрФизЛица()
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновноеДействиеФормыВыполнить(Команда)
	
	Если Сообщение.Направление = "Входящее" Тогда
		
		Если НЕ ЗначениеЗаполнено(ЮрФизЛицо1С) Тогда
			
			ТекстПредупреждения = "Не указан " + НРег(ИмяПоляСообщения) + "!";
			МодульОбъектаКлиент().Предупреждение_КонтурEDI(ТекстПредупреждения);
			Возврат;
			
		КонецЕсли;
		
		СохранятьСоответствие = Истина;
		
		Если КтоМы = "Поставщик" 
			И СвойстваПоляСообщения.Тип1С = "ТочкаДоставкиСторонняя" Тогда
				
			ТочкаДоставки = ПолучитьТД();
			
			Если ЗначениеЗаполнено(ТочкаДоставки.ЮрФизЛицо) Тогда
				СохранятьСоответствие = Ложь;
			КонецЕсли;
			
			Если НЕ СокрЛП(ТочкаДоставки.GLN) = СокрЛП(GLN) Тогда
				
				ТекстПредупреждения = "GLN выбранной точки доставки (" + СокрЛП(ТочкаДоставки.GLN) + 
										") отличается от GLN, отправленного из торговой сети!";
				МодульОбъектаКлиент().Предупреждение_КонтурEDI(ТекстПредупреждения);
				Возврат;
				
			КонецЕсли;
				
		КонецЕсли;
		
		Сообщение[ИмяПоляСообщения+"1С"] = ЮрФизЛицо1С;
		
		Если СохранятьСоответствие Тогда
			
			РезультатСохранения = ВыполнитьСохранениеСоответствияСервер();
			
			Если НЕ РезультатСохранения.Успешно Тогда
				МодульОбъектаКлиент().Предупреждение_КонтурEDI(РезультатСохранения.ОписаниеОшибки);
				Возврат;
			КонецЕсли;

		КонецЕсли;
		
	Иначе
		
		Сообщение[ИмяПоляСообщения+"EDI"] = ЮрФизЛицо;
		
	КонецЕсли;
	
	АдресВВХИзмененногоСообщения = ЗагрузитьСообщениеВВХ();
	
	СтруктураВозврата = новый Структура;
	СтруктураВозврата.Вставить("Успешно",истина);
	СтруктураВозврата.Вставить("АдресВВХСообщения",АдресВВХИзмененногоСообщения);
	СтруктураВозврата.Вставить("ЮрФизЛицо",ЮрФизЛицо);
	СтруктураВозврата.Вставить("ЮрФизЛицо1С",ЮрФизЛицо1С);
	СтруктураВозврата.Вставить("ИмяПоля",ИмяПоляСообщения);
	                                                   
	ЭтаФорма.Закрыть(СтруктураВозврата);

КонецПроцедуры

&НаСервере
Функция ПолучитьТД()
	
	Возврат МодульОбъекта().ПолучитьЭлементСправочника("ТочкиДоставкиСторонние",ЮрФизЛицо1С);	

КонецФункции // ()

&НаСервере
Функция ВыполнитьСохранениеСоответствияСервер()
	
	РезультатСохранения = Новый Структура("Успешно,ОписаниеОшибки", Истина, "");
	
	Если СвойстваПоляСообщения.ВидСтруктурыEDI = Неопределено Тогда
		ТипEDI = СвойстваПоляСообщения.ТипEDI;
	Иначе
		ТипEDI = СвойстваПоляСообщения.ВидСтруктурыEDI;
	КонецЕсли;
	
	Если СвойстваПоляСообщения.Тип1С = "ЮрФизЛицоСтороннее"
		ИЛИ СвойстваПоляСообщения.Тип1С = "ТочкаДоставкиСторонняя" Тогда
		
		Если НЕ ЗначениеЗаполнено(Партнер) Тогда
			РезультатСохранения.Успешно = Ложь;
			РезультатСохранения.ОписаниеОшибки = "Партнер не определен, поэтому не удалось записать соответствие юр./физ. лица.";
		КонецЕсли;
		
		ТекущийПартнер = МодульОбъекта().НайтиПартнераПоЮрФизЛицу(ЮрФизЛицо1С);
		Если ЗначениеЗаполнено(ТекущийПартнер)
			И НЕ ТекущийПартнер = Партнер Тогда
			
			РезультатСохранения.Успешно = Ложь;
			РезультатСохранения.ОписаниеОшибки = 	"Юр./физ. лицо уже сопоставлено с партнером: """ + Строка(ТекущийПартнер) + """. 
			|Поэтому не удалось записать соответствие юр./физ. лица.";
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ РезультатСохранения.Успешно Тогда
		Возврат РезультатСохранения;
	КонецЕсли;
		
	МодульОбъекта().УстановитьСоответствиеПоляШапки(Сообщение[ИмяПоляСообщения+"1С"], Сообщение[ИмяПоляСообщения+"EDI"], СвойстваПоляСообщения.Тип1С, ТипEDI, Партнер);
	
	Возврат РезультатСохранения;
	
КонецФункции

&НаСервере
Функция ЗагрузитьСообщениеВВХ()
	
	АдресВХ = ПоместитьВоВременноеХранилище(Сообщение,Новый УникальныйИдентификатор);
	
	Возврат АдресВХ;	
	
КонецФункции 

&НаКлиенте
Процедура ЮрФизЛицо1СНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Сообщение.Направление = "Входящее" 
		И КтоМы = "Поставщик"
		И СвойстваПоляСообщения.Тип1С = "ТочкаДоставкиСторонняя" Тогда
		
		СтандартнаяОбработка = Ложь;
		МодульОбъектаКлиент().ВыбратьЭлементСправочника_КонтурEDI("ТочкаДоставкиСторонняя", Партнер, ЭтаФорма, "ОбработчикВыбораТочкиТоставкиСторонней", ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикВыбораТочкиТоставкиСторонней(ВыбранноеЗначение, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЮрФизЛицо1С = ВыбранноеЗначение;

КонецПроцедуры

&НаКлиенте
Процедура ЮрФизЛицо1СОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(ЮрФизЛицо1С) Тогда
		
		Если Сообщение.Направление = "Входящее"
			И КтоМы = "Поставщик" 
			И СвойстваПоляСообщения.Тип1С = "ТочкаДоставкиСторонняя" Тогда
					
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("СсылкаТочкиДоставки", ЮрФизЛицо1С);
			
			МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("ТочкиДоставкиСторонние_ЭлементУправляемая", , ПараметрыФормы, ЭтаФорма);
					
			Возврат;
			
		КонецЕсли;
		
		// открыть модально с обработчиком, который ниже
		
		МодульОбъектаКлиент().ОткрытьФормуЗначения_КонтурEDI(ЮрФизЛицо1С, "ОбработчикПросмотраЗначенияЮрФизЛица1С", ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СтроковоеИмяМетаданныхСсылки(Ссылка)
	
	Возврат Метаданные.НайтиПоТипу(ТипЗНЧ(Ссылка)).Имя;
	
КонецФункции

&НаКлиенте
Процедура ОбработчикПросмотраЗначенияЮрФизЛица1С(ОсновнойПараметр, ДопПараметры = Неопределено) Экспорт

	ОбновитьПоляЮрФизЛица();

КонецПроцедуры

&НаСервере
Процедура ОбновитьПоляЮрФизЛица()
	
	Если НЕ ЗначениеЗаполнено(ЮрФизЛицо1С) Тогда
		Возврат;
	КонецЕсли;
	
	ТекGLN = GLN;
	
	ЮрФизЛицо = МодульОбъекта().КонвертироватьЗначение1СвEDI(ЮрФизЛицо1С, СвойстваПоляСообщения.Тип1С, "ЮрФизЛицо", Сообщение);
	
	Если НЕ ЗначениеЗаполнено(ЮрФизЛицо.GLN) Тогда
		ЮрФизЛицо.GLN = ТекGLN;
	КонецЕсли;
	
	ЗаполнитьПоляФормы(ЮрФизЛицо);
	
	мПроверитьПоляЮрФизЛица();
	
КонецПроцедуры

&НаКлиенте
Процедура ЮрФизЛицо1СПриИзменении(Элемент)
	
	мПроверитьПоляЮрФизЛица();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОсновнаяФорма().ПриОткрытииФормы(ЭтаФорма, "ФормаКонтрагентаУправляемая");

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	ОсновнаяФорма().ПриЗакрытииФормы(ЭтаФорма, "ФормаКонтрагентаУправляемая");
	
КонецПроцедуры
