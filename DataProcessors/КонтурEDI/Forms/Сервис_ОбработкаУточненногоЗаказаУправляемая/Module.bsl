&НаСервере
Перем МодульОбъекта;

&НаКлиенте
Перем МодульОбъектаКлиент;

// СЛУЖЕБНЫЕ {

// Инициализация модуля и его экспортных функций
&НаСервере
Функция МодульОбъекта()
	
	Если МодульОбъекта = Неопределено Тогда
		
		Если Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект Тогда
			МодульОбъекта = МодульОбъектаИзКэша();
		КонецЕсли;
		
		Если МодульОбъекта = Неопределено Тогда
			МодульОбъекта = РеквизитФормыВЗначение("Объект");
			МодульОбъекта.ИнициализироватьПодключаемыеМодули(); // TODO Проверить что из этого используется на формах и перенести в ПараметрыКлиентСервер
			МодульОбъекта.ОбновитьКэшМодуляОбъекта();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат МодульОбъекта;
	
КонецФункции

&НаСервере
Функция МодульОбъектаИзКэша()
	
	Результат = Неопределено;
	
	Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда
		АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект;
 		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Структура = ПолучитьИзВременногоХранилища(АдресВХ);
			Если ТипЗнч(Структура) = Тип("Структура") и Структура.Свойство("ОбработкаОбъект") Тогда
			 	Результат = Структура.ОбработкаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульОбъектаКлиент()
	
	Если ТипЗнч(МодульОбъектаКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбъектаКлиент;
	КонецЕсли;
	
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		МодульОбъектаКлиент = ОсновнаяФорма.МодульОбъектаКлиент();
		Возврат МодульОбъектаКлиент;
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
	
	Если ЭтаФорма.ВладелецФормы = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ЭтаФорма.ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;
    	
КонецФункции

&НаКлиенте
Функция НоваяМетрика()
	
	Возврат ОсновнаяФорма().НоваяМетрика();
	
КонецФункции

// } СЛУЖЕБНЫЕ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
	Параметры.Свойство("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	
	НужноПеревыставитьЗаказ = Ложь;
    ЕстьДобавленныеСтроки = Ложь;

	Параметры.МодальностьЗапрещена=МодульОбъекта().МодальностьЗапрещена();
    Заказ = Параметры.Документ;
	ОтклонятьОтветыНаЗаказСДобавленнымТоваром = МодульОбъекта().ПолучитьКонстантуEDI("ОтклонятьОтветыНаЗаказСДобавленнымТоваром");


	ИсходящийЗаказ			= МодульОбъекта().ПрочитатьСообщение(,Параметры.Документ,"ORDERS","Исходящее");
	ВходящийОтветНаЗаказ	= МодульОбъекта().ПрочитатьСообщение(,Параметры.Документ,"ORDRSP","Входящее");
	МодульОбъекта().КонвертироватьТабличнуюЧастьСообщенияEDIв1С(ВходящийОтветНаЗаказ,	"Товары");
		
	СтруктураТаблицы1 = МодульОбъекта().ПолучитьСтруктуруТаблицыТоваров();
	СтруктураТаблицы1.Вид			= "Сообщение";
	СтруктураТаблицы1.ТипСообщения	= "ORDERS";
	СтруктураТаблицы1.Товары		= ИсходящийЗаказ.Товары;
	
	СтруктураТаблицы2 = МодульОбъекта().ПолучитьСтруктуруТаблицыТоваров();
	СтруктураТаблицы2.Вид			= "Сообщение";
	СтруктураТаблицы2.ТипСообщения	= "ORDRSP";
	СтруктураТаблицы2.Товары		= ВходящийОтветНаЗаказ.Товары;
	
	СравниваемыеПоля = Новый СписокЗначений;
	СравниваемыеПоля.Добавить("Количество");
	СравниваемыеПоля.Добавить("ЦенаБезНДС");
	СравниваемыеПоля.Добавить("ЦенаСНДС");
	
	РезультатСравнения = МодульОбъекта().СравнитьТаблицыТоваров(СтруктураТаблицы1,СтруктураТаблицы2,СравниваемыеПоля);
		
	Для Каждого Стр Из РезультатСравнения.ТаблицаСравнения Цикл
		
		НоваяСтрока = ТаблицаСравнения.Добавить();
		
		НоваяСтрока.Номенклатура			= Стр.Номенклатура;
		НоваяСтрока.КоличествоЗаказали		= Стр.Количество1;
		НоваяСтрока.КоличествоПодтвердили	= Стр.Количество2;
		НоваяСтрока.ЦенаБезНДСЗаказали		= Стр.ЦенаБезНДС1;
		НоваяСтрока.ЦенаБезНДСПодтвердили	= Стр.ЦенаБезНДС2;
		НоваяСтрока.ЦенаСНДСЗаказали		= Стр.ЦенаСНДС1;
		НоваяСтрока.ЦенаСНДСПодтвердили		= Стр.ЦенаСНДС2;
		
		НоваяСтрока.Количество = Стр.Количество2;
		
	КонецЦикла;
	
	//проверим добавление
	Для каждого Стр Из ТаблицаСравнения Цикл
		Если (Стр.КоличествоЗаказали = 0) ИЛИ (Стр.КоличествоПодтвердили > Стр.КоличествоЗаказали) Тогда
			ЕстьДобавленныеСтроки = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если ОтклонятьОтветыНаЗаказСДобавленнымТоваром = Истина И ЕстьДобавленныеСтроки Тогда
		Элементы.ФормаПринять.КнопкаПоУмолчанию = Ложь;
		Элементы.ФормаПринять.Доступность = Ложь;
		Элементы.ФормаОтклонить.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	
	//скрываем видимость надписей и таблицу сравнения полей шапки сообщений
    Если ТаблицаСравненияШапкаСообщения.Итог("ЕстьРасхождения") = 0 Тогда
		Элементы.ТаблицаСравненияШапкаСообщения.Видимость 	= Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Принять(Команда)
	
	ПриНажатииКнопки(Команда.Имя);
	
	Если НужноПеревыставитьЗаказ Тогда
		
		ПеревыставитьЗаказОтразивИзменения(Заказ)		
		
	Иначе
		
		ПеренестиНовоеКоличествоВДокумент(Заказ);
		УстановитьСтатусУточнен(Заказ);
		
	КонецЕсли;
	
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСтатусУточнен(Документ)

	МодульОбъекта().УстановитьСтатусДокумента(Документ,"ЗаказУточненПринят","Заказ");

КонецПроцедуры // УстановитьСтатусУточнен()

&НаСервере
Процедура ПеревыставитьЗаказОтразивИзменения(Заказ)
	
	НачатьТранзакцию();
	Попытка
		
		ПеренестиНовоеКоличествоВДокумент(Заказ);
		ПараметрыОтправки = Новый Структура();
		
		СтарыйИсходящийЗаказСсылка = МодульОбъекта().НайтиСообщениеДокумента(Параметры.Документ,"ORDERS","Исходящее");
		Если СтарыйИсходящийЗаказСсылка <> Неопределено Тогда
			СтарыйИсходящийЗаказ=СтарыйИсходящийЗаказСсылка.ПолучитьОбъект();	
			СтарыйИсходящийЗаказ.ТипСообщения = "#" + СтарыйИсходящийЗаказ.ТипСообщения;
			СтарыйИсходящийЗаказ.Записать();
		КонецЕсли;
		
		МодульОбъекта().ОтправитьСообщение("ORDERS",Заказ,ПараметрыОтправки);	
		
		//пометить # ordrsp (т.е. удалить)
		ПолученныйORDRSPОбъект=Параметры.СообщениеСсылка.ПолучитьОбъект();
		ПолученныйORDRSPОбъект.ТипСообщения = "#" + ПолученныйORDRSPОбъект.ТипСообщения;
		ПолученныйORDRSPОбъект.Записать();

	    Если ТранзакцияАктивна() Тогда
	        ЗафиксироватьТранзакцию();
		КонецЕсли;
		
	Исключение
		
	    Если ТранзакцияАктивна() Тогда
	        ОтменитьТранзакцию();
		КонецЕсли;
		
		ТекстОшибки = "Не удалось перевыставить заказ - " + ОписаниеОшибки();
		МодульОбъекта().ЗаписьЖурналаРегистрации_КонтурEDI(УровеньЖурналаРегистрации.Ошибка, ТекстОшибки);
		ВызватьИсключение ТекстОшибки;				
		
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ПеренестиНовоеКоличествоВДокумент(Документ) //перенесем из колонки "Количество"
	
	Если НЕ НеобходимоИзменитьДокумент=Истина тогда
		Возврат;
	КонецЕсли;
	
	ИмяКонфигурации1С=МодульОбъекта().ИмяКонфигурации1С;
	
	Если ИмяКонфигурации1С = "УФ_УТ" Тогда
		//обойдем строки таблички этой формы - для каждой найдем такую(ие) же в документе - и обработаем	
		ДокументОбъект   = Документ.ПолучитьОбъект();
		ТабЧастьЗаказ = ДокументОбъект.Товары;
		Для Каждого СтрокаСравнения Из ТаблицаСравнения Цикл
			Если Не (СтрокаСравнения.КоличествоЗаказали = СтрокаСравнения.КоличествоПодтвердили) Тогда
				
				ОтборСтрок= Новый СТруктура("Отменено, Номенклатура",Ложь,СтрокаСравнения.Номенклатура);
				//СтрокаТабЧастиЗаказа = ТабЧастьЗаказ.Найти(СтрокаСравнения.Номенклатура,"Номенклатура");
				СтрокиЗаказа=ТабЧастьЗаказ.НайтиСтроки(ОтборСтрок);
				
				//ищем такие строки и в случае уменшения количества - разбиваем строку и вычеркиваем, а в случае 
				//увеличения количества - изменяем количество упаковок
				ОбработатьСтрокиЗаказа_УТ11(СтрокиЗаказа,ТабЧастьЗаказ,СтрокаСравнения,Документ);
				
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли ИмяКонфигурации1С = "УФ_БП" Тогда
		
		ДокументОбъект   = Документ.ПолучитьОбъект();
		ТабЧастьЗаказ = ДокументОбъект.Товары;
		
		Для Каждого СтрокаСравнения Из ТаблицаСравнения Цикл
			Если Не (СтрокаСравнения.КоличествоЗаказали = СтрокаСравнения.КоличествоПодтвердили) Тогда
				
				ОтборСтрок= Новый СТруктура("Номенклатура",СтрокаСравнения.Номенклатура);
				//СтрокаТабЧастиЗаказа = ТабЧастьЗаказ.Найти(СтрокаСравнения.Номенклатура,"Номенклатура");
				СтрокиЗаказа=ТабЧастьЗаказ.НайтиСтроки(ОтборСтрок);
				
				//ищем такие строки и в случае уменшения количества - разбиваем строку и вычеркиваем, а в случае 
				//увеличения количества - изменяем количество упаковок
				ОбработатьСтрокиЗаказа_БП30(СтрокиЗаказа,ТабЧастьЗаказ,СтрокаСравнения,Документ);
				
			КонецЕсли;
		КонецЦикла;
		//удалить строки с 0 количеством
		КоличествоСтрок= ТабЧастьЗаказ.Количество();
		Для й=1 по КоличествоСтрок Цикл
			ТекСтр=ТабЧастьЗаказ[КоличествоСтрок-й];
			Если ТекСтр.Количество = 0 Тогда 
				ТабЧастьЗаказ.Удалить(КоличествоСтрок-й);
			КонецЕсли;
		КонецЦикла;
		
		//пересчитать итоги документа!
		Выполнить("ДокументОбъект.СуммаДокумента = УчетНДСПереопределяемый.ПолучитьСуммуДокументаСНДС(ДокументОбъект)");
		
	КонецЕсли;
	
	Попытка
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
	Исключение
		ДокументОбъект.Записать();
	КонецПопытки;
КонецПроцедуры

&НаСервере
Процедура ОбработатьСтрокиЗаказа_УТ11(СтрокиЗаказа,ТабЧастьЗаказ,СтрокаСравнения,ДокументЗаказа)
	
	ОсталосьРаспределить=СтрокаСравнения.Количество;
	ВсегоСтрок=СтрокиЗаказа.количество();
	сч=0;
	Для каждого СтрокаДокументаЗаказ Из СтрокиЗаказа Цикл
		сч=сч+1;
		Если ОсталосьРаспределить > СтрокаДокументаЗаказ.КоличествоУпаковок Тогда 
			Если сч=ВсегоСтрок Тогда  
				//решено заказать больше чем раньше и не осталось иных строк
				СтрокаДокументаЗаказ.КоличествоУпаковок=ОсталосьРаспределить;
				ПересчитатьКоличествоВСтроке(СтрокаДокументаЗаказ,ДокументЗаказа);
			Иначе//остались еще строки, а эта строка уже под завязку
				ОсталосьРаспределить=СтрокаСравнения.Количество - СтрокаДокументаЗаказ.КоличествоУпаковок;	
			КонецЕсли;
		ИначеЕсли ОсталосьРаспределить = СтрокаДокументаЗаказ.КоличествоУпаковок Тогда
			ОсталосьРаспределить=0;
			//видимо уже обработано
			//не будем ничего делать, остальные строки отменим
		ИначеЕсли ОсталосьРаспределить=0 Тогда
			ОтменитьСтроку(СтрокаДокументаЗаказ);
		Иначе //СтрокаСравнения.Количество < СтрокаДокументаЗаказ.КоличествоУпаковок и есть что распределять 
			//разобьем строку на 2 и отменим вторую
			НоваяСтрока=ТабЧастьЗаказ.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаДокументаЗаказ,,"Количество, КоличествоУпаковок");
			НоваяСтрока.КоличествоУпаковок=СтрокаДокументаЗаказ.КоличествоУпаковок-ОсталосьРаспределить;
			СтрокаДокументаЗаказ.КоличествоУпаковок=ОсталосьРаспределить;
			ОтменитьСтроку(НоваяСтрока);
			ПересчитатьКоличествоВСтроке(СтрокаДокументаЗаказ,ДокументЗаказа);
			ПересчитатьКоличествоВСтроке(НоваяСтрока,ДокументЗаказа);
			ОсталосьРаспределить=0;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьСтрокиЗаказа_БП30(СтрокиЗаказа,ТабЧастьЗаказ,СтрокаСравнения,ДокументЗаказа)
	
	ОсталосьРаспределить=СтрокаСравнения.Количество;
	ВсегоСтрок=СтрокиЗаказа.Количество();
	сч=0;
	Для каждого СтрокаДокументаЗаказ Из СтрокиЗаказа Цикл
		сч=сч+1;
		Если ОсталосьРаспределить > СтрокаДокументаЗаказ.Количество Тогда 
			Если сч=ВсегоСтрок Тогда  
				//решено заказать больше чем раньше и не осталось иных строк
				СтрокаДокументаЗаказ.Количество=ОсталосьРаспределить;
				ПересчитатьКоличествоВСтроке(СтрокаДокументаЗаказ,ДокументЗаказа);
			Иначе//остались еще строки, а эта строка уже под завязку
				ОсталосьРаспределить=СтрокаСравнения.Количество - СтрокаДокументаЗаказ.Количество;	
			КонецЕсли;
		ИначеЕсли ОсталосьРаспределить = СтрокаДокументаЗаказ.Количество Тогда
			ОсталосьРаспределить=0;
			//видимо уже обработано
			//не будем ничего делать, остальные строки отменим
		ИначеЕсли ОсталосьРаспределить=0 Тогда
			ОтменитьСтроку(СтрокаДокументаЗаказ);
		Иначе //СтрокаСравнения.Количество < СтрокаДокументаЗаказ.Количество и есть что распределять 
			//в этой строчке зануляем количество
			СтрокаДокументаЗаказ.Количество=ОсталосьРаспределить;
			ПересчитатьКоличествоВСтроке(СтрокаДокументаЗаказ,ДокументЗаказа);
			ОсталосьРаспределить=0;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОтменитьСтроку(СтрокаЗаказаПоставщику)
	Если МодульОбъекта().ИмяКонфигурации1С = "УФ_УТ" Тогда 
		СтрокаЗаказаПоставщику.Отменено = истина;
		СтрокаЗаказаПоставщику.ПричинаОтмены = МодульОбъекта().ПолучитьКонстантуEDI("ПричинаОтменыЗаказаПоставщикуПоУмолчанию");
	Иначе //для других конфигураций
		СтрокаЗаказаПоставщику.Количество=0;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПересчитатьКоличествоВСтроке(СтрокаЗаказаПоставщику,Документ)
	
	Если МодульОбъекта().ИмяКонфигурации1С = "УФ_УТ" Тогда
		
		Если Не ЗначениеЗаполнено(СтрокаЗаказаПоставщику.Упаковка) Тогда 
			СтрокаЗаказаПоставщику.Количество			= СтрокаЗаказаПоставщику.КоличествоУпаковок;//базовая единица
		иначе
			СтрокаЗаказаПоставщику.Количество			= СтрокаЗаказаПоставщику.КоличествоУпаковок*СтрокаЗаказаПоставщику.Упаковка.Коэффициент;//в упаковках
		КонецЕсли;
		флСуммаВключаетНДС=Документ.ЦенаВключаетНДС;
		Попытка
			Выполнить("СтрокаЗаказаПоставщику.СуммаНДС = Ценообразование.РассчитатьСуммуНДС(СтрокаЗаказаПоставщику.Сумма, СтрокаЗаказаПоставщику.СтавкаНДС, флСуммаВключаетНДС)");
		Исключение
			Выполнить("СтрокаЗаказаПоставщику.СуммаНДС = ЦенообразованиеКлиентСервер.РассчитатьСуммуНДС(СтрокаЗаказаПоставщику.Сумма, СтрокаЗаказаПоставщику.СтавкаНДС, флСуммаВключаетНДС)");
		КонецПопытки;
		Выполнить("Ценообразование.ПересчитатьСуммыВСтроке(СтрокаЗаказаПоставщику,ложь,ложь,ложь,флСуммаВключаетНДС)");
		
	ИначеЕсли  МодульОбъекта().ИмяКонфигурации1С = "УФ_Розница"  Тогда
		
		Если Не ЗначениеЗаполнено(СтрокаЗаказаПоставщику.Упаковка) Тогда 
			СтрокаЗаказаПоставщику.Количество			= СтрокаЗаказаПоставщику.КоличествоУпаковок;//базовая единица
		иначе
			СтрокаЗаказаПоставщику.Количество			= СтрокаЗаказаПоставщику.КоличествоУпаковок*СтрокаЗаказаПоставщику.Упаковка.Коэффициент;//в упаковках
		КонецЕсли;
		флСуммаВключаетНДС=Документ.ЦенаВключаетНДС;
		Выполнить("СтрокаЗаказаПоставщику.СуммаНДС = ОбработкаТабличнойЧастиТоварыСервер.РассчитатьСуммуНДС(СтрокаЗаказаПоставщику.Сумма, НоваяСтрока.СтавкаНДС, флСуммаВключаетНДС)");
		Выполнить("ОбработкаТабличнойЧастиТоварыСервер.ОбработатьСтрокуТЧСервер(СтрокаЗаказаПоставщику, Новый Структура(""ПересчитатьСумму""), Неопределено)");
		
	ИначеЕсли МодульОбъекта().ИмяКонфигурации1С = "УФ_БП" Тогда	
		МодульОбъекта().ПересчитатьСтрокуДокумента(СтрокаЗаказаПоставщику,Документ);
	иначе
		//пересчет для других конфигураций
	КонецЕсли;
	
КонецПроцедуры // ПересчитатьКоличествоУТ11()

&НаКлиенте
Процедура Отклонить(Команда)
	
	ТекстВопроса ="Заказ будет отменен целиком, продолжить?";
	КнопкиВопроса=новый СписокЗначений;
	КнопкиВопроса.Добавить("Отправить отмену заказа!");
	КнопкиВопроса.Добавить("Я еще подумаю");
	ДопПараметрДляПередачиВОбработчик=Неопределено;
	РезультатВопроса = Неопределено;
	
	Если Параметры.МодальностьЗапрещена Тогда
		Выполнить("ПоказатьВопрос(Новый ОписаниеОповещения(""ОбработчикСогласияПолнойОтменыЗаказа"", ЭтаФорма, ДопПараметрДляПередачиВОбработчик), ТекстВопроса, КнопкиВопроса,,,""Контур.EDI"")");
	Иначе
		РезультатВопроса = Вопрос(ТекстВопроса, КнопкиВопроса,,,"Контур.EDI");
		ОбработчикСогласияПолнойОтменыЗаказа(РезультатВопроса,ДопПараметрДляПередачиВОбработчик);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриНажатииКнопки(ИмяКоманды)
	
	ПараметрыСравнения = Новый Структура();
	ПараметрыСравнения.Вставить("Заказ",Заказ);
	ПараметрыСравнения.Вставить("ТаблицаСравнения",РеквизитФормыВЗначение("ТаблицаСравнения"));
	ПараметрыСравнения.Вставить("НеобходимоИзменитьДокумент",НеобходимоИзменитьДокумент);
	
	МодульОбъекта().ОбработкаСобытияПодключаемогоМодуля("ПриНажатииКнопки",, Новый Структура("ИмяФормы,ИмяКнопки,Параметры", "ФормаСервис_ОбработкаУточненногоЗаказа", ИмяКоманды, ПараметрыСравнения));

КонецПроцедуры

&НаКлиенте
Процедура ОбработчикСогласияПолнойОтменыЗаказа(РешениеПользователя,ДопПараметр=Неопределено) Экспорт
	
	Если РешениеПользователя="Отправить отмену заказа!" Тогда
		РезультатОтмены = ОтменитьЗаказСервер();
		ЭтаФорма.Закрыть();
	КонецЕсли;
	
КонецПроцедуры // ОбработчикСогласияПолнойОтменыЗаказа()

&НаСервере
Функция ОтменитьЗаказСервер()
	
	ПриНажатииКнопки(Элементы.ФормаОтклонить);
	
	//не хватает ссылки на исходное сообщение
	//пока протяем ее из НайтиСообщениеДокумента
	СуществующееСообщение = МодульОбъекта().НайтиСообщениеДокумента(Заказ, "ORDERS", "Исходящее");
	
	ПараметрыПодготовкиСообщения = Новый Структура();
	ПараметрыПодготовкиСообщения.Вставить("Статус", "Отменить");
	ПараметрыПодготовкиСообщения.Вставить("ПереотправляемоеСообщениеСсылка", СуществующееСообщение);
	Сообщение = МодульОбъекта().ПодготовитьИсходящееСообщение("ORDERS", Заказ, ПараметрыПодготовкиСообщения);
	
	ПараметрыОтправки = Новый Структура("ОтправитьСообщениеИзФормы,Сообщение", Истина, Сообщение);
	
	МодульОбъекта().ОтправитьСообщение("ORDERS", Заказ, ПараметрыОтправки);
	
	//а что в этот момент происходит с ORDRSP от поставщика? По идее его надо аннулировать.
	//ВходящийORDRSPСсылка	= МодульОбъекта().НайтиСообщениеДокумента(Заказ,"ORDRSP","Входящее");
	ОбъектСообщенияORDRSP	= МодульОбъекта().ПолучитьОбъектСообщения(Параметры.СообщениеСсылка);
	ОбъектСообщенияORDRSP.ТипСообщения = "#ORDRSP";
	ОбъектСообщенияORDRSP.Архив = истина;
	МодульОбъекта().СохранитьОбъектСообщения(ОбъектСообщенияORDRSP);//или флаг "Отклонен"?
	
	Возврат Истина;
	
КонецФункции // ОтменитьЗаказСервер()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(Параметры.ПараметрыАвтотестирования) Тогда
		ПодключитьОбработчикОжидания("ЗапуститьАвтотесты",0.1,Истина);
	КонецЕсли;
	
	ОсновнаяФорма().ПриОткрытииФормы(ЭтаФорма, "Сервис_ОбработкаУточненногоЗаказаУправляемая");
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСравненияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСравненияПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСравненияКоличествоПриИзменении(Элемент)
	
	НужноПеревыставитьЗаказ = Ложь;
	
	Для каждого Стр ИЗ ТаблицаСравнения Цикл
		Если НЕ Стр.Количество = Стр.КоличествоПодтвердили Тогда
			
			НужноПеревыставитьЗаказ = Истина;
			Прервать;
			
		КонецЕсли;
	КонецЦикла;
	
	КнопкаПринять = Элементы.ФормаПринять;
	
	Если НужноПеревыставитьЗаказ Тогда
		КнопкаПринять.Заголовок = "Перевыставить заказ";
	Иначе
		КнопкаПринять.Заголовок = "Принять уточнение заказа";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	ОсновнаяФорма().ПриЗакрытииФормы(ЭтаФорма, "Сервис_ОбработкаУточненногоЗаказаУправляемая");	
	
КонецПроцедуры
