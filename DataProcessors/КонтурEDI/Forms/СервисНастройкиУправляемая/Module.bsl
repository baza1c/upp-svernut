
&НаСервере
Перем МодульОбъекта;

&НаКлиенте
Перем МодульОбъектаКлиент;

&НаСервере
Перем ВнешнийМодуль;

&НаКлиенте
Перем МодульОбменКлиент;
      
&НаСервере
Перем ПодключаемыйМодульТип;

// СЛУЖЕБНЫЕ {

// Инициализация модуля и его экспортных функций
&НаСервере
Функция МодульОбъекта()
	
	Если МодульОбъекта = Неопределено Тогда
		
		Если Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект Тогда
			МодульОбъекта = МодульОбъектаИзКэша();
		КонецЕсли;
		
		Если МодульОбъекта = Неопределено Тогда
			МодульОбъекта = РеквизитФормыВЗначение("Объект");
			МодульОбъекта.ИнициализироватьПодключаемыеМодули(); // TODO Проверить что из этого используется на формах и перенести в ПараметрыКлиентСервер
			МодульОбъекта.ОбновитьКэшМодуляОбъекта();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат МодульОбъекта;
	
КонецФункции

&НаСервере
Функция МодульОбъектаИзКэша()
	
	Результат = Неопределено;
	
	Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда
		АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект;
 		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Структура = ПолучитьИзВременногоХранилища(АдресВХ);
			Если ТипЗнч(Структура) = Тип("Структура") и Структура.Свойство("ОбработкаОбъект") Тогда
			 	Результат = Структура.ОбработкаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульОбъектаКлиент()
	
	Если ТипЗнч(МодульОбъектаКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбъектаКлиент;
	КонецЕсли;
	
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		МодульОбъектаКлиент = ОсновнаяФорма.МодульОбъектаКлиент();
		Возврат МодульОбъектаКлиент;
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
	
	Если ЭтаФорма.ВладелецФормы = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ЭтаФорма.ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;
    	
КонецФункции

&НаКлиенте
Функция НоваяМетрика()
	
	Возврат ОсновнаяФорма().НоваяМетрика();
	
КонецФункции

// инициализация клиентского модуля работы с Диадок
&НаКлиенте
Функция МодульОбменКлиент()
	
	Если ТипЗнч(МодульОбменКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбменКлиент;
	КонецЕсли;
	
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		МодульОбменКлиент = ОсновнаяФорма.МодульОбменКлиент();
		Возврат МодульОбменКлиент;
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

// } СЛУЖЕБНЫЕ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
	Параметры.Свойство("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	
	Параметры.МодальностьЗапрещена=МодульОбъекта().МодальностьЗапрещена();
	ПутьКФормам = МодульОбъекта().Метаданные().ПолноеИмя() + ".Форма.";
	
	Если Параметры.ФормаОткрытаСтартовымПомощником Тогда 
		//обязательная запись дефолтных настроек - пока что уберем кнопку отмены 
		Элементы.ФормаЗакрыть.Доступность = Ложь;
	КонецЕсли;
	
	// Автотесты
	Если ЗначениеЗаполнено(Параметры.ПараметрыАвтотестирования) Тогда

		СтруктураПараметров = ПолучитьИзВременногоХранилища(Параметры.ПараметрыАвтотестирования);
		НастройкиМодуля = СтруктураПараметров.Настройки.НастройкиМодуля;

		Если ЗначениеЗаполнено(НастройкиМодуля) Тогда
			Для Каждого Стр ИЗ НастройкиМодуля Цикл
				Если Стр.Настройка = "СоздаваемыйДокументСклад" Тогда
					ТекЗначение = Справочники.Склады.НайтиПоНаименованию(Стр.Значение,Истина);
				Иначе
					ТекЗначение = Стр.Значение;
				КонецЕсли;
				МодульОбъекта().УстановитьКонстантуEDI(Стр.Настройка, ТекЗначение);
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	// TODO пока немного оптимизируем,  вообще разобрать это добро
	// большая часть всего этого уже инициализируется
	СписокСвойствEDI = 
	"Сервер
	|Порт
	|ЛогинПрокси
	|ПарольПрокси
	|ЛогинFTP
	|ПарольFTP
	|ПассивныйРежим
	|ИспользуетсяПрокси
	|ТипПрокси
	|СерверПрокси
	|ПортПрокси
	|ЛогинСлужебныйДиадок
	|ПарольСлужебныйДиадок
	|ПротоколОбмена_КонтурEDI
	|ИспользоватьDialMail
	|ЗагрузкаИзКаталогов
	|АдресКаталогаОбменаEDI
	|МыПоставщик
	|МыТорговаяСеть
	|ГрузополучательИзЮрФизЛицаТД
	|СоздаваемыйДокументСклад
	|ОтклонятьОтветыНаЗаказСДобавленнымТоваром
	|НеПоказыватьПровестиЗаказ
	|НеПоказыватьСоздатьРеализацию
	|ОткрыватьФормуОбратногоЗаказаПередЗаписью
	|ПроводитьЗагруженныеОбратныеЗаказы
	|ОткрыватьФормуОбратногоЗаказаПередЗаписью
	|ПодключаемыйМодульТип
	|ПодключаемыйМодульПуть
	|ПодключаемыйМодульИмяОбработки
	|ПодключаемыйМодульИспользоватьОтладку
	|ПодключаемыйМодульСобытиеОтладки
	|ПодключаемыйМодульСообщатьОСобытиях
	|ШаблонЦепочкиДокументов_Поставщик
	|ШаблонЦепочкиДокументов_Покупатель";

	СтруктураСвойствEDI = МодульОбъекта().ПолучитьСписокСвойствEDI(СписокСвойствEDI);

	Сервер				= СтруктураСвойствEDI.Сервер;
	Порт				= СтруктураСвойствEDI.Порт;
	ЛогинПрокси			= СтруктураСвойствEDI.ЛогинПрокси;
	ПарольПрокси		= СтруктураСвойствEDI.ПарольПрокси;
	ЛогинFTP			= СтруктураСвойствEDI.ЛогинFTP;
	ПарольFTP			= СтруктураСвойствEDI.ПарольFTP;
	ПассивныйРежим	   	= СтруктураСвойствEDI.ПассивныйРежим;
	ИспользуетсяПрокси	= СтруктураСвойствEDI.ИспользуетсяПрокси;
	ТипПрокси 			= СтруктураСвойствEDI.ТипПрокси;
	
	СерверПрокси			= СтруктураСвойствEDI.СерверПрокси;
	ПортПрокси				= СтруктураСвойствEDI.ПортПрокси;
	
	ЛогинСлужебныйДиадок	= СтруктураСвойствEDI.ЛогинСлужебныйДиадок;
	ПарольСлужебныйДиадок	= СтруктураСвойствEDI.ПарольСлужебныйДиадок;
	ПереключательFTP 		= СтруктураСвойствEDI.ПротоколОбмена_КонтурEDI;
	АдресКаталогаОбменаEDI	= СтруктураСвойствEDI.АдресКаталогаОбменаEDI;

	МыПоставщик				= СтруктураСвойствEDI.МыПоставщик;
	МыТорговаяСеть			= СтруктураСвойствEDI.МыТорговаяСеть;
	
	ГрузополучательИзЮрФизЛицаТД	= ?(СтруктураСвойствEDI.ГрузополучательИзЮрФизЛицаТД<>ложь,"Грузополучатель","Контрагент");
	
	СписокВозможныхДокументов = МодульОбъекта().ПолучитьВозможныеТипыСоздаваемыхДокументовПоВходящимЗаказам();
	
	НеПоказыватьПровестиЗаказ			= СтруктураСвойствEDI.НеПоказыватьПровестиЗаказ;
	НеПоказыватьСоздатьРеализацию		= СтруктураСвойствEDI.НеПоказыватьСоздатьРеализацию;
	
	СоздаваемыйДокументРСчет			= МодульОбъекта().ПолучитьСсылкуНаРСчет();
	СоздаваемыйДокументСклад			= СтруктураСвойствEDI.СоздаваемыйДокументСклад;
	
	ПодключаемыйМодульТип			= СтруктураСвойствEDI.ПодключаемыйМодульТип;
	ПодключаемыйМодульПуть			= СтруктураСвойствEDI.ПодключаемыйМодульПуть;
	ПодключаемыйМодульИмяОбработки  = СтруктураСвойствEDI.ПодключаемыйМодульИмяОбработки;
	
	Если ПодключаемыйМодульТип = "1С" Тогда 
		ПодключаемыйМодульСсылка		= МодульОбъекта().ПолучитьСсылкуНаПодключаемыйМодуль();
	КонецЕсли;
	
	СписокОбработокКонфигурации = Новый СписокЗначений;
	Для Каждого Обработка Из Метаданные.Обработки Цикл
		СписокОбработокКонфигурации.Добавить(Обработка.Имя);
	КонецЦикла;
	
	Для Каждого ОбработкаКонфигурации Из СписокОбработокКонфигурации Цикл
        Элементы.ПодключаемыйМодульИмяОбработки.СписокВыбора.Добавить(ОбработкаКонфигурации.Значение, ОбработкаКонфигурации.Значение,,БиблиотекаКартинок.Обработка) ;
    КонецЦикла; 
	
	Если ЗначениеЗаполнено(ПодключаемыйМодульТип) И НЕ ПодключаемыйМодульТип = "Нет" Тогда
		ИспользоватьПодключаемыйМодуль = Истина;
		МестонахождениеПодключаемогоМодуля = ПодключаемыйМодульТип;
	Иначе
		МестонахождениеПодключаемогоМодуля = "Диск";
	КонецЕсли;
	
	Если Порт = 0 Тогда
		Порт = 21;
	КонецЕсли;

	УстановитьТипПоля1С("СоздаваемыйДокументСклад","ТочкаДоставкиСвоя");
	
	Если НЕ МодульОбъекта().ИмяКонфигурации1С = "Розница" И НЕ МодульОбъекта().ИмяКонфигурации1С =  "УФ_Розница" Тогда
		УстановитьТипПоля1С("СоздаваемыйДокументРСчет","РасчетныйСчет");
	КонецЕсли;	
	
    МассивНастроек = МодульОбъекта().ПолучитьМассивДополнительныхНастроекИзМакета("Общие");
	СтруктураНастроек = МодульОбъекта().ПолучитьСписокСвойствEDI(МассивНастроек);
	
	ДеревоОбъект = РеквизитФормыВЗначение("ДеревоНастроек");
	МодульОбъекта().СоздатьДеревоНастроек(ДеревоОбъект,"Общие",СтруктураНастроек);
	ЗначениеВРеквизитФормы(ДеревоОбъект,"ДеревоНастроек");

	Если МыПоставщик=Истина Тогда
		ИмяШаблонаПоставщик = СтруктураСвойствEDI.ШаблонЦепочкиДокументов_Поставщик;
		ЗаполнитьДеревоНастроекДокументовСообщений("Поставщик");
	Иначе
		Элементы.ГруппаШаблоны_Поставщик.Видимость = Ложь;
	КонецЕсли;	
	
	Если МыТорговаяСеть=Истина Тогда
		ИмяШаблонаПокупатель = СтруктураСвойствEDI.ШаблонЦепочкиДокументов_Покупатель;
		ЗаполнитьДеревоНастроекДокументовСообщений("Покупатель");
	Иначе
		Элементы.ГруппаШаблоны_Покупатель.Видимость = Ложь;
	КонецЕсли;
	
	//Построить сертификаты по порганизациям
	ЗаполнитьНазначенныеСертификаты();
	
	//расширения
	ЕстьМетаданныеХраненияРасширений = МодульОбъекта().ПроверитьНаличиеМетаданныхХраненияРасширений();
	Если ЕстьМетаданныеХраненияРасширений Тогда
		ЗаполнитьИнформациюОРасширениях();
	Иначе
		ЗаблокироватьПанельРасширений();
	КонецЕсли;
	
КонецПроцедуры

//расширения ----------------------------------------------------

//заполнение данных
&НаСервере
Процедура ЗаполнитьИнформациюОРасширениях()
	
	ТаблицаДанныхОбУстановленныхРасширениях = МодульОбъекта().ПолучитьДанныеОбУстановленныхРасширениях();
	
	СписокРасширения.Очистить();
	
	Для Каждого СтрокаТаблицы Из ТаблицаДанныхОбУстановленныхРасширениях Цикл
		НоваяСтрокаСписка = СписокРасширения.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаСписка, СтрокаТаблицы);
		
		Если НоваяСтрокаСписка.Использование = Истина Тогда
			НоваяСтрокаСписка.Иконка=1;
		Иначе
			НоваяСтрокаСписка.Иконка=0;
		КонецЕсли;
		
		
		Если НЕ НоваяСтрокаСписка.Обновить И НоваяСтрокаСписка.Типовое и НЕ НоваяСтрокаСписка.Измененное Тогда
			НоваяСтрокаСписка.Картинка = 2;
		ИначеЕсли НЕ НоваяСтрокаСписка.Обновить И НоваяСтрокаСписка.Типовое и НоваяСтрокаСписка.Измененное Тогда
			НоваяСтрокаСписка.Картинка = 0;
		ИначеЕсли НЕ НоваяСтрокаСписка.Типовое Тогда
			НоваяСтрокаСписка.Картинка = 1;
		ИначеЕсли НоваяСтрокаСписка.Обновить Тогда
			НоваяСтрокаСписка.Картинка = 3;
		КонецЕсли;

	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаблокироватьПанельРасширений()
	
	Элементы.СписокРасширения.Видимость 						= Ложь;
	Элементы.ПодсказкаРасширения.Видимость 						= Ложь;
	
	Элементы.НадписьРасширенияНедоступны.Видимость 				= Истина;
	Элементы.НадписьРасширенияНедоступныПодсказка.Видимость 	= Истина;
	
	Элементы.НадписьРасширенияНедоступны.Заголовок 				= "Для работы расширений обновите объекты Контур.EDI.";
	Элементы.НадписьРасширенияНедоступныПодсказка.Заголовок 	= "Сохранить файл обновления на диск";
	
	//Если МыПоставщик = Ложь Тогда
	//	Элементы.НадписьРасширенияНедоступны.Заголовок 			= "Работа с расширениями недоступна.";
	//	Элементы.НадписьРасширенияНедоступныПодсказка.Заголовок = "Данная версия модуля не поддерживает работу с расширениями
	//															  |в режиме работы ""Торговая сеть"".";
	//КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоНастроекДокументовСообщений(РежимРаботы,Очищать = Ложь)
	
	_ДеревоНастроекДокументовСообщений = РеквизитФормыВЗначение("ДеревоНастроекДокументовСообщений");
	
	Если Очищать Тогда
		СуществующаяСтрока = _ДеревоНастроекДокументовСообщений.Строки.Найти(РежимРаботы,"НастройкаНаименование");
		Если СуществующаяСтрока<>Неопределено Тогда
			_ДеревоНастроекДокументовСообщений.Строки.Удалить(СуществующаяСтрока);
		КонецЕсли;	
	КонецЕсли;	
	
	МодульОбъекта().ЗаполнитьДеревоНастроекДокументовСообщений(_ДеревоНастроекДокументовСообщений,РежимРаботы);

	ЗначениеВРеквизитФормы(_ДеревоНастроекДокументовСообщений,"ДеревоНастроекДокументовСообщений");
	
КонецПроцедуры	

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеДоступностьюПолейНастройкиПодключаемогоМодуля();
	УстановитьВидимостьФТПКаталог();
	УстановитьВидимостьЭлементовПрокси();

	//развернуть дерево прочих настроек
	КоллекцияЭлементовДерева=ДеревоНастроек.ПолучитьЭлементы();
    Для Каждого Строка Из КоллекцияЭлементовДерева Цикл    
        ИдентификаторСтроки = Строка.ПолучитьИдентификатор();
        Элементы.ДеревоНастроек.Развернуть(ИдентификаторСтроки,ИСТИНА);
	КонецЦикла;

	//и второе дерево тоже развернем
	КоллекцияЭлементовДерева=ДеревоНастроекДокументовСообщений.ПолучитьЭлементы();
    Для Каждого Строка Из КоллекцияЭлементовДерева Цикл    
        ИдентификаторСтроки = Строка.ПолучитьИдентификатор();
        Элементы.ДеревоНастроекДокументовСообщений.Развернуть(ИдентификаторСтроки,ИСТИНА);
	КонецЦикла;
	
	Если МодульОбъектаКлиент().ПеременнаяСодержитСвойство(Элементы.ПодсказкаРасширения, "АвтоМаксимальнаяШирина") Тогда	
		Элементы.ПодсказкаРасширения.АвтоМаксимальнаяШирина = Ложь;
	КонецЕсли;
	Если МодульОбъектаКлиент().ПеременнаяСодержитСвойство(Элементы.ПодсказкаНастроек, "АвтоМаксимальнаяШирина") Тогда	
		Элементы.ПодсказкаНастроек.АвтоМаксимальнаяШирина = Ложь;
	КонецЕсли;

	Если ЗначениеЗаполнено(Параметры.ПараметрыАвтотестирования) Тогда
		ПодключитьОбработчикОжидания("ЗаписатьНастройкиАвтотестирования",0.1,Истина);
	КонецЕсли;
	
	Если Параметры.ФормаОткрытаСтартовымПомощником Тогда 
		Состояние("Записываю настройки по умолчанию");
		СтруктураНастроек = СобратьСтруктуруНастроек();
		ЗаписатьНастройкиСервер(СтруктураНастроек,Отказ);
	КонецЕсли;
	
	ОсновнаяФорма().ПриОткрытииФормы(ЭтаФорма, "СервисНастройкиУправляемая");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьНастройкиАвтотестирования()
	
	ЗаписатьНастройки("");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТипПоля1С(ИмяНаФорме,Тип1С)

	ПолеФормы	= Элементы[ИмяНаФорме];
	ТипПоля		= МодульОбъекта().ПолучитьТипЗначенияОбъекта(Тип1С);
			
	Если ТипПоля = Неопределено Тогда
		
		Сообщить("Не задан тип объекта 1С для поля с типом "+Тип1С);
		
	Иначе	
		
		Элементы[ИмяНаФорме].ОграничениеТипа = Новый ОписаниеТипов(ТипПоля);
		
		Если НЕ ЗначениеЗаполнено(ЭтаФорма[ИмяНаФорме]) Тогда
			
			ЭтаФорма[ИмяНаФорме] = МодульОбъекта().ПолучитьПустуюСсылкуОбъекта(Тип1С);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьFTP(Команда)
	
	Если НЕ ЗначениеЗаполнено(Сервер) Тогда	
		Предупреждение("Не указан FTP сервер!");
		Элементы.КартинкаПроверкиПодключения1.Видимость	= Истина;
		Элементы.КартинкаПроверкиПодключения2.Видимость	= Ложь;
		Возврат;
	КонецЕсли;	
	
	Если мПодключитьсяКFTP() Тогда
		
		Предупреждение("Проверка подключения прошла успешно!");
		
		Элементы.КартинкаПроверкиПодключения1.Видимость	= Ложь;
		Элементы.КартинкаПроверкиПодключения2.Видимость	= Истина;
		
	Иначе
		
		Предупреждение("Не удалось подключиться к EDI с вашим логином и паролем!
		|Возможные причины:
		| - проверьте еще раз свой логин и пароль
		| - возможно с !СЕРВЕРА 1С! (или вашего компьютера для режима ""толстый клиент"") нет прямого доступа к нашему серверу
		|
		|Обратитесь к вашему системному администраторы для проверки настроек подключения.");
		
		Элементы.КартинкаПроверкиПодключения1.Видимость	= Истина;
		Элементы.КартинкаПроверкиПодключения2.Видимость	= Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция мПодключитьсяКFTP() //проверяем соединение именно со стороны сервера
	
	//взять пароль от учетки этого пользователя         !
	УчетныеЗаписи = МодульОбъекта().ПолучитьСписокЭлементовСправочника("УчетныеЗаписи");
	ЕстьАктивнаяУЗ=Ложь;
	Если УчетныеЗаписи.Количество() = 0 Тогда 
		возврат Ложь;
	иначе
		Для Каждого СтрокаУчетнойЗаписи из УчетныеЗаписи Цикл 
			Если СтрокаУчетнойЗаписи.НеАктивна=Ложь Тогда 
				ЕстьАктивнаяУЗ=Истина;
			Если ПереключательFTP = "DialMail" Тогда  
				//тупиковый путь развития				
			Иначе
				
				Таймаут = 15;
				Попытка
					ФТПСоединение = Новый FTPСоединение(СокрЛП(Сервер), Порт, СокрЛП(СтрокаУчетнойЗаписи.Логин), СокрЛП(СтрокаУчетнойЗаписи.Пароль), , ПассивныйРежим, Таймаут);
				Исключение
					Возврат Ложь;
				КонецПопытки;
				
			КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Если ЕстьАктивнаяУЗ тогда 
			Возврат Истина;   //все ок вроде
		иначе
			возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	//оставляем на случай, если FTPСоединение научится корректно работать с прокси
	//Если ИспользуетсяПрокси Тогда
	//	ПроксиСервер = Новый ИнтернетПрокси();
	//	ПроксиСервер.НеИспользоватьПроксиДляЛокальныхАдресов = Истина;
	//	ПроксиСервер.Пользователь = ЛогинПрокси;
	//	ПроксиСервер.Пароль = ПарольПрокси;
	//	ПроксиСервер.Установить("ftp",СерверПрокси,ПортПрокси);  
	//	Попытка
	//		ФТПСоединение = Новый FTPСоединение(СокрЛП(Сервер), Порт, СокрЛП(ЛогинFTP), СокрЛП(ПарольFTP), ПроксиСервер, ПассивныйРежим);
	//	Исключение
	//		Возврат Ложь;
	//	КонецПопытки;
	//Иначе
	//КонецЕсли;
	
	
КонецФункции

&НаКлиенте
Процедура СправкаНажатие(Команда)
	
	Элемент = Этаформа.ТекущийЭлемент;
	
	Если Элемент=Неопределено Тогда возврат; КонецЕсли;
	
		Текст = "";
	
	// Заказы покупателей
	
	Если Элемент.Имя = "СправкаНажатиеСоздаваемыйДокументИзORDERS" Тогда
		
		Текст  = "На основании входящего заказа будет создаваться документ выбранного типа.";

	ИначеЕсли Элемент.Имя = "СправкаНажатиеЗначенияПоУмолчанию" или Элемент.Имя = "СправкаНажатиеЗначенияПоУмолчанию1" Тогда
		
		Текст  = "Данные значения будут подставляться автоматически во все создаваемые документы.";
		
	ИначеЕсли Элемент.Имя = "СправкаНажатиеГрузополучательИзЮрФизЛицаТД" Тогда
		
		Текст = "Если каждая точка доставки торговой сети заведена как отдельный контрагент,
		|то можно указать какое поле в заказе будет заполняться: Контрагент или Грузополучатель.
		|
		|Соответствие точки доставки и котрагента настраивается в карточке торговой сети.";
		
	ИначеЕсли Элемент.Имя = "СправкаНажатиеНеПоказыватьФормуВходящегоЗаказаПриЗагрузке" Тогда
		
		Текст = "!Настройка перенесена в настройки партнера! Заказ, у которого проставлены все соответствия, будет создан в 1С автоматически.";
		
	ИначеЕсли Элемент.Имя = "СправкаНажатиеПроводитьЗагруженныеЗаказы" Тогда
		
		Текст  = "!Настройка перенесена в настройки партнера! Созданный заказ 1С будет проведен автоматически.
		|
		|При одновременном использовании с настройкой выше заказы будут создаваться и проводиться полностью автоматически.";
		
	ИначеЕсли Элемент.Имя = "СправкаНажатиеНеПоказыватьСоздатьРеализацию" Тогда
		
		Текст  = "После завершения обработки входящего заказа кнопка для создания документа реализации не будет отображаться.";
		
	ИначеЕсли Элемент.Имя = "СправкаНажатиеНеПоказыватьПровестиЗаказ" Тогда
		
		Текст  = "Станет возможно подтверждать непроведенные заказы.";
	
	// Реализации товаров			
				
	ИначеЕсли Элемент.Имя = "СправкаНажатиеНеПоказыватьНакладныеБезRECADV" Тогда
		
		Текст  = "Документы реализации товаров не будут отображаться в общем списке, 
		|пока не будут получены соответствующие уведомления о приемке (RECADV).
		|
		|Актуально при работе по короткой цепочке RECADV+INVOIC";
		
	//подключаемый модуль	
	
	ИначеЕсли Элемент.Имя = "СправкаНажатиеИспользоватьПодключаемыйМодуль" Тогда
		
		Текст  = "Подключаемый модуль - это внешняя обработка, в которой вы можете описать особенности 
		|программной логики своей конфигурации и ""подключить"" ее к нашей обработке.
		|
		|Это упростит процесс обновления модуля Контур.EDI на новые версии.";
		
	// Прочие настройки	
		
		
	ИначеЕсли Элемент.Имя = "КартинкаСправкаЗагружатьСообщенияПоТД" Тогда
		
		Текст = "Входящие сообщения будут загружаться только для заранее выбранных точек доставки.
		|
		|Для их выбора необходимо зайти в карточку каждой точки доставки и отметить настройку ""Загружать сообщения от данной точки доставки"".";
		
	ИначеЕсли Элемент.Имя = "КартинкаСправкаЗагружатьСообщенияПоНастроеннымСетям" Тогда
		
		Текст = "Входящие сообщения будут загружаться только для настроенных сетей.
		|
		|Для их выбора необходимо добавить сеть в список сетей, заполнить GLN и установить галочку ""Торговая сеть""";
		
	ИначеЕсли Элемент.Имя = "КартинкаСправкаСвойСпраовчникТД" Тогда
		
		Текст = "Выбор собственного справочника точек доставки для сопоставления с точками торговой сети.
		|
		|Соотвествие точки доставки и элемента справочника настраивается в карточке точки доставки.";
		
	ИначеЕсли Элемент.Имя = "КартинкаСправкаНеСоздаватьЗаказыБезТД" Тогда
		
		Текст = "Заказы не будут создаваться для тех точек доставки, которым не сопоставлен элемент из справочника собственных точек.";
		
	ИначеЕсли Элемент.Имя = "КартинкаСправкаНеСоздаватьЗаказыБезЮФЛицаТД" Тогда
		
		Текст = "Заказы не будут создаваться для тех точек доставки, в которых не выбрано юр./физ. лицо.";
		
	ИначеЕсли Элемент.Имя = "КартинкаСправкаРеквизитыПартнераЗаполняютсяНаСервереПоGLN" Тогда
		
		Текст = "В исходящих сообщениях все реквизиты ваших партнеров (ИНН, адреса и т.д) будут подставляться на сервере, а не браться из данных 1С.";
		
	ИначеЕсли Элемент.Имя = "КартинкаСправкаРеквизитыКомпанииЗаполняютсяНаСервереПоGLN" Тогда
		
		Текст = "В исходящих сообщениях все реквизиты вашей компании (ИНН, адреса и т.д) будут подставляться на сервере, а не браться из данных 1С.";
		
	ИначеЕсли Элемент.Имя = "КартинкаСправкаОбновлениеДанныхВСписке" Тогда
		
		Текст = "При работе с документом его статус и требуемое действие меняются.
		|
		|Обновление всего списка позволит видеть сразу все изменения в документах. Полезно при многопользовательской работе.
		|Обновление только строки документа, который обрабатывается, повысит скорость работы модуля.";
		
	ИначеЕсли Элемент.Имя = "КартинкаСправкаОткрыватьЗаказПередСозданием" Тогда
		
		Текст  = "Перед созданием документа по входящему заказу будет открыта 
		|форма незаписанного документа для редактирования.";
		
	ИначеЕсли Элемент.Имя = "КартинкаСправкаНеПоказыватьПровестиЗаказ" Тогда
		
		Текст  = "Станет возможно подтверждать непроведенные заказы.";
		
	ИначеЕсли Элемент.Имя = "КартинкаСправкаОграничениеДнейВыводаДокументовВСписке" Тогда
		
		Текст = "Если указать параметр, то во всех списках документов будет автоматически устанавливаться отбор по датам.
		|Если этот параметр не указан, то отборы устанавливаться не будут.";
		
	КонецЕсли;
	
	СообщитьПользователю_ФормаСервисНастройки(Текст);
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеДоступностьюПолейНастройкиПодключаемогоМодуля()
	
	Элементы.ПодключаемыйМодульРасположение.Видимость 	= ИспользоватьПодключаемыйМодуль;
	Элементы.ПодключаемыйМодульПуть.Видимость 			= (МестонахождениеПодключаемогоМодуля = "Диск");
	Элементы.ПредупреждениеСерверныйПуть.Видимость 		= (МестонахождениеПодключаемогоМодуля = "Диск");
	Элементы.ПодключаемыйМодульСсылка.Видимость 		= (МестонахождениеПодключаемогоМодуля = "1С");
	Элементы.ПодключаемыйМодульИмяОбработки.Видимость 	= (МестонахождениеПодключаемогоМодуля = "Конфигурация");
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьФТПКаталог()
	
	Элементы.ПанельФТП.Видимость 		= (ПереключательFTP = "FTP" ИЛИ ПереключательFTP = "DialMail");
	Элементы.ПанельКаталог.Видимость 	= (ПереключательFTP = "Каталог");
	Элементы.ПанельAPI.Видимость 		= (ПереключательFTP = "API");
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьЭлементовПрокси()

	ПоддерживаетсяПроксиДляИсточника = (ПереключательFTP = "DialMail" ИЛИ ПереключательFTP = "API");
	
	Элементы.НадписьУведомлениеПрокси.Видимость = Не ПоддерживаетсяПроксиДляИсточника;
	Элементы.ИспользуетсяПрокси.Видимость		= ПоддерживаетсяПроксиДляИсточника;
	
	ПоказыватьЭлементыПрокси = ПоддерживаетсяПроксиДляИсточника И (ИспользуетсяПрокси = Истина);
	Элементы.ПараметрыПрокси.Видимость 			= ПоказыватьЭлементыПрокси;
	Элементы.НадписьПроксиПодсказка.Видимость 	= (ПоказыватьЭлементыПрокси И ПереключательFTP = "DialMail");
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСписокТиповПрокси()
	
	ТипыПрокси = Новый СписокЗначений;
	
	//ТипыПрокси.Добавить(0, "Нет прокси - прямое соединение");
	ТипыПрокси.Добавить(1, "Socks5");
	ТипыПрокси.Добавить(2, "Socks4/4a");
	ТипыПрокси.Добавить(3, "HTTP-Tunnel");
	
	Возврат ТипыПрокси;
	
КонецФункции

&НаКлиенте
Процедура ИспользоватьПодключаемыйМодульПриИзменении(Элемент)
	УправлениеДоступностьюПолейНастройкиПодключаемогоМодуля();
КонецПроцедуры

&НаКлиенте
Процедура МестонахождениеПодключаемогоМодуляПриИзменении(Элемент)
	УправлениеДоступностьюПолейНастройкиПодключаемогоМодуля();
КонецПроцедуры

&НаКлиенте
Процедура ПереключательFTPПриИзменении(Элемент)
	
	УстановитьВидимостьФТПКаталог();
	УстановитьВидимостьЭлементовПрокси();

КонецПроцедуры

&НаКлиенте
Процедура ИспользуетсяПроксиПриИзменении(Элемент)
	УстановитьВидимостьЭлементовПрокси();
КонецПроцедуры

&НаКлиенте
Процедура ИспользуетсяПрокси1ПриИзменении(Элемент)
	УстановитьВидимостьЭлементовПрокси();
КонецПроцедуры


&НаКлиенте
Процедура ДеревоНастроекВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Элементы.ДеревоНастроек.ТекущийЭлемент.Имя = "ДеревоНастроекЗначение" 
		И Элементы.ДеревоНастроек.ТекущиеДанные.ЭтоГруппа Тогда
		СтандартнаяОбработка=Ложь;
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Функция ПроверкаНаКоллизиюПройдена(Отказ)
	
    Если Элементы.ДеревоНастроек.ТекущиеДанные=Неопределено Тогда возврат Отказ; КонецЕсли;
	
	Если Элементы.ДеревоНастроек.ТекущиеДанные.НастройкаИД="НеБратьАдресаПартнераИз1С" и Элементы.ДеревоНастроек.ТекущиеДанные.Значение=Истина Тогда
		Сообщить("Для Вашей конфигурации эта настройка не редактируется");
		Отказ=Истина;
	КонецЕсли;
	Если Элементы.ДеревоНастроек.ТекущиеДанные.НастройкаИД="НеБратьСвоиАдресаИз1С" и Элементы.ДеревоНастроек.ТекущиеДанные.Значение=Истина Тогда
		Сообщить("Для Вашей конфигурации эта настройка не редактируется");
		Отказ=Истина;
	КонецЕсли;
	
    Возврат не Отказ;
КонецФункции // ПроверкаНаКоллизиюПройдена()


&НаКлиенте
Процедура ДеревоНастроекПередНачаломИзменения(Элемент, Отказ)
	
	Если Не ПроверкаНаКоллизиюПройдена(Отказ) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Элементы.ДеревоНастроек.ТекущийЭлемент.Имя = "ДеревоНастроекЗначение" 
		И Элементы.ДеревоНастроек.ТекущиеДанные.ЭтоГруппа Тогда
		Отказ=Истина;
	КонецЕсли;	
	
	Если Элементы.ДеревоНастроек.ТекущийЭлемент.Имя = "ДеревоНастроекЗначение"  
		И ТипЗнч(Элементы.ДеревоНастроек.ТекущиеДанные.Значение)=Тип("Булево") Тогда
		Элемент.ТекущиеДанные.Значение = Не Элемент.ТекущиеДанные.Значение;
		Отказ=Истина;
	КонецЕсли;
	
	//фиксированные списки редактировать не дадим
	Если Элементы.ДеревоНастроек.ТекущийЭлемент.Имя = "ДеревоНастроекЗначение"  
		И Элемент.ТекущиеДанные.ОписаниеТипа=Новый ОписаниеТипов("СписокЗначений") Тогда
		Отказ=Истина;
		ДеревоНастроекЗначениеНачалоВыбора(Элемент,Ложь);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоНастроекЗначениеНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ДеревоНастроек.ТекущиеДанные;	
	Если ЗначениеЗаполнено(ТекущиеДанные.Список) Тогда
		СтандартнаяОбработка = Ложь;                                             
		СписокВыбора = ЗначениеИзСтрокиВнутрСервер(ТекущиеДанные.Список);
		Если Параметры.МодальностьЗапрещена Тогда
			Выполнить("СписокВыбора.ПоказатьВыборЭлемента(Новый ОписаниеОповещения(""ОбработатьВыборЭлементаСписка"", ЭтаФорма, ТекущиеДанные))");
		Иначе
			ВыбранныйЭлемент = СписокВыбора.ВыбратьЭлемент();
			ОбработатьВыборЭлементаСписка(ВыбранныйЭлемент, ТекущиеДанные);			
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборЭлементаСписка(Результат, ТекущиеДанные) Экспорт
	
	Если Результат <> Неопределено Тогда
		ТекущиеДанные.Значение = Результат.Значение;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗначениеИзСтрокиВнутрСервер(Параметр1)
	Возврат ЗначениеИзСтрокиВнутр(Параметр1) 
КонецФункции

&НаКлиенте
Процедура ЗаписатьНастройки(Команда)

	СтруктураНастроек = СобратьСтруктуруНастроек();
	
	Отказ = Ложь;
	
	// важный флаг, который позволяет избежать зацикливания вызова СписокРасширенияПриАктивизацииЯчейки, баг платформы 8.2
	РасширениеАктивировано = Истина;
	
	ЗаписатьНастройкиСервер(СтруктураНастроек,Отказ);
	
	Если Отказ = Истина Тогда
		//Предупреждение("Обнаружены некорректные настройки");
		Возврат;//не закрывать
	Иначе
		Если Параметры.ФормаОткрытаСтартовымПомощником Тогда 
			Оповестить("КонтурEDI_НастроитьФорму");
		КонецЕсли;
		ЭтаФорма.Закрыть();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Функция СобратьСтруктуруНастроек()
	
	СтруктураНастроек = Новый Структура;
	
	Если ПереключательFTP="Каталог" Тогда
		СтруктураНастроек.Вставить("АдресКаталогаОбменаEDI",	АдресКаталогаОбменаEDI);//-
	КонецЕсли;
	
    СтруктураНастроек.Вставить("ПротоколОбмена_КонтурEDI", 	ПереключательFTP);
	
	СтруктураНастроек.Вставить("Сервер", 			Сервер);
	СтруктураНастроек.Вставить("Порт", 				Порт);
	
	СтруктураНастроек.Вставить("ЛогинПрокси", 		ЛогинПрокси);
	СтруктураНастроек.Вставить("ПарольПрокси", 		ПарольПрокси);
 	СтруктураНастроек.Вставить("СерверПрокси",		СерверПрокси);
	СтруктураНастроек.Вставить("ПортПрокси",		ПортПрокси);
	
	СтруктураНастроек.Вставить("ПассивныйРежим", 		ПассивныйРежим);
	СтруктураНастроек.Вставить("ИспользуетсяПрокси",	ИспользуетсяПрокси);
	СтруктураНастроек.Вставить("ТипПрокси",				ТипПрокси);
	
	СтруктураНастроек.Вставить("СоздаваемыйДокументСклад",		СоздаваемыйДокументСклад);
	СтруктураНастроек.Вставить("ПодключаемыйМодульСсылка",		ПодключаемыйМодульСсылка);
	
	//СтруктураНастроек.Вставить("МыПоставщик",	МыПоставщик);
	//СтруктураНастроек.Вставить("НеПоказыватьФормуВходящегоЗаказа", НеПоказыватьФормуВходящегоЗаказаПриЗагрузке); //перенесено в партнера
	
	СтруктураНастроек.Вставить("НеПоказыватьПровестиЗаказ", 			НеПоказыватьПровестиЗаказ);
	СтруктураНастроек.Вставить("НеПоказыватьСоздатьРеализацию", 		НеПоказыватьСоздатьРеализацию);	
		
	//СтруктураНастроек.Вставить("ПроводитьЗагруженныеЗаказы", ПроводитьЗагруженныеЗаказы);   //перенесено в партнера
	СтруктураНастроек.Вставить("ГрузополучательИзЮрФизЛицаТД", (ГрузополучательИзЮрФизЛицаТД="Грузополучатель"));
	
	СтруктураНастроек.Вставить("ЛогинСлужебныйДиадок",		ЛогинСлужебныйДиадок);
	СтруктураНастроек.Вставить("ПарольСлужебныйДиадок",		ПарольСлужебныйДиадок);
	
	Если ЗначениеЗаполнено(СоздаваемыйДокументРСчет) Тогда
		СтруктураНастроек.Вставить("СоздаваемыйДокументРСчетВид",	ИмяМетаданныхРСчета(СоздаваемыйДокументРСчет));
		СтруктураНастроек.Вставить("СоздаваемыйДокументРСчетGUID", 	СокрЛП(СоздаваемыйДокументРСчет.УникальныйИдентификатор()));
	Иначе
		СтруктураНастроек.Вставить("СоздаваемыйДокументРСчетВид",	Неопределено);
		СтруктураНастроек.Вставить("СоздаваемыйДокументРСчетGUID", 	Неопределено);
	КонецЕсли;
		
	Если ИспользоватьПодключаемыйМодуль Тогда
		ПодключаемыйМодульТип = МестонахождениеПодключаемогоМодуля;
	Иначе
		ПодключаемыйМодульТип = "Нет";
	КонецЕсли;
	
	СтруктураНастроек.Вставить("ПодключаемыйМодульТип",  	ПодключаемыйМодульТип);
	СтруктураНастроек.Вставить("ПодключаемыйМодульПуть",	ПодключаемыйМодульПуть);
	
	Если ЗначениеЗаполнено(ПодключаемыйМодульСсылка) Тогда
		СтруктураНастроек.Вставить("ПодключаемыйМодульВидСправочника",	ПолучитьИмяМетаданныхПодключаемыйМодульВидСправочника(ПодключаемыйМодульСсылка));
		СтруктураНастроек.Вставить("ПодключаемыйМодульGUID",			СокрЛП(ПодключаемыйМодульСсылка.УникальныйИдентификатор()));
	КонецЕсли;
	
	СтруктураНастроек.Вставить("ПодключаемыйМодульИмяОбработки", 		ПодключаемыйМодульИмяОбработки);
	
	СтруктураНастроек.Вставить("ШаблонЦепочкиДокументов_Поставщик",		ИмяШаблонаПоставщик);
	СтруктураНастроек.Вставить("ШаблонЦепочкиДокументов_Покупатель",	ИмяШаблонаПокупатель);
	
	Возврат СтруктураНастроек;
	
КонецФункции

//некоторые настройки могут быть взаимозависимыми. Проверим их корректность.
&НаСервере
Процедура ПроверитьКорректностьНастроек(СтруктураПараметров,Отказ,РезультатПроверки)
	
	Отказ = Отказ Или НЕ МодульОбъекта().ПроверитьКорректностьОбщихНастроекПередЗаписью(СтруктураПараметров);
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНастройкиСервер(СтруктураНастроек,Отказ)
	
	СписокСоздаваемыхДокументов = МодульОбъекта().ПолучитьВозможныеТипыСоздаваемыхДокументовПоВходящимЗаказам();

	МодульОбъекта().ДобавитьСтрокиДереваВСтруктуру(РеквизитФормыВЗначение("ДеревоНастроек"),СтруктураНастроек);
	
	МодульОбъекта().ДобавитьСтрокиДереваВСтруктуру(РеквизитФормыВЗначение("ДеревоНастроекДокументовСообщений"),СтруктураНастроек);//конструкторские настройка
	
	РезультатПроверки="";
	
	ПроверитьКорректностьНастроек(СтруктураНастроек,Отказ,РезультатПроверки);
	Если Отказ = Истина Тогда 
		Возврат;
	КонецЕсли;
	
	Для Каждого Эл из СтруктураНастроек Цикл
		МодульОбъекта().УстановитьКонстантуEDI(Эл.Ключ, Эл.Значение);
	КонецЦикла;
	
	СохранитьНастройкиСертификатовПоУмолчанию();
	
	//перекэшировать модуль объекта!
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьИмяМетаданныхПодключаемыйМодульВидСправочника(Ссылка)
	Возврат Ссылка.Метаданные().Имя;	
КонецФункции

&НаСервереБезКонтекста
Функция ИмяМетаданныхРСчета(СоздаваемыйДокументРСчет)   //объединить с функцией выше
	Возврат СоздаваемыйДокументРСчет.Метаданные().Имя;
КонецФункции

 &НаСервере
Функция EDIПроверитьСуществованиеКаталога(ИмяКаталога)  //проверяем и предлагаем создать
	
	КаталогНаДиске = Новый Файл(ИмяКаталога);
	Если КаталогНаДиске.Существует() Тогда
		Возврат Истина;
	Иначе
		Попытка
			СоздатьКаталог(ИмяКаталога);
			Возврат Истина;
		Исключение
			Возврат Ложь;
		КонецПопытки;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура СохранитьШаблонПМНаДиск(Команда)
	
	ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогВыбора.Заголовок		= "Укажите файл, в который нужно сохранить подключаемый модуль";
	ДиалогВыбора.Фильтр		= "Файл внешней обработки 1С (*.epf)|*.epf";      
	ДиалогВыбора.ПолноеИмяФайла = "КонтурEDI_ПМ.epf";
	
	Если ДиалогВыбора.Выбрать() Тогда      
		
		ПутьКФайлу = ДиалогВыбора.ПолноеИмяФайла;
		
	Иначе
		
		//ЭтаФорма.Закрыть();
		Возврат;
		
	КонецЕсли;
	
	СохранитьШаблонПМНаДискСервер(ПутьКФайлу);
	
	
КонецПроцедуры

&НаСервере
Процедура СохранитьШаблонПМНаДискСервер(ПутьКФайлу)
	
	МодульОбъекта().ПолучитьМакет("ШаблонПодключаемогоМодуля").Записать(ПутьКФайлу);
	
	Если ПодключаемыйМодульПуть="" и не ЗначениеЗаполнено(ПодключаемыйМодульСсылка) Тогда 
		ПодключаемыйМодульПуть= ПутьКФайлу;
		МестонахождениеПодключаемогоМодуля="Диск";
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоНастроекДокументовСообщенийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Элементы.ДеревоНастроекДокументовСообщений.ТекущийЭлемент.Имя = "ДеревоНастроекДокументовСообщенийЗначение" 
		И Элементы.ДеревоНастроекДокументовСообщений.ТекущиеДанные.ЭтоГруппа Тогда
		СтандартнаяОбработка=Ложь;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоНастроекДокументовСообщенийПередНачаломИзменения(Элемент, Отказ)
	
	Если Элементы.ДеревоНастроекДокументовСообщений.ТекущийЭлемент.Имя = "ДеревоНастроекДокументовСообщенийЗначение" 
		И Элементы.ДеревоНастроекДокументовСообщений.ТекущиеДанные.ЭтоГруппа Тогда
		Отказ=Истина;
	КонецЕсли;	
	
	Если Элементы.ДеревоНастроекДокументовСообщений.ТекущийЭлемент.Имя = "ДеревоНастроекДокументовСообщенийЗначение"  
		И ТипЗнч(Элементы.ДеревоНастроекДокументовСообщений.ТекущиеДанные.Значение)=Тип("Булево") Тогда
		Элемент.ТекущиеДанные.Значение = Не Элемент.ТекущиеДанные.Значение;
		Отказ=Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяШаблонаПоставщикНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Список = ПолучитьСписокДоступныхШаблоновЦепочки("Поставщик");
	ДопПараметры=Новый Структура("Режим","Поставщик");
	Если Параметры.МодальностьЗапрещена Тогда
		Выполнить("ПоказатьВыборИзСписка(Новый ОписаниеОповещения(""ОбработчикВыбораШаблонаПоставщикПокупатель"", ЭтаФорма,ДопПараметры), Список,Элемент,)");
	Иначе
		Выбранный = Список.ВыбратьЭлемент();
		ОбработчикВыбораШаблонаПоставщикПокупатель(Выбранный,ДопПараметры); 
	КонецЕсли;
	
	
	Если Выбранный<>Неопределено Тогда
		
		ИмяШаблонаПоставщик = Выбранный.Представление;
		ПерезаполнитьДеревоНастроекДокументовСообщений("Поставщик",Выбранный.Значение);
	КонецЕсли;
	
	КоллекцияЭлементовДерева=ДеревоНастроекДокументовСообщений.ПолучитьЭлементы();
    Для Каждого Строка Из КоллекцияЭлементовДерева Цикл    
        ИдентификаторСтроки = Строка.ПолучитьИдентификатор();
        Элементы.ДеревоНастроекДокументовСообщений.Развернуть(ИдентификаторСтроки,ИСТИНА);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяШаблонаПокупательНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Список = ПолучитьСписокДоступныхШаблоновЦепочки("Покупатель");
	ДопПараметры=Новый Структура("Режим","Покупатель");
	
	Если Параметры.МодальностьЗапрещена Тогда
		Выполнить("ПоказатьВыборИзСписка(Новый ОписаниеОповещения(""ОбработчикВыбораШаблонаПоставщикПокупатель"", ЭтаФорма,ДопПараметры), Список,Элемент,)");
	Иначе
		Выбранный = Список.ВыбратьЭлемент();
		ОбработчикВыбораШаблонаПоставщикПокупатель(Выбранный,ДопПараметры); 
	КонецЕсли;
	
	
	Если Выбранный<>Неопределено Тогда
		
		ИмяШаблонаПокупатель = Выбранный.Представление;
		ПерезаполнитьДеревоНастроекДокументовСообщений("Покупатель",Выбранный.Значение);
	КонецЕсли;
	
	КоллекцияЭлементовДерева=ДеревоНастроекДокументовСообщений.ПолучитьЭлементы();
    Для Каждого Строка Из КоллекцияЭлементовДерева Цикл    
        ИдентификаторСтроки = Строка.ПолучитьИдентификатор();
        Элементы.ДеревоНастроекДокументовСообщений.Развернуть(ИдентификаторСтроки,ИСТИНА);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСписокДоступныхШаблоновЦепочки(РежимРаботы)
	
	Возврат МодульОбъекта().ПолучитьСписокДоступныхШаблоновЦепочки(РежимРаботы);
		
КонецФункции

&НаКлиенте
Процедура ОбработчикВыбораШаблонаПоставщикПокупатель(ВыбранноеЗначение,ДопПараметр) Экспорт
	Выбранный=ВыбранноеЗначение;
	Если Выбранный<>Неопределено Тогда
		
		Выполнить("ИмяШаблона"+ДопПараметр.Режим+" = Выбранный.Представление;");
		ПерезаполнитьДеревоНастроекДокументовСообщений(ДопПараметр.Режим,Выбранный.Значение);
	КонецЕсли;
	
	КоллекцияЭлементовДерева=ДеревоНастроекДокументовСообщений.ПолучитьЭлементы();
    Для Каждого Строка Из КоллекцияЭлементовДерева Цикл    
        ИдентификаторСтроки = Строка.ПолучитьИдентификатор();
        Элементы.ДеревоНастроекДокументовСообщений.Развернуть(ИдентификаторСтроки,ИСТИНА);
	КонецЦикла;
КонецПроцедуры
	
&НаСервере
Процедура ПерезаполнитьДеревоНастроекДокументовСообщений(РежимРаботы,ТекстШаблона)
	
	Если РежимРаботы="Поставщик" Тогда
		МодульОбъекта().ШаблонЗапросаЦепочкиДокументов_Поставщик = ТекстШаблона;
	Иначе
		МодульОбъекта().ШаблонЗапросаЦепочкиДокументов_Покупатель = ТекстШаблона;
	КонецЕсли;	
	
	ТабСборки = МодульОбъекта().ПолучитьТаблицуДляСборкиЗапросов(РежимРаботы);
	МодульОбъекта().ЗаполнитьСоответствиеДокументовСообщений(РежимРаботы);
	
	ЗаполнитьДеревоНастроекДокументовСообщений(РежимРаботы,Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьПоказатьШаблонПоставщикаНажатие(Элемент)
	
	РежимРаботы = ?(Элемент.Имя = "НадписьПоказатьШаблонПоставщика","Поставщик","Покупатель");
	ИмяШаблона = ?(РежимРаботы = "Поставщик",ИмяШаблонаПоставщик,ИмяШаблонаПокупатель);
	ТД = СформироватьМакетСоответствийДокументовСообщений(РежимРаботы);
	ТД.Показать(РежимРаботы+": шаблон "+ИмяШаблона);
	
КонецПроцедуры

&НаСервере
Функция СформироватьМакетСоответствийДокументовСообщений(РежимРаботы)
	
	СтрокиТаблицыТипов = МодульОбъекта().ТаблицаТипов.НайтиСтроки(Новый Структура("Сторона",РежимРаботы));
	
	Макет = МодульОбъекта().ПолучитьМакет("ПечатьСоответствияДокументовСообщений");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	
	ТД = Новый ТабличныйДокумент;
	ТД.Вывести(Макет.ПолучитьОбласть("Заголовок"));
	
	Для каждого Стр Из СтрокиТаблицыТипов Цикл
		ОбластьСтрока.Параметры.Направление = МодульОбъекта().ПолучитьНаправлениеСообщения(Стр.ТипСообщения,РежимРаботы);
		ОбластьСтрока.Параметры.ТипДокумента1С = СтрЗаменить(Стр.ИмяТипа,"ДокументСсылка.","");
		ОбластьСтрока.Параметры.ТипСообщения = Стр.ТипСообщения+" ("+МодульОбъекта().ПеревестиТипСообщения(Стр.ТипСообщения)+")";
		
		ТД.Вывести(ОбластьСтрока);
	КонецЦикла;
	
	ТД.ТолькоПросмотр = Истина;
	Возврат ТД;
	
КонецФункции	

&НаКлиенте
Процедура ПодключаемыйМодульПутьНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
   МодульОбъектаКлиент().ОткрытьДиалогВыбораФайла_КонтурEDI("ОбработатьВыборФайла_ПодключаемогоМодуля",ЭтаФорма, , , "Внешняя обработка 1С:Предприятия 8 (*.epf)|*.epf", Заголовок); 	
	
КонецПроцедуры   

 &НаКлиенте
 Процедура ОбработатьВыборФайла_ПодключаемогоМодуля(ПомещенныеФайлы, ДополнительныеПараметры) Экспорт
     
	 Если ПомещенныеФайлы = Неопределено ИЛИ ПомещенныеФайлы.Количество() = 0 Тогда
		 Возврат;
	 КонецЕсли; 
	 
	 ОписаниеФайла = ПомещенныеФайлы[0];
	 
	 ПодключаемыйМодульПуть = ОписаниеФайла.Имя;
	 
 КонецПроцедуры 

&НаКлиенте
Процедура ЗакрытьЭтуФорму(Команда)
	Если ЭтаФорма.Открыта() Тогда 
		ЭтаФорма.Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикВыбораШаблонаПоставщик(ВыбранноеЗначение,ДопПараметр=Неопределено) Экспорт
	Выбранный=ВыбранноеЗначение;
	Если Выбранный<>Неопределено Тогда
		
		ИмяШаблонаПоставщик = Выбранный.Представление;
		ПерезаполнитьДеревоНастроекДокументовСообщений("Поставщик",Выбранный.Значение);
	КонецЕсли;
	
	КоллекцияЭлементовДерева=ДеревоНастроекДокументовСообщений.ПолучитьЭлементы();
    Для Каждого Строка Из КоллекцияЭлементовДерева Цикл    
        ИдентификаторСтроки = Строка.ПолучитьИдентификатор();
        Элементы.ДеревоНастроекДокументовСообщений.Развернуть(ИдентификаторСтроки,ИСТИНА);
	КонецЦикла;
КонецПроцедуры


&НаКлиенте
Процедура СписокРасширенияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ=Истина;
КонецПроцедуры


&НаКлиенте
Процедура СписокРасширенияПередУдалением(Элемент, Отказ)
	Отказ=Истина;
КонецПроцедуры


&НаКлиенте
Процедура ДобавитьРасширениеБиблиотека(Команда)
	
	Состояние("Открываю библиотеку ",75);
	
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("ФормаРасширения_СписокУправляемая", , , ЭтаФорма, "ОбработчикПерезаполнитьРасширения", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикПерезаполнитьРасширения(Параметр1=Неопределено,ДопПараметр=Неопределено) Экспорт 
	
	ЗаполнитьИнформациюОРасширениях();
	
	Если ЗначениеЗаполнено(Параметр1) Тогда 
		ПозиционироватьНаСтрокуРасширения(Параметр1);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДопПараметр) Тогда 
		ПозиционироватьНаСтрокуРасширения(ДопПараметр);
	КонецЕсли;
		
КонецПроцедуры // ОбработчикПерезаполнитьРасширения()

&НаКлиенте
Процедура СписокРасширенияПередНачаломИзменения(Элемент, Отказ)
	Отказ=Истина;
КонецПроцедуры

&НаКлиенте
Процедура СписокРасширенияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка=Ложь;
	
	Колонка = Поле;
	ТекСтрока = Элемент.ТекущиеДанные;
	
	Если ТекСтрока = Неопределено или Колонка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	//запоминаем позицию курсора  
	Идентификатор = ТекСтрока.Идентификатор;
   	
	Если Колонка.Имя = "СписокРасширенияАктивация" Тогда
		
		
		
	ИначеЕсли Колонка.Имя = "СписокРасширенияНаименование" Тогда
		//изменяем настройку расширения
		Если ТекСтрока.НеОткрыватьИнтерфейс Тогда
			ТекСтрока.НеОткрыватьИнтерфейс = Ложь; //сбрасываем после вкл./откл. расширения
		Иначе
			РезультатОткрытия = ОткрытьИнтерфейсНастройкиРасширения(ТекСтрока.Ссылка,ТекСтрока.Интерфейс);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
//включение/отключение 
Функция ВключитьРасширениеСервер(РасширениеСсылка,Значение)
	
	РезультатВключения = МодульОбъекта().УстановитьФлагИспользованиеРасширения(РасширениеСсылка,Значение);
	
	Возврат РезультатВключения;
	
КонецФункции

&НаКлиенте
Процедура УстановитьДоступностьРасширения()
	
	
	ТекущаяСтрока = Элементы.СписокРасширения.ТекущиеДанные;
	
	Кнопка_Библиотека      			  = Элементы.СписокРасширенияДобавитьРасширениеБиблиотека;
	Кнопка_КонтекстноеМеню_Библиотека = Элементы.Расширение_КонтекстноеМеню_Библиотека;
	
	Кнопка_ДобавитьИзБиблиотеки      			= Элементы.СписокРасширенияДобавитьРасширениеБиблиотека1;
	Кнопка_КонтекстноеМеню_ДобавитьИзБиблиотеки = Элементы.Расширение_КонтекстноеМеню_ДобавитьИзБиблиотеки;
	
	Кнопка_ДобавитьВручную      		   = Элементы.СписокРасширенияДобавитьРасширениеВручную;
	Кнопка_КонтекстноеМеню_ДобавитьВручную = Элементы.Расширение_КонтекстноеМеню_ДобавитьВручную;

	Кнопка_ДобавитьИзФайла      		   = Элементы.СписокРасширенияДобавитьРасширенияИзФайла;
	Кнопка_КонтекстноеМеню_ДобавитьИзФайла = Элементы.Расширение_КонтекстноеМеню_ДобавитьИзФайла;
	
	Кнопка_Настроить      			 = Элементы.СписокРасширенияНастроитьРасширение;
	Кнопка_КонтекстноеМеню_Настроить = Элементы.Расширение_КонтекстноеМеню_Настроить;

	Кнопка_Удалить      		   = Элементы.СписокРасширенияУдалитьРасширение;
	Кнопка_КонтекстноеМеню_Удалить = Элементы.Расширение_КонтекстноеМеню_Удалить;

	Кнопка_ПереместитьВверх  				= Элементы.СписокРасширенияПереместитьВверхРасширения;
	Кнопка_КонтекстноеМеню_ПереместитьВверх = Элементы.Расширение_КонтекстноеМеню_ПереместитьВверх;
	
	Кнопка_ПереместитьВниз				   = Элементы.СписокРасширенияПереместитьВнизРасширения;
	Кнопка_КонтекстноеМеню_ПереместитьВниз = Элементы.Расширение_КонтекстноеМеню_ПереместитьВниз;
	
	Кнопка_ОткрытьИнтерфейс		            = Элементы.СписокРасширенияОткрытьИнтерфейсРасширения;
	Кнопка_КонтекстноеМеню_ОткрытьИнтерфейс = Элементы.Расширение_КонтекстноеМеню_ОткрытьИнтерфейс;
	
	Кнопка_Переименовать			     = Элементы.СписокРасширенияПереименоватьРасширение;
	Кнопка_КонтекстноеМеню_Переименовать = Элементы.Расширение_КонтекстноеМеню_Переименовать;
	
	Кнопка_ПроверитьОбновления      		   = Элементы.СписокРасширенияПроверитьОбновленияРасширений;
	Кнопка_КонтекстноеМеню_ПроверитьОбновления = Элементы.Расширение_КонтекстноеМеню_ПроверитьОбновления;
	
	Кнопка_ВосстановитьИсходныйКод  			   = Элементы.СписокРасширенияВосстановитьИсходныйКодРасширение;
	Кнопка_КонтекстноеМеню_ВосстановитьИсходныйКод = Элементы.Расширение_КонтекстноеМеню_ВосстановитьИсходныйКод;

	Кнопка_СохранитьТекущуюВерсию 				  = Элементы.СписокРасширенияСохранитьТекущуюВерсиюРасширение;
	Кнопка_КонтекстноеМеню_СохранитьТекущуюВерсию = Элементы.Расширение_КонтекстноеМеню_СохранитьТекущуюВерсию;

	Кнопка_ВосстановитьИзСохраненноеВерсии  			   = Элементы.СписокРасширенияВосстановитьИзСохраненноеВерсии;
	Кнопка_КонтекстноеМеню_ВосстановитьИзСохраненноеВерсии = Элементы.Расширение_КонтекстноеМеню_ВосстановитьИзСохраненноеВерсии;
	
	Кнопка_Изменить					= Элементы.СписокРасширенияИзменитьРасширение;
	Кнопка_КонтекстноеМеню_Изменить = Элементы.Расширение_КонтекстноеМеню_Изменить;
	
	Если ТекущаяСтрока = Неопределено Тогда
		
		Кнопка_Настроить.Доступность 	   			       = Ложь;
		Кнопка_Удалить.Доступность			 	   		   = Ложь;
		Кнопка_ПереместитьВверх.Доступность 	           = Ложь;
		Кнопка_ПереместитьВниз.Доступность 			       = Ложь;
		Кнопка_ОткрытьИнтерфейс.Доступность  			   = Ложь;
		Кнопка_Переименовать.Доступность				   = Ложь;
		Кнопка_ВосстановитьИсходныйКод.Доступность         = Ложь;
		Кнопка_СохранитьТекущуюВерсию.Доступность    	   = Ложь;
		Кнопка_ВосстановитьИзСохраненноеВерсии.Доступность = Ложь;
		Кнопка_Изменить.Доступность  					   = Ложь;
		
		Кнопка_КонтекстноеМеню_Удалить.Доступность  					   = Ложь;
		Кнопка_КонтекстноеМеню_Настроить.Доступность				       = Ложь;
		Кнопка_КонтекстноеМеню_ПереместитьВверх.Доступность 			   = Ложь;
		Кнопка_КонтекстноеМеню_ПереместитьВниз.Доступность 				   = Ложь;
		Кнопка_КонтекстноеМеню_ОткрытьИнтерфейс.Доступность 			   = Ложь;
		Кнопка_КонтекстноеМеню_Переименовать.Доступность  	   			   = Ложь;
		Кнопка_КонтекстноеМеню_ВосстановитьИсходныйКод.Доступность  	   = Ложь;
		Кнопка_КонтекстноеМеню_СохранитьТекущуюВерсию.Доступность  		   = Ложь;
		Кнопка_КонтекстноеМеню_ВосстановитьИзСохраненноеВерсии.Доступность = Ложь;	
		Кнопка_КонтекстноеМеню_Изменить.Доступность  					   = Ложь;
		
		Возврат;
	Иначе
		Кнопка_Настроить.Доступность 	   			       = Истина;
		Кнопка_Удалить.Доступность			 	   		   = Истина;
		Кнопка_ПереместитьВверх.Доступность 	           = Истина;
		Кнопка_ПереместитьВниз.Доступность 			       = Истина;
		Кнопка_ОткрытьИнтерфейс.Доступность  			   = Истина;
		Кнопка_Переименовать.Доступность				   = Истина;
		Кнопка_ВосстановитьИсходныйКод.Доступность         = Истина;
		Кнопка_СохранитьТекущуюВерсию.Доступность    	   = Истина;
		Кнопка_ВосстановитьИзСохраненноеВерсии.Доступность = Истина;
		Кнопка_Изменить.Доступность  					   = Истина;
		
		Кнопка_КонтекстноеМеню_Удалить.Доступность  					   = Истина;
		Кнопка_КонтекстноеМеню_Настроить.Доступность				       = Истина;
		Кнопка_КонтекстноеМеню_ПереместитьВверх.Доступность 			   = Истина;
		Кнопка_КонтекстноеМеню_ПереместитьВниз.Доступность 				   = Истина;
		Кнопка_КонтекстноеМеню_ОткрытьИнтерфейс.Доступность 			   = Истина;
		Кнопка_КонтекстноеМеню_Переименовать.Доступность  	   			   = Истина;
		Кнопка_КонтекстноеМеню_ВосстановитьИсходныйКод.Доступность  	   = Истина;
		Кнопка_КонтекстноеМеню_СохранитьТекущуюВерсию.Доступность  		   = Истина;
		Кнопка_КонтекстноеМеню_ВосстановитьИзСохраненноеВерсии.Доступность = Истина;	
		Кнопка_КонтекстноеМеню_Изменить.Доступность  					   = Истина;
	КонецЕсли; 
	
	ИндексТекущейСтроки = СписокРасширения.Индекс(ТекущаяСтрока);
	Если ИндексТекущейСтроки = 0 Тогда
		Кнопка_ПереместитьВверх.Доступность = Ложь;
		Кнопка_КонтекстноеМеню_ПереместитьВверх.Доступность = Ложь;
	Иначе
		Кнопка_ПереместитьВверх.Доступность = Истина;
		Кнопка_КонтекстноеМеню_ПереместитьВверх.Доступность = Истина;
	КонецЕсли;
	
	Если ИндексТекущейСтроки+1 = СписокРасширения.Количество() Тогда
		Кнопка_ПереместитьВниз.Доступность = Ложь;
		Кнопка_КонтекстноеМеню_ПереместитьВниз.Доступность = Ложь;
	Иначе
		Кнопка_ПереместитьВниз.Доступность = Истина;
		Кнопка_КонтекстноеМеню_ПереместитьВниз.Доступность = Истина;
	КонецЕсли;
	
	//Интерфейс
	Если ТекущаяСтрока.Интерфейс = Истина Тогда
		Кнопка_ОткрытьИнтерфейс.Доступность = Истина;
		Кнопка_КонтекстноеМеню_ОткрытьИнтерфейс.Доступность = Истина;
	Иначе
		Кнопка_ОткрытьИнтерфейс.Доступность = Ложь;
		Кнопка_КонтекстноеМеню_ОткрытьИнтерфейс.Доступность = Ложь;
	КонецЕсли;	
	
	
	Если НЕ ТекущаяСтрока.ЕстьСохраненнаяВерсия Тогда 
		Кнопка_ВосстановитьИзСохраненноеВерсии.Доступность = Ложь;
		Кнопка_КонтекстноеМеню_ВосстановитьИзСохраненноеВерсии.Доступность = Ложь;
	Иначе
		Кнопка_ВосстановитьИзСохраненноеВерсии.Доступность = Истина;
		Кнопка_КонтекстноеМеню_ВосстановитьИзСохраненноеВерсии.Доступность = Истина;
		//ДатаСохранения = ТекущаяСтрока.ДатаСохранения;
		//Если НЕ ДатаСохранения = Неопределено Тогда
		//	Кнопка_ВосстановитьИзСохраненноеВерсии.Заголовок = "Восстановить из сохраненное версии (" + ДатаСохранения + ")";
		//	Кнопка_КонтекстноеМеню_ВосстановитьИзСохраненноеВерсии.Заголовок = "Восстановить из сохраненное версии (" + ДатаСохранения + ")";
		//КонецЕсли;
	КонецЕсли;
	
	Если НЕ ТекущаяСтрока.Типовое Тогда 
		Кнопка_Переименовать.Доступность		   = Истина;
		Кнопка_КонтекстноеМеню_Переименовать.Доступность		   = Истина;
		
	Иначе 
		Кнопка_Переименовать.Доступность		   = Ложь;
		Кнопка_КонтекстноеМеню_Переименовать.Доступность		   = Ложь;
	КонецЕсли;
	
	
	
КонецПроцедуры

&НаСервере
Функция ПолучитьЗначениеДопРеквизитаСправочника_КонтурEDIНаСервере(РасширениеСсылка, ИмяРеквизита)
	Возврат МодульОбъекта().ПолучитьЗначениеДопРеквизитаСправочника_КонтурEDI(РасширениеСсылка, ИмяРеквизита);
КонецФункции


&НаКлиенте
Процедура ИзменитьРасширение(Команда)
	
	ТекущаяСтрока = Элементы.СписокРасширения.ТекущиеДанные;
	
	Если Не ТекущаяСтрока = Неопределено Тогда
		Идентификатор = ТекущаяСтрока.Идентификатор;
		ОткрытьФормуРасширения(ТекущаяСтрока.Ссылка, Идентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуРасширения(СсылкаРасширения, Идентификатор = Неопределено)

 	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РасширениеСсылка",СсылкаРасширения); 
	
 	ПараметрОбработчика = Идентификатор;
	
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("ФормаРасширения_ЭлементУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбработчикПерезаполнитьРасширения", ЭтаФорма, ПараметрОбработчика);

КонецПроцедуры // ОткрытьФормуРасширения()

&НаКлиенте
Процедура УдалитьРасширение(Команда)
	
	ТекущаяСтрока = Элементы.СписокРасширения.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекстВопроса ="Вы действительно хотите удалить расширение """ + ТекущаяСтрока.Наименование + """?";
	КнопкиВопроса=новый СписокЗначений;
	КнопкиВопроса.Добавить("Да, удалить");
	КнопкиВопроса.Добавить("Отмена");
	ДопПараметрДляПередачиВОбработчик=ТекущаяСтрока.Ссылка;
	РезультатВопроса = Неопределено;
	
	Если Параметры.МодальностьЗапрещена Тогда
		Выполнить("ПоказатьВопрос(Новый ОписаниеОповещения(""ОбработчикСогласияУдаления"", ЭтаФорма, ДопПараметрДляПередачиВОбработчик), ТекстВопроса, КнопкиВопроса,,,""Контур.EDI"")");
	Иначе
		РезультатВопроса = Вопрос(ТекстВопроса, КнопкиВопроса,,,"Контур.EDI");
		ОбработчикСогласияУдаления(РезультатВопроса,ДопПараметрДляПередачиВОбработчик);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикСогласияУдаления(РезультатВопроса,РасширениеСсылка=Неопределено) Экспорт

	Если РезультатВопроса = "Да, удалить" и РасширениеСсылка<>Неопределено Тогда
		// важный флаг, который позволяет избежать зацикливания вызова СписокРасширенияПриАктивизацииЯчейки, баг платформы 8.2
		РасширениеАктивировано = Истина;
		
		РезультатУдаления = УдалитьРасширениеВызовСервера(РасширениеСсылка);
		Если Не РезультатУдаления.Успешно Тогда
			Состояние(РезультатУдаления.ОписаниеОшибки);
		КонецЕсли;
		ЗаполнитьИнформациюОРасширениях();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция УдалитьРасширениеВызовСервера(РасширениеСсылка)

	Возврат МодульОбъекта().УдалитьРасширение(РасширениеСсылка);

КонецФункции // УдалитьРасширениеВызовСервера()

&НаКлиенте
Процедура ПереместитьВверхРасширения(Команда)
	
	ТекущаяСтрока = Элементы.СписокРасширения.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторТекСтр = ТекущаяСтрока.ПолучитьИдентификатор();
	Идентификатор = ТекущаяСтрока.Идентификатор;
	
	ТекущаяСтрокаИндекс = СписокРасширения.Индекс(ТекущаяСтрока);
	
	ВерхняяСтрокаИндекс = ТекущаяСтрокаИндекс - 1;
	Попытка
		ВерхняяСтрока 		= СписокРасширения.Получить(ВерхняяСтрокаИндекс);
		Состояние("Меняю порядок выполнения...");
		
		ИдентификаторПредСтр=ВерхняяСтрока.ПолучитьИдентификатор();
		
		СменитьПорядокВыполненияРасширенийВызовСервера(ИдентификаторТекСтр,ИдентификаторПредСтр);
		ПозиционироватьНаСтрокуРасширения(Идентификатор);
		
		Состояние("Готово!");
	Исключение
		Возврат;
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьВнизРасширения(Команда)
	
	ТекущаяСтрока = Элементы.СписокРасширения.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторТекСтр=ТекущаяСтрока.ПолучитьИдентификатор();
	Идентификатор = ТекущаяСтрока.Идентификатор;
	
	ТекущаяСтрокаИндекс = СписокРасширения.Индекс(ТекущаяСтрока);
	
	НижняяСтрокаИндекс = ТекущаяСтрокаИндекс + 1;
	Попытка
		НижняяСтрока 		= СписокРасширения.Получить(НижняяСтрокаИндекс);
		Состояние("Меняю порядок выполнения...");
		
		ИдентификаторСледСтр=НижняяСтрока.ПолучитьИдентификатор();
		
		СменитьПорядокВыполненияРасширенийВызовСервера(ИдентификаторСледСтр,ИдентификаторТекСтр);
		ПозиционироватьНаСтрокуРасширения(Идентификатор);
		Состояние("Готово!");
	Исключение
		Возврат;
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура  СменитьПорядокВыполненияРасширенийВызовСервера(ИдентификаторТекСтр,ИдентификаторПредСтр)
	
	МассивСтрокТЗ = Новый Массив;
	
	ПредСтрокаЭлементКоллекции = СписокРасширения.НайтиПоИдентификатору(ИдентификаторПредСтр);
	ТекСтрокаЭлементКоллекции  = СписокРасширения.НайтиПоИдентификатору(ИдентификаторТекСтр);
	
	ТЗРасширений=РеквизитФормыВЗначение("СписокРасширения");
	
	МассивСтрокТЗ.Добавить(ТЗРасширений.Найти(ПредСтрокаЭлементКоллекции.Идентификатор,"Идентификатор"));
	МассивСтрокТЗ.Добавить(ТЗРасширений.Найти(ТекСтрокаЭлементКоллекции.Идентификатор,"Идентификатор"));
	
	РасширенияДляСменыПорядка = ТЗРасширений.Скопировать(МассивСтрокТЗ,"Ссылка,ПорядокВыполнения");
	
	РезультатСменыПорядка = МодульОбъекта().СменитьПорядокВыполненияРасширений(РасширенияДляСменыПорядка);
	
	Если РезультатСменыПорядка.Успешно Тогда
		ЗаполнитьИнформациюОРасширениях();
	Иначе
		Сообщить("Не удалось изменить порядок выполнения расширений: """ + РезультатСменыПорядка.ОписаниеОшибки + """.");
	КонецЕсли;
	
КонецПроцедуры // СменитьПорядокВыполненияРасширенийВызовСервера()

&НаКлиенте
Процедура ОткрытьИнтерфейсРасширения(Команда)
	
	ТекущаяСтрока = Элементы.СписокРасширения.ТекущиеДанные;
	
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	РезультатОткрытия = ОткрытьИнтерфейсНастройкиРасширения(ТекущаяСтрока.Ссылка,ТекущаяСтрока.Интерфейс);
	Если Не РезультатОткрытия.Успешно Тогда
		СообщитьПользователю_ФормаСервисНастройки(РезультатОткрытия.ОписаниеОшибки);
	КонецЕсли;

КонецПроцедуры 

&НаКлиенте 
Процедура СообщитьПользователю_ФормаСервисНастройки(Текст)

	СообщениеПользователю = Новый СообщениеПользователю;
	СообщениеПользователю.Текст = Текст;
	СообщениеПользователю.Сообщить();

КонецПроцедуры

&НаКлиенте
Функция ОткрытьИнтерфейсНастройкиРасширения(РасширениеСсылка, ЕстьИнтерфейс)
		
	РезультатОткрытия = Новый Структура("Успешно,ОписаниеОшибки",Ложь,"");
	
	//проверим, есть ли интерфейс
	Если Не ЕстьИнтерфейс Тогда
		РезультатОткрытия.Успешно = Истина;
		Возврат РезультатОткрытия;	
	КонецЕсли;
	
	//получаем обработку   	
	ИмяПодключеннойОбработки = МодульОбъектаКлиент().ВыполнитьМетодМодуляОбъекта("Функция", "ПодключитьИнтерфейсРасширения", Новый Структура("РасширениеСсылка",РасширениеСсылка));
	
	Если НЕ ЗначениеЗаполнено(ИмяПодключеннойОбработки) Тогда
		РезультатОткрытия.ОписаниеОшибки = "Не удалось подключить интерфейс расширения";
		Возврат РезультатОткрытия;		
	КонецЕсли;

	ПараметрыОткрытия = Новый Структура("КлючВарианта, ОбработкаОбъект", "", Объект);
    Уникальность = "ВнешняяОбработка." + ИмяПодключеннойОбработки + "/КлючВарианта.";
    ОткрытьФорму("ВнешняяОбработка." + ИмяПодключеннойОбработки + ".Форма", ПараметрыОткрытия, ЭтаФорма, Уникальность); 
	
	РезультатОткрытия.Успешно = Истина;   
	
	Возврат РезультатОткрытия;
	
КонецФункции

&НаКлиенте
Процедура ДобавитьРасширениеВручную(Команда)
	СоздатьРасширение();		
КонецПроцедуры

&НаКлиенте
Процедура СоздатьРасширение()
	
	ОткрытьФормуРасширения(Неопределено)

КонецПроцедуры

&НаКлиенте
Процедура НадписьРасширенияНедоступныПодсказкаНажатие(Элемент)
	
	Режим = РежимДиалогаВыбораФайла.Сохранение;
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
	ДиалогОткрытияФайла.Заголовок = "Укажите файл, в который нужно сохранить обновление для вашей конфигурации";
	ДиалогОткрытияФайла.Фильтр			= "Файл конфигурации 1С (*.cf)|*.cf";      
	ДиалогОткрытияФайла.ПолноеИмяФайла = "КонтурEDI_upd.cf";
	
	Если ДиалогОткрытияФайла.Выбрать() Тогда
		АдресДляСохраненияФайла = ДиалогОткрытияФайла.ПолноеИмяФайла;
		Состояние("Сохраняю файл обновления");
		ДвоичныеДанные=ПолучитьДвоичныеДанныеФайлаОбновленияКонфигурацииВызовСервера();
		
		Если ДвоичныеДанные<>Неопределено Тогда 
			ДвоичныеДанные.Записать(АдресДляСохраненияФайла);
			Состояние("Сохранен");
		Иначе
			Сообщить("Не удалось сохранить файл");
			Состояние("Ошибка сохранения");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДвоичныеДанныеФайлаОбновленияКонфигурацииВызовСервера()
	Возврат МодульОбъекта().ДвоичныеДанныеФайлаОбновленияКонфигурацииСервер();	
КонецФункции

//сертификаты
&НаСервере
Процедура ЗаполнитьНазначенныеСертификаты()
	
	ОтборПоСертификатам = Новый Структура;
	ОтборПоСертификатам.Вставить("СертификатыПоУмолчанию", Истина);
	
	СертификатыПоУмолчанию = МодульОбъекта().ПолучитьТаблицуСохраненныхСертификатов(ОтборПоСертификатам);
	ЗначениеВРеквизитФормы(СертификатыПоУмолчанию, "СписокНазначенныхСертификатов");	
	
КонецПроцедуры // ЗаполнитьНазначенныеСертификаты()

&НаКлиенте
Процедура СписокСертификатовПередУдалением(Элемент, Отказ)
	Отказ=Истина;
КонецПроцедуры

&НаКлиенте
Процедура СписокСертификатовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ=Истина;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСертификатыПоУмолчаниюКлиент()
	
	//Сходить за текущими сертами
	ТолькоДействующиеСертификаты = Ложь;
	МассивСертификатов = ОсновнаяФорма().МодульОбменКлиент().МассивСертификатов(ТолькоДействующиеСертификаты);
	
	Если МассивСертификатов.Количество()=0 Тогда
		Элементы.СертификатыОргнизацийПоУмолчанию.Заголовок="Не установлен CAPICOM или список сертификатов пуст!";
		Элементы.СтраницаЭДО.Заголовок="ЭДО (Диадок) (!)";
		Возврат;
	КонецЕсли;
	
	Для Каждого НазначенныйОтпечаток Из СписокНазначенныхСертификатов Цикл
		НоваяСтрокаСертификата = СписокСертификатов.Добавить();
		НоваяСтрокаСертификата.Организация = НазначенныйОтпечаток.Организация;
		
		Для каждого СуществующийСертификат Из МассивСертификатов Цикл
			Если СуществующийСертификат.Отпечаток = НазначенныйОтпечаток.Отпечаток Тогда 
				ЗаполнитьЗначенияСвойств(НоваяСтрокаСертификата, СуществующийСертификат);
			КонецЕсли;
		КонецЦикла;
		
		Если ЗначениеЗаполнено(НоваяСтрокаСертификата.ОкончаниеПериодаДействия) Тогда
			НоваяСтрокаСертификата.ОсталосьДней = РассчитатьОсталосьДнейДействияСертификата(НоваяСтрокаСертификата.ОкончаниеПериодаДействия);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция РассчитатьОсталосьДнейДействияСертификата(ОкончаниеПериодаДействия)

	Возврат Окр((НачалоДня(ОкончаниеПериодаДействия) - НачалоДня(ТекущаяДата())) / (60*60*24));	

КонецФункции // РассчитатьОсталосьДней()

&НаКлиенте
Процедура ИзменитьСертификатПоУмолчанию(Команда)
	
	// Предложим выбрать сертификат через специальную форму
	Состояние("Подготовка выбора сертификата...",0);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОбъектПараметрыКлиентСервер", Объект.ПараметрыКлиентСервер);
	ПараметрыФормы.Вставить("Организация", Элементы.СписокСертификатов.ТекущиеДанные.Организация);
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Действие", 				"ВыборСертификата");
	ДопПараметры.Вставить("ФинальныйОбработчик", 	"Оповестить_КонтурEDI_ПроверкаСертификата");
	ДопПараметры.Вставить("ЗапомнитьВыбор", 		Ложь);
	ДопПараметры.Вставить("НомерПопытки", 			1);
	ДопПараметры.Вставить("Успешно", 				Ложь);
	ДопПараметры.Вставить("ОписаниеОшибки", 		"");
	
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Сертификаты_СписокУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбработчикВыбораСертификата", МодульОбменКлиент(), ДопПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПанельНастроекПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если ТекущаяСтраница.Имя="СтраницаЭДО" И СписокСертификатов.Количество()=0 Тогда 
		
		Состояние("Строю список сертификатов");
		//Узнаем сертификаты
		ЗаполнитьСертификатыПоУмолчаниюКлиент();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиСертификатовПоУмолчанию()

	ТаблицаСертификатов = СписокСертификатов.Выгрузить(,"Организация,Отпечаток,ОкончаниеПериодаДействия");
	
	МодульОбъекта().СохранитьСертификатыПоУмолчанию(ТаблицаСертификатов);

КонецПроцедуры // СозранитьНастройкиСертификатовПоУмолчанию()

&НаКлиенте
Процедура ОчиститьСертификатПоУмолчаниюПоОрганизации(Команда)
	
	ТекСтрока = Элементы.СписокСертификатов.ТекущиеДанные;
	ТекСтрока.НаименованиеОрганизацииСубъекта	= "";
	ТекСтрока.НаименованиеСубъекта				= "";
	ТекСтрока.НаименованиеУдостоверяющегоЦентра	= "";
	ТекСтрока.Отпечаток							= "";
	ТекСтрока.НачалоПериодаДействия				= Дата(1,1,1);
	ТекСтрока.ОкончаниеПериодаДействия			= Дата(1,1,1);
	ТекСтрока.Расположение						= "";
	ТекСтрока.РасположениеПользовательское		= "";
	ТекСтрока.ОсталосьДней						= 0;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокСертификатовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИзменитьСертификатПоУмолчанию("Заглушка");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "КонтурEDI_ПроверкаСертификата" Тогда
		
		Если Параметр.Успешно = Истина Тогда
			ТекДанные = Элементы.СписокСертификатов.ТекущиеДанные;
			ЗаполнитьЗначенияСвойств(ТекДанные, Параметр.РеквизитыСертификата);
			ТекДанные.ОсталосьДней = РассчитатьОсталосьДнейДействияСертификата(ТекДанные.ОкончаниеПериодаДействия);
		Иначе
			Сообщить(Параметр.ОписаниеОшибки);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	ОсновнаяФорма().ПриЗакрытииФормы(ЭтаФорма, "СервисНастройкиУправляемая");	
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьРасширенияИзФайла(Команда)
			
	МодульОбъектаКлиент().ВыбратьФайл_КонтурEDI("УстановитьРасширенияИзФайла",
												ЭтаФорма,
												РежимДиалогаВыбораФайла.Открытие,
												
												"Выберите файл для сохранения расширений",
												"КонтурEDI_Расширения.zip");
		
КонецПроцедуры	

&НаКлиенте
Процедура УстановитьРасширенияИзФайла(РезультатВыбораФайла, ДопПараметры = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(РезультатВыбораФайла) Тогда
		Возврат;	
	КонецЕсли;
	Если ТипЗнч(РезультатВыбораФайла) = Тип("Массив") Тогда
		ФайлДляЗагрузки = РезультатВыбораФайла[0];
	Иначе
		ФайлДляЗагрузки = РезультатВыбораФайла;
	КонецЕсли;
	
	//Может быть выбран как xml конкретного расширения, так и архив с несколькими расширениями	
	Если СтрЧислоВхождений(ФайлДляЗагрузки, ".xml") Тогда
		
		Если СтрЧислоВхождений(ФайлДляЗагрузки, "\content.xml") Тогда
			УстановитьРасширениеИзФайла(ФайлДляЗагрузки, Ложь);
		Иначе
			МодульОбъектаКлиент().СообщениеПользователю_КонтурEDI(
			"Ошибка выбора файла:" + Символы.ПС + "Имя файла расширения не ""content.xml""."
			, ЭтаФорма.УникальныйИдентификатор);
		КонецЕсли;
		
	ИначеЕсли СтрЧислоВхождений(ФайлДляЗагрузки, ".zip") Тогда
		
		КаталогВыгрузкиРасширений = КаталогВременныхФайлов() + "ВыгрузкаРасширений";	
		Файл = Новый Файл(КаталогВыгрузкиРасширений);
		Если Файл.Существует() Тогда
			УдалитьФайлы(КаталогВыгрузкиРасширений);	
		КонецЕсли;	
		СоздатьКаталог(КаталогВыгрузкиРасширений);
	
		ЧтениеZipФайла = Новый ЧтениеZipФайла(ФайлДляЗагрузки);
		ЧтениеZipФайла.ИзвлечьВсе(КаталогВыгрузкиРасширений + "\", РежимВосстановленияПутейФайловZIP.Восстанавливать);
		
		ПоискРасширения = НайтиФайлы(КаталогВыгрузкиРасширений, "content.xml", Истина);
		Если ПоискРасширения.Количество() = 0 Тогда
			
			МодульОбъектаКлиент().СообщениеПользователю_КонтурEDI(
			"Ошибка выбора файла:" + Символы.ПС + "В архиве не найдены файлы расширения ""content.xml""."
			, ЭтаФорма.УникальныйИдентификатор);
			
			Возврат;
			
		Иначе
			
			Для Каждого НайденныйФайлРасширения Из ПоискРасширения Цикл
				УстановитьРасширениеИзФайла(НайденныйФайлРасширения.ПолноеИмя);	
			КонецЦикла;
			
		КонецЕсли;
		
		УдалитьФайлы(КаталогВыгрузкиРасширений);
		
	Иначе
		
		МодульОбъектаКлиент().СообщениеПользователю_КонтурEDI(
		"Ошибка выбора файла:" + Символы.ПС + "Файл должен иметь расширение "".zip"" или имя ""content.xml""."
		, ЭтаФорма.УникальныйИдентификатор);
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура УстановитьРасширениеИзФайла(ФайлРасширения, ЗагружатьДопФайлы = Истина) Экспорт
	
	ДДРасширения = Новый ДвоичныеДанные(ФайлРасширения);
	
	МассивДанныхДопФайлов = Новый Массив;
	Если ЗагружатьДопФайлы Тогда
		КаталогПоиска = МодульОбъектаКлиент().ВернутьКаталогВыбранногоФайл_КонтурEDI(ФайлРасширения);
		ДопФайлы = НайтиФайлы(КаталогПоиска, "*.*");
		Для Каждого ДопФайл Из ДопФайлы Цикл
			Если ДопФайл.Расширение = "" ИЛИ ДопФайл.ПолноеИмя = ФайлРасширения Или ДопФайл.Имя = "Для типовых.txt" Тогда
				Продолжить;	
			КонецЕсли;
			ДД = Новый ДвоичныеДанные(ДопФайл.ПолноеИмя);
			МассивДанныхДопФайлов.Добавить(Новый Структура("ИмяФайла, ДвоичныеДанные", ДопФайл.Имя, ДД));  
		КонецЦикла;
	КонецЕсли;
	
	ПараметрыВыполненияЗагрузки         = Новый Структура;
	ПараметрыВыполненияЗагрузки.Вставить("ДанныеРаширения", ДДРасширения);
	ПараметрыВыполненияЗагрузки.Вставить("ДанныеДопФайлов", МассивДанныхДопФайлов);
	
	РезультатУстановки = МодульОбъектаКлиент().ВыполнитьМетодМодуляОбъекта("Функция", "ЗагрузитьРасширение", Новый Структура("ПараметрыВыполненияЗагрузки",ПараметрыВыполненияЗагрузки));
	
	Если Не РезультатУстановки.Успешно Тогда            
        Сообщить(РезультатУстановки.ОписаниеОшибки);            
    Иначе
		ЗаполнитьИнформациюОРасширениях();
		ПодключитьОбработчикОжидания("ПозиционироватьНаПоследнююСтрокуСпискаРасширения", 0.1 , Истина);
        Сообщить("Расширение успешно загружено в модуль - " + Строка(РезультатУстановки.РасширениеСсылка));
    КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРасширенияПриАктивизацииСтроки(Элемент)
	
	Если Элементы.ПанельНастроек.ТекущаяСтраница = Неопределено Тогда 
	 	Возврат;
	КонецЕсли;
	Если Элементы.ПанельНастроек.ТекущаяСтраница.Имя = "СтраницаРасширения" Тогда 
		УстановитьДоступностьРасширения();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРасширенияПриАктивизацииЯчейки(Элемент)
	
	ТекущаяСтрока = Элементы.СписокРасширения.ТекущиеДанные;
	
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Элемент.ТекущийЭлемент.Имя = "СписокРасширенияАктивация" 
		И НЕ РасширениеАктивировано Тогда
		
		Состояние("Переключаю...");
		
		// важный флаг, который позволяет избежать зацикливания вызова СписокРасширенияПриАктивизацииЯчейки, баг платформы 8.2
		РасширениеАктивировано = Истина;
		
		//активируем/деактивируем расширения
		ТекСтрока = Элемент.ТекущиеДанные;
		
		ТекСтрока.Использование = Не ТекСтрока.Использование;
		ТекСтрока.Иконка = ?(ТекСтрока.Иконка=1,0,1);
		
		РезультатВключения = ВключитьРасширениеСервер(ТекСтрока.Ссылка, ТекСтрока.Использование);
		
		ТекСтрока = Элементы.СписокРасширения.ТекущиеДанные;
		Если Не РезультатВключения.Успешно Тогда
			ТекСтрока.Использование = Не ТекСтрока.Использование;
			ТекСтрока.Иконка = ?(ТекСтрока.Иконка=1,0,1);
			Сообщить("Не удалось " + ?(ТекСтрока.Использование, "выключить", "включить") + " расширение: """ + РезультатВключения.ОписаниеОшибки + """.");
		КонецЕсли;
		ТекСтрока.НеОткрыватьИнтерфейс = Истина; //не открываем интерфейс, когда позиционируемся на колонке наименование и срабатывает выбор строки
		Состояние(?(ТекСтрока.Использование=Истина,"Включено!","Выключено!"));
		
		// перенесем фокус на колонку Наименование
		Элемент.ТекущийЭлемент = Элементы.СписокРасширенияНаименование;
		
	ИначеЕсли Элемент.ТекущийЭлемент.Имя = "СписокРасширенияКартинка"
		И ТекущаяСтрока.Обновить Тогда
		
		 Идентификатор = ТекущаяСтрока.Идентификатор;
		 
		 РезультатУстановки = УстановитьРасширениеСервер(ТекущаяСтрока.Наименование,ТекущаяСтрока.Идентификатор,ТекущаяСтрока.Интерфейс,ТекущаяСтрока.Ссылка);
		 Если Не РезультатУстановки.Успешно Тогда
			 СообщитьПользователю_ФормаСервисНастройки(РезультатУстановки.ОписаниеОшибки);
		 Иначе
			 ЗаполнитьИнформациюОРасширениях();
		 КонецЕсли;		
		 
		 ПозиционироватьНаСтрокуРасширения(Идентификатор);		 
		
	КонецЕсли;
	
	// важный флаг, который позволяет избежать зацикливания вызова СписокРасширенияПриАктивизацииЯчейки, баг платформы 8.2
	РасширениеАктивировано = Ложь;

КонецПроцедуры

&НаСервере
Функция УстановитьРасширениеСервер(Наименование,Идентификатор,Интерфейс,Ссылка)
	
	Результат = МодульОбъекта().УстановитьРасширение(Наименование,Идентификатор,Интерфейс,Ссылка);
	Результат.Удалить("ДанныеЧтения");
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПереименоватьРасширение(Команда)
	
	ТекущаяСтрока = Элементы.СписокРасширения.ТекущиеДанные;
	
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Идентификатор = ТекущаяСтрока.Идентификатор;
	
	ТекНаименование = ТекущаяСтрока.Наименование;
	Подсказка = "Введите новое имя расширения";
	Длинна = 20;
	Многострочность = Ложь;
	
	ДопПараметрДляПередачиВОбработчик = Новый Структура("Ссылка,Идентификатор" , ТекущаяСтрока.Ссылка, Идентификатор);
	
	Если Объект.ПараметрыКлиентСервер.МодальностьЗапрещена Тогда 
		Выполнить("ПоказатьВводСтроки(Новый ОписаниеОповещения(""Обработчик_ПереименоватьРасширение"", ЭтаФорма, ДопПараметрДляПередачиВОбработчик), ТекНаименование, Подсказка, Длинна , Многострочность)"); 
	Иначе
		Если ВвестиСтроку(ТекНаименование, Подсказка) Тогда
			Обработчик_ПереименоватьРасширение(ТекНаименование, ДопПараметрДляПередачиВОбработчик);	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Обработчик_ПереименоватьРасширение(Наименование, ДопПараметрОбработчика = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Наименование) Тогда 
		Возврат;
	КонецЕсли;
	
	Если ДопПараметрОбработчика.Свойство("Ссылка") И
		ЗначениеЗаполнено(ДопПараметрОбработчика.Ссылка) Тогда 
		
		Результат = ПереименоватьРасширениеСервер(ДопПараметрОбработчика.Ссылка, Наименование);
		
		Если НЕ Результат.Успешно Тогда 
			
			СообщитьПользователю_ФормаСервисНастройки(Результат.ОписаниеОшибки);
			Возврат;
			
		КонецЕсли;
		
	ЗаполнитьИнформациюОРасширениях();
	
	ПозиционироватьНаСтрокуРасширения(ДопПараметрОбработчика.Идентификатор)
	КонецЕсли;	
	
КонецПроцедуры 

&НаКлиенте
Процедура ПозиционироватьНаПоследнююСтрокуСпискаРасширения() Экспорт
	
	КоличествоРасширений = СписокРасширения.Количество();
	
	Если НЕ КоличествоРасширений = 0 Тогда 
		ИндексПоследнейСтроки = КоличествоРасширений - 1 ;
		ПоследняяСтрока = СписокРасширения.Получить(ИндексПоследнейСтроки);
		
		Элементы.СписокРасширения.ТекущаяСтрока = ПоследняяСтрока.ПолучитьИдентификатор();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПозиционироватьНаСтрокуРасширения(Идентификатор)
	
	Если НЕ ЗначениеЗаполнено(Идентификатор) Тогда 
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура("Идентификатор", Идентификатор);
	НайденныеСтроки = СписокРасширения.НайтиСтроки(Отбор);
	
	Если НЕ НайденныеСтроки.Количество() = 0 Тогда 

		Элементы.СписокРасширения.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере 
Функция ПереименоватьРасширениеСервер(Ссылка, Наименование)
	
		Результат = МодульОбъекта().Расширения_Переименовать(Ссылка, Наименование);
		Возврат Результат;	
	
КонецФункции

&НаКлиенте
Процедура ПроверитьОбновленияРасширений(Команда)
	
	ТекущаяСтрока = Элементы.СписокРасширения.ТекущиеДанные;
	
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Результат = ПроверитьОбновленияРасширенийНаСервере();
	
	Если НЕ Результат.Успешно Тогда 
		СообщитьПользователю_ФормаСервисНастройки(Результат.ОписаниеОшибки);
		Возврат;		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьОбновленияРасширенийНаСервере()
	
	Результат = МодульОбъекта().Расширения_ПроверитьОбновленияРасширений();
	
	Если НЕ Результат.Успешно Тогда 
		Возврат Результат;		
	КонецЕсли;
	
	ТаблицаДанныхОбУстановленныхРасширениях = Результат.ТаблицаДанныхОбУстановленныхРасширениях;
	
	СписокРасширения.Очистить();
	
	Для Каждого СтрокаТаблицы Из ТаблицаДанныхОбУстановленныхРасширениях Цикл   
		НоваяСтрокаСписка = СписокРасширения.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаСписка, СтрокаТаблицы);
		
		Если НоваяСтрокаСписка.Использование = Истина Тогда
			НоваяСтрокаСписка.Иконка=1;
		Иначе
			НоваяСтрокаСписка.Иконка=0;
		КонецЕсли;
		
		Если НЕ НоваяСтрокаСписка.Обновить И НоваяСтрокаСписка.Типовое и НЕ НоваяСтрокаСписка.Измененное Тогда
			НоваяСтрокаСписка.Картинка = 2;
		ИначеЕсли НЕ НоваяСтрокаСписка.Обновить И НоваяСтрокаСписка.Типовое и НоваяСтрокаСписка.Измененное Тогда
			НоваяСтрокаСписка.Картинка = 0;
		ИначеЕсли НЕ НоваяСтрокаСписка.Типовое Тогда
			НоваяСтрокаСписка.Картинка = 1;
		ИначеЕсли НоваяСтрокаСписка.Обновить Тогда
			НоваяСтрокаСписка.Картинка = 3;
		КонецЕсли;

	КонецЦикла;
	
	Результат.Удалить("ТаблицаДанныхОбУстановленныхРасширениях");
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ВосстановитьИсходныйКодРасширение(Команда)
	
	ТекущаяСтрока = Элементы.СписокРасширения.ТекущиеДанные;
	Идентификатор = ТекущаяСтрока.Идентификатор;
	
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Результат = ВосстановитьИсходныйКодРасширениеНаСервере(ТекущаяСтрока.Ссылка);
	
	Если НЕ Результат.Успешно Тогда 
		СообщитьПользователю_ФормаСервисНастройки(Результат.ОписаниеОшибки);
		Возврат;		
	КонецЕсли;
	
	ЗаполнитьИнформациюОРасширениях();
	ПозиционироватьНаСтрокуРасширения(Идентификатор);

КонецПроцедуры

&НаСервере
Функция ВосстановитьИсходныйКодРасширениеНаСервере(РасширениеСсылка)
	
	Результат = МодульОбъекта().Расширения_ВосстановитьИсходныйКод(РасширениеСсылка);
	Возврат Результат;	
	
КонецФункции

&НаКлиенте
Процедура СохранитьТекущуюВерсиюРасширение(Команда)
	
	ТекущаяСтрока = Элементы.СписокРасширения.ТекущиеДанные;
	Идентификатор = ТекущаяСтрока.Идентификатор;
	
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;

		Результат = АрхивироватьТекущуюВерсиюРасширениеНаСервере(ТекущаяСтрока.Ссылка);
		
		Если НЕ Результат.Успешно Тогда 
			СообщитьПользователю_ФормаСервисНастройки(Результат.ОписаниеОшибки);
			Возврат;		
		КонецЕсли;
		
		ЗаполнитьИнформациюОРасширениях();
		ПозиционироватьНаСтрокуРасширения(Идентификатор);

КонецПроцедуры

&НаСервере
Функция АрхивироватьТекущуюВерсиюРасширениеНаСервере(РасширениеСсылка)
	
	Результат = МодульОбъекта().Расширения_АрхивироватьТекущуюВерсию(РасширениеСсылка);
	Возврат Результат;	
	
КонецФункции

&НаКлиенте
Процедура ВосстановитьИзСохраненноеВерсии(Команда)
	
	ТекущаяСтрока = Элементы.СписокРасширения.ТекущиеДанные;
	Идентификатор = ТекущаяСтрока.Идентификатор;
	
	Если Не ТекущаяСтрока = Неопределено Тогда
		
		Идентификатор = ТекущаяСтрока.Идентификатор;
		
		Результат = ВосстановитьАрхивнуюВерсиюНаСервере(ТекущаяСтрока.Ссылка);
		
		Если НЕ Результат.Успешно Тогда 
			СообщитьПользователю_ФормаСервисНастройки(Результат.ОписаниеОшибки);
			Возврат;		
		КонецЕсли;

		ЗаполнитьИнформациюОРасширениях();
		ПозиционироватьНаСтрокуРасширения(Идентификатор);
		
	КонецЕсли;
	
	
КонецПроцедуры

&НаСервере
Функция ВосстановитьАрхивнуюВерсиюНаСервере(РасширениеСсылка)
	
	Результат = МодульОбъекта().Расширения_ВосстановитьАрхивнуюВерсию(РасширениеСсылка);
	Возврат Результат;	
	
КонецФункции

&НаСервере
Функция ПроверитьАрхивнуюВерсиюНаСервере(РасширениеСсылка)
	Результат = МодульОбъекта().Расширения_ПолучитьАрхивнуюВерсию(РасширениеСсылка);
	Возврат Результат;	
КонецФункции

&НаКлиенте
Процедура ВыгрузитьРасширения(Команда)
		
	МодульОбъектаКлиент().ВыбратьФайл_КонтурEDI("ОбработатьВыборФайлаДляВыгрузкиРасширений",
												ЭтаФорма,
												РежимДиалогаВыбораФайла.Сохранение,
												"Выберите файл для сохранения расширений",
												"КонтурEDI_Расширения.zip",	
												"(*.zip)|*.zip",
												,
												".zip");	
												
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборФайлаДляВыгрузкиРасширений(РезультатВыбораФайла, ДопПараменты = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(РезультатВыбораФайла) Тогда
		Возврат;	
	КонецЕсли;
	Если ТипЗнч(РезультатВыбораФайла) = Тип("Массив") Тогда
		ФайлДляВыгрузки = РезультатВыбораФайла[0];
	Иначе
		ФайлДляВыгрузки = РезультатВыбораФайла;
	КонецЕсли;
	
	КаталогВыгрузкиРасширений = КаталогВременныхФайлов() + "ВыгрузкаРасширений";	
	Файл = Новый Файл(КаталогВыгрузкиРасширений);
	Если Файл.Существует() Тогда
		УдалитьФайлы(КаталогВыгрузкиРасширений);	
	КонецЕсли;	
	СоздатьКаталог(КаталогВыгрузкиРасширений);
	
	ЗаписьZipФайла = Новый ЗаписьZipФайла(ФайлДляВыгрузки);
	
	ПорядокРасширения = 1;
	СтрокаИнформации = "";
	
	ВыделенныеСтроки = Элементы.СписокРасширения.ВыделенныеСтроки;	
	СписокЗначенийДляСортировки = Новый СписокЗначений;
	СписокЗначенийДляСортировки.ЗагрузитьЗначения(ВыделенныеСтроки);
	СписокЗначенийДляСортировки.СортироватьПоЗначению();	
	ОтсортированныеСтроки = СписокЗначенийДляСортировки.ВыгрузитьЗначения();
	
	Для Каждого ИдентификаторСтроки Из ОтсортированныеСтроки Цикл
		ДанныеСтроки = СписокРасширения.НайтиПоИдентификатору(ИдентификаторСтроки);
		
		РасширениеСсылка = ДанныеСтроки.Ссылка;
		Если НЕ ЗначениеЗаполнено(РасширениеСсылка) 
			ИЛИ НЕ ТипЗнч(РасширениеСсылка) = Тип("СправочникСсылка.КонтурEDI_ДополнительныеСправочники") Тогда
			Продолжить;
		КонецЕсли;
		
		ИдентификаторРасширения = СтрЗаменить(ДанныеСтроки.Идентификатор, " ", "");
		Если ИдентификаторРасширения = "" Тогда
			ИдентификаторРасширения = Строка(Новый УникальныйИдентификатор);	
		КонецЕсли;
		
		ИдентификаторРасширения = Строка(ПорядокРасширения) + "__" + ИдентификаторРасширения;
					
		КаталогВыгрузки = КаталогВыгрузкиРасширений + "\" + ИдентификаторРасширения;

		КаталогНаДиске = Новый Файл(КаталогВыгрузки);
		Если НЕ КаталогНаДиске.Существует() Тогда       
			СоздатьКаталог(КаталогВыгрузки);
		КонецЕсли;
		
		ВыгрузитьРасширениеВКаталог(РасширениеСсылка, КаталогВыгрузки);
		
		ЗаписьZipФайла.Добавить(КаталогВыгрузки + "\", РежимСохраненияПутейZIP.СохранятьОтносительныеПути, РежимОбработкиПодкаталоговZIP.ОбрабатыватьРекурсивно);
		
		Если ДанныеСтроки.Типовое Тогда
			Если ДанныеСтроки.Измененное Тогда
				Происхождение = "Типовое(измененное)";	
			Иначе
				Происхождение = "Типовое";		
			КонецЕсли;
		Иначе
			Происхождение = "Нетиповое";	
		КонецЕсли;
		СтрокаИнформации = СтрокаИнформации + Строка(ПорядокРасширения) + " " +  Строка(РасширениеСсылка) + " - " + Происхождение + Символы.ПС;
		
		ПорядокРасширения = ПорядокРасширения + 1;
	КонецЦикла;
	
	ИмяФайлаИнформации = КаталогВыгрузкиРасширений + "\" + "Информация о выгруженных расширениях.txt";
	ЗаписьТекста = Новый ЗаписьТекста(ИмяФайлаИнформации);
	ЗаписьТекста.Записать(СтрокаИнформации);
	ЗаписьТекста.Закрыть();
	ЗаписьZipФайла.Добавить(ИмяФайлаИнформации);
	
	ЗаписьZipФайла.Записать();
	УдалитьФайлы(КаталогВыгрузкиРасширений);
	
	ЗапуститьПриложение(ФайлДляВыгрузки);
			
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьРасширениеВКаталог(РасширениеСсылка, КаталогВыгрузки) Экспорт
		
	СтруктураПараметровДляФункцииМодуля = Новый Структура("РасширениеСсылка, ПолучатьФайлы", РасширениеСсылка, Истина);
	ДанныеДляВыгрузкиРасширения = МодульОбъектаКлиент().ВыполнитьМетодМодуляОбъекта("Функция", "ПолучитьДанныеДляВыгрузкиРасширения", СтруктураПараметровДляФункцииМодуля);
	
	//Основной xml расширения
    ПолноеИмяФайла = КаталогВыгрузки + "\content.xml";
	ТД = Новый ТекстовыйДокумент;
	ТД.УстановитьТекст(ДанныеДляВыгрузкиРасширения.XMLРасширения);
	ТД.Записать(ПолноеИмяФайла,"UTF-8");

	//Вспомагательный файл для типовых
	ПолноеИмяФайла = КаталогВыгрузки + "\Для типовых.txt";	
	ТД = Новый ТекстовыйДокумент;
	ТД.УстановитьТекст(ДанныеДляВыгрузкиРасширения.ВспомагательныйXMLТиповогоРасширения);
	ТД.Записать(ПолноеИмяФайла,"UTF-8");
	
	Если ЗначениеЗаполнено(ДанныеДляВыгрузкиРасширения.ИнтерфейсФайл) Тогда
		ДанныеДляВыгрузкиРасширения.ИнтерфейсФайл.Записать(КаталогВыгрузки + "\interface.epf");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеДляВыгрузкиРасширения.Файлы) Тогда
		Для Каждого Файл Из ДанныеДляВыгрузкиРасширения.Файлы Цикл
			Файл.ДвоичныеДанные.Записать(КаталогВыгрузки + "\" + Файл.ИмяФайла);	
		КонецЦикла;
	КонецЕсли;
		
	Сообщить("Расширение успешно сохранено во внешнем файле - " + Строка(РасширениеСсылка));

КонецПроцедуры
		