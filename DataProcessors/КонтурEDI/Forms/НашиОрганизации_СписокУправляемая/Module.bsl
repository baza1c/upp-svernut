&НаСервере
Перем МодульОбъекта;

&НаКлиенте
Перем МодульОбъектаКлиент;

// СЛУЖЕБНЫЕ {

// Инициализация модуля и его экспортных функций
&НаСервере
Функция МодульОбъекта()
	
	Если МодульОбъекта = Неопределено Тогда
		
		Если Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект Тогда
			МодульОбъекта = МодульОбъектаИзКэша();
		КонецЕсли;
		
		Если МодульОбъекта = Неопределено Тогда
			МодульОбъекта = РеквизитФормыВЗначение("Объект");
			МодульОбъекта.ИнициализироватьПодключаемыеМодули(); // TODO Проверить что из этого используется на формах и перенести в ПараметрыКлиентСервер
			МодульОбъекта.ОбновитьКэшМодуляОбъекта();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат МодульОбъекта;
	
КонецФункции

&НаСервере
Функция МодульОбъектаИзКэша()
	
	Результат = Неопределено;
	
	Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда
		АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект;
 		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Структура = ПолучитьИзВременногоХранилища(АдресВХ);
			Если ТипЗнч(Структура) = Тип("Структура") и Структура.Свойство("ОбработкаОбъект") Тогда
			 	Результат = Структура.ОбработкаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульОбъектаКлиент()
	
	Если ТипЗнч(МодульОбъектаКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбъектаКлиент;
	КонецЕсли;
	
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		МодульОбъектаКлиент = ОсновнаяФорма.МодульОбъектаКлиент();
		Возврат МодульОбъектаКлиент;
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
	
	Если ЭтаФорма.ВладелецФормы = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ЭтаФорма.ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;
    	
КонецФункции

&НаКлиенте
Функция НоваяМетрика()
	
	Возврат ОсновнаяФорма().НоваяМетрика();
	
КонецФункции

// } СЛУЖЕБНЫЕ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
	Параметры.Свойство("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	
	Параметры.МодальностьЗапрещена=МодульОбъекта().МодальностьЗапрещена();
	ПутьКФормам = МодульОбъекта().Метаданные().ПолноеИмя() + ".Форма.";
	
	ОбновитьСписокСправочника();
		
    УстановитьЗаголовокОсновнойКнопки();
	
КонецПроцедуры

&НаКлиенте
Процедура КоманднаяПанельСпискаКнопкаДобавить(Кнопка)
	
	ОткрытьФормуОрганизацииДляРедактированияИлиДобавления(Истина);

КонецПроцедуры

// Устанавливает заголовок основной кнопки в "Открыть" или в "Выбрать"
&НаСервере
Процедура УстановитьЗаголовокОсновнойКнопки()

	Если Параметры.ФормаОткрытаКакВыбор Тогда
		
		Элементы.ФормаОткрыть.Заголовок = "Выбрать";
		
	КонецЕсли;
	
КонецПроцедуры // ()

&НаСервере
Процедура ОбновитьСписокСправочника()
	
	СправочникСписок.Очистить();
	
	НашиОрганизации = МодульОбъекта().ПолучитьСписокЭлементовСправочника("НашиОрганизации");
		
	Для каждого Стр ИЗ НашиОрганизации Цикл
			
		НоваяСтрока = СправочникСписок.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Стр);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОсновноеДействие(Команда)
	
	Если Параметры.ФормаОткрытаКакВыбор Тогда
		Если Элементы.СправочникСписок.ТекущиеДанные<>Неопределено Тогда 
			ЭтаФорма.Закрыть(Элементы.СправочникСписок.ТекущиеДанные.Ссылка);
		Иначе
			Сообщить("Выберите организацию API");
		КонецЕсли;
	иначе
		ОткрытьФормуОрганизацииДляРедактированияИлиДобавления(ложь);	
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуОрганизацииДляРедактированияИлиДобавления(ЭтоДобавление=Ложь)
	
	ТекСтрока = Элементы.СправочникСписок.ТекущиеДанные;
	
	Если (НЕ ТекСтрока=Неопределено) или ЭтоДобавление=Истина Тогда
		Если ТекСтрока<>Неопределено и ЗначениеЗаполнено(ТекСтрока.Ссылка) и не ЭтоДобавление Тогда
			СсылкаНаОрганизацию = ТекСтрока.Ссылка;
		иначе
			СсылкаНаОрганизацию = Неопределено;
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ссылка", 	СсылкаНаОрганизацию);
		ПараметрыФормы.Вставить("Новая", 	ЭтоДобавление);
		МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("НашиОрганизации_ЭлементУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбработчикПослеРедактированияОрганизации", ЭтаФорма);
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикПослеРедактированияОрганизации(ПараметрыЗакрытияИнтерактивнойФормы, ДополнительныеПараметры) Экспорт
	
	//обновить список организаций
	ОбновитьСписокСправочника();	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьССервера(Команда)
	ОбновитьССервераСервер();
КонецПроцедуры

&НаСервере
Процедура ОбновитьССервераСервер()
	
	МодульОбъекта().ПротоколОбмена_КонтурEDI = МодульОбъекта().ПолучитьКонстантуEDI("ПротоколОбмена_КонтурEDI");
	
	Если МодульОбъекта().ПротоколОбмена_КонтурEDI <> "API" Тогда
//		ВывестиПредупреждение_КонтурEDI("Только для API");
		//убрать видимость кнопки
		Возврат;
	КонецЕсли;
	
	МодульОбъекта().ОбновитьСписокОрганизацийПоУчетнымЗаписям();
	ОбновитьСписокСправочника();
	
КонецПроцедуры	

&НаКлиенте
Процедура СправочникСписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыполнитьОсновноеДействие("");

КонецПроцедуры

&НаКлиенте
Процедура Изменить(Команда)
	ОткрытьФормуОрганизацииДляРедактированияИлиДобавления(ложь);	
КонецПроцедуры

&НаКлиенте
Процедура СправочникСписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ=Истина;
КонецПроцедуры

&НаКлиенте
Процедура СправочникСписокПередУдалением(Элемент, Отказ)
	Отказ=Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОсновнаяФорма().ПриОткрытииФормы(ЭтаФорма, "НашиОрганизации_СписокУправляемая");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	ОсновнаяФорма().ПриЗакрытииФормы(ЭтаФорма, "НашиОрганизации_СписокУправляемая");	

КонецПроцедуры
