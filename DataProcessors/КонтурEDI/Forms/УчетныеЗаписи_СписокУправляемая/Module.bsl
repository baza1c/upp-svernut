&НаСервере
Перем МодульОбъекта;

&НаКлиенте
Перем МодульОбъектаКлиент;

// СЛУЖЕБНЫЕ {

// Инициализация модуля и его экспортных функций
&НаСервере
Функция МодульОбъекта()
	
	Если МодульОбъекта = Неопределено Тогда
		
		Если Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект Тогда
			МодульОбъекта = МодульОбъектаИзКэша();
		КонецЕсли;
		
		Если МодульОбъекта = Неопределено Тогда
			МодульОбъекта = РеквизитФормыВЗначение("Объект");
			МодульОбъекта.ИнициализироватьПодключаемыеМодули(); // TODO Проверить что из этого используется на формах и перенести в ПараметрыКлиентСервер
			МодульОбъекта.ОбновитьКэшМодуляОбъекта();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат МодульОбъекта;
	
КонецФункции

&НаСервере
Функция МодульОбъектаИзКэша()
	
	Результат = Неопределено;
	
	Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда
		АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект;
 		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Структура = ПолучитьИзВременногоХранилища(АдресВХ);
			Если ТипЗнч(Структура) = Тип("Структура") и Структура.Свойство("ОбработкаОбъект") Тогда
			 	Результат = Структура.ОбработкаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульОбъектаКлиент()
	
	Если ТипЗнч(МодульОбъектаКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбъектаКлиент;
	КонецЕсли;
	
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		МодульОбъектаКлиент = ОсновнаяФорма.МодульОбъектаКлиент();
		Возврат МодульОбъектаКлиент;
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
	
	Если ЭтаФорма.ВладелецФормы = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ЭтаФорма.ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;
    	
КонецФункции

&НаКлиенте
Функция НоваяМетрика()
	
	Возврат ОсновнаяФорма().НоваяМетрика();
	
КонецФункции

// } СЛУЖЕБНЫЕ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
	Параметры.Свойство("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	
	Параметры.МодальностьЗапрещена=МодульОбъекта().МодальностьЗапрещена();
	ПутьКФормам = МодульОбъекта().Метаданные().ПолноеИмя() + ".Форма.";
	
	ОбновитьСписокУчетныхЗаписей();

КонецПроцедуры

&НаКлиенте
Процедура КоманднаяПанельСпискаКнопкаДобавить(Кнопка)
	
	ОткрытьФормуУчетнойЗаписиДляРедактированияИлиДобавления(Истина);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьОсновной(Команда)
	
	ТекСтрока = Элементы.СписокУчетныхЗаписей.ТекущиеДанные;
	
	Если НЕ ТекСтрока = Неопределено Тогда
		
		УстановитьУчетнуюЗаписьОсновнойСервер(ТекСтрока.Ссылка);
		ОбновитьСписокУчетныхЗаписей();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокУчетныхЗаписей()
	
	УчетнаяЗаписьПоУмолчанию = МодульОбъекта().ПолучитьКонстантуEDI("УчетнаяЗаписьПоУмолчанию");
	
	СписокУчетныхЗаписей.Очистить();
	
	УчетныеЗаписи = МодульОбъекта().ПолучитьСписокЭлементовСправочника("УчетныеЗаписи");
		
	Для каждого Стр ИЗ УчетныеЗаписи Цикл
			
		НоваяСтрока = СписокУчетныхЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Стр);
		
		Если Стр.Ссылка = УчетнаяЗаписьПоУмолчанию Тогда
			НоваяСтрока.Основная = Истина;
		КонецЕсли;
			
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУчетнуюЗаписьОсновнойСервер(УчетнаяЗаписьСсылка)
	
	МодульОбъекта().УстановитьГлобальноеСвойство("УчетнаяЗаписьПоУмолчанию", УчетнаяЗаписьСсылка);
	
КонецПроцедуры // УстановитьУчетнуюЗаписьОсновнойСервер()

&НаКлиенте
Процедура ОткрытьФормуУчетнойЗаписи(Команда)
	
	ОткрытьФормуУчетнойЗаписиДляРедактированияИлиДобавления(ложь);	
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуУчетнойЗаписиДляРедактированияИлиДобавления(ЭтоДобавление=Ложь)
	
	ТекСтрока = Элементы.СписокУчетныхЗаписей.ТекущиеДанные;
	
	Если (НЕ ТекСтрока=Неопределено) или ЭтоДобавление=Истина Тогда
		Если ТекСтрока<>Неопределено и ЗначениеЗаполнено(ТекСтрока.Ссылка) и не ЭтоДобавление Тогда
			СсылкаНаУЗ = ТекСтрока.Ссылка;
		иначе
			СсылкаНаУЗ = Неопределено;
		КонецЕсли;
		
		ПараметрыФормы=	Новый Структура;
		ПараметрыФормы.Вставить("СсылкаУчетнойЗаписи", 	СсылкаНаУЗ);
		ПараметрыФормы.Вставить("Новая", 				ЭтоДобавление);
		МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("УчетныеЗаписи_ЭлементУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбработчикПослеРедактированияУчетнойЗаписи", ЭтаФорма);
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикПослеРедактированияУчетнойЗаписи(ПараметрыЗакрытияИнтерактивнойФормы, ДополнительныеПараметры) Экспорт
	
	//обновить список УЗ
	ОбновитьСписокУчетныхЗаписей();	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокУчетныхЗаписейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОткрытьФормуУчетнойЗаписиДляРедактированияИлиДобавления(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОсновнаяФорма().ПриОткрытииФормы(ЭтаФорма, "УчетныеЗаписи_СписокУправляемая");

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	ОсновнаяФорма().ПриЗакрытииФормы(ЭтаФорма, "УчетныеЗаписи_СписокУправляемая");	
	
КонецПроцедуры
