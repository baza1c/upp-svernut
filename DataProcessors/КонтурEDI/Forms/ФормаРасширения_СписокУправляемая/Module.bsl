&НаСервере
Перем МодульОбъекта;

&НаКлиенте
Перем МодульОбъектаКлиент;

// СЛУЖЕБНЫЕ {

// Инициализация модуля и его экспортных функций
&НаСервере
Функция МодульОбъекта()
	
	Если МодульОбъекта = Неопределено Тогда
		
		Если Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект Тогда
			МодульОбъекта = МодульОбъектаИзКэша();
		КонецЕсли;
		
		Если МодульОбъекта = Неопределено Тогда
			МодульОбъекта = РеквизитФормыВЗначение("Объект");
			МодульОбъекта.ИнициализироватьПодключаемыеМодули(); // TODO Проверить что из этого используется на формах и перенести в ПараметрыКлиентСервер
			МодульОбъекта.ОбновитьКэшМодуляОбъекта();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат МодульОбъекта;
	
КонецФункции

&НаСервере
Функция МодульОбъектаИзКэша()
	
	Результат = Неопределено;
	
	Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда
		АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект;
 		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Структура = ПолучитьИзВременногоХранилища(АдресВХ);
			Если ТипЗнч(Структура) = Тип("Структура") и Структура.Свойство("ОбработкаОбъект") Тогда
			 	Результат = Структура.ОбработкаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульОбъектаКлиент()
	
	Если ТипЗнч(МодульОбъектаКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбъектаКлиент;
	КонецЕсли;
	
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		МодульОбъектаКлиент = ОсновнаяФорма.МодульОбъектаКлиент();
		Возврат МодульОбъектаКлиент;
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
	
	Если ЭтаФорма.ВладелецФормы = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ЭтаФорма.ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;
    	
КонецФункции

&НаКлиенте
Функция НоваяМетрика()
	
	Возврат ОсновнаяФорма().НоваяМетрика();
	
КонецФункции

// } СЛУЖЕБНЫЕ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
	Параметры.Свойство("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	
	Партнер = Параметры.Партнер;
	
	НастроитьФормуСогласноРежимуРаботы();
	ЗаполнитьСписокПартнеровСервер();
	ЗаполнитьДанныеОРасширениях();

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораЭлемента(Элемент, Список)
	
	Элемент.СписокВыбора.Очистить();
	
	Для каждого Эл Из Список Цикл
		Элемент.СписокВыбора.Добавить(Эл.Значение, Эл.Представление);
	КонецЦикла;	

КонецПроцедуры

&НаСервере
Процедура НастроитьФормуСогласноРежимуРаботы()

	МыПоставщик = МодульОбъекта().НастройкиМодуля.МыПоставщик;
	МыТорговаяСеть = МодульОбъекта().НастройкиМодуля.МыТорговаяСеть;	
	
	РежимРаботыДистрибьютор = МыПоставщик И МыТорговаяСеть;
	СписокРежимовРаботы = Новый СписокЗначений;
	
	Если РежимРаботыДистрибьютор Тогда
		СписокРежимовРаботы.Добавить("Поставщик,Сеть", "Поставщик и сеть (дистрибьютор)");
	КонецЕсли;
	
	Элементы.РежимРаботы.Видимость 						= РежимРаботыДистрибьютор;
	Элементы.СписокРасширенияДляПоставщика.Видимость 	= РежимРаботыДистрибьютор;
	Элементы.СписокРасширенияДляСети.Видимость 			= РежимРаботыДистрибьютор;
	
	Если МыПоставщик Тогда
		СписокРежимовРаботы.Добавить("Поставщик");
	КонецЕсли;
	Если МыТорговаяСеть Тогда
		СписокРежимовРаботы.Добавить("Сеть");
	КонецЕсли;
	
	ЗаполнитьСписокВыбораЭлемента(Элементы.РежимРаботы, СписокРежимовРаботы);
	РежимРаботы = Элементы.РежимРаботы.СписокВыбора.Получить(0).Значение;

КонецПроцедуры

&НаКлиенте
Процедура СписокРасширенияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ=Истина;
КонецПроцедуры

&НаКлиенте
Процедура СписокРасширенияПередУдалением(Элемент, Отказ)
	Отказ=Истина;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеОРасширениях()
	
	ДляПоставщика = НЕ Найти(РежимРаботы, "Поставщик") = 0;
	ДляСети = НЕ Найти(РежимРаботы, "Сеть") = 0;
	
	РезультатДанныеОРасширениях = МодульОбъекта().ПолучитьДанныеОРасширенияхССервера(ТолькоРекомендованные, 
																						Партнер, 
																						ДляПоставщика, 
																						ДляСети, 
																						ПоНазванию);
																						
							
	Если Не РезультатДанныеОРасширениях.Успешно Тогда
		
		ТекстПредупреждения = "Не удалось получить данные о расширениях: """ 
								+ РезультатДанныеОРасширениях.ОписаниеОшибки + """.";
								
		МодульОбъекта().ВывестиПредупреждение_КонтурEDI(ТекстПредупреждения);
		Отказ = Истина;
		
	КонецЕсли;
	
	ОтобразитьДанныеОРасширениях(РезультатДанныеОРасширениях.ДанныеОРасширениях);	
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьДанныеОРасширениях(ДанныеОРасширениях)
	
	//СписокРасширения.Очистить();
	Дерево = ДанныеФормыВЗначение(СписокРасширения,Тип("ДеревоЗначений"));
	Дерево.Строки.Очистить();
	
	//СОздатьПапки	
	ТипыРасширений=ДанныеОРасширениях.Скопировать(,"ТипРасширения");
	ТипыРасширений.Свернуть("ТипРасширения");
	ТипыРасширений.Сортировать("ТипРасширения");
	Для Каждого СтрокаТипа Из ТипыРасширений Цикл
		ГруппаТипа = Дерево.Строки.Добавить();
		ГруПпаТипа.Наименование = СтрокаТипа.ТипРасширения;
		СтрокиЭтогоТипа = ДанныеОРасширениях.НайтиСтроки(Новый Структура("ТипРасширения",СтрокаТипа.ТипРасширения));
		Для Каждого СтрокаЭтогоТипа Из СтрокиЭтогоТипа Цикл
			НоваяСтрокаРасширения = ГруппаТипа.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаРасширения,СтрокаЭтогоТипа);
			Если НоваяСтрокаРасширения.Установить = "обновить" Тогда 
				НоваяСтрокаРасширения.ИндексКартинки = 1;
			ИначеЕсли НоваяСтрокаРасширения.Установить = "подключен" Тогда 
				НоваяСтрокаРасширения.ИндексКартинки = 2;
			ИначеЕсли НоваяСтрокаРасширения.Установить = "установить" Тогда 
				НоваяСтрокаРасширения.ИндексКартинки = 3;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	Дерево.Строки.Сортировать("ТипРасширения");
	
	//СписокРасширения = ДанныеОРасширениях;	       
	//ЗначениеВРеквизитФормы(ДанныеОРасширениях,"СписокРасширения");
	ЗначениеВРеквизитФормы(Дерево,"СписокРасширения");
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРасширенияПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СписокРасширенияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Колонка = Поле;
	ТекСтрока = Элемент.ТекущиеДанные;
	
	Если ТекСтрока = Неопределено или Колонка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	//установка / обновление расширения
	Если Колонка.Имя = "СписокРасширенияУстановить" 
		И (ТекСтрока.Установить = "установить" ИЛИ ТекСтрока.Установить = "обновить") Тогда
		
		УстановитьРасширениеВызовСервера(ТекСтрока.Наименование, 
											ТекСтрока.Идентификатор, 
											ТекСтрока.ТекИнтерфейс, 
											ТекСтрока.Ссылка);
		
	ИначеЕсли Колонка.Имя = "СписокРасширенияУстановить" 
		И ТекСтрока.Установить = "удалить" Тогда
		
		УдалитьРасширениеВызовСервера(ТекСтрока.Ссылка);
		
	ИначеЕсли Колонка.Имя = "СписокРасширенияУстановить" 
		И ТекСтрока.Установить = "устарело" Тогда
		
		ТекстПредупреждения = "Когда-то расширение трудилось на благо поставщиков, но теперь оно никому не нужно.";
		МодульОбъектаКлиент().Предупреждение_КонтурEDI(ТекстПредупреждения);
		
		Возврат;
		
	КонецЕсли;
	
	РазвернутьРасширения();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьРасширениеВызовСервера(Наименование, Идентификатор, Интерфейс, Знач РасширениеСсылка = Неопределено)

	РезультатУстановки = МодульОбъекта().УстановитьРасширение(Наименование, Идентификатор, Интерфейс, РасширениеСсылка);
	
	Если Не РезультатУстановки.Успешно Тогда
		МодульОбъекта().ВывестиПредупреждение_КонтурEDI(РезультатУстановки.ОписаниеОшибки);
	Иначе
		ЗаполнитьДанныеОРасширениях();	
	КонецЕсли;

КонецПроцедуры // УстановитьРасширениеВызовСервера()

&НаСервере
Процедура УдалитьРасширениеВызовСервера(РасширениеСсылка)

	РезультатУдаления = МодульОбъекта().УдалитьРасширение(РасширениеСсылка);
	
	Если Не РезультатУдаления.Успешно Тогда
		МодульОбъекта().ВывестиПредупреждение_КонтурEDI(РезультатУдаления.ОписаниеОшибки);
	Иначе
		ЗаполнитьДанныеОРасширениях();	
	КонецЕсли;

КонецПроцедуры // УдалитьРасширениеВызовСервера()

&НаКлиенте
Процедура РазвернутьРасширения()
	
	Элементы1Ур = СписокРасширения.ПолучитьЭлементы();
	
	Для Каждого Элемент1Ур ИЗ Элементы1Ур Цикл
		Элементы.СписокРасширения.Развернуть(Элемент1Ур.ПолучитьИдентификатор());
	КонецЦикла;
	
КонецПроцедуры // РазвернутьРасширения()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РазвернутьРасширения();
	
	ОсновнаяФорма().ПриОткрытииФормы(ЭтаФорма, "ФормаРасширения_СписокУправляемая");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзменении_ЗаполнитьДанныеОРасширениях()
	
	Состояние("Ищем нужные расширения...",,"применяем отборы",БиблиотекаКартинок.ДобавитьВИзбранное);
	ЗаполнитьДанныеОРасширениях();
	РазвернутьРасширения();
	Состояние("Готово!",,,БиблиотекаКартинок.ВыполнитьЗадачу);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокПартнеровСервер()
	
	СписокПартнеров = Новый СписокЗначений;
	МассивПартнеров = МодульОбъекта().ПолучитьСписокЭлементовСправочника("Партнеры").ВыгрузитьКолонку("Ссылка");
	СписокПартнеров.ЗагрузитьЗначения(МассивПартнеров);
	
	ЗаполнитьСписокВыбораЭлемента(Элементы.Партнер, СписокПартнеров);

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	ОсновнаяФорма().ПриЗакрытииФормы(ЭтаФорма, "ФормаРасширения_СписокУправляемая");	
	
КонецПроцедуры
