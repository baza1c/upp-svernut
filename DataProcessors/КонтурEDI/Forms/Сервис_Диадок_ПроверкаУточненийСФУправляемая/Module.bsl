&НаСервере
Перем МодульОбъекта;

&НаКлиенте
Перем МодульОбъектаКлиент;

// СЛУЖЕБНЫЕ {

// Инициализация модуля и его экспортных функций
&НаСервере
Функция МодульОбъекта()
	
	Если МодульОбъекта = Неопределено Тогда
		
		Если Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект Тогда
			МодульОбъекта = МодульОбъектаИзКэша();
		КонецЕсли;
		
		Если МодульОбъекта = Неопределено Тогда
			МодульОбъекта = РеквизитФормыВЗначение("Объект");
			МодульОбъекта.ИнициализироватьПодключаемыеМодули(); // TODO Проверить что из этого используется на формах и перенести в ПараметрыКлиентСервер
			МодульОбъекта.ОбновитьКэшМодуляОбъекта();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат МодульОбъекта;
	
КонецФункции

&НаСервере
Функция МодульОбъектаИзКэша()
	
	Результат = Неопределено;
	
	Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда
		АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект;
 		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Структура = ПолучитьИзВременногоХранилища(АдресВХ);
			Если ТипЗнч(Структура) = Тип("Структура") и Структура.Свойство("ОбработкаОбъект") Тогда
			 	Результат = Структура.ОбработкаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульОбъектаКлиент()
	
	Если ТипЗнч(МодульОбъектаКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбъектаКлиент;
	КонецЕсли;
	
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		МодульОбъектаКлиент = ОсновнаяФорма.МодульОбъектаКлиент();
		Возврат МодульОбъектаКлиент;
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
	
	Если ЭтаФорма.ВладелецФормы = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ЭтаФорма.ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;
    	
КонецФункции

&НаКлиенте
Функция НоваяМетрика()
	
	Возврат ОсновнаяФорма().НоваяМетрика();
	
КонецФункции

// } СЛУЖЕБНЫЕ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
	Параметры.Свойство("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	
	Для Каждого СтрокаУточнения Из Параметры.МассивДокументовСУточнениями Цикл
		НоваяСтрока = ТаблицаСФ.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаУточнения);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьСчетаФактурыКакНеподписанные(Команда)
	ПометитьСчетаФактурыКакНеподписанныеСервер();	
	ЭтаФорма.Закрыть();
КонецПроцедуры

&НаСервере
Процедура ПометитьСчетаФактурыКакНеподписанныеСервер()

	_ТаблицаСФ=РеквизитФормыВЗначение("ТаблицаСФ");
	
	ТаблицаДокументов = _ТаблицаСФ.Скопировать(,"Документ");
	ТаблицаДокументов.Свернуть("Документ");
	
	Для каждого Стр ИЗ ТаблицаДокументов Цикл
		Сообщение = МодульОбъекта().ПрочитатьСообщение(,Стр.Документ,"INVOIC","Исходящее");
		МодульОбъекта().УдалитьСообщение(Сообщение,Стр.Документ);
		МодульОбъекта().УдалитьЗначениеСвойстваОбъекта(Стр.Документ, "boxId");
		МодульОбъекта().УдалитьЗначениеСвойстваОбъекта(Стр.Документ, "invoiceId");
		МодульОбъекта().УдалитьЗначениеСвойстваОбъекта(Стр.Документ, "messageId");
		МодульОбъекта().УдалитьЗначениеСвойстваОбъекта(Стр.Документ, "torg12Id");
	КонецЦикла;
	
КонецПроцедуры // ПометитьСчетаФактурыКакНеподписанные()

&НаКлиенте
Процедура ТаблицаСФВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
		
	Если Поле.Имя = "ТаблицаСФДокумент" Тогда
		Если Параметры.МодальностьЗапрещена Тогда 
			Выполнить("ПоказатьЗначение(Элемент.ТекущиеДанные.Документ)");
		Иначе
			ОткрытьЗначение(Элемент.ТекущиеДанные.Документ);
		КонецЕсли;
	Иначе
		ТекстПредупреждения=""+Элемент.ТекущиеДанные.Статус + Символы.ПС +Элемент.ТекущиеДанные.Комментарий;
		Если Параметры.МодальностьЗапрещена Тогда 
			Выполнить("ПоказатьПредупреждение(,ТекстПредупреждения,,""Контур.EDI"")");
		Иначе
			Предупреждение(ТекстПредупреждения,,"Контур.EDI");
		КонецЕсли;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОсновнаяФорма().ПриОткрытииФормы(ЭтаФорма, "Сервис_Диадок_ПроверкаУточненийСФУправляемая");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	ОсновнаяФорма().ПриЗакрытииФормы(ЭтаФорма, "Сервис_Диадок_ПроверкаУточненийСФУправляемая");	
	
КонецПроцедуры

