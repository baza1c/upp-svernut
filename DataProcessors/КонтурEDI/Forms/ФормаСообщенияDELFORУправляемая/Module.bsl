&НаСервере
Перем МодульОбъекта;

&НаКлиенте
Перем МодульОбъектаКлиент;

// Служебные

&НаСервере
Функция МодульОбъекта()
	
	Если МодульОбъекта = Неопределено Тогда
		
		Если Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект Тогда
			МодульОбъекта = МодульОбъектаИзКэша();
		КонецЕсли;
		
		Если МодульОбъекта = Неопределено Тогда
			МодульОбъекта = РеквизитФормыВЗначение("Объект");
			МодульОбъекта.ИнициализироватьПодключаемыеМодули();
			МодульОбъекта.ОбновитьКэшМодуляОбъекта();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат МодульОбъекта;
	
КонецФункции

&НаСервере
Функция МодульОбъектаИзКэша()
	
	Результат = Неопределено;
	
	Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда
		АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект;
 		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Структура = ПолучитьИзВременногоХранилища(АдресВХ);
			Если ТипЗнч(Структура) = Тип("Структура") и Структура.Свойство("ОбработкаОбъект") Тогда
			 	Результат = Структура.ОбработкаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульОбъектаКлиент()
	
	Если ТипЗнч(МодульОбъектаКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбъектаКлиент;
	КонецЕсли;
	
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		МодульОбъектаКлиент = ОсновнаяФорма.МодульОбъектаКлиент();
		Возврат МодульОбъектаКлиент;
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
	
	Если ЭтаФорма.ВладелецФормы = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ЭтаФорма.ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;
    	
КонецФункции

// Заполнение формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
	Параметры.Свойство("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	
	ТипСообщения = "DELFOR";
	РежимРаботы = Параметры.РежимРаботы;
	
	Если РежимРаботы = "Поставщик" Тогда
		СторонаЮрФизЛицаПартнера = "Покупатель";
		СторонаЮрФизЛицаОрганизации = "Продавец";	
	ИначеЕсли РежимРаботы = "Покупатель" Тогда
		СторонаЮрФизЛицаПартнера = "Продавец";
		СторонаЮрФизЛицаОрганизации = "Покупатель";	
	КонецЕсли;
	
	Если Параметры.Свойство("ПереотправляемоеСообщениеСсылка") И ЗначениеЗаполнено(Параметры.ПереотправляемоеСообщениеСсылка) Тогда
		ПереотправляемоеСообщениеСсылка = Параметры.ПереотправляемоеСообщениеСсылка;
	КонецЕсли;
	
	Если Параметры.Свойство("СообщениеСсылка") И ЗначениеЗаполнено(Параметры.СообщениеСсылка) Тогда
		СообщениеСсылка = Параметры.СообщениеСсылка;
		Сообщение = МодульОбъекта().ПрочитатьСообщение(СообщениеСсылка,, ТипСообщения, Параметры.Направление);
		ЗаполнитьПоляПоСообщению(Сообщение);
		ПредставлениеПолучателя = МодульОбъекта().ПолучитьПредставлениеЭлементаСправочника(Получатель);
	Иначе
		ЗаполнитьПоляНаФормеЗначениямиПоУмолчанию();	
	КонецЕсли;	
	
	ВыполнитьНастройкиФормы();
			  
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоляПоСообщению(Сообщение, ЭтоЗаполнениеНаОсновании = Ложь)
		
	Если НЕ ЭтоЗаполнениеНаОсновании Тогда	
		ДатаDELFOR = Сообщение.ДокументEDI.Дата;
		НомерDELFOR = Сообщение.ДокументEDI.Номер;
	
		СтатусDELFOR = "";
		
		Если Сообщение.Направление = "Исходящее" Тогда						//Сохраненное исходящее сообщение
			ТипDELFOR = Сообщение.ТипDELFOR;
			ТолькоПросмотр = НЕ ЗначениеЗаполнено(ПереотправляемоеСообщениеСсылка);
		Иначе																//Входящее сообщение
			ЗаполнениеПоВходящемуСообщению = Истина;
			Если Сообщение.ТипDELFOR = "Schedule" Тогда
				ТипDELFOR = "Response";	
			ИначеЕсли Сообщение.ТипDELFOR = "Response" Тогда
				ТипDELFOR = "Response входящий";	
			КонецЕсли;
		КонецЕсли;
			
		Если ЗаполнениеПоВходящемуСообщению Тогда
			Если ТипDELFOR = "Response входящий" Тогда
				Если Сообщение.СтатусDELFOR = "AcceptedWithAmendment" Тогда
					СтатусDELFOR = "Принят с уточнениями";
				ИначеЕсли Сообщение.СтатусDELFOR = "Accepted" Тогда
					СтатусDELFOR = "Принят";
				ИначеЕсли Сообщение.СтатусDELFOR = "NotAccepted" Тогда
					СтатусDELFOR = "Отклонен";	
				КонецЕсли; 
			КонецЕсли;
			Отправитель = Сообщение.Получатель1С;
			Получатель = Сообщение.Отправитель1С;			
		Иначе
			Отправитель = Сообщение.Отправитель1С;
			Если ЗначениеЗаполнено(Сообщение.Получатель1С) Тогда
				Получатель = Сообщение.Получатель1С;
			Иначе
				Получатель = Сообщение.Партнер;
			КонецЕсли;
		КонецЕсли;
		
		ЗаполнитьСписокЮрФизЛицПартнера(Получатель);
		
		Продавец 	= Сообщение.Продавец1С;
		Покупатель 	= Сообщение.Покупатель1С;

		ПериодГрафика.ДатаНачала = Сообщение.НачалоДействияГрафика;	
		ПериодГрафика.ДатаОкончания = Сообщение.ОкончаниеДействияГрафика;
		ДлительностьГрафика = Сообщение.ДлительностьГрафика;
		
		НомерДоговора = Сообщение.НомерДоговора;
		ДатаДоговора = Сообщение.ДатаДоговора;
		
		РегионПродаж = Сообщение.РегионПродаж;
		СегментАссортимента = Сообщение.СегментАссортимента;
		
		Комментарий = Сообщение.Комментарий;
		
		Заголовок = ПолучитьЗаголовокDELFOR(Сообщение.Направление, Сообщение.ТипDELFOR, ДатаDELFOR, НомерDELFOR, СтатусDELFOR);
		
	КонецЕсли;

	Графики.Очистить();
	НСтр = 1;
	
	Для Каждого ГрафикСообщения Из Сообщение.Графики Цикл
		НСтрГрафиков = Графики.Добавить();
		ЗаполнитьЗначенияСвойств(НСтрГрафиков, ГрафикСообщения);
		НСтрГрафиков.ПериодГрафика.ДатаНачала = ГрафикСообщения.НачалоДействияГрафика;
		НСтрГрафиков.ПериодГрафика.ДатаОкончания = ГрафикСообщения.ОкончаниеДействияГрафика;
		НСтрГрафиков.ТорговаяТочка = МодульОбъекта().ОпределитьТочкуДоставкиПоGLN(НСтрГрафиков.ТорговаяТочкаGLN, РежимРаботы);
		Если Не ЗначениеЗаполнено(НСтрГрафиков.ТорговаяТочка) Тогда
			ТаблицаОшибок.Добавить().ОписаниеОшибки = "Не удалось определить торговую точку по GLN " + НСтрГрафиков.ТорговаяТочкаGLN + " в строке № " + НСтр;
		КонецЕсли;
		НСтр = НСтр + 1;
	КонецЦикла;
	
	ОткрытьПанельОшибокПриНеобходимостиСервер();
			
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоляНаФормеЗначениямиПоУмолчанию()
	
	ДатаDELFOR = ТекущаяДата();
	НомерDELFOR = Формат(ТекущаяДата(), "ДФ=ггггММддЧЧммсс");
	ТипDELFOR ="Schedule";
	Заголовок = ПолучитьЗаголовокDELFOR("Исходящее", ТипDELFOR, ДатаDELFOR, НомерDELFOR);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьЗаголовокDELFOR(Направление, Тип, Дата, Номер, Статус = "")
	
	Если Направление = "Исходящее" Тогда
		НаправлениеЗаголовок = "Исходящий";
	Иначе
		НаправлениеЗаголовок = "Входящий";
	КонецЕсли;
	
	Если Тип = "Schedule" Тогда
		ТипЗаголовок = "график поставок";
	ИначеЕсли Тип = "Response" Тогда
		ТипЗаголовок = "ответ на график поставок";
	Иначе
		ВызватьИсключение "Неверный тип DELFOR";
	КонецЕсли;
	
	Возврат НаправлениеЗаголовок + " " + ТипЗаголовок + " (" + "DELFOR " + Тип + ")" +
			" № " + Номер + " от " + Формат(Дата, "ДЛФ=Д") + " " + Статус;
   
КонецФункции

&НаСервере
Процедура ВыполнитьНастройкиФормы()

	НастройкиФормы = МодульОбъекта().ПолучитьНастройкиФормыНезависимогоСообщения(ТипСообщения, Направление);
	ЗаполнитьСпискиВыбораПолей();
	ЗаполнитьСпискиВыбораКолонок();
    УстановитьВидимостьДоступностьЭлементов();
	УстановитьТипыПолей1С();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСпискиВыбораПолей()
	
	ЗаполнитьСписокВыбораЭлемента(Элементы.ДлительностьГрафика, НастройкиФормы.СпискиВыбораПолей.СписокДлительностейГрафиков);
		
	СписокЮрФизЛицСвоих = Новый СписокЗначений;
	СписокЮрФизЛицСвоих.ЗагрузитьЗначения(МодульОбъекта().ПолучитьСписокЭлементовСправочника("ЮрФизЛицаСвои").ВыгрузитьКолонку("ЮрФизЛицо"));
	
	ЗаполнитьСписокВыбораЭлемента(Элементы.Отправитель, СписокЮрФизЛицСвоих);
	ЗаполнитьСписокВыбораЭлемента(Элементы[СторонаЮрФизЛицаОрганизации], СписокЮрФизЛицСвоих);
	
	Если Элементы.Отправитель.СписокВыбора.Количество() = 1 Тогда
		Отправитель = Элементы.Отправитель.СписокВыбора.Получить(0).Значение;
		ЭтаФорма[СторонаЮрФизЛицаОрганизации] = Отправитель;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСпискиВыбораКолонок()
		
	ЗаполнитьСписокВыбораЭлемента(Элементы.ГрафикиСтатусСогласования, 	НастройкиФормы.СпискиВыбораКолонок.СписокСогласований);  
	ЗаполнитьСписокВыбораЭлемента(Элементы.ГрафикиСтатусГрафика, 		НастройкиФормы.СпискиВыбораКолонок.СписокСтатусовГрафиков);
	ЗаполнитьСписокВыбораЭлемента(Элементы.ГрафикиДлительностьГрафика, 	НастройкиФормы.СпискиВыбораПолей.СписокДлительностейГрафиков);
	ЗаполнитьСписокВыбораЭлемента(Элементы.ГрафикиРегулярностьВНеделях, НастройкиФормы.СпискиВыбораКолонок.СписокПериодичностейНедели);
	ЗаполнитьСписокВыбораЭлемента(Элементы.ГрафикиРегулярностьВДнях, 	НастройкиФормы.СпискиВыбораКолонок.СписокПериодичностейДни);
	
	СписокЕдиницИзмеренияВеса = МодульОбъекта().ЗагрузитьКлассификаторИзМакета("КодЕдиницИзмеренияВеса"); 
	ЗаполнитьСписокВыбораЭлемента(Элементы.ГрафикиМинимальныйВесЗаказаЕдИзм, СписокЕдиницИзмеренияВеса);
	
	СписокЕдиницИзмеренияПаллет = Новый СписокЗначений();
	СписокЕдиницИзмеренияПаллет.Добавить("PCE", "шт.");
	ЗаполнитьСписокВыбораЭлемента(Элементы.ГрафикиМинимальноеКолличествоПаллетЕдИзм, СписокЕдиницИзмеренияПаллет);
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораЭлемента(Элемент, Список)
	
	Элемент.СписокВыбора.Очистить();
	
	Для каждого Эл Из Список Цикл
		Элемент.СписокВыбора.Добавить(Эл.Значение, Эл.Представление);
	КонецЦикла;	

КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступностьЭлементов()
	
	// По умолчанию все элементы доступны и видимы
	Элементы.ОтправитьСообщение.Видимость = НЕ ТолькоПросмотр;
	Если ТипDELFOR = "Schedule" Тогда 
		Элементы.ГрафикиКоманднаяПанель.ПодчиненныеЭлементы.КнопкиResponse.Видимость = Ложь;
		Элементы.ГрафикиСтатусСогласования.Видимость = Ложь;
		Элементы.ОтправитьСообщение.Заголовок = "Отправить график";
	ИначеЕсли ТипDELFOR = "Response" ИЛИ ТипDELFOR = "Response входящий"  Тогда
		Элементы.ГрафикиКоманднаяПанель.ПодчиненныеЭлементы.КнопкиSchedule.Видимость = Ложь;
		Элементы.Графики.ТолькоПросмотр = Истина;
		Элементы.ГруппаШапка.ТолькоПросмотр = Истина;
		Элементы.ДополнительнаяИнформацияСтраница.ТолькоПросмотр = Истина;
		Элементы.ДлительностьГрафика.Видимость = Ложь;
		Элементы.ПериодГрафика.Видимость = Ложь;
		Если ТипDELFOR = "Response" Тогда
			Элементы.ОтправитьСообщение.Заголовок = "Отправить ответ";
		ИначеЕсли ТипDELFOR = "Response входящий" Тогда
			Элементы.ГрафикиСтатусГрафика.Видимость = Ложь;
			Элементы.ГрафикиКоманднаяПанель.ПодчиненныеЭлементы.КнопкиResponse.Видимость = Ложь;
			Элементы.ОтправитьСообщение.Заголовок = "Принять ответ";
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПереотправляемоеСообщениеСсылка) Тогда
		Элементы.ОтправитьСообщение.Заголовок = "Переотправить сообщение";
		ОписаниеОшибки = Параметры.СообщениеСсылка.ОписаниеОшибки; 
		Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
			ТаблицаОшибок.Добавить().ОписаниеОшибки = ОписаниеОшибки;
		КонецЕсли;
	КонецЕсли;
	
	ОткрытьПанельОшибокПриНеобходимостиСервер();

КонецПроцедуры

&НаСервере
Процедура УстановитьТипыПолей1С()
	
	ТипЮрФизЛицоСвое 		= МодульОбъекта().ПолучитьТипЗначенияОбъекта("ЮрФизЛицоСвое");
    ТипЮрФизЛицоСтороннее	= МодульОбъекта().ПолучитьТипЗначенияОбъекта("ЮрФизЛицоСтороннее");
	
	Если ЗначениеЗаполнено(ТипЮрФизЛицоСвое) Тогда
		Элементы.Отправитель.ОграничениеТипа = Новый ОписаниеТипов(ТипЮрФизЛицоСвое);
		Элементы[СторонаЮрФизЛицаОрганизации].ОграничениеТипа = Новый ОписаниеТипов(ТипЮрФизЛицоСвое);
		
		Если Не ЗначениеЗаполнено(Отправитель) Тогда
			ПустаяСсылкаЮрФизЛицоСвое = МодульОбъекта().ПолучитьПустуюСсылкуОбъекта("ЮрФизЛицоСвое");
			Отправитель = ПустаяСсылкаЮрФизЛицоСвое;
			ЭтаФорма[СторонаЮрФизЛицаОрганизации] = ПустаяСсылкаЮрФизЛицоСвое;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТипЮрФизЛицоСтороннее) Тогда
		Элементы[СторонаЮрФизЛицаПартнера].ОграничениеТипа = Новый ОписаниеТипов(ТипЮрФизЛицоСтороннее);
		
		Если Не ЗначениеЗаполнено(ЭтаФорма[СторонаЮрФизЛицаПартнера]) Тогда
			ПустаяСсылкаЮрФизЛицоСтороннее = МодульОбъекта().ПолучитьПустуюСсылкуОбъекта("ЮрФизЛицоСтороннее");
			ЭтаФорма[СторонаЮрФизЛицаПартнера] = ПустаяСсылкаЮрФизЛицоСтороннее;
		КонецЕсли;
	КонецЕсли;
	
	Если РежимРаботы = "Поставщик" Тогда
		СвойСправочникТочекДоставки = МодульОбъекта().ПолучитьКонстантуEDI("СвойСправочникТочекДоставки");
		Если Не ЗначениеЗаполнено(СвойСправочникТочекДоставки) Тогда
			ТипТочкиДоставки = "СправочникСсылка.КонтурEDI_ТочкиДоставки";
			
			НоваяСвязь = Новый СвязьПараметраВыбора("Отбор.Партнер", "Получатель");
			МассивСвязей = Новый Массив();
			МассивСвязей.Добавить(НоваяСвязь);
			Элементы.ГрафикиТорговаяТочка.СвязиПараметровВыбора = Новый ФиксированныйМассив(МассивСвязей);
		Иначе
			//TODO - свой справочник ТД
		КонецЕсли;
	ИначеЕсли РежимРаботы = "Покупатель" Тогда
		ТипТочкиДоставки = МодульОбъекта().ПолучитьТипЗначенияОбъекта("ТочкаДоставкиСвоя");		
	КонецЕсли;
	Элементы.ГрафикиТорговаяТочка.ОграничениеТипа = Новый ОписаниеТипов(ТипТочкиДоставки);	
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СформироватьДанныеСтрокиПоУмолчанию();
	
	ОсновнаяФорма().ПриОткрытииФормы(ЭтаФорма, "ФормаСообщенияDELFORУправляемая");
	
КонецПроцедуры

//Вспомагательные 

&НаСервере
Процедура ЗаполнитьТорговыеТочкиНаСервере()
		
	Если РежимРаботы = "Поставщик" Тогда
		Если НЕ ЗначениеЗаполнено(Получатель) Тогда
			Сообщить("Не указан партнер");
			Возврат;
		КонецЕсли;
		РеквизитСсылкиТД = "Ссылка";
	ИначеЕсли РежимРаботы = "Покупатель" Тогда
		РеквизитСсылкиТД = "ТочкаДоставки";
	КонецЕсли;
	
	СписокТорговыхТочек = ПолучитьСписокТорговыхТочек();
	
	Графики.Очистить();
	Для Каждого ТорговаяТочка Из СписокТорговыхТочек Цикл
		НовыйГрафик = Графики.Добавить();	
		НовыйГрафик.ТорговаяТочка = ТорговаяТочка[РеквизитСсылкиТД];
		НовыйГрафик.ТорговаяТочкаGLN = ТорговаяТочка.GLN; 
		ЗаполнитьЗначенияСвойств(НовыйГрафик, ДанныеСтрокиПоУмолчанию); 
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьДанныеСтрокиПоУмолчанию()
	
	ДанныеСтрокиПоУмолчанию = Новый Структура;
	ДанныеСтрокиПоУмолчанию.Вставить("СтатусГрафика", "Добавлен");
	ДанныеСтрокиПоУмолчанию.Вставить("ДлительностьГрафика", ДлительностьГрафика);
	ДанныеСтрокиПоУмолчанию.Вставить("ПериодГрафика", ПериодГрафика);
	ДанныеСтрокиПоУмолчанию.Вставить("МинимальныйВесЗаказаЕдИзм", "KGM");
	ДанныеСтрокиПоУмолчанию.Вставить("МинимальноеКолличествоПаллетЕдИзм", "PCE");
	
КонецПроцедуры
	
&НаСервере
Функция ПолучитьСписокТорговыхТочек()
	                                      
	Если РежимРаботы = "Поставщик" Тогда	
		Возврат МодульОбъекта().ПолучитьСписокЭлементовСправочника("ТочкиДоставкиСторонние", Получатель); 
	ИначеЕсли РежимРаботы = "Покупатель" Тогда
		Возврат МодульОбъекта().ПолучитьСписокЭлементовСправочника("ТочкиДоставкиСвои", Отправитель);
	КонецЕсли;
	
КонецФункции 	

&НаСервере
Функция ПолучитьGLNТочкиДоставки(ТочкаДоставки)
	
	Если ТипЗнч(ТочкаДоставки) = Тип("СправочникСсылка.КонтурEDI_ТочкиДоставки") Тогда	//Сторонняя точка доставки
		Возврат ТочкаДоставки.GLN; 		
	Иначе																				//Наша точка доставки
		Возврат МодульОбъекта().ПолучитьЗначениеСвойстваОбъектаEDI(ТочкаДоставки, "GLN_НашейТочкиДоставки");
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьПанельОшибокКлиент()
	
	Элементы.ГруппаОшибки.Видимость = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОткрытьПанельОшибокПриНеобходимостиСервер()
	
	Если ТаблицаОшибок.Количество() Тогда
		Элементы.ГруппаОшибки.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ЗакрытьПанельОшибокКлиент()
	
	Элементы.ГруппаОшибки.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнениеФормы()

	ТаблицаОшибок.Очистить();
	
	Если ЗначениеЗаполнено(ДатаДоговора) И Не ЗначениеЗаполнено(НомерДоговора) Тогда
		ТаблицаОшибок.Добавить().ОписаниеОшибки = "Не заполнено поле Номер договора";		
	КонецЕсли;
	Если ЗначениеЗаполнено(НомерДоговора) И Не ЗначениеЗаполнено(ДатаДоговора) Тогда
		ТаблицаОшибок.Добавить().ОписаниеОшибки = "Не заполнено поле Дата договора";		
	КонецЕсли;      
	
	ОбязательныеПоляШапки = НастройкиФормы.ОбязательныеПоляШапки;
	
	Для Каждого ОбязательноеПолеШапки Из ОбязательныеПоляШапки Цикл
		Если НЕ ЗначениеЗаполнено(ЭтаФорма[ОбязательноеПолеШапки.Ключ]) Тогда
			ТаблицаОшибок.Добавить().ОписаниеОшибки = "Не заполнено поле " + ОбязательноеПолеШапки.Значение;	
		КонецЕсли;
	КонецЦикла;
	
	ОбязательныеПоляСтроки = НастройкиФормы.ОбязательныеПоляТЧ;
	
	Если Графики.Количество() Тогда 
		НСтр = 1;
		Для Каждого СтрокаГрафика Из Графики Цикл
			
			Для Каждого ОбязательноеПолеСтроки Из ОбязательныеПоляСтроки Цикл
				Если НЕ ЗначениеЗаполнено(СтрокаГрафика[ОбязательноеПолеСтроки.Ключ]) Тогда
					ТаблицаОшибок.Добавить().ОписаниеОшибки = "Не заполнено обязательное поле " + ОбязательноеПолеСтроки.Значение + " в строке № " + НСтр;
				КонецЕсли;	
			КонецЦикла;
			
			Если ТипDELFOR = "Schedule" Тогда
				Если СтрокаГрафика.СтатусГрафика = "Удален" И НЕ ЗначениеЗаполнено(СтрокаГрафика.ДатаУдаления) Тогда
					ТаблицаОшибок.Добавить().ОписаниеОшибки = "Не заполнено обязательное поле Дата удаления в строке № " + НСтр;
				КонецЕсли;
			КонецЕсли;
			
			Если ТипDELFOR = "Response" Тогда
				Если Не ЗначениеЗаполнено(СтрокаГрафика.СтатусСогласования) Тогда
					ТаблицаОшибок.Добавить().ОписаниеОшибки = "Не заполнено согласование в строке № " + НСтр;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(СтрокаГрафика.ТорговаяТочкаGLN) Тогда
					Если Не ЗначениеЗаполнено(СтрокаГрафика.ТорговаяТочка) Тогда
						ТаблицаОшибок.Добавить().ОписаниеОшибки = "Не удалось определить торговую точку по GLN " + СтрокаГрафика.ТорговаяТочкаGLN + " в строке № " + НСтр;		
					КонецЕсли;
				Иначе
					ТаблицаОшибок.Добавить().ОписаниеОшибки = "Не заполнен GLN торговой точки в строке № " + НСтр;	
				КонецЕсли;
			КонецЕсли;
			
			НСтр = НСтр + 1;
		КонецЦикла;
	Иначе
		ТаблицаОшибок.Добавить().ОписаниеОшибки = "В табличной части не указан ни один график";
	КонецЕсли;
	
	ФормаСодержитОшибки = ТаблицаОшибок.Количество();

КонецПроцедуры

&НаСервереБезКонтекста                                                   
Функция ПолучитьСписокПоследнихСообщенийDELFOR_ВХ(Получатель, ТипСообщения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 30
	|	КонтурEDI_Сообщения.Ссылка КАК Код,
	|	КонтурEDI_Сообщения.Партнер КАК Партнер,
	|	КонтурEDI_Сообщения.НомерДокумента КАК НомерДокумента,
	|	КонтурEDI_Сообщения.ДатаДокумента КАК ДатаДокумента,
	|	КонтурEDI_Сообщения.НомерЗаказа КАК НомерДоговора,
	|	КонтурEDI_Сообщения.ДатаЗаказа КАК ДатаДоговора
	|ИЗ
	|	Справочник.КонтурEDI_Сообщения КАК КонтурEDI_Сообщения
	|ГДЕ
	|	КонтурEDI_Сообщения.Партнер = &Партнер
	|	И КонтурEDI_Сообщения.ТипСообщения = &ТипСообщения
	|	И КонтурEDI_Сообщения.Направление = ""Исходящее""
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента УБЫВ";
	
	Запрос.УстановитьПараметр("Партнер", Получатель);
	Запрос.УстановитьПараметр("ТипСообщения", ТипСообщения);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	ЕстьСообщенияПоПартнеру = Истина;
	Если НЕ РезультатЗапроса.Количество() Тогда
		ЕстьСообщенияПоПартнеру = Ложь;	
	КонецЕсли;
	
	Возврат Новый Структура("ЕстьСообщенияПоПартнеру,Результат", ЕстьСообщенияПоПартнеру, ПоместитьВоВременноеХранилище(РезультатЗапроса));
	
КонецФункции

&НаСервере
Функция ПолучитьПараметрыДляПодготовкиСообщения(ТипСообщения, ДопПараметры = Неопределено)
	
	СтруктураПараметры = Новый Структура;
	                                                                        
	СтруктураПараметры.Вставить("НомерDELFOR", 					НомерDELFOR);
	СтруктураПараметры.Вставить("ДатаDELFOR", 					ДатаDELFOR);
	СтруктураПараметры.Вставить("ТипDELFOR", 					ТипDELFOR);
	СтруктураПараметры.Вставить("Отправитель", 					Отправитель);
	СтруктураПараметры.Вставить("Получатель", 					Получатель);
	СтруктураПараметры.Вставить("Покупатель", 					Покупатель); 
	СтруктураПараметры.Вставить("Продавец", 					Продавец);
	СтруктураПараметры.Вставить("Партнер", 						Получатель);
	СтруктураПараметры.Вставить("НачалоДействияГрафика", 		ПериодГрафика.ДатаНачала);	
	СтруктураПараметры.Вставить("ОкончаниеДействияГрафика", 	ПериодГрафика.ДатаОкончания);
	СтруктураПараметры.Вставить("ДлительностьГрафика", 			ДлительностьГрафика);
	СтруктураПараметры.Вставить("НомерДоговора", 				НомерДоговора);
	СтруктураПараметры.Вставить("ДатаДоговора", 				ДатаДоговора);
	СтруктураПараметры.Вставить("РегионПродаж", 				РегионПродаж);
	СтруктураПараметры.Вставить("СегментАссортимента", 			СегментАссортимента);
	СтруктураПараметры.Вставить("Комментарий", 					Комментарий);
	
	Если ТипDELFOR = "Response" Тогда
		СтруктураПараметры.Вставить("СсылкаНаВходящееСообщение",	СообщениеСсылка);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПереотправляемоеСообщениеСсылка) Тогда
		СтруктураПараметры.Вставить("ПереотправляемоеСообщениеСсылка", ПереотправляемоеСообщениеСсылка);
	КонецЕсли;
	
	Возврат СтруктураПараметры;
	
КонецФункции

&НаСервере
Функция ПолучитьСписокЮрФизЛицПартнера(Получатель)
		
	СписокЮрФизЛицПартнера = Новый СписокЗначений;
	
	ДанныеПартнера = МодульОбъекта().ТаблицаКэшПартнеровКонтрагентов.НайтиСтроки(Новый Структура("Партнер", Получатель));
	Для Каждого Элемент Из ДанныеПартнера Цикл
		СписокЮрФизЛицПартнера.Добавить(Элемент.Контрагент);		
	КонецЦикла;
	
	Возврат СписокЮрФизЛицПартнера;
	
КонецФункции

//Обработчики событий элементов формы

&НаКлиенте
Процедура ОтправительОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ЭтаФорма[СторонаЮрФизЛицаОрганизации] = ВыбранноеЗначение;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПолучателяНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Состояние("Открытие формы выбора партнеров");
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОткрытаКакВыбор", Истина);
	
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Партнеры_СписокУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбработчикВыбораПартнера", ЭтаФорма);
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикВыбораПартнера(Значение, ДопПараметр = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(Значение) И Значение <> Получатель Тогда 	
		Состояние("Обработка выбора партнера");
		Получатель = Значение;
		ОбработчикВыбораПартнераСервер();	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПолучателяОчистка(Элемент, СтандартнаяОбработка)
	
	Состояние("Очистка списка юр. лиц партнера");
	ПредставлениеПолучателяОчисткаСервер();
	
КонецПроцедуры

&НаСервере
Процедура ПредставлениеПолучателяОчисткаСервер()

	ПустойСписок = Новый СписокЗначений;
	ЗаполнитьСписокВыбораЭлемента(Элементы[СторонаЮрФизЛицаПартнера], ПустойСписок);	
	ЭтаФорма[СторонаЮрФизЛицаПартнера] = МодульОбъекта().ПолучитьПустуюСсылкуОбъекта("ЮрФизЛицоСтороннее");

КонецПроцедуры 

&НаСервере
Процедура ОбработчикВыбораПартнераСервер()
	
	ПредставлениеПолучателя = МодульОбъекта().ПолучитьПредставлениеЭлементаСправочника(Получатель);
	ЗаполнитьСписокЮрФизЛицПартнера(Получатель);
	ВыбратьЮрФизЛицоПартнераПоУмолчанию(СторонаЮрФизЛицаПартнера);
	
	Если РежимРаботы = "Поставщик" Тогда
		Графики.Очистить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыбратьЮрФизЛицоПартнераПоУмолчанию(СторонаЮрФизЛицаПартнера)

	Если Элементы[СторонаЮрФизЛицаПартнера].СписокВыбора.Количество() = 1 Тогда
		ЭтаФорма[СторонаЮрФизЛицаПартнера] = Элементы[СторонаЮрФизЛицаПартнера].СписокВыбора.Получить(0).Значение;
	Иначе
		ЭтаФорма[СторонаЮрФизЛицаПартнера] = МодульОбъекта().ПолучитьПустуюСсылкуОбъекта("ЮрФизЛицоСтороннее");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокЮрФизЛицПартнера(Получатель)

   	СписокЮрФизЛицПартнера = ПолучитьСписокЮрФизЛицПартнера(Получатель);
	ЗаполнитьСписокВыбораЭлемента(Элементы[СторонаЮрФизЛицаПартнера], СписокЮрФизЛицПартнера);	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТорговыеТочки(Команда)
	
	Если Графики.Количество() Тогда	
		ТекстВопроса = "Графики будут перезаполнены, продолжить?";
		ИмяОбработчика = "ОбработчикВопросаЗаполненияТД";
		ПараметрыОбработчика = Неопределено;
		МодульОбъектаКлиент().ПоказатьВопрос_КонтурEDI(ИмяОбработчика, ЭтаФорма, ПараметрыОбработчика, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);	
	Иначе
		ЗаполнитьТорговыеТочкиНаСервере();	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикВопросаЗаполненияТД(Результат, ДопПараметры = Неопределено) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		ЗаполнитьТорговыеТочкиНаСервере();	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГрафикиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И Не Копирование Тогда
		ЗаполнитьЗначенияСвойств(Элемент.ТекущиеДанные, ДанныеСтрокиПоУмолчанию);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьСообщение(Команда)
	
	Если ТипDELFOR = "Response входящий" Тогда
		ПринятьСообщениеСервер();			
	Иначе
		ПроверитьЗаполнениеФормы();
		Если ФормаСодержитОшибки Тогда
			ОткрытьПанельОшибокКлиент();
			Возврат;
		КонецЕсли;
		
		ОтправитьСообщениеСервер();
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

&НаСервере                                                   
Процедура ПринятьСообщениеСервер()
	
	СтруктураСообщения = МодульОбъекта().ПрочитатьСообщение(СообщениеСсылка);
	МодульОбъекта().КнопкаФормыСообщенияНажатие("ОтметитьКакОбработанное", СтруктураСообщения);	
	
КонецПроцедуры

&НаСервере
Процедура ОтправитьСообщениеСервер()
	                                                                             
	Для Каждого СтрГрафиков Из Графики Цикл 
		СтрГрафиков.НачалоДействияГрафика = СтрГрафиков.ПериодГрафика.ДатаНачала;
	    СтрГрафиков.ОкончаниеДействияГрафика = СтрГрафиков.ПериодГрафика.ДатаОкончания;
	КонецЦикла;
	ГрафикиКопия = РеквизитФормыВЗначение("Графики");
	СтруктураПараметры = ПолучитьПараметрыДляПодготовкиСообщения(ТипСообщения);
	МодульОбъекта().ОтправитьНезависимоеСообщение(ТипСообщения, ГрафикиКопия, СтруктураПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ДлительностьГрафикаПриИзменении(Элемент)
	
	СформироватьДанныеСтрокиПоУмолчанию();
	
	Если Не ЗначениеЗаполнено(ДлительностьГрафика) Тогда
		Возврат;	
	КонецЕсли;
		
	Для Каждого График Из Графики Цикл
		Если НЕ ЗначениеЗаполнено(График.ДлительностьГрафика) Тогда
			График.ДлительностьГрафика = ДлительностьГрафика;		
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодГрафикаПриИзменении(Элемент)
	
	СформироватьДанныеСтрокиПоУмолчанию();
	
	Если Не ЗначениеЗаполнено(ПериодГрафика) Тогда
		Возврат;	
	КонецЕсли;
	
	Для Каждого График Из Графики Цикл
		Если НЕ ЗначениеЗаполнено(График.ПериодГрафика) Тогда
			График.ПериодГрафика = ПериодГрафика;		
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьТаблицуОшибок(Команда)
	
	ЗакрытьПанельОшибокКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ГрафикиТорговаяТочкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Графики.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		Если ЗначениеЗаполнено(ТекущиеДанные.ТорговаяТочка) Тогда
			ТекущиеДанные.ТорговаяТочкаGLN = ПолучитьGLNТочкиДоставки(ТекущиеДанные.ТорговаяТочка);
		Иначе
			ТекущиеДанные.ТорговаяТочкаGLN = "";	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СогласоватьВсе(Команда)
	
	УстановитьСтатусСогласованияСтрок("Принят");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтклонитьВсе(Команда)
	
	УстановитьСтатусСогласованияСтрок("Отклонен");
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусСогласованияСтрок(Статус)
	
	Для Каждого СтрокаГрафика Из Графики Цикл
		СтрокаГрафика.СтатусСогласования = Статус;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОшибки(Команда)
	
	ПроверитьЗаполнениеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ГрафикиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ТипDELFOR = "Response" И Поле.Имя = "ГрафикиСтатусСогласования" Тогда
		Если Элемент.ТекущиеДанные.СтатусСогласования = "Принят" Тогда
			Элемент.ТекущиеДанные.СтатусСогласования = "Отклонен";
		Иначе
			Элемент.ТекущиеДанные.СтатусСогласования = "Принят";
		КонецЕсли;		
	КонецЕсли;
			
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЗначениеВоВсехСтроках(Команда)
	
	ИмяКолонки = СтрЗаменить(Элементы.Графики.ТекущийЭлемент.Имя, "Графики", "");
	Если ИмяКолонки = "ТорговаяТочка" Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Графики.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(ИмяКолонки) И ТекущиеДанные <> Неопределено Тогда
		ТекущееЗначение = ТекущиеДанные[ИмяКолонки];
		Если ЗначениеЗаполнено(ТекущееЗначение) Тогда
			Для Каждого СтрокаГрафиков Из Графики Цикл
				СтрокаГрафиков[ИмяКолонки] = ТекущееЗначение;	
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДаннымиДругогоСообщения(Команда)
	
	Если НЕ ЗначениеЗаполнено(Получатель) Тогда
		Сообщить("Для заполнения данными другого сообщения выберите партнера");	
		Возврат;
	КонецЕсли;
	
	Если Графики.Количество() Тогда	
		ТекстВопроса = "Графики будут перезаполнены, продолжить?";
		ИмяОбработчика = "ОбработчикВопросаЗаполненияПоСообщению";
		ПараметрыОбработчика = Неопределено;
		МодульОбъектаКлиент().ПоказатьВопрос_КонтурEDI(ИмяОбработчика, ЭтаФорма, ПараметрыОбработчика, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);	
	Иначе
		ОткрытьТаблицуВыбораСообщений();	
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикВопросаЗаполненияПоСообщению(Результат, ДопПараметры = Неопределено) Экспорт
	
	Если НЕ Результат = КодВозвратаДиалога.ОК Тогда
		Возврат;		
	КонецЕсли;
		
	ОткрытьТаблицуВыбораСообщений();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьТаблицуВыбораСообщений() Экспорт
			
	РезультатПомещенияСообщенийВХ = ПолучитьСписокПоследнихСообщенийDELFOR_ВХ(Получатель, ТипСообщения);
	
	Если РезультатПомещенияСообщенийВХ.ЕстьСообщенияПоПартнеру Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("АдресТаблицыВХранилище",РезультатПомещенияСообщенийВХ.Результат);
		МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("УниверсальнаяФормаВыбораУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбработчикВыбораСообщенияДляЗаполнения", ЭтаФорма, ПараметрыФормы);
	Иначе
		Сообщить("По выбранному партнеру нет сообщений");	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикВыбораСообщенияДляЗаполнения(Результат, ДопПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		ЗаполнитьДаннымиДругогоСообщенияСервер(Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДаннымиДругогоСообщенияСервер(АдресДанныхВХ) Экспорт
	
	ДанныеЗаполнения = ПолучитьИзВременногоХранилища(АдресДанныхВХ);
	
	СсылкаНаСообщениеДляЗаполнения = Неопределено;
	Для Каждого СтрокаВыбора Из ДанныеЗаполнения Цикл
		Если СтрокаВыбора.Выбор Тогда
			СсылкаНаСообщениеДляЗаполнения = СтрокаВыбора.Код;	
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(СсылкаНаСообщениеДляЗаполнения) Тогда
		Сообщение = МодульОбъекта().ПрочитатьСообщение(СсылкаНаСообщениеДляЗаполнения, , ТипСообщения, "Исходящее");
		ЗаполнитьПоляПоСообщению(Сообщение, Истина);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	ОсновнаяФорма().ПриЗакрытииФормы(ЭтаФорма, "ФормаСообщенияDELFORУправляемая");
	
КонецПроцедуры
