&НаСервере
Перем МодульОбъекта;

&НаКлиенте
Перем МодульОбъектаКлиент;

// СЛУЖЕБНЫЕ {

// Инициализация модуля и его экспортных функций
&НаСервере
Функция МодульОбъекта()
	
	Если МодульОбъекта = Неопределено Тогда
		
		Если Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект Тогда
			МодульОбъекта = МодульОбъектаИзКэша();
		КонецЕсли;
		
		Если МодульОбъекта = Неопределено Тогда
			МодульОбъекта = РеквизитФормыВЗначение("Объект");
			МодульОбъекта.ИнициализироватьПодключаемыеМодули(); // TODO Проверить что из этого используется на формах и перенести в ПараметрыКлиентСервер
			МодульОбъекта.ОбновитьКэшМодуляОбъекта();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат МодульОбъекта;
	
КонецФункции

&НаСервере
Функция МодульОбъектаИзКэша()
	
	Результат = Неопределено;
	
	Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда
		АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект;
 		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Структура = ПолучитьИзВременногоХранилища(АдресВХ);
			Если ТипЗнч(Структура) = Тип("Структура") и Структура.Свойство("ОбработкаОбъект") Тогда
			 	Результат = Структура.ОбработкаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульОбъектаКлиент()
	
	Если ТипЗнч(МодульОбъектаКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбъектаКлиент;
	КонецЕсли;
	
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		МодульОбъектаКлиент = ОсновнаяФорма.МодульОбъектаКлиент();
		Возврат МодульОбъектаКлиент;
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
	
	Если ЭтаФорма.ВладелецФормы = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ЭтаФорма.ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;
    	
КонецФункции

&НаКлиенте
Функция НоваяМетрика()
	
	Возврат ОсновнаяФорма().НоваяМетрика();
	
КонецФункции

// } СЛУЖЕБНЫЕ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
	Параметры.Свойство("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ИнтервалАвтообмена=0 Тогда ИнтервалАвтообмена=300; КонецЕсли; //3 минуты
	ПодключитьОбработчикОжидания("ВыполнитьФоновыйОбмен",1,Истина); //первый автообмен сразу
	
	ОсновнаяФорма().ПриОткрытииФормы(ЭтаФорма, "ФоновыйОбменУправляемая");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьФоновыйОбмен()
	Элементы.Состояние.Заголовок="Фоновый обмен исполняется";
	
	ВыполнитьОбменСервер();
	
	ПодключитьОбработчикОжидания("ВыполнитьФоновыйОбмен",ИнтервалАвтообмена,Истина);
	Элементы.ПоследнийРазИсполнен.Заголовок="Фоновый обмен последний раз исполнен: "+Формат(ТекущаяДата(),"ДЛФ=T");
	Элементы.Состояние.Заголовок="Фоновый обмен будет запущен в: "+Формат(ТекущаяДата()+ИнтервалАвтообмена,"ДЛФ=T");
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьОбменСервер()
	МодульОбъекта().ВыполнитьОбменССервером();
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьАвтообмен(Команда)
	ВернутьДоступностьКнопкиВыполнитьОбменРодительскойФормы();
	ЭтаФорма.Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ИнтервалАвтообменаПриИзменении(Элемент)
	МинимальныйИнтервалСрабатываения = 300;
	Если ИнтервалАвтообмена<МинимальныйИнтервалСрабатываения Тогда 
		 ИнтервалАвтообмена=МинимальныйИнтервалСрабатываения;
	КонецЕсли;
	
	ОтключитьОбработчикОжидания("ВыполнитьФоновыйОбмен");
	ПодключитьОбработчикОжидания("ВыполнитьФоновыйОбмен",ИнтервалАвтообмена,Истина);
	
	Элементы.Состояние.Заголовок="Фоновый обмен будет запущен в: "+Формат(ТекущаяДата()+ИнтервалАвтообмена,"ДЛФ=T");
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	ВернутьДоступностьКнопкиВыполнитьОбменРодительскойФормы();
КонецПроцедуры

&НаКлиенте
Процедура ВернутьДоступностьКнопкиВыполнитьОбменРодительскойФормы()
	
	Попытка
		ЭтаФорма.ВладелецФормы.Элементы.ВыполнитьОбмен.Доступность=истина;
		ЭтаФорма.ВладелецФормы.Элементы.ВыполнитьОбмен.Заголовок="Обмен с сервером";
	Исключение
		Возврат;
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	ОсновнаяФорма().ПриЗакрытииФормы(ЭтаФорма, "ФоновыйОбменУправляемая");	
	
КонецПроцедуры

