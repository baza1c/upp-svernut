&НаСервере
Перем МодульОбъекта;

&НаКлиенте
Перем МодульОбъектаКлиент;

&НаСервере
Перем ТочкиДоставкиОбъект;

// СЛУЖЕБНЫЕ {

// Инициализация модуля и его экспортных функций
&НаСервере
Функция МодульОбъекта()
	
	Если МодульОбъекта = Неопределено Тогда
		
		Если Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект Тогда
			МодульОбъекта = МодульОбъектаИзКэша();
		КонецЕсли;
		
		Если МодульОбъекта = Неопределено Тогда
			МодульОбъекта = РеквизитФормыВЗначение("Объект");
			МодульОбъекта.ИнициализироватьПодключаемыеМодули(); // TODO Проверить что из этого используется на формах и перенести в ПараметрыКлиентСервер
			МодульОбъекта.ОбновитьКэшМодуляОбъекта();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат МодульОбъекта;
	
КонецФункции

&НаСервере
Функция МодульОбъектаИзКэша()
	
	Результат = Неопределено;
	
	Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда
		АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект;
 		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Структура = ПолучитьИзВременногоХранилища(АдресВХ);
			Если ТипЗнч(Структура) = Тип("Структура") и Структура.Свойство("ОбработкаОбъект") Тогда
			 	Результат = Структура.ОбработкаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульОбъектаКлиент()
	
	Если ТипЗнч(МодульОбъектаКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбъектаКлиент;
	КонецЕсли;
	
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		МодульОбъектаКлиент = ОсновнаяФорма.МодульОбъектаКлиент();
		Возврат МодульОбъектаКлиент;
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
	
	Если ЭтаФорма.ВладелецФормы = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ЭтаФорма.ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;
    	
КонецФункции

&НаКлиенте
Функция НоваяМетрика()
	
	Возврат ОсновнаяФорма().НоваяМетрика();
	
КонецФункции

// } СЛУЖЕБНЫЕ

//обработчики---------------------------------------------------------------------------------

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
	Параметры.Свойство("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	
	КодСети = Параметры.КодСети;
	ЗаполнитьДеревоТочекДоставкиСФильтром();
	ЭтаФорма.Заголовок=ЭтаФорма.Заголовок+": "+Параметры.НаименованиеПартнера;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоТочекДоставкиСФильтром(Фильтр = "все")
	
	ТочкиДоставкиОбъект = РеквизитФормыВЗначение("ТочкиДоставки");
	ТочкиДоставкиОбъект.Строки.Очистить();
	_ТочкиДоставкиОбъект = МодульОбъекта().ПолучитьДеревоТочекДоставки(КодСети, Фильтр);
	Если _ТочкиДоставкиОбъект=Неопределено Тогда
		ЗначениеВРеквизитФормы(ТочкиДоставкиОбъект,"ТочкиДоставки");		
		Возврат;
	КонецЕсли;
	ЗаполнитьДеревоРекурсивно(_ТочкиДоставкиОбъект.Строки, ТочкиДоставкиОбъект.Строки);	
	ЗначениеВРеквизитФормы(ТочкиДоставкиОбъект,"ТочкиДоставки");		
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьЗначениеВсехСтрок("Обновить", 1, "УдалилиТД" , 1 );
	УстановитьИтоги();
	
	УстановитьДоступностьОсновногоДействияФормы();
	
	ОсновнаяФорма().ПриОткрытииФормы(ЭтаФорма, "Сервис_ОбновлениеДанныхПартнераУправляемая");
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьОсновногоДействияФормы()

	Если ТочкиДоставки.ПолучитьЭлементы().Количество()>0 Тогда
		Элементы.ФормаОбновитьИЗакрыть.Доступность=Истина;
	Иначе
		Элементы.ФормаОбновитьИЗакрыть.Доступность=Ложь;
	КонецЕсли;
	

КонецПроцедуры // УстановитьДоступностьОсновногоДействияФормы()

&НаКлиенте
Процедура ТочкиДоставкиОбновитьПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ТочкиДоставки.ТекущиеДанные;
	
	Если ТекущиеДанные.Обновить = 2 Тогда
		ТекущиеДанные.Обновить = 0;
	КонецЕсли;
	
	УстановитьСвойствоПодчиненным(ТекущиеДанные, "Обновить", ТекущиеДанные.Обновить);
		
	ОбновитьФлажокРодителя(ТекущиеДанные);
	
	УстановитьИтоги(?(ТекущиеДанные.ПолучитьРодителя()=Неопределено, ТекущиеДанные, ТекущиеДанные.ПолучитьРодителя()));
	
КонецПроцедуры

&НаКлиенте
Процедура ТочкиДоставкиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	УстановитьПризнакОтредактированаТД(ТекущиеДанные);	
	
КонецПроцедуры

&НаКлиенте
Процедура ТочкиДоставкиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТочкиДоставкиПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсе(Команда)
	
	УстановитьЗначениеВсехСтрок("Обновить", 1, "УдалилиТД" , 1 );
	УстановитьИтоги();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьНовые(Команда)
	
	Для Каждого СтрокаРегион Из ТочкиДоставки.ПолучитьЭлементы() Цикл
		Для Каждого Строка Из СтрокаРегион.ПолучитьЭлементы() Цикл	
			Если Строка.НоваяТД = 1 Тогда 
				УстановитьСвойствоПодчиненным(Строка, "Обновить", 1);
			Иначе
				УстановитьСвойствоПодчиненным(Строка, "Обновить", 0);
			КонецЕсли;
		КонецЦикла;
		ОбновитьФлажокРодителя(Строка);
	КонецЦикла;
	УстановитьИтоги();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьНеактуальные(Команда)
	
	Для Каждого СтрокаРегион Из ТочкиДоставки.ПолучитьЭлементы() Цикл
		Для Каждого Строка Из СтрокаРегион.ПолучитьЭлементы() Цикл	
			Если Строка.ИзмениласьТД = 1 Тогда 
				УстановитьСвойствоПодчиненным(Строка, "Обновить", 1);
			Иначе
				УстановитьСвойствоПодчиненным(Строка, "Обновить", 0);
			КонецЕсли;
		КонецЦикла;
		ОбновитьФлажокРодителя(Строка);
	КонецЦикла;
	УстановитьИтоги();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьВыбор(Команда)
	
	УстановитьЗначениеВсехСтрок("Обновить", 0);	
	УстановитьИтоги();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьНовые(Команда)
	
	ЗаполнитьДеревоТочекДоставкиСФильтром("новые");	
	УстановитьЗначениеВсехСтрок("Обновить", 1, "УдалилиТД" , 1 );
	УстановитьИтоги();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьУдаленные(Команда)
	
	ЗаполнитьДеревоТочекДоставкиСФильтром("на_удаление");	
	УстановитьЗначениеВсехСтрок("Обновить", 1, "УдалилиТД" , 1 );
	УстановитьИтоги();

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСоСменойПартнера(Команда)
	
	ЗаполнитьДеревоТочекДоставкиСФильтром("смена_партнера");	
	УстановитьЗначениеВсехСтрок("Обновить", 1, "УдалилиТД" , 1 );
	УстановитьИтоги();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьНеактуальные(Команда)
	
	ЗаполнитьДеревоТочекДоставкиСФильтром("изменившиеся");	
	УстановитьЗначениеВсехСтрок("Обновить", 1, "УдалилиТД" , 1 );
	УстановитьИтоги();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВсе(Команда)
	
	ЗаполнитьДеревоТочекДоставкиСФильтром();	
	УстановитьЗначениеВсехСтрок("Обновить", 1, "УдалилиТД" , 1 );
	УстановитьИтоги();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИЗакрыть(Команда)
	
	Если ТочкиДоставки.ПолучитьЭлементы().Количество()>0 Тогда
		
		ЗаписатьДанные = Ложь;
		Если  ЕстьНаСменаПартнера() Тогда 	
			Текст = "В списке были выбраны точки доставки, у которых произошла смена партнера.
			|Выполнить изменение партнера по выбранным точкам?
			|
			|Примечание: для того, чтобы посмотреть точки доставки со сменившимся партнером, выберите фильтр ""Показать со сменой партнера""";
			
			СписокДействий = Новый СписокЗначений;
			СписокДействий.Добавить(1,"Да, выполнить смену партнера у точек");								
			СписокДействий.Добавить(2,"Нет, вернуться к редактированию списка");
			
			Если Объект.ПараметрыКлиентСервер.МодальностьЗапрещена Тогда
				ЗадатьВопрос = "";
				ЗадатьВопрос = ЗадатьВопрос + Символы.ПС + "ПараметрыВопроса = Новый Структура(""Вопрос"",""СменаПартнера"");";
				ЗадатьВопрос = ЗадатьВопрос + Символы.ПС + "Оповещение = Новый ОписаниеОповещения(""ПослеЗакрытияВопроса"", ЭтаФорма,ПараметрыВопроса);"; 
				ЗадатьВопрос = ЗадатьВопрос + Символы.ПС + "ПоказатьВопрос(Оповещение, Текст, СписокДействий);"; 	 
				Выполнить(ЗадатьВопрос);
			Иначе 				
				Ответ = Вопрос(Текст, СписокДействий, 0);
				
				Если Ответ = 1 Тогда
					ЗаписатьДанные = Истина;
				КонецЕсли;
			КонецЕсли;
		Иначе
			ЗаписатьДанные = Истина;
		КонецЕсли;
		
		Если ЗаписатьДанные Тогда
			ЗаписатьДанные();
			ЭтаФорма.Закрыть();
		КонецЕсли; 		
	Иначе
		УстановитьДоступностьОсновногоДействияФормы();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЕстьНаСменаПартнера()

	ПоискСменыКонтрагента = Новый Структура;
	ПоискСменыКонтрагента.Вставить("СменаПартнера_в_ТД", 1);
	ПоискСменыКонтрагента.Вставить("Обновить"		   , 1);
	
	ТочкиДоставкиОбъект = РеквизитФормыВЗначение("ТочкиДоставки");
	
	Строки_С_ВыборомСменыПартнера = ТочкиДоставкиОбъект.Строки.НайтиСтроки(ПоискСменыКонтрагента,Истина);    	
	 
	Возврат Строки_С_ВыборомСменыПартнера.Количество() > 0;		

КонецФункции // ПроверитьНаСменуПартнера()

&НаКлиенте
Процедура Отмена(Команда)
	
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопроса(Ответ, ДопПараметры = Неопределено) Экспорт

	Если ДопПараметры <> Неопределено Тогда  		
		Если ДопПараметры.Свойство("Вопрос") Тогда
			Если ДопПараметры.Вопрос = "СменаПартнера" тогда
				Если Ответ = 1 Тогда
					ЗаписатьДанные();
					ЭтаФорма.Закрыть();
				КонецЕсли;
			КонецЕсли;   
		КонецЕсли; 				
	КонецЕсли;

КонецПроцедуры

//сохранение данных ---------------------------------------------------------------------------------

&НаСервере
Процедура ЗаписатьДанные()
	
	ТочкиДоставкиОбъект = РеквизитФормыВЗначение("ТочкиДоставки");
		
	РезультатЗаписи = МодульОбъекта().ЗаписатьДанныеТочекДоставки(КодСети, ТочкиДоставкиОбъект);
	
	Для Каждого Строка Из РезультатЗаписи.ТаблицаУведомлений Цикл
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Строка.Текст;
		Сообщение.ИдентификаторНазначения = Новый УникальныйИдентификатор(Параметры.GUIDФормыВыводаУведомлений);
		Сообщение.Сообщить();
	КонецЦикла;
	
КонецПроцедуры	

//подготовка, обработка и установка данных дерева ----------------------------------------------------

&НаСервере
Процедура ЗаполнитьДеревоРекурсивно(_Строки, Строки)
	
	Для Каждого _Строка Из _Строки Цикл
		Строка = Строки.Добавить();
		ЗаполнитьЗначенияСвойств(Строка, _Строка);
		ЗаполнитьДеревоРекурсивно(_Строка.Строки, Строка.Строки);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗначениеВсехСтрок(Колонка, Значение, КромеКолонка = Неопределено, КромеЗначение = Неопределено)
	
	//устанавливает в выбранной колонке переданное значение
	
	Для Каждого Строка Из ТочкиДоставки.ПолучитьЭлементы() Цикл
		УстановитьСвойствоПодчиненным(Строка, Колонка, Значение, КромеКолонка,КромеЗначение);	
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСвойствоПодчиненным(Строка, Колонка, Значение, КромеКолонка = Неопределено, КромеЗначение = Неопределено)
	
	//процедура рекурсивно устанавливает значения дочерним строкам
	
	//устанавливаем значение для текущей строки
	Установить = Ложь;
	Если КромеКолонка = Неопределено ИЛИ КромеЗначение = Неопределено Тогда
		Установить = Истина;
	ИначеЕсли Строка[КромеКолонка] <>  КромеЗначение Тогда
		Установить = Истина;  
	КонецЕсли;
	
	Если Установить Тогда
		Строка[Колонка] = Значение;
	КонецЕсли;	
	
	//заполняем новые данные на основании текущих данных/данных ТС
	Если Не Строка.ПолучитьРодителя() = Неопределено Тогда
		Если Строка.Обновить = 1 Тогда
			Строка.НаименованиеНовое		= Строка.НаименованиеТС;
			Строка.АдресТочкиДоставкиНовый 	= Строка.АдресТочкиДоставкиТС;
		ИначеЕсли Строка.Обновить = 0 Тогда
			Строка.НаименованиеНовое		= Строка.НаименованиеТекущее;
			Строка.АдресТочкиДоставкиНовый 	= Строка.АдресТочкиДоставкиТекущий;
		КонецЕсли;
	КонецЕсли;
	
	//обновим признак отредактирована ТД
	УстановитьПризнакОтредактированаТД(Строка);
		
	//рекурсивно устанавливаем значения на более низком уровне
	Для Каждого Подстрока Из Строка.ПолучитьЭлементы() Цикл
		Установить = Ложь;
		Если КромеКолонка = Неопределено ИЛИ КромеЗначение = Неопределено Тогда
			Установить = Истина;
		ИначеЕсли Подстрока[КромеКолонка] <>  КромеЗначение Тогда
			Установить = Истина;  
		КонецЕсли;
		
		Если Установить Тогда
			Подстрока[Колонка] = Значение;
		КонецЕсли;
		
		УстановитьСвойствоПодчиненным(Подстрока, Колонка, Значение,КромеКолонка,КромеЗначение);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПризнакОтредактированаТД(Строка)
	
	//Если Не (Элемент.ТекущаяКолонка = Неопределено) и
	//	 Не (Элемент.ТекущиеДанные  = Неопределено) Тогда
	Если Не Строка = Неопределено Тогда
		//Если Элемент.ТекущаяКолонка.Имя = "Обновить" 				или
		//	 Элемент.ТекущаяКолонка.Имя = "НаименованиеНовое" 		или
		//	 Элемент.ТекущаяКолонка.Имя = "АдресТочкиДоставкиНовый" Тогда
			Если Строка.Обновить = 0 Тогда 
					Если Не СокрЛП(Строка.НаименованиеТекущее) 		 = СокрЛП(Строка.НаименованиеНовое)		или
						 Не СокрЛП(Строка.АдресТочкиДоставкиТекущий) = СокрЛП(Строка.АдресТочкиДоставкиНовый) Тогда
						Строка.ОтредактированаТД = 1;
					Иначе
						Строка.ОтредактированаТД = 0;
					КонецЕсли;
			ИначеЕсли Строка.Обновить = 1 Тогда
				Если Не СокрЛП(Строка.НаименованиеТС) 		= СокрЛП(Строка.НаименованиеНовое)		или
					 Не СокрЛП(Строка.АдресТочкиДоставкиТС) = СокрЛП(Строка.АдресТочкиДоставкиНовый) Тогда
					Строка.ОтредактированаТД = 1;
				Иначе
					Строка.ОтредактированаТД = 0;
				КонецЕсли;		
			КонецЕсли;	
		//КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьФлажокРодителя(Строка)
	
	//обновляет значение флажка родительской строки (регион) в зависимости от
	//комбинации значений дочерних строк (для установки единого/смешанного значения)
	
	Если Не Строка.ПолучитьРодителя() = Неопределено Тогда
		
		ЕстьОбратныеЗначения = Ложь;
		СтрокиРегиона = Строка.ПолучитьРодителя().ПолучитьЭлементы();
		Для Каждого ТД из СтрокиРегиона Цикл
			Если Не ТД.Обновить = Строка.Обновить Тогда
				ЕстьОбратныеЗначения = Истина;	
			КонецЕсли;
		КонецЦикла;
		
		Если ЕстьОбратныеЗначения Тогда
			Строка.ПолучитьРодителя().Обновить = 2;
		Иначе
			Строка.ПолучитьРодителя().Обновить = Строка.Обновить;
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИтоги(Регион = Неопределено)
	
	Если Не Регион = Неопределено Тогда
		УстановитьИтогиРегиона(Регион);
	Иначе
	    Для Каждого Регион Из ТочкиДоставки.ПолучитьЭлементы() Цикл
			УстановитьИтогиРегиона(Регион);	
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИтогиРегиона(Регион)
	
	КоличествоНовых 				= 0;
	КоличествоИзменились 			= 0;
	КоличествоСменилиПартнера       = 0;
	КоличествоУдалились             = 0;
	
	Для Каждого ТД Из Регион.ПолучитьЭлементы() Цикл
		КоличествоНовых 			= КоличествоНовых 		+ ТД.НоваяТД;
		КоличествоИзменились		= КоличествоИзменились 	+ ТД.ИзмениласьТД;
		КоличествоСменилиПартнера 	= КоличествоСменилиПартнера 		+ ТД.СменаПартнера_в_ТД;
		КоличествоУдалились			= КоличествоУдалились 	+ ТД.УдалилиТД;		
	КонецЦикла;
	
	КоличествоНовыхСтрока      	 	= СтрЗаменить(Строка(КоличествоНовых), 							Символы.НПП, "");
	КоличествоИзменилисьСтрока		= СтрЗаменить(Строка(КоличествоИзменились), 					Символы.НПП, "");
	КоличествоСменилиПартнераСтрока = СтрЗаменить(Строка(КоличествоСменилиПартнера),				Символы.НПП, "");
	КоличествоУдалилисьСтрока		= СтрЗаменить(Строка(КоличествоУдалились), 						Символы.НПП, "");

	КоличествоВсегоСтрока			= СтрЗаменить(Строка(Регион.ПолучитьЭлементы().Количество()), 	Символы.НПП, "");
	СтрокаИтоговОбновления			= ПолучитьСтрокуИтоговОбновления(Регион);
	
	Регион.GLN 						= "Всего: "+КоличествоВсегоСтрока;
	Регион.АдресТочкиДоставкиТС		= "Новых: " + КоличествоНовыхСтрока + "; изменившихся: " + КоличествоИзменилисьСтрока + "; удалились: " + КоличествоУдалилисьСтрока+ "; сменили партнера: " + КоличествоСменилиПартнераСтрока;
	Регион.АдресТочкиДоставкиНовый	= СтрокаИтоговОбновления;
			
	Регион.НоваяТД					= Число(КоличествоНовыхСтрока);
	Регион.ИзмениласьТД				= Число(КоличествоИзменилисьСтрока);
	Регион.СменаПартнера_в_ТД		= Число(КоличествоСменилиПартнераСтрока);
	Регион.УдалилиТД				= Число(КоличествоУдалилисьСтрока);
	
КонецПроцедуры

&НаКлиенте
Функция  ПолучитьСтрокуИтоговОбновления(СтрокаРегион)
	
	//возвращает строку с количествами точек доставки, которые будут добавлены и обновлены
	//при текущих настройках, для переданной родительской строки региона
	
	СтрокаРезультат = "";
	КоличествоБудетДобавлено = 0;
	КоличествоБудетОбновлено = 0;
		
	Для Каждого Строка Из СтрокаРегион.ПолучитьЭлементы() Цикл
		Обновить				= Строка.Обновить = 1;
		ЭтоНоваяТД 				= Строка.НоваяТД = 1;
		ЭтоИзменившаясяТД 		= Строка.ИзмениласьТД = 1;
		ЭтоОтредактированнаяТД 	= Строка.ОтредактированаТД = 1;
		Если Обновить и ЭтоНоваяТД Тогда
			КоличествоБудетДобавлено = КоличествоБудетДобавлено + 1;
		КонецЕсли;
		Если (Обновить 	  и ЭтоИзменившаясяТД) 		или
			 (Обновить    и ЭтоОтредактированнаяТД) или
			 (Не Обновить и ЭтоОтредактированнаяТД) Тогда
			КоличествоБудетОбновлено = КоличествоБудетОбновлено + 1; 
		КонецЕсли;				 
	КонецЦикла;
	
	СтрокаРезультат = "Добавятся: " + СтрЗаменить(Строка(КоличествоБудетДобавлено), Символы.НПП, "") + "; обновятся: " + СтрЗаменить(Строка(КоличествоБудетОбновлено), Символы.НПП, "");
	
	Возврат СтрокаРезультат;
	
КонецФункции

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	ОсновнаяФорма().ПриЗакрытииФормы(ЭтаФорма, "Сервис_ОбновлениеДанныхПартнераУправляемая");
	
КонецПроцедуры

