&НаСервере
Перем МодульОбъекта;

&НаКлиенте
Перем МодульОбъектаКлиент;

// СЛУЖЕБНЫЕ {

// Инициализация модуля и его экспортных функций
&НаСервере
Функция МодульОбъекта()
	
	Если МодульОбъекта = Неопределено Тогда
		
		Если Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект Тогда
			МодульОбъекта = МодульОбъектаИзКэша();
		КонецЕсли;
		
		Если МодульОбъекта = Неопределено Тогда
			МодульОбъекта = РеквизитФормыВЗначение("Объект");
			МодульОбъекта.ИнициализироватьПодключаемыеМодули(); // TODO Проверить что из этого используется на формах и перенести в ПараметрыКлиентСервер
			МодульОбъекта.ОбновитьКэшМодуляОбъекта();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат МодульОбъекта;
	
КонецФункции

&НаСервере
Функция МодульОбъектаИзКэша()
	
	Результат = Неопределено;
	
	Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда
		АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект;
 		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Структура = ПолучитьИзВременногоХранилища(АдресВХ);
			Если ТипЗнч(Структура) = Тип("Структура") и Структура.Свойство("ОбработкаОбъект") Тогда
			 	Результат = Структура.ОбработкаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульОбъектаКлиент()
	
	Если ТипЗнч(МодульОбъектаКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбъектаКлиент;
	КонецЕсли;
	
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		МодульОбъектаКлиент = ОсновнаяФорма.МодульОбъектаКлиент();
		Возврат МодульОбъектаКлиент;
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
	
	Если ЭтаФорма.ВладелецФормы = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ЭтаФорма.ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;
    	
КонецФункции

&НаКлиенте
Функция НоваяМетрика()
	
	Возврат ОсновнаяФорма().НоваяМетрика();
	
КонецФункции

// } СЛУЖЕБНЫЕ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
	Параметры.Свойство("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	
	ПутьКФормам 					= МодульОбъекта().Метаданные().ПолноеИмя() + ".Форма.";
	Параметры.МодальностьЗапрещена	= МодульОбъекта().МодальностьЗапрещена();
	СсылкаТочкиДоставки 			= Параметры.СсылкаТочкиДоставки;
	Партнер 						= Параметры.Партнер;
	
	ОпределитьТипСвоегоСправочникаТочекДоставки();
	
	Если ЗначениеЗаполнено(СсылкаТочкиДоставки) Тогда
		
		СтруктураЭлемента = МодульОбъекта().ПолучитьЭлементСправочника("ТочкиДоставкиСторонние", СсылкаТочкиДоставки);
		
		Если Не СтруктураЭлемента = Неопределено Тогда
			
			Наименование				= СтруктураЭлемента.Наименование;
			Партнер						= СтруктураЭлемента.Партнер;
			
			ПредставлениеПартнера = МодульОбъекта().ПолучитьПредставлениеЭлементаСправочника(Партнер);
			
			GLN							= СтруктураЭлемента.GLN;
			АдресДоставки				= СтруктураЭлемента.АдресДоставки;
			ЮрФизЛицо					= СтруктураЭлемента.ЮрФизЛицо;
			ТочкаДоставкиСвойСправочник = СтруктураЭлемента.ТочкаДоставкиСвойСправочник;
			ЗагружатьСообщенияПоТочкеДоставки = СтруктураЭлемента.ЗагружатьСообщенияПоТочкеДоставки;
			ТипЛокализации  			= СтруктураЭлемента.ТипЛокализации;
			
		КонецЕсли;
		
	Иначе
		
		Партнер = Параметры.Партнер;
		Наименование = Параметры.Наименование;
		АдресДоставки = Параметры.АдресДоставки;
		GLN = Параметры.GLN;
		ЮрФизЛицо = Параметры.ЮрФизЛицо;
		
		ПредставлениеПартнера = МодульОбъекта().ПолучитьПредставлениеЭлементаСправочника(Партнер);
		
	КонецЕсли;
	
	Если МодульОбъекта().ПараметрыПользователяEDI.ФильтрОбменаПоТочкамДоставки = Истина Тогда
		Элементы.ГруппаЗагружатьСообщения.Видимость = Истина;
	Иначе
		Элементы.ГруппаЗагружатьСообщения.Видимость = Ложь;
	КонецЕсли;
		
	Если Не МодульОбъекта().ВнешнееХранилище Тогда
		УстановитьТипПоля1С("Партнер",		"Партнер");
	КонецЕсли;	
	УстановитьТипПоля1С("ЮрФизЛицо",	"ЮрФизЛицоСтороннее");
	
	УстановитьЗаголовокПодсказкиЮрФизлица();
	УправлениеВидимостьюСправочникаТочекДоставок();	
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьТипСвоегоСправочникаТочекДоставки()
	
	СвойСправочникТочекДоставки = МодульОбъекта().ПолучитьКонстантуEDI("СвойСправочникТочекДоставки");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТипПоля1С(ИмяНаФорме,Тип1С)
	
	ПолеФормы	= Элементы[ИмяНаФорме];
	ТипПоля		= МодульОбъекта().ПолучитьТипЗначенияОбъекта(Тип1С);
			
	Если ТипПоля = Неопределено Тогда
		СП = Новый СообщениеПользователю;
		СП.Текст = "Не задан тип объекта 1С для поля с типом "+Тип1С;
		СП.Сообщить();
	Иначе	
		Элементы[ИмяНаФорме].ОграничениеТипа = Новый ОписаниеТипов(ТипПоля);
		Если НЕ ЗначениеЗаполнено(ЭтаФорма[ИмяНаФорме]) Тогда
			ЭтаФорма[ИмяНаФорме] = МодульОбъекта().ПолучитьПустуюСсылкуОбъекта(Тип1С);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗаписатьДанные()
	
	Успешно = Ложь;
	
	Если НЕ ПроверитьЗначенияФормы() Тогда
		Возврат Успешно;
	КонецЕсли;
	
	СтруктураПолей = Новый Структура();
	СтруктураПолей.Вставить("Партнер",								Партнер);
	СтруктураПолей.Вставить("Наименование",							Наименование);
	СтруктураПолей.Вставить("GLN",									GLN);
	СтруктураПолей.Вставить("АдресДоставки",						АдресДоставки);
	СтруктураПолей.Вставить("ЮрФизЛицо",							ЮрФизЛицо);
    СтруктураПолей.Вставить("ТочкаДоставкиСвойСправочник",			ТочкаДоставкиСвойСправочник);
    СтруктураПолей.Вставить("ЗагружатьСообщенияПоТочкеДоставки",	ЗагружатьСообщенияПоТочкеДоставки);
    СтруктураПолей.Вставить("ТипЛокализации",						ТипЛокализации);
	
	МодульОбъекта().СохранитьЭлементСправочника("ТочкиДоставкиСторонние",СсылкаТочкиДоставки,СтруктураПолей);
	
	Успешно = Истина;
	
	Возврат Успешно;
		
КонецФункции

&НаСервере
Функция ПроверитьЗначенияФормы()
	
	// 1. инициализируем таблицу вывода ошибок
	ТаблицаОшибок = МодульОбъекта().ИнициализироватьТаблицуОшибок();
	
	// 2. передать структуру для проверки
	МодульОбъекта().ПроверитьПолеФормы(ТаблицаОшибок, Партнер,					, Истина, "Партнер");
	МодульОбъекта().ПроверитьПолеФормы(ТаблицаОшибок, АдресДоставки,			, Истина, "АдресДоставки");
	МодульОбъекта().ПроверитьПолеФормы(ТаблицаОшибок, GLN, 				"GLN"	, Истина, "GLN");

	//Уникальность точек доставки проверять нальзя т.к. реальна ситуация нескольких GLN на 1 адрес доставки и как результат неуникальность ТД
	//МодульОбъекта().ПроверитьСвязанныеСправочникиТочкиДоставкиНаПовтор(Партнер,СсылкаТочкиДоставки,ТаблицаОшибок, ЮрФизЛицо, ТочкаДоставкиСвойСправочник);
	
	Если ТаблицаОшибок.Количество() > 0 Тогда
		Если ПустаяСтрока(Наименование) Тогда
			ТекстЗаголовка = "При заполнении точки доставки найдены ошибки.";
		Иначе
			ТекстЗаголовка = "При заполнении точки доставки """+СокрЛП(Наименование)+""" найдены ошибки.";
		КонецЕсли;
		//ОткрытьФормуВыводаОшибок(ТекстЗаголовка,ТаблицаОшибок,ЭтаФорма);
		Для Каждого Строка Из ТаблицаОшибок Цикл
			СП = Новый СообщениеПользователю;
			СП.Текст = Строка.СведенияОбОшибках;
			СП.Поле = Строка.ИмяПоля;
			СП.Сообщить();
		КонецЦикла;
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
	// 3. обработать результат
	
КонецФункции

&НаКлиенте
Процедура ОткрытьНастройки(Команда)
	
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("СервисНастройкиУправляемая", , , ЭтаФорма, "ОбработчикПослеЗакрытияНастроек", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикПослеЗакрытияНастроек(ПараметрыЗакрытия, ДополнительныеПараметры = Неопределено) Экспорт
	
	УправлениеВидимостьюСправочникаТочекДоставок();
	УстановитьЗаголовокПодсказкиЮрФизлица();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(Параметры.ПараметрыАвтотестирования) Тогда
		ПодключитьОбработчикОжидания("ЗаписатьНастройкиАвтотестирования",0.1,Истина);
	КонецЕсли;
	
	ОсновнаяФорма().ПриОткрытииФормы(ЭтаФорма, "ТочкиДоставкиСторонние_ЭлементУправляемая");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьНастройкиАвтотестирования()
	
	ОК("");
	
КонецПроцедуры

&НаСервере
Процедура УправлениеВидимостьюСправочникаТочекДоставок()
	
	ОпределитьТипСвоегоСправочникаТочекДоставки();
	
	Если НЕ ЗначениеЗаполнено(СвойСправочникТочекДоставки) Тогда
		
		Элементы.ТочкаДоставкиСвойСправочник.Доступность 	= Ложь;
		Элементы.ГруппаСвязанныеОбъектыПодсказка.Видимость	= Истина;
			
	Иначе
		
		Элементы.ТочкаДоставкиСвойСправочник.Доступность 		= Истина;
		Элементы.ТочкаДоставкиСвойСправочник.ОграничениеТипа 	= Новый ОписаниеТипов("СправочникСсылка."+СокрЛП(СвойСправочникТочекДоставки));
		Если НЕ ЗначениеЗаполнено(ТочкаДоставкиСвойСправочник) Тогда
			ТочкаДоставкиСвойСправочник = ПредопределенноеЗначение("Справочник."+СвойСправочникТочекДоставки+".ПустаяСсылка");
		КонецЕсли;
		Элементы.ГруппаСвязанныеОбъектыПодсказка.Видимость		= Ложь;
		
	КонецЕсли;
	      	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовокПодсказкиЮрФизлица()
	ГрузополучательИзЮрФизЛицаТД						= (МодульОбъекта().ПолучитьКонстантуEDI("ГрузополучательИзЮрФизЛицаТД") = Истина);
	ЗаполняемоеПолеВЗаказе = ?(ГрузополучательИзЮрФизЛицаТД, "Грузополучатель","Контрагент");
	Элементы.ПодсказкаЮрФизЛицо.Заголовок = "Согласно текущим настройкам, будет использовано для заполнения поля """+ЗаполняемоеПолеВЗаказе+""" в документе заказа.";
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	ДанныеУспешноЗаписаны = ЗаписатьДанные();	
	
	Если ДанныеУспешноЗаписаны Тогда 
		ЭтаФорма.Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	ОсновнаяФорма().ПриЗакрытииФормы(ЭтаФорма, "ТочкиДоставкиСторонние_ЭлементУправляемая");	

КонецПроцедуры


