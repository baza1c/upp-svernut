&НаСервере
Перем МодульОбъекта;

&НаКлиенте
Перем МодульОбъектаКлиент;

&НаСервере
Перем ПараметрыВывода;

// СЛУЖЕБНЫЕ {

// Инициализация модуля и его экспортных функций
&НаСервере
Функция МодульОбъекта()
	
	Если МодульОбъекта = Неопределено Тогда
		
		Если Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект Тогда
			МодульОбъекта = МодульОбъектаИзКэша();
		КонецЕсли;
		
		Если МодульОбъекта = Неопределено Тогда
			МодульОбъекта = РеквизитФормыВЗначение("Объект");
			МодульОбъекта.ИнициализироватьПодключаемыеМодули(); // TODO Проверить что из этого используется на формах и перенести в ПараметрыКлиентСервер
			МодульОбъекта.ОбновитьКэшМодуляОбъекта();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат МодульОбъекта;
	
КонецФункции

&НаСервере
Функция МодульОбъектаИзКэша()
	
	Результат = Неопределено;
	
	Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда
		АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект;
 		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Структура = ПолучитьИзВременногоХранилища(АдресВХ);
			Если ТипЗнч(Структура) = Тип("Структура") и Структура.Свойство("ОбработкаОбъект") Тогда
			 	Результат = Структура.ОбработкаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульОбъектаКлиент()
	
	Если ТипЗнч(МодульОбъектаКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбъектаКлиент;
	КонецЕсли;
	
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		МодульОбъектаКлиент = ОсновнаяФорма.МодульОбъектаКлиент();
		Возврат МодульОбъектаКлиент;
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
	
	Если ЭтаФорма.ВладелецФормы = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ЭтаФорма.ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;
    	
КонецФункции

&НаКлиенте
Функция НоваяМетрика()
	
	Возврат ОсновнаяФорма().НоваяМетрика();
	
КонецФункции

// } СЛУЖЕБНЫЕ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
	Параметры.Свойство("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	
	Макет = МодульОбъекта().ПолучитьМакет("МакетСравненияТабличныхЧастей");
	
	ВходныеПараметрыВалидны = ПроверитьВходныеПараметры();
	
	Если ВходныеПараметрыВалидны Тогда
		
		ПараметрыРасхождения = Новый Структура;
		Если ЗначениеЗаполнено(Параметры.Параметры) И ТипЗнч(Параметры.Параметры) = Тип("Структура") Тогда
			ПараметрыРасхождения = Параметры.Параметры;
		КонецЕсли;
		Если ЗначениеЗаполнено(Параметры.СравниваемыеПоля) Тогда
			ПараметрыРасхождения.Вставить("СравниваемыеПоля", Параметры.СравниваемыеПоля);
		КонецЕсли;
		
		ВидРасхождения = Параметры.ВидРасхождения;
			
		Если ВидРасхождения = "РасхожденияЗаказа" Тогда
			
			Заказ 					= Параметры.Заказ;
			ПараметрыВывода 		= МодульОбъекта().ПолучитьДанныеРасхожденияЗаказаПокупателя(Заказ, ПараметрыРасхождения);
						
		ИначеЕсли ВидРасхождения = "РасхожденияПриемки" Тогда
			
			Накладная				= Параметры.Накладная;
			ПараметрыВывода 		= МодульОбъекта().ПолучитьДанныеРасхожденияПриемки(Накладная, ПараметрыРасхождения);
			
		ИначеЕсли ВидРасхождения = "РасхожденияВерсийЗаказа" Тогда
			
			Заказ					= Параметры.Заказ;
			ПараметрыВывода 		= МодульОбъекта().ПолучитьДанныеРасхожденияВерсийЗаказа(Заказ, ПараметрыРасхождения);
			
		КонецЕсли; 
		
		ПараметрыВыводаВалидны = ПроверитьПараметрыВывода();
		
		Если ПараметрыВыводаВалидны Тогда
			
			МодульОбъекта().ВывестиРасхождения(Таблица, Макет, ПараметрыВывода);	
			
		КонецЕсли;
		
	КонецЕсли;
	    	
КонецПроцедуры

&НаСервере
Процедура ДобавитьОписаниеОшибки(ТаблицаОшибок, ТекстОшибки)
	
	НоваяСтрока = ТаблицаОшибок.Добавить();
	НоваяСтрока.ТекстОшибки = ТекстОшибки;	
	
КонецПроцедуры

&НаСервере
Функция ПроверитьВходныеПараметры()
	
	Успешно = Истина;	
	ТаблицаОшибок = Новый ТаблицаЗначений;
	ТаблицаОшибок.Колонки.Добавить("ТекстОшибки");
	
	ВидРасхождения = Параметры.ВидРасхождения;
	
	Если ВидРасхождения = "РасхожденияЗаказа" Тогда
		
		Если Не ЗначениеЗаполнено(Параметры.Заказ) Тогда
			Успешно = Ложь;
			ДобавитьОписаниеОшибки(ТаблицаОшибок, "Отсутствует документ заказа в 1С.");
		КонецЕсли;	
		
	ИначеЕсли ВидРасхождения = "РасхожденияПриемки" Тогда
		
		Если Не ЗначениеЗаполнено(Параметры.Накладная) Тогда
			Успешно = Ложь;
			ДобавитьОписаниеОшибки(ТаблицаОшибок, "Отсутствует документ накладной в 1С.");
		КонецЕсли;
		
	ИначеЕсли ВидРасхождения = "РасхожденияВерсийЗаказа" Тогда
		
		Если Не ЗначениеЗаполнено(Параметры.Заказ) Тогда
			Успешно = Ложь;
			ДобавитьОписаниеОшибки(ТаблицаОшибок, "Отсутствует Ссылка на сообщение в 1С.");
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не Успешно Тогда
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = "Невозможно выполнить анализ расхождений:";
		СообщениеПользователю.Сообщить();
		Для Каждого Строка Из ТаблицаОшибок Цикл
			СообщениеПользователю = Новый СообщениеПользователю;
			СообщениеПользователю.Текст = Строка.ТекстОшибки;
			СообщениеПользователю.Сообщить();
        КонецЦикла;
	КонецЕсли;
	
	Возврат Успешно;	
	
КонецФункции

&НаСервере
Функция ПроверитьПараметрыВывода()
	
	Если Не ПараметрыВывода.Успешно	Тогда
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = "Невозможно выполнить анализ расхождений: " + ПараметрыВывода.ОписаниеОшибки;
		СообщениеПользователю.Сообщить();
	КонецЕсли;
	
	Возврат ПараметрыВывода.Успешно;
	
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ОсновнаяФорма().ПриОткрытииФормы(ЭтаФорма, "Сервис_ПросмотрРасхожденийУправляемая");	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	ОсновнаяФорма().ПриЗакрытииФормы(ЭтаФорма, "Сервис_ПросмотрРасхожденийУправляемая");	
	
КонецПроцедуры
