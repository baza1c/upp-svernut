&НаСервере
Перем МодульОбъекта;

&НаКлиенте
Перем ПараметрыКлиентСервер;


// ИНИЦИАЛИЗАЦИЯ {

&НаКлиенте
Процедура Инициализировать() Экспорт
	
	ПараметрыКлиентСервер = Объект.ПараметрыКлиентСервер;
	
КонецПроцедуры	

// } ИНИЦИАЛИЗАЦИЯ

// СЛУЖЕБНЫЕ {

&НаСервере
Функция МодульОбъекта()
	
	Если МодульОбъекта = Неопределено Тогда

		Если Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект Тогда
			МодульОбъекта = МодульОбъектаИзКэша();
		КонецЕсли;
		
		Если МодульОбъекта = Неопределено Тогда
			МодульОбъекта = РеквизитФормыВЗначение("Объект");
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат МодульОбъекта;
	
КонецФункции

&НаСервере
Функция МодульОбъектаИзКэша()
	
	Результат = Неопределено;
	
	Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда
		АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект;
 		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Структура = ПолучитьИзВременногоХранилища(АдресВХ);
			Если ТипЗнч(Структура) = Тип("Структура") и Структура.Свойство("ОбработкаОбъект") Тогда
			 	Результат = Структура.ОбработкаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПоместитьВоВХ(Имя, Значение)
	
	ПоместитьВоВременноеХранилище(Значение, Объект.ПараметрыКлиентСервер.АдресаВХ[Имя]);
				
КонецПроцедуры

&НаКлиенте
Функция ИзвлечьИзВХ(Имя)
	
	АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ[Имя];
	
	Если Не ЭтоАдресВременногоХранилища(АдресВХ) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ПолучитьИзВременногоХранилища(АдресВХ);
			
КонецФункции

&НаКлиенте
Функция EDI_РазложитьСтрокуВМассивСлов(Знач Строка, РазделителиСлов="") Экспорт
    
    Слова = Новый Массив;
    
    Если СтрДлина(РазделителиСлов) = 1 Тогда
        
        НайденныйСимвол = Найти(Строка,РазделителиСлов);
        Пока НайденныйСимвол>0 Цикл
            Слова.Добавить(Лев(Строка,НайденныйСимвол-1));
            Строка = Сред(Строка,НайденныйСимвол+1);
            НайденныйСимвол = Найти(Строка,РазделителиСлов);
        КонецЦикла;
        
        Если НЕ ПустаяСтрока(Строка) Тогда
            Слова.Добавить(Строка);
        КонецЕсли;
        
    Иначе
        
        Для Сч = 1 По СтрДлина(РазделителиСлов) Цикл
            Строка = СтрЗаменить(Строка,Сред(РазделителиСлов,Сч,1),Символы.ПС);
        КонецЦикла;
        
        Для Сч=1 По СтрЧислоСтрок(Строка) Цикл
            ТекСлово = СокрЛП(СтрПолучитьСтроку(Строка,Сч));
            Если ТекСлово<>"" Тогда
                Слова.Добавить(ТекСлово);
            КонецЕсли;    
        КонецЦикла;    
        
    КонецЕсли;
    
    Возврат Слова;
    
КонецФункции

// } СЛУЖЕБНЫЕ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
	Параметры.Свойство("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
		
КонецПроцедуры

// ВСПОМОГАТЕЛЬНЫЕ {

&НаСервере
Функция ВыполнитьМетодМодуляОбъекта(ТипМетода, ИмяМетода, ПараметрыМетода = Неопределено) Экспорт
	
	Результат = Неопределено;
	
	МодульОбъекта = МодульОбъекта();
	
	СтрокаПараметров = "";
	Если ЗначениеСоответствуетТипу(ПараметрыМетода, "Структура") Тогда
		Для Каждого Параметр Из ПараметрыМетода Цикл
			СтрокаПараметров = СтрокаПараметров + ?(ПустаяСтрока(СтрокаПараметров),"",",") + "ПараметрыМетода." + Параметр.Ключ;				
		КонецЦикла;
	КонецЕсли;
	
	Если ТипМетода = "Процедура" Тогда
		ТекстКоманды = "МодульОбъекта." + ИмяМетода + "(" + СтрокаПараметров + ");";	
	ИначеЕсли ТипМетода = "Функция" Тогда
		ТекстКоманды = "Результат =  МодульОбъекта." + ИмяМетода + "(" + СтрокаПараметров + ");";
	КонецЕсли;
	
	Выполнить(ТекстКоманды);	
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ВыполнитьПакетМетодовМодуляОбъекта(ОписаниеПакетаМетодов)
	
	РезультатВыполненияПакета = Новый Структура;
	
	Для Каждого ОписаниеМетода Из ОписаниеПакетаМетодов Цикл
		РезультатВыполненияМетода = ВыполнитьМетодМодуляОбъекта( ОписаниеМетода.ТипМетода, ОписаниеМетода.ИмяМетода, ?(ОписаниеМетода.Свойство("ПараметрыМетода"),ОписаниеМетода.ПараметрыМетода,Неопределено) );
		РезультатВыполненияПакета.Вставить(ОписаниеМетода.ИмяМетода, РезультатВыполненияМетода);
	КонецЦикла;
	
	Возврат РезультатВыполненияПакета;
		
КонецФункции

&НаСервере
Функция ВыполнитьМетодМодуляОбъектаПМ(ТипМетода, ИмяМетода, ПараметрыМетода = Неопределено) Экспорт
	
	ПараметрыМетодаПодключаемогоМодуля = Новый Структура;
	ПараметрыМетодаПодключаемогоМодуля.Вставить("ТипМетода", ТипМетода);
	ПараметрыМетодаПодключаемогоМодуля.Вставить("ИмяМетода",  ИмяМетода);
	ПараметрыМетодаПодключаемогоМодуля.Вставить("ПараметрыМетода", ПараметрыМетода);
	
	Возврат ВыполнитьМетодМодуляОбъекта("Функция", "ВыполнитьМетодПодключаемогоМодуля", ПараметрыМетодаПодключаемогоМодуля);
			
КонецФункции

// За эту функцию спасибо ребятам, которые пишут библиотеку xUnitFor1C
// https://github.com/xDrivenDevelopment/xUnitFor1C
//
// Универсальная функция для проверки наличия 
// свойств у значения любого типа данных
// Переменные:
// 1. Переменная - переменная любого типа, 
// для которой необходимо проверить наличие свойства
// 2. ИмяСвойства - переменная типа "Строка", 
// содержащая искомое свойства
// 
&НаКлиенте
Функция ПеременнаяСодержитСвойство(Переменная, ИмяСвойства) Экспорт

     // Инициализируем структуру для теста 
     // с ключом (значение переменной "ИмяСвойства") 
     // и значением произвольного GUID'а
     GUIDПроверка = Новый УникальныйИдентификатор;
     СтруктураПроверка = Новый Структура;
     СтруктураПроверка.Вставить(ИмяСвойства, GUIDПроверка);
     // Заполняем созданную структуру из переданного 
     // значения переменной
     ЗаполнитьЗначенияСвойств(СтруктураПроверка, Переменная);
     // Если значение для свойства структуры осталось 
     // NULL, то искомое свойство не найдено, 
     // и наоборот.
     Если СтруктураПроверка[ИмяСвойства] = GUIDПроверка Тогда
          Возврат Ложь;
     Иначе
          Возврат Истина;
	 КонецЕсли;
	 
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЗначениеСоответствуетТипу(Значение, СтрокаТип)
	
	//Метод присутствует в клиентском и серверном модулях
	
	Возврат ТипЗнч(Значение) = Тип(СтрокаТип);
	 	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоПримитивныйТип_КонтурEDI(Значение) 

	ТипЗначения				= ТипЗнч(Значение);
	МассивПримитивныхТипов	= ПолучитьМассивПримитивныхТипов_КонтурEDI();  
	
	Возврат  МассивПримитивныхТипов.Найти(ТипЗначения) <> Неопределено;

КонецФункции // ПолучитьМассивПримитивныхТипов_КонтурEDI()


&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьМассивПримитивныхТипов_КонтурEDI() 

	МассивПримитивныхТипов = Новый Массив;
	МассивПримитивныхТипов.Добавить(Тип("Число"));
	МассивПримитивныхТипов.Добавить(Тип("Строка"));
    МассивПримитивныхТипов.Добавить(Тип("Булево"));
    МассивПримитивныхТипов.Добавить(Тип("Дата")); 
	
	Возврат МассивПримитивныхТипов;

КонецФункции // ПолучитьМассивПримитивныхТипов_КонтурEDI()

&НаСервереБезКонтекста 
Функция ПолучитьИмяМетаданных_КонтурEDI(Значение)
    Возврат Значение.Метаданные().ПолноеИмя();
КонецФункции

// } ВСПОМОГАТЕЛЬНЫЕ

// Красивое представление даты {

&НаКлиенте
Функция ПолучитьНаименованиеДняНедели(Дата)
	
	НомерДняНедели = ДеньНедели(Дата);
	
	Если НомерДняНедели = 1 Тогда
		Возврат "понедельник";
	ИначеЕсли НомерДняНедели = 2 Тогда
		Возврат "вторник";
	ИначеЕсли НомерДняНедели = 3 Тогда
		Возврат "среда";
	ИначеЕсли НомерДняНедели = 4 Тогда
		Возврат "четверг";
	ИначеЕсли НомерДняНедели = 5 Тогда
		Возврат "пятница";
	ИначеЕсли НомерДняНедели = 6 Тогда
		Возврат "суббота";
	ИначеЕсли НомерДняНедели = 7 Тогда
		Возврат "воскресенье";
	КонецЕсли;
	
	Возврат "";
		
КонецФункции

&НаКлиенте
Функция ПолучитьТекстРазностиЧасовМинут(КоличествоСекунд)
	
	Часов = Цел(КоличествоСекунд/3600);
	Минут = Цел((КоличествоСекунд-Часов*3600)/60);
	
	Если Часов = 0 Тогда
		Если Минут = 0 Тогда
			Минут = 1;
		КонецЕсли;
		Возврат СокрЛП(Минут)+" мин.";
	Иначе
		Если Минут = 0 Тогда
			Возврат СокрЛП(Часов)+" час.";
		Иначе
			Возврат СокрЛП(Часов)+" час. "+СокрЛП(Минут)+" мин.";
		КонецЕсли;	
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция ПолучитьТекстРазностиДней(КоличествоДней)
	
	Стр = ЧислоПрописью(КоличествоДней,"НД = Ложь","день,дня,дней,м");
	
	Если Найти(Стр,"день") Тогда
		Стр = "день";
	ИначеЕсли Найти(Стр,"дней") Тогда	
		Стр = "дней";
	ИначеЕсли Найти(Стр,"дня") Тогда
		Стр = "дня";
	Иначе
		Стр = "дней";
	КонецЕсли;
	
	Возврат СокрЛП(КоличествоДней)+" "+Стр;
	
КонецФункции

&НаКлиенте
Функция ПолучитьПредставлениеДатыПоставки_КонтурEDI(ДатаПоставки) Экспорт

	Если НЕ ЗначениеЗаполнено(ДатаПоставки) Тогда
		Возврат "не указана";
	КонецЕсли;
	
	Если ДатаПоставки = НачалоДня(ДатаПоставки) Тогда
		ТекстДаты = Формат(ДатаПоставки,"ДФ=dd.MM.yyyy")+" г.";
	Иначе
		ТекстДаты = Формат(ДатаПоставки,"ДФ=dd.MM.yyyy") + " г. в " + Формат(ДатаПоставки,"ДФ=HH:mm");
	КонецЕсли;
	
	ИмяДняНедели = ПолучитьНаименованиеДняНедели(ДатаПоставки);
	
	ТекДата = ТекущаяДата();
	
	НачалоДатыПоставки	= НачалоДня(ДатаПоставки);
	НачалоТекущейДаты	= НачалоДня(ТекДата);
	
	ИмяРазности = "";
	
	Если НачалоДатыПоставки = НачалоТекущейДаты Тогда
		Если ДатаПоставки = НачалоДатыПоставки Тогда
			ИмяРазности = "сегодня";
		ИначеЕсли ДатаПоставки<ТекДата Тогда	
			ТекстРазности = ПолучитьТекстРазностиЧасовМинут(ТекДата-ДатаПоставки);
			ИмяРазности = "сегодня "+ТекстРазности + " назад";
		ИначеЕсли ДатаПоставки>ТекДата Тогда	
			ТекстРазности = ПолучитьТекстРазностиЧасовМинут(ДатаПоставки-ТекДата);
			ИмяРазности = "сегодня через "+ТекстРазности;
		Иначе
			ИмяРазности = "сейчас";
		КонецЕсли;
	ИначеЕсли НачалоДатыПоставки > НачалоТекущейДаты Тогда
		
		РазностьДат = Цел((НачалоДатыПоставки-НачалоТекущейДаты)/(3600*24));
		
		Если РазностьДат = 1 Тогда
			ИмяРазности = "завтра";
		ИначеЕсли РазностьДат = 2 Тогда
			ИмяРазности = "послезавтра";
		Иначе
			ИмяРазности = "через "+ПолучитьТекстРазностиДней(РазностьДат-1);
		КонецЕсли;                      
		
	Иначе
		
		РазностьДат = Цел((НачалоТекущейДаты-НачалоДатыПоставки)/(3600*24));
		
		Если РазностьДат = 1 Тогда
			ИмяРазности = "вчера";
		ИначеЕсли РазностьДат = 2 Тогда
			ИмяРазности = "позавчера";
		Иначе
			ИмяРазности = ПолучитьТекстРазностиДней(РазностьДат-1) + " назад";
		КонецЕсли;                      
		
	КонецЕсли;

	Возврат ТекстДаты+" ("+ИмяДняНедели+", "+ИмяРазности+")";
	
КонецФункции

// } Красивое представление даты

// РАБОТА С ФОРМАМИ И ССЫЛКАМИ {

&НаКлиенте
Функция ОткрытьФорму_КонтурEDI(ИмяФормы, 
								ПолныйПутьКФорме = Неопределено, 
								ПараметрыФормы = Неопределено, 
								ВладелецФормы = Неопределено, 
								ИмяОбработчика = Неопределено, 
								КонтекстОбработчика = Неопределено, 
								ПараметрыОбработчика = Неопределено, 
								РежимОткрытияОкна = Неопределено,
								Уникальность = Ложь, 
								Окно = Неопределено, 
								НавигационнаяСсылка = Неопределено) Экспорт
	
	Если ПараметрыФормы = Неопределено Тогда
		ПараметрыФормы = Новый Структура;
	КонецЕсли;
	Если ТипЗнч(ПараметрыФормы) = Тип("Структура") Тогда
		ПараметрыФормы.Вставить("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
		ПараметрыФормы.Вставить("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	КонецЕсли;
	
	Если ПолныйПутьКФорме = Неопределено Тогда
		ПолныйПутьКФорме = ПараметрыКлиентСервер.ПутьКФормам;
	КонецЕсли;
	ПолноеИмяФормы = ПолныйПутьКФорме + ИмяФормы;
		
	Если ПараметрыКлиентСервер.МодальностьЗапрещена Тогда
		Возврат ОткрытьФормуАсинхронно(ПолноеИмяФормы, 
										ПараметрыФормы, 
										ВладелецФормы, 
										Уникальность, 
										Окно, 
										РежимОткрытияОкна, 
										НавигационнаяСсылка, 
										ИмяОбработчика, 
										КонтекстОбработчика, 
										ПараметрыОбработчика);
	Иначе
		Результат = ОткрытьФормуСинхронно(ПолноеИмяФормы, 
											ПараметрыФормы,
											ВладелецФормы, 
											Уникальность, 
											Окно, 
											РежимОткрытияОкна);
		Если (ИмяОбработчика <> Неопределено) 
			И (КонтекстОбработчика <> Неопределено) Тогда 
			Выполнить("КонтекстОбработчика." + ИмяОбработчика + "(Результат, ПараметрыОбработчика);");
		КонецЕсли;
	КонецЕсли;
		
КонецФункции

&НаКлиенте
Функция ОткрытьФормуАсинхронно(ПолноеИмяФормы, 
								ПараметрыФормы, 
								ВладелецФормы, 
								Уникальность, 
								Окно, 
								РежимОткрытияОкна, 
								НавигационнаяСсылка, 
								ИмяОбработчика, 
								КонтекстОбработчика, 
								ПараметрыОбработчика)
	
	Если РежимОткрытияОкна = Неопределено Тогда
		РежимОткрытияОкна =	РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	КонецЕсли;
		
	ОписаниеОбработчика = Неопределено;
	Если (ИмяОбработчика <> Неопределено) 
		И (КонтекстОбработчика <> Неопределено) Тогда
		Выполнить("ОписаниеОбработчика = Новый ОписаниеОповещения(ИмяОбработчика, КонтекстОбработчика, ПараметрыОбработчика)");
	КонецЕсли;
	
	Результат = Неопределено;
	Выполнить("Результат = ОткрытьФорму(ПолноеИмяФормы, ПараметрыФормы, ВладелецФормы, Уникальность, Окно, НавигационнаяСсылка, ОписаниеОбработчика, РежимОткрытияОкна)");
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ОткрытьФормуСинхронно(ПолноеИмяФормы, 
								ПараметрыФормы, 
								ВладелецФормы, 
								Уникальность, 
								Окно, 
								РежимОткрытияОкна)
	
	Результат = Неопределено;
	
	Форма = ПолучитьФорму(ПолноеИмяФормы, ПараметрыФормы, ВладелецФормы, Уникальность, Окно);
	Если РежимОткрытияОкна = РежимОткрытияОкнаФормы.Независимый Тогда
		Форма.Открыть();
	Иначе
		Результат = Форма.ОткрытьМодально();
	КонецЕсли;
	
	Возврат Результат;
		
КонецФункции

&НаКлиенте
Функция ПолучитьФорму_КонтурEDI(ИмяФормы,
								ПолныйПутьКФорме = Неопределено,
								ПараметрыФормы = Неопределено, 
								ВладелецФормы  = Неопределено, 
								Уникальность = Неопределено) Экспорт
	
	Если ПараметрыФормы = Неопределено Тогда
		ПараметрыФормы = Новый Структура;
	КонецЕсли;
	Если ТипЗнч(ПараметрыФормы) = Тип("Структура") Тогда
		ПараметрыФормы.Вставить("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
		ПараметрыФормы.Вставить("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	КонецЕсли;
	
	Если ПолныйПутьКФорме = Неопределено Тогда
		ПолныйПутьКФорме = ПараметрыКлиентСервер.ПутьКФормам;
	КонецЕсли;
	ПолноеИмяФормы = ПолныйПутьКФорме + ИмяФормы;
	
	Возврат ПолучитьФорму(ПолноеИмяФормы,
							ПараметрыФормы,
							ВладелецФормы,
							Уникальность);
	
КонецФункции
						
&НаКлиенте
Процедура ОткрытьФормуЗначения_КонтурEDI(Значение, ИмяОбработчика = "", КонтекстОбработчика = Неопределено, ПараметрыОбработчика = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Значение) Тогда
		Возврат;
	КонецЕсли; 
	
	Если ЭтоПримитивныйТип_КонтурEDI(Значение) Тогда
		Алгоритм = "";
		Если ПараметрыКлиентСервер.МодальностьЗапрещена Тогда
			Если  НЕ ИмяОбработчика = "" Тогда
				Алгоритм = Алгоритм + "ОписаниеОповещения = Новый ОписаниеОповещения(ИмяОбработчика, КонтекстОбработчика,ПараметрыОбработчика);" + Символы.ПС;
			Иначе
				Алгоритм = Алгоритм + "ОписаниеОповещения = Неопределено;" + Символы.ПС;
			КонецЕсли; 			
			Алгоритм = Алгоритм + "ПоказатьЗначение(ОписаниеОповещения,Значение);"; 
		Иначе
			Алгоритм = "ОткрытьЗначение(Значение);" + Символы.ПС;
			Если  НЕ ИмяОбработчика = ""  Тогда 			
				Алгоритм = Алгоритм + "КонтекстОбработчика." + ИмяОбработчика + "(ПараметрыОбработчика);";
			КонецЕсли; 			
		КонецЕсли;
		Выполнить(Алгоритм);
	Иначе
		ИмяФормыДляОткрытия = "" +ПолучитьИмяМетаданных_КонтурEDI(Значение);
		ПараметрыФормы = Новый Структура("Ключ", Значение); 
		ОткрытьФорму_КонтурEDI(".ФормаОбъекта", ИмяФормыДляОткрытия,ПараметрыФормы,,ИмяОбработчика ,КонтекстОбработчика, ПараметрыОбработчика);  		
	КонецЕсли;
	
КонецПроцедуры

// } РАБОТА С ФОРМАМИ И ССЫЛКАМИ 

// РАБОТА С ФАЙЛАМИ {

&НаКлиенте
Процедура ОткрытьДиалогВыбораФайла_КонтурEDI(ИмяОбработчика,
												КонтекстОбработчика,
												ПараметрыОбработчика = Неопределено,
												МножественныйВыбор = Ложь,
												Фильтр = Неопределено, 
												Заголовок = Неопределено, 
												РежимДиалога = Неопределено, 
												ПолучаемыеФайлы = Неопределено)  Экспорт
	
	Если РежимДиалога = Неопределено Тогда
		РежимДиалога = РежимДиалогаВыбораФайла.Открытие;
	КонецЕсли;
	
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалога);
    
    ДиалогОткрытияФайла.МножественныйВыбор    = Ложь;
    ДиалогОткрытияФайла.Фильтр                = Фильтр; 
    ДиалогОткрытияФайла.Заголовок             = Заголовок;
	
	Если ТипЗнч(ПараметрыОбработчика) = Тип("Структура") Тогда    
		ПараметрыОбработчика.Вставить("ДиалогВыбораФайла", ДиалогОткрытияФайла);
	Иначе
		ПараметрыОбработчика = Новый Структура("ДиалогВыбораФайла", ДиалогОткрытияФайла);
	КонецЕсли;
	
    Если ПараметрыКлиентСервер.МодальностьЗапрещена Тогда
        Алгоритм = "ОписаниеОповещения = Новый ОписаниеОповещения(ИмяОбработчика, КонтекстОбработчика, ПараметрыОбработчика);" + Символы.ПС;
		Если ПолучаемыеФайлы <> Неопределено
			И (РежимДиалога = РежимДиалогаВыбораФайла.Сохранение 
			ИЛИ РежимДиалога = РежимДиалогаВыбораФайла.ВыборКаталога) Тогда
			Алгоритм = Алгоритм + "НачатьПолучениеФайлов(ОписаниеОповещения, ПолучаемыеФайлы, ДиалогОткрытияФайла, Истина);";
		Иначе
			Алгоритм = Алгоритм + "НачатьПомещениеФайлов(ОписаниеОповещения, , ДиалогОткрытияФайла, Истина, УникальныйИдентификатор);";
		КонецЕсли;                 
    Иначе
		Если ПолучаемыеФайлы <> Неопределено
			И (РежимДиалога = РежимДиалогаВыбораФайла.Сохранение 
			ИЛИ РежимДиалога = РежимДиалогаВыбораФайла.ВыборКаталога) Тогда
			ПолучитьФайлы(ПолучаемыеФайлы,  , ДиалогОткрытияФайла, Истина)
		Иначе
			РезультатВыбораФайла = ?(ДиалогОткрытияФайла.Выбрать(), ДиалогОткрытияФайла.ВыбранныеФайлы, Неопределено); 
			ПолучаемыеФайлы = Новый Массив;
			Для каждого ПутьКВыбранномуФайлу Из РезультатВыбораФайла Цикл
				ДвоичныеДанные				= Новый ДвоичныеДанные(ПутьКВыбранномуФайлу);                     
				АдресВоВременномХранилище	= ПоместитьВоВременноеХранилище(ДвоичныеДанные);
				ОписаниеФайла = Новый ОписаниеПередаваемогоФайла(ПутьКВыбранномуФайлу, АдресВоВременномХранилище);
				ПолучаемыеФайлы.Добавить(ОписаниеФайла);
			КонецЦикла; 
		КонецЕсли;
        Алгоритм = "КонтекстОбработчика." + ИмяОбработчика + "(ПолучаемыеФайлы, ПараметрыОбработчика);";  
    КонецЕсли;
    
    Выполнить(Алгоритм);

КонецПроцедуры // ОткрытьДиалогВыбораФайла_КонтурEDI()

//Для получения пути выбранного файла без помещения во временное хранилище
&НаКлиенте
Процедура ВыбратьФайл_КонтурEDI(ИмяОбработчика,
								КонтекстОбработчика, РежимДиалога,
								Заголовок = "",
								НачальноеИмя = "",
								Фильтр = Неопределено,
								МножественныйВыбор = Ложь,
								Расширение = "") Экспорт
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалога);
	ДиалогВыбораФайла.ПолноеИмяФайла = НачальноеИмя;
	ДиалогВыбораФайла.Заголовок = Заголовок;
	ДиалогВыбораФайла.Фильтр = Фильтр;
	ДиалогВыбораФайла.МножественныйВыбор = МножественныйВыбор;
	ДиалогВыбораФайла.Расширение = Расширение;
	
	Если ПараметрыКлиентСервер.МодальностьЗапрещена Тогда
		Алгоритм = "ДиалогВыбораФайла.Показать(Новый ОписаниеОповещения(""" + ИмяОбработчика + """, КонтекстОбработчика))";
		Выполнить(Алгоритм);
	Иначе
		Если ДиалогВыбораФайла.Выбрать() Тогда	
			Алгоритм = "КонтекстОбработчика." + ИмяОбработчика + "(ДиалогВыбораФайла.ПолноеИмяФайла)";
			Выполнить(Алгоритм);
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Функция ВернутьКаталогВыбранногоФайл_КонтурEDI(Знач ИмяФайла)  Экспорт

    Каталог = "";
    
    ПутьКФайлуРазложенныйВМассив = EDI_РазложитьСтрокуВМассивСлов(ИмяФайла, "\");
    
    КоличествоЧастейПути = ПутьКФайлуРазложенныйВМассив.Количество();
    
    Если КоличествоЧастейПути > 0 Тогда 
        
        ИндексПоследнейЧасти = КоличествоЧастейПути-1;
        
        ПутьКФайлуРазложенныйВМассив.Удалить(ИндексПоследнейЧасти); 
        
        Для каждого ЧастьПути Из ПутьКФайлуРазложенныйВМассив Цикл
            Каталог = Каталог + ЧастьПути+"\";
        КонецЦикла;
    КонецЕсли;
    
    Возврат Каталог ;
     
КонецФункции // ВернутьКаталогВыбранногоФайл_КонтурEDI()

&НаКлиенте
Процедура ФайлСуществует_КонтурEDI(ИмяОбработчика,КонтекстОбработчика,Знач ПутьКФайлу,ПараметрыОбработчика = Неопределено) Экспорт
    
    Файл = Новый Файл(ПутьКФайлу);
    Если ПараметрыКлиентСервер.МодальностьЗапрещена Тогда
        Алгоритм =                 "ОписаниеОповещения = Новый ОписаниеОповещения(ИмяОбработчика, КонтекстОбработчика,ПараметрыОбработчика);" + Символы.ПС;
        Алгоритм = Алгоритм +     "Файл.НачатьПроверкуСуществования(ОписаниеОповещения);"; 
    Иначе         
        Алгоритм = "КонтекстОбработчика." + ИмяОбработчика + "(Файл.Существует(), ПараметрыОбработчика);";
    КонецЕсли;
    
    Выполнить(Алгоритм);
    
 КонецПроцедуры  
 
 // } РАБОТА С ФАЙЛАМИ

// ВЗАИМОДЕЙСТВИЕ С ПОЛЬЗОВАТЕЛЕМ {

&НаКлиенте
Процедура ПоказатьВопрос_КонтурEDI(ИмяОбработчика,
									КонтекстОбработчика,
									ПараметрыОбработчика = Неопределено,
									ТекстВопроса,
									Кнопки,
									Таймаут = 0,
									КнопкаПоУмолчанию = Неопределено,
									Заголовок = "Контур.EDI",
									КнопкаТаймаута = Неопределено) Экспорт
	
	Если ПараметрыКлиентСервер.МодальностьЗапрещена Тогда
		
		ОписаниеОбработчика = Неопределено;
		Выполнить("ОписаниеОбработчика = Новый ОписаниеОповещения(ИмяОбработчика, КонтекстОбработчика, ПараметрыОбработчика)");
		Выполнить("ПоказатьВопрос(ОписаниеОбработчика, ТекстВопроса, Кнопки, Таймаут, КнопкаПоУмолчанию, Заголовок, КнопкаТаймаута)");
		
	Иначе
		РезультатВопроса = Вопрос(ТекстВопроса, Кнопки, Таймаут, КнопкаПоУмолчанию, Заголовок, КнопкаТаймаута);
		Выполнить("КонтекстОбработчика." + ИмяОбработчика + "(РезультатВопроса, ПараметрыОбработчика);");
	КонецЕсли; 	

КонецПроцедуры

&НаКлиенте
Процедура СообщениеПользователю_КонтурEDI(Текст, ИдентификаторНазначения = Неопределено) Экспорт

	СП = Новый СообщениеПользователю;
	
	Если ИдентификаторНазначения = Неопределено Тогда
		ИдентификаторНазначения = Новый УникальныйИдентификатор(ПараметрыКлиентСервер.IDОсновнойФормы);
	КонецЕсли;
	
	СП.ИдентификаторНазначения = ИдентификаторНазначения;
	СП.Текст = Текст;
	СП.Сообщить();
		
КонецПроцедуры

&НаКлиенте
Процедура Предупреждение_КонтурEDI(ТекстПредупреждения,
									Таймаут = 0, 
									Заголовок = "Контур.EDI",
									ИмяОбработчика = Неопределено, 
									КонтекстОбработчика = Неопределено, 
									ПараметрыОбработчика = Неопределено) Экспорт

	Если ПараметрыКлиентСервер.МодальностьЗапрещена Тогда 
		
		ОписаниеОбработчика = Неопределено;
		Если НЕ ИмяОбработчика = Неопределено 
			И НЕ КонтекстОбработчика = Неопределено Тогда
			Выполнить("ОписаниеОбработчика = Новый ОписаниеОповещения(ИмяОбработчика, КонтекстОбработчика, ПараметрыОбработчика)");
		КонецЕсли;
		Выполнить("ПоказатьПредупреждение(ОписаниеОбработчика, ТекстПредупреждения, Таймаут, Заголовок)");
		
	Иначе
		Предупреждение(ТекстПредупреждения, Таймаут, Заголовок);
		Если НЕ ИмяОбработчика = Неопределено 
			И НЕ КонтекстОбработчика = Неопределено Тогда
			Выполнить("КонтекстОбработчика." + ИмяОбработчика + "(ПараметрыОбработчика);");
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // Предупреждение_КонтурEDI()

&НаКлиенте
Процедура ВыбратьИзСписка_КонтурEDI(ИмяОбработчика, 
									КонтекстОбработчика, 
									ПараметрыОбработчика = Неопределено, 
									СписокЗначений, 
									Элемент = Неопределено, 
									НачальноеЗначение = Неопределено) Экспорт
									
	Если ПараметрыОбработчика = Неопределено Тогда
		ПараметрыОбработчика = Новый Структура;
	КонецЕсли;
	Если ПараметрыКлиентСервер.МодальностьЗапрещена Тогда 
		ОписаниеОбработчика = Неопределено;
		Если (ИмяОбработчика <> Неопределено) 
			И (КонтекстОбработчика <> Неопределено) Тогда
			Выполнить("ОписаниеОбработчика = Новый ОписаниеОповещения(ИмяОбработчика, КонтекстОбработчика, ПараметрыОбработчика)");
		КонецЕсли;
		Выполнить("КонтекстОбработчика.ПоказатьВыборИзСписка(ОписаниеОбработчика, СписокЗначений, Элемент, НачальноеЗначение)");
	Иначе
		Результат = КонтекстОбработчика.ВыбратьИзСписка(СписокЗначений, Элемент, НачальноеЗначение);
		Выполнить("КонтекстОбработчика." + ИмяОбработчика + "(Результат, ПараметрыОбработчика);");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВыборЭлемента_КонтурEDI(ИмяОбработчика, 
									КонтекстОбработчика, 
									ПараметрыОбработчика = Неопределено, 
									СписокЗначений, 
									Элемент = Неопределено, 
									Заголовок = Неопределено) Экспорт
									
	Если ПараметрыОбработчика = Неопределено Тогда
		ПараметрыОбработчика = Новый Структура;
	КонецЕсли;
	
	Если ПараметрыКлиентСервер.МодальностьЗапрещена Тогда 
		
		ОписаниеОбработчика = Неопределено;
		
		Если (ИмяОбработчика <> Неопределено) 
			И (КонтекстОбработчика <> Неопределено) Тогда
			
			Выполнить("ОписаниеОбработчика = Новый ОписаниеОповещения(ИмяОбработчика, КонтекстОбработчика, ПараметрыОбработчика)");
			
		КонецЕсли;
		
		Выполнить("СписокЗначений.ПоказатьВыборЭлемента(ОписаниеОбработчика, Заголовок, Элемент)");
		
	Иначе
		
		Результат = СписокЗначений.ВыбратьЭлемент(Заголовок, Элемент);
		Выполнить("КонтекстОбработчика." + ИмяОбработчика + "(Результат, ПараметрыОбработчика);");
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИзМеню_КонтурEDI(ИмяОбработчика, 
									КонтекстОбработчика,
									ПараметрыОбработчика = Неопределено, 
									СписокЗначений, 
									Элемент = Неопределено) Экспорт
	
	Если ПараметрыОбработчика = Неопределено Тогда
		ПараметрыОбработчика = Новый Структура;
	КонецЕсли;
	
	Если ПараметрыКлиентСервер.МодальностьЗапрещена Тогда 
		ОписаниеОбработчика = Неопределено;
		Если (ИмяОбработчика <> Неопределено) 
			И (КонтекстОбработчика <> Неопределено) Тогда
			Выполнить("ОписаниеОбработчика = Новый ОписаниеОповещения(ИмяОбработчика, КонтекстОбработчика, ПараметрыОбработчика)");
		КонецЕсли;
		Выполнить("КонтекстОбработчика.ПоказатьВыборИзМеню(ОписаниеОбработчика, СписокЗначений, Элемент)"); 
	Иначе
		Результат = КонтекстОбработчика.ВыбратьИзМеню(СписокЗначений, Элемент);
		Выполнить("КонтекстОбработчика." + ИмяОбработчика + "(Результат, ПараметрыОбработчика);");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВводДаты_КонтурEDI(ИмяОбработчика, 
										КонтекстОбработчика,
										ПараметрыОбработчика = Неопределено, 
										Дата = Неопределено, 
										Подсказка = "",
										ЧастьДаты = Неопределено) Экспорт
									
	Если ПараметрыОбработчика = Неопределено Тогда
		ПараметрыОбработчика = Новый Структура;
	КонецЕсли;
	
	Если ЧастьДаты = Неопределено Тогда
		ЧастьДаты = ЧастиДаты.ДатаВремя;
	КонецЕсли;
	
	Если ПараметрыКлиентСервер.МодальностьЗапрещена Тогда 
		ОписаниеОбработчика = Неопределено;
		Если (ИмяОбработчика <> Неопределено) 
			И (КонтекстОбработчика <> Неопределено) Тогда
			Выполнить("ОписаниеОбработчика = Новый ОписаниеОповещения(ИмяОбработчика, КонтекстОбработчика, ПараметрыОбработчика)");
		КонецЕсли;
		Выполнить("ПоказатьВводДаты(ОписаниеОбработчика, Дата, Подсказка, ЧастьДаты)"); 
	Иначе
		ВвестиДату(Дата, Подсказка, ЧастьДаты);
		Выполнить("КонтекстОбработчика." + ИмяОбработчика + "(Дата, ПараметрыОбработчика);");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВводЧисла_КонтурEDI(ИмяОбработчика, 
										КонтекстОбработчика,
										ПараметрыОбработчика = Неопределено, 
										Число = 0, 
										Подсказка = "",
										Длина = 0,
										Точность = 0) Экспорт
									
	Если ПараметрыОбработчика = Неопределено Тогда
		ПараметрыОбработчика = Новый Структура;
	КонецЕсли;
	
	Если ПараметрыКлиентСервер.МодальностьЗапрещена Тогда 
		ОписаниеОбработчика = Неопределено;
		Если (ИмяОбработчика <> Неопределено) 
			И (КонтекстОбработчика <> Неопределено) Тогда
			Выполнить("ОписаниеОбработчика = Новый ОписаниеОповещения(ИмяОбработчика, КонтекстОбработчика, ПараметрыОбработчика)");
		КонецЕсли;
		Выполнить("ПоказатьВводЧисла(ОписаниеОбработчика, Число, Подсказка, Длина, Точность)"); 
	Иначе
		ВвестиЧисло(Число, Подсказка, Длина, Точность);
		Выполнить("КонтекстОбработчика." + ИмяОбработчика + "(Число, ПараметрыОбработчика);");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВводСтроки_КонтурEDI(ИмяОбработчика, 
										КонтекстОбработчика,
										ПараметрыОбработчика = Неопределено, 
										Строка = "", 
										Подсказка = "",
										Длина = 0,
										Многострочность = Ложь) Экспорт
									
	Если ПараметрыОбработчика = Неопределено Тогда
		ПараметрыОбработчика = Новый Структура;
	КонецЕсли;
	
	Если ПараметрыКлиентСервер.МодальностьЗапрещена Тогда 
		ОписаниеОбработчика = Неопределено;
		Если (ИмяОбработчика <> Неопределено) 
			И (КонтекстОбработчика <> Неопределено) Тогда
			Выполнить("ОписаниеОбработчика = Новый ОписаниеОповещения(ИмяОбработчика, КонтекстОбработчика, ПараметрыОбработчика)");
		КонецЕсли;
		Выполнить("ПоказатьВводСтроки (ОписаниеОбработчика, Строка, Подсказка, Длина, Многострочность)"); 
	Иначе
		ВвестиСтроку(Строка,Подсказка,Длина,Многострочность);		
		Выполнить("КонтекстОбработчика." + ИмяОбработчика + "(Строка, ПараметрыОбработчика);");
	КонецЕсли;
	
КонецПроцедуры

// } ВЗАИМОДЕЙСТВИЕ С ПОЛЬЗОВАТЕЛЕМ

// РАБОТА СО СПРАВОЧНИКАМИ KONTUR.EDI {

&НаКлиенте
Процедура ВыбратьЭлементСправочника_КонтурEDI(ИмяСправочника, 
												Владелец = Неопределено, 
												ВладелецФормы, 
												ИмяОбработчика, 
												КонтекстОбработчика) Экспорт
	
	Если ИмяСправочника = "ТочкаДоставкиСторонняя" Тогда
		
		АдресТаблицыВХранилище = ПоместитьТаблицуТочекДоставкиПартнераВВХСервер(Владелец);
		ПараметрыФормы = Новый Структура("АдресТаблицыВХранилище", АдресТаблицыВХранилище);
		ДополнительныеПараметры = Новый Структура("ИмяСправочника,ИмяОбработчика,КонтекстОбработчика", 
			ИмяСправочника, ИмяОбработчика, КонтекстОбработчика);
			
		ОткрытьФорму_КонтурEDI("УниверсальнаяФормаВыбораУправляемая", , 
			ПараметрыФормы, ВладелецФормы, "ОбработчикВыбораЭлементаСправочника", ЭтаФорма, ДополнительныеПараметры);
		
	Иначе
		
		ВызватьИсключение "Не реализовано.";
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПоместитьТаблицуТочекДоставкиПартнераВВХСервер(Партнер)
	
	ТаблицаТД = МодульОбъекта().ПолучитьСписокЭлементовСправочника("ТочкиДоставкиСторонние", Партнер);
	
	СписокКолонокДляКопирования = "Ссылка, GLN, АдресДоставки, ЮрФизЛицо, ТочкаДоставкиСвойСправочник, ТипЛокализации";
	ФильтрОбменаПоТочкамДоставки = МодульОбъекта().НастройкиМодуля.ФильтрОбменаПоТочкамДоставки;
	
	Если ФильтрОбменаПоТочкамДоставки = Истина Тогда
		СписокКолонокДляКопирования = СписокКолонокДляКопирования + ", ЗагружатьСообщения";
	КонецЕсли;
	
	ТаблицаТДКопия = ТаблицаТД.Скопировать(, СписокКолонокДляКопирования);
	АдресВоВХ = ПоместитьВоВременноеХранилище(ТаблицаТДКопия, Объект.ПараметрыКлиентСервер.IDОсновнойФормы);
	
	Возврат АдресВоВХ; 
	
КонецФункции

&НаКлиенте
Процедура ОбработчикВыбораЭлементаСправочника(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт
	
	Если ВыбранноеЗначение = Неопределено Тогда
		
		ВыбранноеЗначениеСсылка = Неопределено;
		
	Иначе
		
		Если ДополнительныеПараметры.ИмяСправочника = "ТочкаДоставкиСторонняя" Тогда
			ВыбранноеЗначениеСсылка = ОбработчикВыбораТочкиТоставкиСтороннейСервер(ВыбранноеЗначение);
		КонецЕсли;
		
	КонецЕсли;
	
	КонтекстОбработчика = ДополнительныеПараметры.КонтекстОбработчика;
	ИмяОбработчика = ДополнительныеПараметры.ИмяОбработчика;
	ПараметрыОбработчика = Неопределено;
	
	Если ПараметрыКлиентСервер.МодальностьЗапрещена Тогда
		
		ОписаниеОбработчика = Неопределено;
		Выполнить("ОписаниеОбработчика = Новый ОписаниеОповещения(ИмяОбработчика, КонтекстОбработчика, ПараметрыОбработчика);");
		Выполнить("ВыполнитьОбработкуОповещения(ОписаниеОбработчика, ВыбранноеЗначениеСсылка);");
		
	Иначе
		
		Выполнить("КонтекстОбработчика." + ИмяОбработчика + "(ВыбранноеЗначениеСсылка, ПараметрыОбработчика);");
	
	КонецЕсли;
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОбработчикВыбораТочкиТоставкиСтороннейСервер(АдресВоВХ)
	
	ТабРезультат = ПолучитьИзВременногоХранилища(АдресВоВХ);
	НайденнаяСтрока = ТабРезультат.Найти(Истина, "Выбор");
	
	Возврат НайденнаяСтрока.Ссылка;
	
КонецФункции

// } РАБОТА СО СПРАВОЧНИКАМИ KONTUR.EDI

// ОБРАБОТКА СЕРВЕРНЫХ ВЫЗОВОВ {

&НаКлиенте
Процедура ЗаписьЖурналаРегистрацииНаКлиенте_КонтурEDI(УровеньСобытияКлиент, Комментарий, Данные = Неопределено) Экспорт
	
	ЗаписьЖурналаРегистрацииНаСервере(УровеньСобытияКлиент, Комментарий, Данные); 

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписьЖурналаРегистрацииНаСервере(УровеньСобытияКлиент, Комментарий, Данные = Неопределено)

	Если УровеньСобытияКлиент = "Информация" Тогда
		УровеньСобытия = УровеньЖурналаРегистрации.Информация;
	ИначеЕсли УровеньСобытияКлиент = "Ошибка" Тогда
		УровеньСобытия = УровеньЖурналаРегистрации.Ошибка;
	ИначеЕсли УровеньСобытияКлиент = "Предупреждение" Тогда
		УровеньСобытия = УровеньЖурналаРегистрации.Предупреждение;
	Иначе
		УровеньСобытия = УровеньЖурналаРегистрации.Примечание;
	КонецЕсли;
	
	ЗаписьЖурналаРегистрации("Контур.EDI", УровеньСобытия, , Данные, Комментарий); 

КонецПроцедуры

// } ОБРАБОТКА СЕРВЕРНЫХ ВЫЗОВОВ
