&НаСервере
Перем МодульОбъекта;

&НаКлиенте
Перем МодульОбъектаКлиент;

// СЛУЖЕБНЫЕ {

// Инициализация модуля и его экспортных функций
&НаСервере
Функция МодульОбъекта()
	
	Если МодульОбъекта = Неопределено Тогда
		
		Если Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект Тогда
			МодульОбъекта = МодульОбъектаИзКэша();
		КонецЕсли;
		
		Если МодульОбъекта = Неопределено Тогда
			МодульОбъекта = РеквизитФормыВЗначение("Объект");
			МодульОбъекта.ИнициализироватьПодключаемыеМодули(); // TODO Проверить что из этого используется на формах и перенести в ПараметрыКлиентСервер
			МодульОбъекта.ОбновитьКэшМодуляОбъекта();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат МодульОбъекта;
	
КонецФункции

&НаСервере
Функция МодульОбъектаИзКэша()
	
	Результат = Неопределено;
	
	Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда
		АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект;
 		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Структура = ПолучитьИзВременногоХранилища(АдресВХ);
			Если ТипЗнч(Структура) = Тип("Структура") и Структура.Свойство("ОбработкаОбъект") Тогда
			 	Результат = Структура.ОбработкаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульОбъектаКлиент()
	
	Если ТипЗнч(МодульОбъектаКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбъектаКлиент;
	КонецЕсли;
	
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		МодульОбъектаКлиент = ОсновнаяФорма.МодульОбъектаКлиент();
		Возврат МодульОбъектаКлиент;
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
	
	Если ЭтаФорма.ВладелецФормы = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ЭтаФорма.ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;
    	
КонецФункции

&НаКлиенте
Функция НоваяМетрика()
	
	Возврат ОсновнаяФорма().НоваяМетрика();
	
КонецФункции

// } СЛУЖЕБНЫЕ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
	Параметры.Свойство("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	
	Параметры.МодальностьЗапрещена=МодульОбъекта().МодальностьЗапрещена();
	ПутьКФормам = МодульОбъекта().Метаданные().ПолноеИмя() + ".Форма.";
	
	Ссылка=Параметры.Ссылка;
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		
		СтруктураПолей = МодульОбъекта().ПолучитьЭлементСправочника("НашиОрганизации", Ссылка);
		
		Если НЕ СтруктураПолей = Неопределено Тогда
		
			partyId 		= СтруктураПолей.partyId;
			Наименование 	= СтруктураПолей.Наименование;
			GLN 			= СтруктураПолей.GLN;
			УчетнаяЗапись 	= СтруктураПолей.УчетнаяЗапись;
			ОсновнойЯщик 	= СтруктураПолей.ОсновнойЯщик;
			Неактивная		= СтруктураПолей.Неактивная;
//			УчетнаяЗаписьПредставление = ПолучитьПредставлениеЭлементаСправочника(УчетнаяЗапись);
			
			//показать транспортные ящики
			ОбновитьСписокТранспортныхЯщиков();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТипПоля1С(ИмяНаФорме,Тип1С)
	
	ПолеФормы	= Элементы[ИмяНаФорме];
	ТипПоля		= МодульОбъекта().ПолучитьТипЗначенияОбъекта(Тип1С);
			
	Если ТипПоля = Неопределено Тогда
		
		Сообщить("Не задан тип объекта 1С для поля с типом "+Тип1С);
		
	Иначе	
		
		Элементы[ИмяНаФорме].ОграничениеТипа = Новый ОписаниеТипов(ТипПоля);
		
		Если НЕ ЗначениеЗаполнено(ЭтаФорма[ИмяНаФорме]) Тогда
			
			ЭтаФорма[ИмяНаФорме] = МодульОбъекта().ПолучитьПустуюСсылкуОбъекта(Тип1С);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаВыполнитьНажатие(Команда)
	
	УспешноеСохранение = СохранитьНашуОрганизациюСервер();

	Если УспешноеСохранение=Истина Тогда 
		Оповестить("КонтурEDI_ОбновитьСписокНашихОрганизаций");//вообще нам нет смысла что-то обновлять
		
		Соединение = Неопределено;
		
		ЭтаФорма.Закрыть(Истина);
	Иначе
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СохранитьНашуОрганизациюСервер()
	
	Если НЕ ПроверитьЗначенияФормы() Тогда
		Возврат ложь;
	КонецЕсли;
	
	СтруктураПолей = СоздатьСтруктуруСправочникаДляСохранения();  

	МодульОбъекта().СохранитьЭлементСправочника("НашиОрганизации", Ссылка, СтруктураПолей);
	
    Возврат Истина;
	
КонецФункции

&НаСервере
Функция ПроверитьЗначенияФормы()
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция  СоздатьСтруктуруСправочникаДляСохранения()
	СтруктураПолей = Новый Структура();
	СтруктураПолей.Вставить("PartyId", 									partyId);
	СтруктураПолей.Вставить("Наименование", 							Наименование);
	СтруктураПолей.Вставить("GLN", 										GLN);
	СтруктураПолей.Вставить("УчетнаяЗапись",							УчетнаяЗапись);
	СтруктураПолей.Вставить("ОсновнойЯщик",								ОсновнойЯщик);
	СтруктураПолей.Вставить("Неактивная",								Неактивная);
	Возврат СтруктураПолей;
КонецФункции	

&НаКлиенте
Процедура УстановитьОсновнойЯщик(Команда)
	Если Элементы.ТранспортныеЯщики.ТекущиеДанные<>Неопределено Тогда
		ОсновнойЯщик = Элементы.ТранспортныеЯщики.ТекущиеДанные.Ссылка;
		ОбновитьСписокТранспортныхЯщиков();
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокТранспортныхЯщиков()
	
	СписокЯщиков = МодульОбъекта().ПолучитьСписокЭлементовСправочника("ТранспортныеЯщики",Ссылка);
	ТранспортныеЯщики.Очистить();
	Для Каждого СтрокаЯщиков Из СписокЯщиков Цикл
		НовСтрока = ТранспортныеЯщики.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтрока,СтрокаЯщиков);
		НовСтрока.Основной = (НовСтрока.Ссылка = ОсновнойЯщик);
	КонецЦикла;	
	
КонецПроцедуры	

&НаКлиенте
Процедура ТранспортныеЯщикиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	// Вставить содержимое обработчика.
	ОткрытьФормуТранспортногоЯщика();
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуТранспортногоЯщика()
	
	ТекСтрока = Элементы.ТранспортныеЯщики.ТекущиеДанные;
	
	Если ТекСтрока<>Неопределено Тогда
		СсылкаНаЯщик = ТекСтрока.Ссылка;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ссылка", 				СсылкаНаЯщик);
		ПараметрыФормы.Вставить("Новая", 				Ложь);
		ПараметрыФормы.Вставить("МодальностьЗапрещена", Параметры.МодальностьЗапрещена);
		
		МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("ТранспортныеЯщики_ЭлементУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбработчикПослеРедактированияТранспортногоЯщика", ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикПослеРедактированияТранспортногоЯщика(ПараметрыЗакрытияИнтерактивнойФормы, ДополнительныеПараметры) Экспорт
	
	//обновить список организаций
	ОбновитьСписокТранспортныхЯщиков();	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОсновнаяФорма().ПриОткрытииФормы(ЭтаФорма, "НашиОрганизации_ЭлементУправляемая");

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	ОсновнаяФорма().ПриЗакрытииФормы(ЭтаФорма, "НашиОрганизации_ЭлементУправляемая");	

КонецПроцедуры

