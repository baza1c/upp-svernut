&НаСервере
Перем МодульОбъекта;

&НаКлиенте
Перем МодульОбъектаКлиент;

// СЛУЖЕБНЫЕ {

// Инициализация модуля и его экспортных функций
&НаСервере
Функция МодульОбъекта()
	
	Если МодульОбъекта = Неопределено Тогда
		
		Если Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект Тогда
			МодульОбъекта = МодульОбъектаИзКэша();
		КонецЕсли;
		
		Если МодульОбъекта = Неопределено Тогда
			МодульОбъекта = РеквизитФормыВЗначение("Объект");
			МодульОбъекта.ИнициализироватьПодключаемыеМодули(); // TODO Проверить что из этого используется на формах и перенести в ПараметрыКлиентСервер
			МодульОбъекта.ОбновитьКэшМодуляОбъекта();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат МодульОбъекта;
	
КонецФункции

&НаСервере
Функция МодульОбъектаИзКэша()
	
	Результат = Неопределено;
	
	Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда
		АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект;
 		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Структура = ПолучитьИзВременногоХранилища(АдресВХ);
			Если ТипЗнч(Структура) = Тип("Структура") и Структура.Свойство("ОбработкаОбъект") Тогда
			 	Результат = Структура.ОбработкаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульОбъектаКлиент()
	
	Если ТипЗнч(МодульОбъектаКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбъектаКлиент;
	КонецЕсли;
	
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		МодульОбъектаКлиент = ОсновнаяФорма.МодульОбъектаКлиент();
		Возврат МодульОбъектаКлиент;
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
	
	Если ЭтаФорма.ВладелецФормы = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ЭтаФорма.ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;
    	
КонецФункции

&НаКлиенте
Функция НоваяМетрика()
	
	Возврат ОсновнаяФорма().НоваяМетрика();
	
КонецФункции

// } СЛУЖЕБНЫЕ

&НаКлиенте
Процедура ДобавитьПользователя(Команда)
	
ОткрытьФормуПользователяДляРедактированияИлиДобавления(истина);	

КонецПроцедуры

&НаКлиенте
Процедура УдалитьПользователя(Команда)
	
	ТекущаяСтрока = Элементы.СписокПользователей.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = "Вы действительно хотите удалить пользователя """ + ТекущаяСтрока.Пользователь + """?";
	КнопкиВопроса = Новый СписокЗначений;
	КнопкиВопроса.Добавить("Да, удалить");
	КнопкиВопроса.Добавить("Отмена");
	ДопПараметрДляПередачиВОбработчик=ТекущаяСтрока.Пользователь;
	РезультатВопроса = Неопределено;
	
	Если Параметры.МодальностьЗапрещена Тогда
		Выполнить("ПоказатьВопрос(Новый ОписаниеОповещения(""ОбработчикСогласияУдаления"", ЭтаФорма, ДопПараметрДляПередачиВОбработчик), ТекстВопроса, КнопкиВопроса,,,""Контур.EDI"")");
	Иначе
		РезультатВопроса = Вопрос(ТекстВопроса, КнопкиВопроса,,,"Контур.EDI");
		ОбработчикСогласияУдаления(РезультатВопроса,ДопПараметрДляПередачиВОбработчик);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикСогласияУдаления(РезультатВопроса,ПользовательСсылка=Неопределено) Экспорт

	Если РезультатВопроса = "Да, удалить" и ПользовательСсылка<>Неопределено Тогда
		РезультатУдаления = УдалитьПользователяВызовСервера(ПользовательСсылка);
		Если Не РезультатУдаления.Успешно Тогда
			Если Параметры.МодальностьЗапрещена Тогда 
				Выполнить("ПоказатьПредупреждение(,РезультатУдаления.ОписаниеОшибки,,""Контур.EDI"")");
			Иначе
				Предупреждение(РезультатУдаления.ОписаниеОшибки,,"Контур.EDI");
			КонецЕсли;
		КонецЕсли;
		ОбновитьСписокПользователей();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция УдалитьПользователяВызовСервера(ПользовательСсылка)

	Возврат МодульОбъекта().УдалитьПользователя(ПользовательСсылка);

КонецФункции // УдалитьРасширениеВызовСервера()

&НаКлиенте
Процедура СписокПользователейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекСтрока = Элементы.СписокПользователей.ТекущиеДанные;
	
	Если НЕ ТекСтрока=Неопределено Тогда
		Если ЗначениеЗаполнено(ТекСтрока.Пользователь) Тогда
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Пользователь", ТекСтрока.Пользователь);
			МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Пользователи_ЭлементУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбработчикПослередактированияПользователя", ЭтаФорма);
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуПользователяДляРедактированияИлиДобавления(ЭтоДобавление=Ложь)
	
	ТекСтрока = Элементы.СписокПользователей.ТекущиеДанные;
	
	Если (НЕ ТекСтрока=Неопределено) или ЭтоДобавление=Истина Тогда
		Если ТекСтрока<>Неопределено и ЗначениеЗаполнено(ТекСтрока.Пользователь) и ЭтоДобавление=Ложь Тогда
			СсылкаНаП = ТекСтрока.Пользователь;
		иначе
			СсылкаНаП = Неопределено;
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Пользователь", СсылкаНаП);
		ПараметрыФормы.Вставить("Новый", 		ЭтоДобавление);
		МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Пользователи_ЭлементУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбработчикПослередактированияПользователя", ЭтаФорма);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработчикПослередактированияПользователя(Параметр1=Неопределено,ДопПараметр=Неопределено) Экспорт

	 //обновить список
	 ОбновитьСписокПользователей();

КонецПроцедуры // ОбработчикПослередактированияПользователя()

&НаСервере
Процедура ОбновитьСписокПользователей()
	
	СписокПользователей.Очистить();
	
	СписокЭлементов = МодульОбъекта().ПолучитьСписокЭлементовСправочника("Пользователи");
	
	Для каждого Стр Из СписокЭлементов Цикл
		
		НоваяСтрока = СписокПользователей.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Стр);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
	Параметры.Свойство("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	
	ПутьКФормам = МодульОбъекта().Метаданные().ПолноеИмя() + ".Форма.";
	Параметры.МодальностьЗапрещена=МодульОбъекта().МодальностьЗапрещена();
	ОбновитьСписокПользователей()
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	//проверим, что в списке содержится хотя бы 1 пользователь, имеющий роль "Полные права"
	Если СписокПользователей.Количество() > 0 Тогда 
		ЕстьПользовательСПолнымиПравами = Ложь;
		Для Каждого Пользователь Из СписокПользователей Цикл
			Если Пользователь.Роль = "ПолныеПрава" Тогда
				ЕстьПользовательСПолнымиПравами = Истина;
			КонецЕсли;
		КонецЦикла;
		Если Не ЕстьПользовательСПолнымиПравами Тогда
			Предупреждение("В списке пользователей не найден ни один пользователь с ролью ""Полные права""." + Символы.ПС +
			"Необходимо установить роль ""Полные права"" хотя бы одному из списка пользователей.");
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "КонтурEDI_ОбновитьСписокПользователей" Тогда
		
		//предусмотрено оновление в обработчике, который был указан при сохдании блокирующей/модальной формы элемента  ОбработчикПослередактированияПользователя
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПользователейПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СписокПользователейПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОсновнаяФорма().ПриОткрытииФормы(ЭтаФорма, "Пользователи_СписокУправляемая");

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	ОсновнаяФорма().ПриЗакрытииФормы(ЭтаФорма, "Пользователи_СписокУправляемая");	

КонецПроцедуры

