&НаСервере
Перем МодульОбъекта;

&НаКлиенте
Перем МодульОбъектаКлиент;

// СЛУЖЕБНЫЕ {

// Инициализация модуля и его экспортных функций
&НаСервере
Функция МодульОбъекта()
	
	Если МодульОбъекта = Неопределено Тогда
		
		Если Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект Тогда
			МодульОбъекта = МодульОбъектаИзКэша();
		КонецЕсли;
		
		Если МодульОбъекта = Неопределено Тогда
			МодульОбъекта = РеквизитФормыВЗначение("Объект");
			МодульОбъекта.ИнициализироватьПодключаемыеМодули(); // TODO Проверить что из этого используется на формах и перенести в ПараметрыКлиентСервер
			МодульОбъекта.ОбновитьКэшМодуляОбъекта();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат МодульОбъекта;
	
КонецФункции

&НаСервере
Функция МодульОбъектаИзКэша()
	
	Результат = Неопределено;
	
	Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда
		АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект;
 		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Структура = ПолучитьИзВременногоХранилища(АдресВХ);
			Если ТипЗнч(Структура) = Тип("Структура") и Структура.Свойство("ОбработкаОбъект") Тогда
			 	Результат = Структура.ОбработкаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульОбъектаКлиент()
	
	Если ТипЗнч(МодульОбъектаКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбъектаКлиент;
	КонецЕсли;
	
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		МодульОбъектаКлиент = ОсновнаяФорма.МодульОбъектаКлиент();
		Возврат МодульОбъектаКлиент;
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
	
	Если ЭтаФорма.ВладелецФормы = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ЭтаФорма.ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;
    	
КонецФункции

&НаКлиенте
Функция НоваяМетрика()
	
	Возврат ОсновнаяФорма().НоваяМетрика();
	
КонецФункции

// } СЛУЖЕБНЫЕ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
	Параметры.Свойство("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	
	Параметры.МодальностьЗапрещена = МодульОбъекта().МодальностьЗапрещена();
	ПутьКФормам = МодульОбъекта().Метаданные().ПолноеИмя() + ".Форма.";
	
	URL = Параметры.URL;
	Если URL = "" Тогда
		URL = Объект.ПараметрыКлиентСервер.URLАдреса.EDIБоевойСервер + "/1c/history.html";
		КодРаздела = "ПоказатьОбновления_НомерРелиза";
		Элементы.ФормаНеПоказыватьБольше.Заголовок = "Не показывать до следующего релиза";
	Иначе
		Заголовок = "Обучение";
	КонецЕсли;
	
	HTMLТекст = URL;
	
КонецПроцедуры

&НаКлиенте
Процедура НеПоказыватьБольше(Команда)
	
	НеПоказыватьБольшеСервер();
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	Оповестить("КонтурEDI_ЗакрытаФормаСправки");
	
	Если ОсновнаяФорма() <> Неопределено Тогда
		ОсновнаяФорма().ПриЗакрытииФормы(ЭтаФорма, "Сервис_НовоеВРелизеУправляемая");
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура НеПоказыватьБольшеСервер()

	МодульОбъекта().УстановитьКонстантуEDI("ПоказатьОбновления_НомерРелиза", Объект.ПараметрыКлиентСервер.НомерРелизаМодуля);

КонецПроцедуры // НеПоказыватьБольше()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОсновнаяФорма().ПриОткрытииФормы(ЭтаФорма, "Сервис_НовоеВРелизеУправляемая");
	
КонецПроцедуры
