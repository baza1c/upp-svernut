
&НаСервере
Перем МодульОбъекта;

&НаКлиенте
Перем МодульОбъектаКлиент;

// кэш
&НаСервере
Перем ТаблицаМаппинга,
	Значение,
	Сообщение;

// СЛУЖЕБНЫЕ {

// Инициализация модуля и его экспортных функций
&НаСервере
Функция МодульОбъекта()
	
	Если МодульОбъекта = Неопределено Тогда
		
		Если Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект Тогда
			МодульОбъекта = МодульОбъектаИзКэша();
		КонецЕсли;
		
		Если МодульОбъекта = Неопределено Тогда
			МодульОбъекта = РеквизитФормыВЗначение("Объект");
			МодульОбъекта.ИнициализироватьПодключаемыеМодули(); // TODO Проверить что из этого используется на формах и перенести в ПараметрыКлиентСервер
			МодульОбъекта.ОбновитьКэшМодуляОбъекта();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат МодульОбъекта;
	
КонецФункции

&НаСервере
Функция МодульОбъектаИзКэша()
	
	Результат = Неопределено;
	
	Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда
		АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект;
 		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Структура = ПолучитьИзВременногоХранилища(АдресВХ);
			Если ТипЗнч(Структура) = Тип("Структура") и Структура.Свойство("ОбработкаОбъект") Тогда
			 	Результат = Структура.ОбработкаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульОбъектаКлиент()
	
	Если ТипЗнч(МодульОбъектаКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбъектаКлиент;
	КонецЕсли;
	
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		МодульОбъектаКлиент = ОсновнаяФорма.МодульОбъектаКлиент();
		Возврат МодульОбъектаКлиент;
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
	
	Если ЭтаФорма.ВладелецФормы = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ЭтаФорма.ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;
    	
КонецФункции

&НаКлиенте
Функция НоваяМетрика()
	
	Возврат ОсновнаяФорма().НоваяМетрика();
	
КонецФункции

// } СЛУЖЕБНЫЕ

&НаСервере
Функция Значение()
	
	Если Значение = Неопределено
		И ЭтоАдресВременногоХранилища(ЗначениеАдресВоВХ) Тогда
		Значение = ПолучитьИзВременногоХранилища(ЗначениеАдресВоВХ);
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

&НаСервере
Функция ТаблицаМаппинга()
	
	Если ТаблицаМаппинга = Неопределено 
		И ЭтоАдресВременногоХранилища(ТаблицаМаппингаАдресВоВХ) Тогда
		ТаблицаМаппинга = ПолучитьИзВременногоХранилища(ТаблицаМаппингаАдресВоВХ);
	КонецЕсли;
	
	Возврат ТаблицаМаппинга;
	
КонецФункции

&НаСервере
Функция Сообщение()
	
	Если Сообщение = Неопределено
		И ЭтоАдресВременногоХранилища(СообщениеАдресВоВХ) Тогда
		Сообщение = ПолучитьИзВременногоХранилища(СообщениеАдресВоВХ);
	КонецЕсли;
	
	Возврат Сообщение;
	
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
	Параметры.Свойство("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	
	ЗаполнитьРеквизитыФормы();
	
	МодульОбъекта().ОбработкаСобытияПодключаемогоМодуля("ИзменитьНастройкиУниверсальнойФормыРедактирования"
														,
														, Новый Структура("Форма,ПараметрыФормы", 
																			ЭтаФорма, ПолучитьСтруктуруПарамеровФормы()));
															
															
	ОпределитьТипЗначения();
	
	Если НЕ ПустаяСтрока(ТипЗначения) Тогда
		ВывестиЗначениеНаФорму(Значение());
	КонецЕсли;
	
	ВыполнитьНастройкуФормы();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыФормы()

	ИмяСтруктуры				= Параметры.ИмяСтруктуры;
	ЗначениеАдресВоВХ			= Параметры.ЗначениеАдресВоВХ;		
	НастройкиФормы 				= Параметры.НастройкиФормы;		
	СообщениеАдресВоВХ 			= Параметры.СообщениеАдресВоВХ;		
	ТаблицаМаппингаАдресВоВХ 	= Параметры.ТаблицаМаппингаАдресВоВХ;		

КонецПроцедуры

&НаСервере
Процедура ВыполнитьНастройкуФормы()

	Если НЕ ТипЗнч(НастройкиФормы) = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если НастройкиФормы.Свойство("ИзменятьСоставСтрок") Тогда
		Элементы.ДеревоРеквизитов.ИзменятьСоставСтрок = Истина;//Параметры.НастройкиФормы.ИзменятьСоставСтрок;	
	КонецЕсли;
	
	Если НастройкиФормы.Свойство("НадписьИнформация") Тогда
		НадписьИнформация = НастройкиФормы.НадписьИнформация;	
	КонецЕсли;
	
	Если НастройкиФормы.Свойство("ТолькоПросмотр") Тогда
		ТолькоПросмотр = НастройкиФормы.ТолькоПросмотр;
	КонецЕсли;
	Элементы.ДеревоРеквизитов.ТолькоПросмотр = ТолькоПросмотр;
	Элементы.Сохранить.Видимость = НЕ ТолькоПросмотр;
	Заголовок = ?(ТолькоПросмотр, "Просмотр", "Редактирование");
	
	УстановитьДоступностьКолонок();
	УстановитьУсловноеОформление();
	ДобавитьУсловноеОформлениеСписковВыбора();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	Если ТипЗначения = "Структура" Тогда
		
		ИмяПоляОформления 	= "ДеревоРеквизитовЗначение";
		ИмяПоляОтбора 		= "ДеревоРеквизитов.ПолеНеДоступно";
		
		Элемент = УсловноеОформление.Элементы.Добавить();
		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ИмяПоляОформления);
		
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоляОтбора);
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Истина;
		
		Элемент.Оформление.УстановитьЗначениеПараметра("Доступность", Ложь);
		Элемент.Оформление.УстановитьЗначениеПараметра("ЦветФона", Новый Цвет(239, 239, 239));
		
	КонецЕсли;
	
	// Покажем ссылку на новое сложное значение
	МассивИменПолейОформления = Новый Массив;
	Если ТипЗначения = "Структура" Тогда
		МассивИменПолейОформления.Добавить("ДеревоРеквизитовЗначение");
	Иначе
		Для каждого Эл Из КолонкиИПоляСложногоТипа Цикл
			МассивИменПолейОформления.Добавить(Эл.Значение);
		КонецЦикла;
	КонецЕсли;
	
	Для каждого ИмяПоля Из МассивИменПолейОформления Цикл
	
		ИмяПоляОформления 	= "ДеревоРеквизитов" + ИмяПоля;
		ИмяПоляОтбора 		= "ДеревоРеквизитов." + ИмяПоля;
		
		Элемент = УсловноеОформление.Элементы.Добавить();
		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ИмяПоляОформления);
		
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоляОтбора);
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Содержит;
		ОтборЭлемента.ПравоеЗначение = "e1cib/tempstorage";
		
		Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
		Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", Новый Цвет(70, 130, 180));
		Элемент.Оформление.УстановитьЗначениеПараметра("Текст", "Подробнее");
		
	КонецЦикла;
	
КонецПроцедуры // УстановитьУсловноеОформление()

&НаСервере
Процедура ДобавитьУсловноеОформлениеСписковВыбора()
	
	Если ТипЗначения = "Структура" Тогда
		
		Для каждого Реквизит Из ДеревоРеквизитов.ПолучитьЭлементы() Цикл
			
			ИмяПоляОформления 	= "ДеревоРеквизитовЗначение";
			ИмяПоляОтбора 		= "ДеревоРеквизитов.Значение";
			
			Если ЗначениеЗаполнено(Реквизит.СписокВыбора) Тогда
				
				Для каждого Эл Из Реквизит.СписокВыбора Цикл
					
					Элемент = УсловноеОформление.Элементы.Добавить();
					ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
					ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ИмяПоляОформления);
					
					ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
					ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоляОтбора);
					ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
					ОтборЭлемента.ПравоеЗначение = Эл.Значение;
					
					Элемент.Оформление.УстановитьЗначениеПараметра("Текст", Эл.Представление);
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли ТипЗначения = "ТаблицаЗначений"
		ИЛИ ТипЗначения = "ДеревоЗначений" Тогда
		
		Для каждого Элемент Из Элементы.ДеревоРеквизитов.ПодчиненныеЭлементы Цикл
			
			Если НЕ Элемент.Вид = ВидПоляФормы.ПолеВвода Тогда
				Продолжить;
			КонецЕсли;
			
			ИмяПоляОформления 	= Элемент.Имя;
			ИмяПоляОтбора 		= Элемент.ПутьКДанным;
			
			Если ЗначениеЗаполнено(Элемент.СписокВыбора) Тогда
				
				Для каждого Эл Из Элемент.СписокВыбора Цикл
					
					Элемент = УсловноеОформление.Элементы.Добавить();
					ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
					ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ИмяПоляОформления);
					
					ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
					ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоляОтбора);
					ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
					ОтборЭлемента.ПравоеЗначение = Эл.Значение;
					
					Элемент.Оформление.УстановитьЗначениеПараметра("Текст", Эл.Представление);
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьКолонок()

	Если НастройкиФормы.Свойство("НедоступныеПоляИКолонки") 
		И ТипЗнч(НастройкиФормы.НедоступныеПоляИКолонки) = Тип("Массив") Тогда
		Для каждого ИмяКолонки Из НастройкиФормы.НедоступныеПоляИКолонки Цикл
			Колонка = Элементы.Найти("ДеревоРеквизитов" + ИмяКолонки);
			Если НЕ Колонка = Неопределено Тогда
				 Колонка.Видимость = Ложь;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // УстановитьДоступностьКолонок()

&НаСервере
Процедура ОпределитьТипЗначения()
	
	ТипЗначения = МодульОбъекта().ОпределитьИмяТипаЗначенияСложногоТипа(Значение()); 

КонецПроцедуры

&НаСервере
Функция ПолучитьСтруктуруПарамеровФормы()
	
	СтруктураПараметров = Новый Структура;
	
	СтруктураПараметров.Вставить("ИмяСтруктуры", 	ИмяСтруктуры);
	СтруктураПараметров.Вставить("Значение", 		Значение());
	СтруктураПараметров.Вставить("НастройкиФормы", 	НастройкиФормы);
	СтруктураПараметров.Вставить("Сообщение", 		Сообщение());
	СтруктураПараметров.Вставить("ТаблицаМаппинга", ТаблицаМаппинга());
	
	Возврат СтруктураПараметров;
	
КонецФункции

&НаСервере
Процедура ВывестиЗначениеНаФорму(Значение)
	
	Если ТипЗначения = "Структура" Тогда
		
		ПодготовитьФормуДляВыводаСтруктуры(Значение);
		
		ЕстьВложенные = ВывестиСтруктуруРекурсивно(Значение, ИмяСтруктуры);
		Элементы.ДеревоРеквизитов.Отображение = ОтображениеТаблицы.Дерево;
 		УстановитьДоступностьПолейСтруктуры();

	ИначеЕсли ТипЗначения = "ТаблицаЗначений" Тогда
		
		ПодготовитьФормуДляВыводаТаблицыИДереваЗначений(Значение);
		Элементы.ДеревоРеквизитов.Отображение = ОтображениеТаблицы.Список;
		СписокИменКолонокСложногоТипа = ПолучитьСписокКолонокСложногоТипаСтрокой(КолонкиИПоляСложногоТипа);
		
		Для каждого Строка Из Значение Цикл
			НоваяСтрока = ДеревоРеквизитов.ПолучитьЭлементы().Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка, , СписокИменКолонокСложногоТипа);	
			Для каждого Эл Из КолонкиИПоляСложногоТипа Цикл
				НоваяСтрока[Эл.Значение] = ПоместитьВоВременноеХранилище(Строка[Эл.Значение], ЭтаФорма.УникальныйИдентификатор);		
			КонецЦикла;
		КонецЦикла;
		
	ИначеЕсли ТипЗначения = "ДеревоЗначений" Тогда
		
		ПодготовитьФормуДляВыводаТаблицыИДереваЗначений(Значение);
		Элементы.ДеревоРеквизитов.Отображение = ОтображениеТаблицы.Дерево;
		СписокИменКолонокСложногоТипа = ПолучитьСписокКолонокСложногоТипаСтрокой(КолонкиИПоляСложногоТипа);
		ВывестиСтрокиДереваЗначенийРекурсивно(Значение.Строки, , СписокИменКолонокСложногоТипа);		
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСписокКолонокСложногоТипаСтрокой(КолонкиИПоляСложногоТипа)
	
	СписокИменКолонокСтрокой = "";
	
	МассивИменКолонок = КолонкиИПоляСложногоТипа.ВыгрузитьЗначения();
	Для каждого ИмяКолонки Из МассивИменКолонок Цикл
		СписокИменКолонокСтрокой = СписокИменКолонокСтрокой + ИмяКолонки + ","; 	
	КонецЦикла;
	
	Возврат СписокИменКолонокСтрокой;
	
КонецФункции

&НаСервере
Процедура ВывестиСтрокиДереваЗначенийРекурсивно(СтрокиИсточника, СтрокаПриемника = Неопределено, СписокИменКолонокСложногоТипа)

	Для каждого Строка Из СтрокиИсточника Цикл
		Если СтрокаПриемника = Неопределено Тогда
			НоваяСтрока = ДеревоРеквизитов.ПолучитьЭлементы().Добавить();
		Иначе
			НоваяСтрока = СтрокаПриемника.ПолучитьЭлементы().Добавить();
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка, , СписокИменКолонокСложногоТипа);		
		Для каждого Эл Из КолонкиИПоляСложногоТипа Цикл
			НоваяСтрока[Эл.Значение] = ПоместитьВоВременноеХранилище(Строка[Эл.Значение], ЭтаФорма.УникальныйИдентификатор);		
		КонецЦикла;
		Если НЕ Строка.Строки.Количество() = 0 Тогда
			ВывестиСтрокиДереваЗначенийРекурсивно(Строка.Строки, НоваяСтрока, СписокИменКолонокСложногоТипа);
		КонецЕсли;
	КонецЦикла;	

КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуДляВыводаТаблицыИДереваЗначений(Значение)
	
	ИспользоватьПорядокВыводаКолонок = Ложь;
	
	Если Параметры.НастройкиФормы.Свойство("ПорядокКолонок")
		И ТипЗнч(Параметры.НастройкиФормы.ПорядокКолонок) = Тип("Массив") Тогда
		ПорядокКолонок = Параметры.НастройкиФормы.ПорядокКолонок;
		ИспользоватьПорядокВыводаКолонок = Истина;
	КонецЕсли;
	
	Если ИспользоватьПорядокВыводаКолонок Тогда
		Для каждого ИмяКолонки Из ПорядокКолонок Цикл
			Колонка = Значение.Колонки.Найти(ИмяКолонки);
			Если НЕ Колонка = Неопределено Тогда
				ВывестиКолонкуТаблицыИДереваЗначений(Колонка);	
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Для каждого Колонка Из Значение.Колонки Цикл
		// выведем колонки, которых не было в порядке колонок, зато есть в значении
		ВывестиКолонкуТаблицыИДереваЗначений(Колонка);
	КонецЦикла;
	
	Если Параметры.НастройкиФормы.Свойство("КолонкиТолькоПросмотр")
		И ТипЗнч(Параметры.НастройкиФормы.КолонкиТолькоПросмотр) = Тип("Массив") Тогда
		КолонкиТолькоПросмотр = Параметры.НастройкиФормы.КолонкиТолькоПросмотр;
		Для каждого ИмяКолонки Из КолонкиТолькоПросмотр Цикл
			Колонка = Элементы.Найти("ДеревоРеквизитов" + ИмяКолонки);
			Если НЕ Колонка = Неопределено Тогда
				УстановитьРежимКолонкиТолькоПросмотр(Колонка);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ПолучитьМассивДопустимыхТиповКолонки()

	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("Строка"));
	МассивТипов.Добавить(Тип("Число"));
	МассивТипов.Добавить(Тип("Булево"));
	МассивТипов.Добавить(Тип("Дата"));
	
	Возврат МассивТипов;
	
КонецФункции // ПолучитьМассивДопустимыхТиповКолонки()

&НаСервере
Процедура ДобавитьНужныеСсылочныеТипы(ОписаниеТипов)

	ОписаниеТипов = Новый ОписаниеТипов(ОписаниеТипов, Справочники.ТипВсеСсылки().Типы());	
	ОписаниеТипов = Новый ОписаниеТипов(ОписаниеТипов, Документы.ТипВсеСсылки().Типы());	
	ОписаниеТипов = Новый ОписаниеТипов(ОписаниеТипов, Перечисления.ТипВсеСсылки().Типы());	
	ОписаниеТипов = Новый ОписаниеТипов(ОписаниеТипов, ПланыВидовХарактеристик.ТипВсеСсылки().Типы());	

КонецПроцедуры

&НаСервере
Процедура ВывестиКолонкуТаблицыИДереваЗначений(Колонка)

	Если Не ЗначениеЗаполнено(Колонка.ТипЗначения) Тогда 
		ДопустимыеТипы = Новый ОписаниеТипов(ПолучитьМассивДопустимыхТиповКолонки());
		ДобавитьНужныеСсылочныеТипы(ДопустимыеТипы);
	ИначеЕсли МодульОбъекта().ОписаниеТипаСодержитСложныйТип(Колонка.ТипЗначения) Тогда
		// здесь будут ссылки на ВХ
 		ДопустимыеТипы = Новый ОписаниеТипов("Строка");
		КолонкиИПоляСложногоТипа.Добавить(Колонка.Имя);
	Иначе
		ДопустимыеТипы = Колонка.ТипЗначения;
	КонецЕсли;
	
	НовыеРеквизитыФормы = Новый Массив;
	ДобавитьРеквизитФормы(НовыеРеквизитыФормы, Колонка.Имя, ДопустимыеТипы, Колонка.Заголовок);
	ИзменитьРеквизиты(НовыеРеквизитыФормы);
	
	НоваяКолонкаНаФорме = Элементы.Найти("ДеревоРеквизитов" + Колонка.Имя);
	
	Если НЕ НоваяКолонкаНаФорме = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	ЗаголовокКолонки = Неопределено;
	СтрокаМаппинга = НайтиСтрокуТаблицыМаппинга(Новый Структура("ИмяПоля,ИмяОбъекта", Колонка.Имя, ИмяСтруктуры));
	Если НЕ СтрокаМаппинга = Неопределено
		И ЗначениеЗаполнено(СтрокаМаппинга.Представление) Тогда
		ЗаголовокКолонки = СтрокаМаппинга.Представление;
	ИначеЕсли ЗначениеЗаполнено(Колонка.Заголовок) Тогда
		ЗаголовокКолонки = Колонка.Заголовок;
	Иначе
		ЗаголовокКолонки = Колонка.Имя;
	КонецЕсли;
	
	НоваяКолонкаНаФорме = Элементы.Добавить("ДеревоРеквизитов" + Колонка.Имя, Тип("ПолеФормы"), Элементы.ДеревоРеквизитов);
	
	Если Колонка.ТипЗначения = Новый ОписаниеТипов("Булево") Тогда
		НоваяКолонкаНаФорме.Вид = ВидПоляФормы.ПолеФлажка; 
		НоваяКолонкаНаФорме.РежимРедактирования = РежимРедактированияКолонки.Непосредственно;
	Иначе	
		НоваяКолонкаНаФорме.Вид = ВидПоляФормы.ПолеВвода; 
		НоваяКолонкаНаФорме.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Лево;
		
		Если ЗначениеЗаполнено(Колонка.ТипЗначения) Тогда
			НоваяКолонкаНаФорме.КнопкаВыбора = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	НоваяКолонкаНаФорме.Заголовок 		= ЗаголовокКолонки;
	НоваяКолонкаНаФорме.ПутьКДанным 	= "ДеревоРеквизитов." + Колонка.Имя;
	НоваяКолонкаНаФорме.Подсказка 		= ЗаголовокКолонки;
	//НоваяКолонкаНаФорме.УстановитьДействие("ПриИзменении", "");

	Если НЕ СтрокаМаппинга = Неопределено
		И ЗначениеЗаполнено(СтрокаМаппинга.ИмяСпискаВыбора) Тогда
		СписокВыбора = МодульОбъекта().ПолучитьСписокВыбораПоИмени_КонтурEDI(СтрокаМаппинга.ИмяСпискаВыбора, СтрокаМаппинга.ИмяПоля);
		ЗаполнитьСписокВыбораЭлемента(НоваяКолонкаНаФорме, СписокВыбора);
		НоваяКолонкаНаФорме.РежимВыбораИзСписка = Истина;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораЭлемента(Элемент, Список)
	
	Элемент.СписокВыбора.Очистить();
	
	Для каждого Эл Из Список Цикл
		Элемент.СписокВыбора.Добавить(Эл.Значение, Эл.Представление);
	КонецЦикла;	

КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьПолейСтруктуры()

	НедоступныеПоляИКолонки = Новый Массив;
	
	Если Параметры.НастройкиФормы.Свойство("НедоступныеПоляИКолонки")
		И ТипЗнч(Параметры.НастройкиФормы.НедоступныеПоляИКолонки) = Тип("Массив") Тогда
		НедоступныеПоляИКолонки = Параметры.НастройкиФормы.НедоступныеПоляИКолонки;
	КонецЕсли;
	
	УстановитьДоступностьПолейСтруктурыРекурсивно(НедоступныеПоляИКолонки, ДеревоРеквизитов.ПолучитьЭлементы());

КонецПроцедуры // УстановитьДоступностьПолейСтруктуры()

&НаСервере
Процедура УстановитьДоступностьПолейСтруктурыРекурсивно(НедоступныеПоля, Строки, ИмяРодителя = Неопределено)

	Для каждого Строка Из Строки Цикл
		Если НЕ ИмяРодителя = Неопределено Тогда
			ПолноеИмяПоля = ИмяРодителя + "." + Строка.Наименование;
		Иначе
			ПолноеИмяПоля = Строка.Наименование;
		КонецЕсли;
		Если НЕ НедоступныеПоля.Найти(ПолноеИмяПоля) = Неопределено Тогда 
			Строка.ПолеНеДоступно = Истина;
		КонецЕсли;
		Если НЕ Строка.ПолучитьЭлементы().Количество() = 0 Тогда
			Строка.ПолеНеДоступно = Истина;
			УстановитьДоступностьПолейСтруктурыРекурсивно(НедоступныеПоля, Строка.ПолучитьЭлементы(), ПолноеИмяПоля);	
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // УстановитьДоступностьПолейСтруктурыРекурсивно()

&НаСервере
Функция ВывестиСтруктуруРекурсивно(Структура, ИмяРодителя = Неопределено, СтрокаПриемника = Неопределено)
	
	ЕстьВложенные = Ложь;
	
	Для каждого Эл Из Структура Цикл
		
		Если СтрокаПриемника = Неопределено Тогда
			НоваяСтрока = ДеревоРеквизитов.ПолучитьЭлементы().Добавить();
		Иначе
			НоваяСтрока = СтрокаПриемника.ПолучитьЭлементы().Добавить();
		КонецЕсли;
		
		ПредставлениеПоля = Неопределено;
		СтрокаМаппинга = НайтиСтрокуТаблицыМаппинга(Новый Структура("ИмяПоля,ИмяОбъекта", Эл.Ключ, ИмяРодителя));
		Если НЕ СтрокаМаппинга = Неопределено Тогда
			Если ЗначениеЗаполнено(СтрокаМаппинга.Представление) Тогда
				ПредставлениеПоля = СтрокаМаппинга.Представление;
			КонецЕсли;
		КонецЕсли;
		Если ПредставлениеПоля = Неопределено Тогда	
			ПредставлениеПоля = Эл.Ключ;
		КонецЕсли;
		
		НоваяСтрока.Наименование 	= Эл.Ключ;
		НоваяСтрока.Представление   = ПредставлениеПоля;
		
		Если ТипЗнч(Эл.Значение) = Тип("Структура") Тогда
			
			ЕстьВложенные = Истина;
			Если НЕ СтрокаМаппинга = Неопределено Тогда
				ИмяВложеннойСтруктуры = СтрокаМаппинга.ИмяСтруктуры;
 			КонецЕсли;
			ВывестиСтруктуруРекурсивно(Эл.Значение, ИмяВложеннойСтруктуры, НоваяСтрока);
			
		ИначеЕсли МодульОбъекта().ЭтоЗначениеСложногоТипа_КонтурEDI(Эл.Значение) Тогда
			
			НоваяСтрока.Значение 	= ПоместитьВоВременноеХранилище(Эл.Значение, ЭтаФорма.УникальныйИдентификатор);
			КолонкиИПоляСложногоТипа.Добавить(Эл.Ключ);
			
		Иначе
			
			НоваяСтрока.Значение 	= Эл.Значение;
			
			Если НЕ СтрокаМаппинга = Неопределено Тогда
				НоваяСтрока.СписокВыбора = МодульОбъекта().ПолучитьСписокВыбораПоИмени_КонтурEDI(СтрокаМаппинга.ИмяСпискаВыбора, СтрокаМаппинга.ИмяПоля);
				
				Если НЕ ЗначениеЗаполнено(НоваяСтрока.Значение)
					И ЗначениеЗаполнено(СтрокаМаппинга.ТипПоля) Тогда
					ТипПоля = МодульОбъекта().ПолучитьТип_КонтурEDI(СтрокаМаппинга.ТипПоля);
					Если ЗначениеЗаполнено(ТипПоля) Тогда
						НоваяСтрока.Значение = ТипПоля.ПривестиЗначение();	
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;

	КонецЦикла;
	
	Возврат ЕстьВложенные;

КонецФункции

&НаСервере
Функция НайтиСтрокуТаблицыМаппинга(Отбор)
	
	СтрокаТаблицы = Неопределено;
	
	Если ТаблицаМаппинга() = Неопределено Тогда
		Возврат СтрокаТаблицы;
	КонецЕсли;
	
	НайденныеСтроки = ТаблицаМаппинга().НайтиСтроки(Отбор);
	
	Если НЕ НайденныеСтроки.Количество() = 0 Тогда
		Возврат НайденныеСтроки[0];
	КонецЕсли;
	
	Возврат СтрокаТаблицы;

КонецФункции // НайтиСтрокуТаблицыМаппинга()

&НаСервере
Процедура ДобавитьРеквизитФормы(МассивРеквизитов, ИмяРеквизита, ТипРеквизита, ЗаголовокРеквизита = "", СохраняемыеДанные = Ложь)
	
	Для каждого РеквизитФормы Из ЭтаФорма.ПолучитьРеквизиты("ДеревоРеквизитов") Цикл
		Если РеквизитФормы.Имя = ИмяРеквизита Тогда
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
    ПутьРеквизита = "ДеревоРеквизитов";
	МассивРеквизитов.Добавить(Новый РеквизитФормы(ИмяРеквизита, ТипРеквизита, ПутьРеквизита, ЗаголовокРеквизита, СохраняемыеДанные)); 	

КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуДляВыводаСтруктуры(Значение)
	
	Элементы.ДеревоРеквизитов.ИзменятьСоставСтрок = Ложь;
	Элементы.ДеревоРеквизитов.Шапка = Ложь;
	
	МассивТипов = ПолучитьМассивДопустимыхТиповКолонки();
	ДопустимыеТипы = Новый ОписаниеТипов(МассивТипов);
 	ДобавитьНужныеСсылочныеТипы(ДопустимыеТипы);
 
	НовыеРеквизитыФормы = Новый Массив;
	
	ДобавитьРеквизитФормы(НовыеРеквизитыФормы, "Наименование", 		Новый ОписаниеТипов("Строка"));
	ДобавитьРеквизитФормы(НовыеРеквизитыФормы, "Представление", 	Новый ОписаниеТипов("Строка"));
	ДобавитьРеквизитФормы(НовыеРеквизитыФормы, "Значение", 			ДопустимыеТипы);
	ДобавитьРеквизитФормы(НовыеРеквизитыФормы, "СписокВыбора", 		Новый ОписаниеТипов("СписокЗначений"));
	ДобавитьРеквизитФормы(НовыеРеквизитыФормы, "ПолеНеДоступно", 	Новый ОписаниеТипов("Булево"));
	
	ИзменитьРеквизиты(НовыеРеквизитыФормы);
	
	КолонкаНаименование 	= Элементы.Найти("ДеревоРеквизитовНаименование");
	КолонкаЗначение 		= Элементы.Найти("ДеревоРеквизитовЗначение");
	КолонкаПредставление 	= Элементы.Найти("ДеревоРеквизитовПредставление");
	КолонкаСписокВыбора 	= Элементы.Найти("ДеревоРеквизитовСписокВыбора");
	КолонкаПолеНеДоступно 	= Элементы.Найти("ДеревоРеквизитовПолеНеДоступно");
	
	Если КолонкаНаименование = Неопределено Тогда
		КолонкаНаименование = Элементы.Добавить("ДеревоРеквизитовНаименование", Тип("ПолеФормы"), Элементы.ДеревоРеквизитов);
		КолонкаНаименование.ПутьКДанным = "ДеревоРеквизитов.Наименование";
		КолонкаНаименование.Видимость = Ложь;
	КонецЕсли;
	Если КолонкаПредставление = Неопределено Тогда
		КолонкаПредставление = Элементы.Добавить("ДеревоРеквизитовПредставление", Тип("ПолеФормы"), Элементы.ДеревоРеквизитов);
		КолонкаПредставление.ПутьКДанным = "ДеревоРеквизитов.Представление";
		УстановитьРежимКолонкиТолькоПросмотр(КолонкаПредставление);
	КонецЕсли;
	Если КолонкаЗначение = Неопределено Тогда
		КолонкаЗначение = Элементы.Добавить("ДеревоРеквизитовЗначение", Тип("ПолеФормы"), Элементы.ДеревоРеквизитов);
		КолонкаЗначение.ПутьКДанным = "ДеревоРеквизитов.Значение";
		КолонкаЗначение.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Лево;
		КолонкаЗначение.Вид = ВидПоляФормы.ПолеВвода; 
		КолонкаЗначение.КнопкаВыбора = Истина;
		КолонкаЗначение.КнопкаОчистки = Истина;
	КонецЕсли;
	Если КолонкаСписокВыбора = Неопределено Тогда
		КолонкаСписокВыбора = Элементы.Добавить("ДеревоРеквизитовСписокВыбора", Тип("ПолеФормы"), Элементы.ДеревоРеквизитов);
		КолонкаСписокВыбора.ПутьКДанным = "ДеревоРеквизитов.СписокВыбора";
		КолонкаСписокВыбора.Видимость = Ложь;
	КонецЕсли;
	Если КолонкаПолеНеДоступно = Неопределено Тогда
		КолонкаПолеНеДоступно = Элементы.Добавить("ДеревоРеквизитовПолеНеДоступно", Тип("ПолеФормы"), Элементы.ДеревоРеквизитов);
		КолонкаПолеНеДоступно.ПутьКДанным = "ДеревоРеквизитов.ПолеНеДоступно";
		КолонкаПолеНеДоступно.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьРежимКолонкиТолькоПросмотр(Колонка)
	
	Колонка.ЦветФона = Новый Цвет(239, 239, 239);
	Колонка.ТолькоПросмотр = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	АдресДляВХ = ЭтаФорма.ВладелецФормы.УникальныйИдентификатор;
	ПеренестиДанныеФормыВЗначениеПоМаппингу(АдресДляВХ);
	Закрыть(ЗначениеАдресВоВХ);
	
КонецПроцедуры

&НаСервере
Процедура ПеренестиДанныеФормыВЗначениеПоМаппингу(АдресДляВХ)

	ЗначениеДеревоРеквизитов = РеквизитФормыВЗначение("ДеревоРеквизитов");
	
	Если ТипЗначения = "Структура" Тогда	
		ПеренестиДанныеФормыВСтруктуруРекурсивно(ЗначениеДеревоРеквизитов.Строки, Значение());
	ИначеЕсли ТипЗначения = "ТаблицаЗначений"
		ИЛИ ТипЗначения = "ДеревоЗначений" Тогда
		Если ТипЗначения = "ТаблицаЗначений" Тогда
			ЗначениеДеревоРеквизитовВТаблице = МодульОбъекта().ВыгрузитьДеревоЗначенийВТаблицуЗначений(ЗначениеДеревоРеквизитов);
			МодульОбъекта().ПерезаполнитьЗначениеСогласноЕгоТипу(Значение(), ЗначениеДеревоРеквизитовВТаблице);
			СтрокиЗначения = Значение();
		Иначе
			МодульОбъекта().ПерезаполнитьЗначениеСогласноЕгоТипу(Значение(), ЗначениеДеревоРеквизитов);
			СтрокиЗначения = Значение().Строки;
		КонецЕсли;
		ЗаполнитьЗначенияКолонокИзВХДереваЗначенийРекурсивно(СтрокиЗначения, ЗначениеДеревоРеквизитов.Строки);
	КонецЕсли;
	
	ЗначениеАдресВоВХ = ПоместитьВоВременноеХранилище(Значение(), АдресДляВХ);
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗначенияКолонокИзВХДереваЗначенийРекурсивно(СтрокиЗначения, СтрокиДерева)
	
	Для Сч = 0 По СтрокиДерева.Количество() - 1 Цикл
		СтрокаДерева 	= СтрокиДерева[Сч];
		СтрокаЗначения 	= СтрокиЗначения[Сч];
		Для каждого Эл Из КолонкиИПоляСложногоТипа Цикл
			Если ЭтоАдресВременногоХранилища(СтрокаДерева[Эл.Значение]) Тогда
				СтрокаЗначения[Эл.Значение] = ПолучитьИзВременногоХранилища(СтрокаДерева[Эл.Значение]);
			КонецЕсли;
		КонецЦикла;
		Если ТипЗначения = "ДеревоЗначений" Тогда
			ЗаполнитьЗначенияКолонокИзВХДереваЗначенийРекурсивно(СтрокаЗначения.Строки, СтрокаДерева.Строки);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоПолеСложногоТипа(ИмяПоля, СписокПолей)
	
	Возврат НЕ СписокПолей.НайтиПоЗначению(ИмяПоля) = Неопределено;	

КонецФункции // ЭтоПолеСложногоТипа()

&НаСервере
Процедура ПеренестиДанныеФормыВСтруктуруРекурсивно(Строки, ТекущаяСтруктура)

	Для каждого Строка Из Строки Цикл
		Если НЕ Строка.Строки.Количество() = 0 Тогда
			ПеренестиДанныеФормыВСтруктуруРекурсивно(Строка.Строки, ТекущаяСтруктура[Строка.Наименование]);	
		Иначе
			Если ЭтоПолеСложногоТипа(Строка.Наименование, КолонкиИПоляСложногоТипа) Тогда
				ПомещаемоеЗначение = ПолучитьИзВременногоХранилища(Строка.Значение);
			Иначе
				ПомещаемоеЗначение = Строка.Значение;
 			КонецЕсли;
			ТекущаяСтруктура.Вставить(Строка.Наименование, ПомещаемоеЗначение);		
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ДеревоРеквизитовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ВыбраннаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = Элемент.ТекущиеДанные;
	ИмяПоля = СтрЗаменить(Поле.Имя, "ДеревоРеквизитов", "");
	НужноОткрытьНовуюФорму = Ложь;
	НужноПоказатьСписокВыбора = Ложь;

	Если ТипЗначения = "Структура"
		И ИмяПоля = "Значение"
		И НЕ ТолькоПросмотр
		И ЗначениеЗаполнено(ТекущаяСтрока.СписокВыбора) Тогда
		
		СтандартнаяОбработка = Ложь;
		НужноПоказатьСписокВыбора = Истина;
		СписокВыбораЭлемента = ТекущаяСтрока.СписокВыбора;
		НачальноеЗначение = ТекущаяСтрока.Значение;
		
	ИначеЕсли (ТипЗначения = "ТаблицаЗначений"
		ИЛИ ТипЗначения = "ДеревоЗначений")
		И НЕ ТолькоПросмотр
		И НЕ ТипЗнч(ТекущаяСтрока[ИмяПоля]) = Тип("Булево")
		И ЗначениеЗаполнено(Элемент.ТекущийЭлемент.СписокВыбора) Тогда
		
		СтандартнаяОбработка = Ложь;
		НужноПоказатьСписокВыбора = Истина;
		СписокВыбораЭлемента = Элемент.ТекущийЭлемент.СписокВыбора;
		НачальноеЗначение = ТекущаяСтрока[ИмяПоля];
		
	ИначеЕсли ТипЗначения = "Структура"
		И ИмяПоля = "Значение" 
		И ЭтоПолеСложногоТипа(ТекущаяСтрока.Наименование, КолонкиИПоляСложногоТипа) Тогда
		
		СтандартнаяОбработка = Ложь;
		НужноОткрытьНовуюФорму = Истина;
		ИмяНовогоПоля = ТекущаяСтрока.Наименование;
		НовоеЗначениеАдресВоВХ = ТекущаяСтрока.Значение;
		
	ИначеЕсли (ТипЗначения = "ТаблицаЗначений"
		ИЛИ ТипЗначения = "ДеревоЗначений")
		И ЭтоПолеСложногоТипа(ИмяПоля, КолонкиИПоляСложногоТипа) Тогда
		
		СтандартнаяОбработка = Ложь;
		НужноОткрытьНовуюФорму = Истина;
		ИмяНовогоПоля = ИмяПоля;
		НовоеЗначениеАдресВоВХ = ТекущаяСтрока[ИмяНовогоПоля];
		
	ИначеЕсли НЕ ТипЗначения = "Структура"
		И НЕ ТолькоПросмотр
		И ТипЗнч(ТекущаяСтрока[ИмяПоля]) = Тип("Булево") Тогда
		
		СтандартнаяОбработка = Ложь;
		ТекущаяСтрока[ИмяПоля] = Не ТекущаяСтрока[ИмяПоля];
		
	КонецЕсли;
	
	Если НужноОткрытьНовуюФорму Тогда
		
		НовыеНастройкиФормы = Новый Структура;
		Для каждого Эл Из НастройкиФормы Цикл
			НовыеНастройкиФормы.Вставить(Эл.Ключ, Эл.Значение);
		КонецЦикла;
		НовыеНастройкиФормы.Вставить("НадписьИнформация", НастройкиФормы.НадписьИнформация + " -> " + ИмяНовогоПоля);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("НастройкиФормы",			НовыеНастройкиФормы);
		ПараметрыФормы.Вставить("ИмяСтруктуры",				ИмяСтруктуры);
		ПараметрыФормы.Вставить("ТаблицаМаппингаАдресВоВХ",	ТаблицаМаппингаАдресВоВХ);
		ПараметрыФормы.Вставить("СообщениеАдресВоВХ",		СообщениеАдресВоВХ);
		ПараметрыФормы.Вставить("ЗначениеАдресВоВХ",		НовоеЗначениеАдресВоВХ);
		
		ДопПараметрыОбработчика = Новый Структура;
		ДопПараметрыОбработчика.Вставить("ИмяПоля", 		ИмяПоля);
		ДопПараметрыОбработчика.Вставить("ИдСтроки", 		ВыбраннаяСтрока);
		
		МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("УниверсальнаяФормаРедактированияУправляемая2", 
														, 
														ПараметрыФормы, 
														ЭтаФорма, 
														"ОбработчикПослеЗакрытияУниверсальнойФормыРедактирования", 
														ЭтаФорма, 
														ДопПараметрыОбработчика,
														,
														Новый УникальныйИдентификатор);
		
														
	ИначеЕсли НужноПоказатьСписокВыбора Тогда
		
		ДопПараметрыОбработчика = Новый Структура;
		ДопПараметрыОбработчика.Вставить("ИмяПоля", 		ИмяПоля);
		ДопПараметрыОбработчика.Вставить("ИдСтроки", 		ВыбраннаяСтрока);
		
		МодульОбъектаКлиент().ВыбратьИзМеню_КонтурEDI("ОбработчикВыбораИзСпискаУниверсальнойФормыРедактирования", 
														ЭтаФорма, 
														ДопПараметрыОбработчика, 
														СписокВыбораЭлемента, 
														Элемент.ТекущийЭлемент);
														
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикВыбораИзСпискаУниверсальнойФормыРедактирования(РезультатВыбора = Неопределено, ДопПараметрыОбработчика = Неопределено) Экспорт

	Если РезультатВыбора = Неопределено
		ИЛИ ДопПараметрыОбработчика = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Состояние("Сохраняем значение");
	
	СтрокаВыбора = ДеревоРеквизитов.НайтиПоИдентификатору(ДопПараметрыОбработчика.ИдСтроки);
	Если НЕ СтрокаВыбора = Неопределено Тогда
		Если ТипЗначения = "Структура" Тогда
			СтрокаВыбора.Значение = РезультатВыбора.Значение;
		ИначеЕсли ТипЗначения = "ТаблицаЗначений"
			ИЛИ ТипЗначения = "ДеревоЗначений" Тогда
			СтрокаВыбора[ДопПараметрыОбработчика.ИмяПоля] = РезультатВыбора.Значение;
		КонецЕсли;
	КонецЕсли;	

КонецПроцедуры // ОбработчикВыбораИзСпискаУниверсальнойФормыРедактирования()
 
&НаКлиенте
Процедура ОбработчикПослеЗакрытияУниверсальнойФормыРедактирования(РезультатРедактирования = Неопределено, ДопПараметрыОбработчика = Неопределено) Экспорт
	
	Если РезультатРедактирования = Неопределено 
		ИЛИ ДопПараметрыОбработчика = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Состояние("Сохраняем значение");
	
	СтрокаРедактирования = ДеревоРеквизитов.НайтиПоИдентификатору(ДопПараметрыОбработчика.ИдСтроки);
	Если НЕ СтрокаРедактирования = Неопределено Тогда
		Если ТипЗначения = "Структура" Тогда
			СтрокаРедактирования.Значение = РезультатРедактирования;
		ИначеЕсли ТипЗначения = "ТаблицаЗначений"
			ИЛИ ТипЗначения = "ДеревоЗначений" Тогда
			СтрокаРедактирования[ДопПараметрыОбработчика.ИмяПоля] = РезультатРедактирования;
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры
