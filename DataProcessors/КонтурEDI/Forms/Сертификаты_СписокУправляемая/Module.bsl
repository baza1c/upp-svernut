&НаСервере
Перем МодульОбъекта;

&НаКлиенте
Перем МодульОбъектаКлиент;

&НаКлиенте
Перем МодульОбменКлиент;

// СЛУЖЕБНЫЕ {

// Инициализация модуля и его экспортных функций
&НаСервере
Функция МодульОбъекта()
	
	Если МодульОбъекта = Неопределено Тогда
		
		Если Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект Тогда
			МодульОбъекта = МодульОбъектаИзКэша();
		КонецЕсли;
		
		Если МодульОбъекта = Неопределено Тогда
			МодульОбъекта = РеквизитФормыВЗначение("Объект");
			МодульОбъекта.ИнициализироватьПодключаемыеМодули(); // TODO Проверить что из этого используется на формах и перенести в ПараметрыКлиентСервер
			МодульОбъекта.ОбновитьКэшМодуляОбъекта();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат МодульОбъекта;
	
КонецФункции

&НаСервере
Функция МодульОбъектаИзКэша()
	
	Результат = Неопределено;
	
	Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда
		АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект;
 		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Структура = ПолучитьИзВременногоХранилища(АдресВХ);
			Если ТипЗнч(Структура) = Тип("Структура") и Структура.Свойство("ОбработкаОбъект") Тогда
			 	Результат = Структура.ОбработкаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульОбъектаКлиент()
	
	Если ТипЗнч(МодульОбъектаКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбъектаКлиент;
	КонецЕсли;
	
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		МодульОбъектаКлиент = ОсновнаяФорма.МодульОбъектаКлиент();
		Возврат МодульОбъектаКлиент;
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
	
	Если ЭтаФорма.ВладелецФормы = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ЭтаФорма.ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;
    	
КонецФункции

&НаКлиенте
Функция НоваяМетрика()
	
	Возврат ОсновнаяФорма().НоваяМетрика();
	
КонецФункции

&НаКлиенте
Функция МодульОбменКлиент()
	
	Если ТипЗнч(МодульОбменКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбменКлиент;
	КонецЕсли;
	
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		МодульОбменКлиент = ОсновнаяФорма.МодульОбменКлиент();
		Возврат МодульОбменКлиент;
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

// } СЛУЖЕБНЫЕ

&НаСервере
Функция БазаДанныхФайловая()
	
	СтрокаСоединенияИБ = СтрокаСоединенияИнформационнойБазы();
	
	Возврат Найти( Врег(СтрокаСоединенияИБ),"FILE=" ) = 1;
		
КонецФункции

//обработчики -------------------------------------------------------------------------------------

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
	Параметры.Свойство("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	
	Если ЗначениеЗаполнено(Параметры.Организация) Тогда 
		ВыборДляОрганизации=Параметры.Организация;
		ЭтаФорма.Заголовок = "Выберите сертификат для организации """ + ВыборДляОрганизации+"""";
	КонецЕсли;
	
	Если Параметры.ПоказыватьФлагЗапомнитьВыбор = Истина Тогда
		Элементы.ЗапомнитьВыбор.Видимость = Истина;
	КонецЕсли;
	
	Если Не БазаДанныхФайловая() Тогда
		ЗаполнитьСписокСертификатовСервер();
	КонецЕсли;
	
КонецПроцедуры

//заполнение данных о серверных сертификатах ------------------------------------------------------

&НаСервере
Процедура ЗаполнитьСписокСертификатовСервер()
	
	//Заготовка на будущее Пока что только клиентские серты
	
	//МассивСертификатовСервер = МодульОбъекта().МассивСертификатов();
	//Для Каждого Элемент Из МассивСертификатовСервер Цикл
	//	НоваяСтрока = СписокСертификатов.Добавить();
	//	ЗаполнитьЗначенияСвойств(НоваяСтрока,Элемент);
	//	НоваяСтрока.РасположениеПользовательское = ?(Элемент.Расположение = "клиент", "локально", "на сервере");
	//КонецЦикла;
	
КонецПроцедуры

//-------------------------------------------------------------------------------------------------

//обработчики

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Состояние("Получение списка клиентских сертификатов...",50);	
	
	ЗаполнитьСписокСертификатовКлиент();
	
	Состояние("");
	
	ОсновнаяФорма().ПриОткрытииФормы(ЭтаФорма, "Сертификаты_СписокУправляемая");
		
КонецПроцедуры

&НаКлиенте
Процедура СписокСертификатовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	РезультатВыбора = Новый Структура;
	РеквизитыСертификата = Новый Структура;
	
	ТекущиеДанные = Элементы.СписокСертификатов.ТекущиеДанные;
	
	Для Каждого ПолеСписка Из Элементы.СписокСертификатов.ПодчиненныеЭлементы Цикл
		Идентификатор = ПолеСписка.Имя;
		Значение = ТекущиеДанные[Идентификатор];
		РеквизитыСертификата.Вставить(Идентификатор,Значение);	
	КонецЦикла;
	
	РезультатВыбора = Новый Структура;
	РезультатВыбора.Вставить("Действие",   				"ВыборСертификата");
	РезультатВыбора.Вставить("Организация",   			ВыборДляОрганизации);
	РезультатВыбора.Вставить("СертификатДействуетПо",   РеквизитыСертификата.ОкончаниеПериодаДействия);
	РезультатВыбора.Вставить("РеквизитыСертификата",	РеквизитыСертификата);
	РезультатВыбора.Вставить("Отпечаток",   			ТекущиеДанные.Отпечаток);
	РезультатВыбора.Вставить("ЗапомнитьВыбор",			ЗапомнитьВыбор);
	
	Этаформа.Закрыть(РезультатВыбора);

КонецПроцедуры

//заполнение данных о клиентских сертификатах ------------------------------------------------------

&НаКлиенте
Процедура ЗаполнитьСписокСертификатовКлиент()
			
	МассивСертификатовКлиент = МодульОбменКлиент().МассивСертификатов();
	Для Каждого Элемент Из МассивСертификатовКлиент Цикл
		НоваяСтрока = СписокСертификатов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Элемент);
		НоваяСтрока.РасположениеПользовательское = ?(Элемент.Расположение = "клиент", "локально", "на сервере");
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	СписокСертификатовВыбор("", "", "", "");

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	ОсновнаяФорма().ПриЗакрытииФормы(ЭтаФорма, "Сертификаты_СписокУправляемая");
	
КонецПроцедуры

