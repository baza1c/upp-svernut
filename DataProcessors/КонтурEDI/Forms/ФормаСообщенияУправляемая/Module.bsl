&НаСервере
Перем МодульОбъекта;

&НаСервере
Перем ОсобенностиСообщений;

&НаСервере
Перем ПараметрыАвтотестирования Экспорт;

////Перем Сообщение; // для создания формы  перенесено в реквизиты формы
//Перем НастройкаФормы;
Перем МетаданныеСообщения;
Перем ВнешнееХранилище;
Перем ОбязательныйКодТовараПокупателя;     
Перем СообщениеАлко;
Перем ПараметрыДействия;
Перем СтруктураВременногоХранилища;

&НаКлиенте
Перем ЗакрытиеРазрешено;

&НаКлиенте
Перем МодульОбъектаКлиент;

// СЛУЖЕБНЫЕ {

// Инициализация модуля и его экспортных функций
&НаСервере
Функция МодульОбъекта()
	
	Если МодульОбъекта = Неопределено Тогда
		
		Если Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект Тогда
			МодульОбъекта = МодульОбъектаИзКэша();
		КонецЕсли;
		
		Если МодульОбъекта = Неопределено Тогда
			МодульОбъекта = РеквизитФормыВЗначение("Объект");
			МодульОбъекта.ИнициализироватьПодключаемыеМодули(); // TODO Проверить что из этого используется на формах и перенести в ПараметрыКлиентСервер
			МодульОбъекта.ОбновитьКэшМодуляОбъекта();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат МодульОбъекта;
	
КонецФункции

&НаСервере
Функция МодульОбъектаИзКэша()
	
	Результат = Неопределено;
	
	Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда
		АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект;
 		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Структура = ПолучитьИзВременногоХранилища(АдресВХ);
			Если ТипЗнч(Структура) = Тип("Структура") и Структура.Свойство("ОбработкаОбъект") Тогда
			 	Результат = Структура.ОбработкаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульОбъектаКлиент()
	
	Если ТипЗнч(МодульОбъектаКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбъектаКлиент;
	КонецЕсли;
	
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		МодульОбъектаКлиент = ОсновнаяФорма.МодульОбъектаКлиент();
		Возврат МодульОбъектаКлиент;
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
	
	Если ЭтаФорма.ВладелецФормы = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ЭтаФорма.ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;
    	
КонецФункции

&НаКлиенте
Функция НоваяМетрика()
	
	Возврат ОсновнаяФорма().НоваяМетрика();
	
КонецФункции

// } СЛУЖЕБНЫЕ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
	Параметры.Свойство("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	
	// TODO устаревший параметр
	Параметры.МодальностьЗапрещена = Объект.ПараметрыКлиентСервер.МодальностьЗапрещена;
	ТолькоПросмотрСообщения = Параметры.ТолькоПросмотр;
	
	ТипСообщения = Параметры.ТипСообщения;   //нужны для условного оформления
	РежимРаботы = Параметры.РежимРаботы;     

	СформироватьСообщениеДляОткрытияКарточки(); //Здесь заполняем ключевой реквизит формы "Сообщение"
	
	//заполнение формы
	ПроизвестиПервоначальноеЗаполнениеПолейНаФорме();
	ПоместитьТаблицыЗначенийПроизвольныхРеквизитовВХранилище();	
	УничтожитьТаблицыЗначенийПроизвольныхРеквизитов();
	
	УстановитьЗаголовокФормы_КонтурEDI();
	
КонецПроцедуры

//Формирование сообщения которое будет показываться в карточке
&НаСервере
Процедура СформироватьСообщениеДляОткрытияКарточки()

	Если Параметры.ПроизвольныеПараметры = Неопределено Тогда
		Параметры.ПроизвольныеПараметры = Новый Структура;
	КонецЕсли;
	
	// В ТабСообщения направление может не соответствовать сообщению, которое формируем,
	// поэтому будем проверять направление по совокупности факторов: тип сообщения и режим работы
	НаправлениеСообщения = МодульОбъекта().ПолучитьНаправлениеСообщения(ТипСообщения, Параметры.РежимРаботы);
	
	Если ТолькоПросмотрСообщения
		ИЛИ НаправлениеСообщения = "Входящее" Тогда
		
		// открываем уже существующее сообщение по ссылке и это все только для просмотра
		Сообщение = МодульОбъекта().ПрочитатьСообщение(Параметры.СообщениеСсылка, , ТипСообщения, Параметры.СообщениеСсылка.Направление);
		Возврат;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.СообщениеСсылка) Тогда
		
		// похоже здесь так, потому что в основном списке задач может быть строка с другим типом сообщения 
		НайденноеСообщениеСсылка = МодульОбъекта().НайтиСообщениеДокумента(Параметры.Документ1С, ТипСообщения, "Исходящее");
		Параметры.ПроизвольныеПараметры.Вставить("СообщениеСсылка", НайденноеСообщениеСсылка);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ПереотправляемоеСообщениеСсылка) Тогда
		
		// сообщение существует, но пользователь хочет его переотправить
		Параметры.ПроизвольныеПараметры.Вставить("Действие", 						"Переотправка"); 		
		Параметры.ПроизвольныеПараметры.Вставить("ПереотправляемоеСообщениеСсылка", Параметры.ПереотправляемоеСообщениеСсылка);		
		
	КонецЕсли;
				
	Сообщение = МодульОбъекта().ПодготовитьИсходящееСообщение(ТипСообщения, Параметры.Документ1С, Параметры.ПроизвольныеПараметры);

КонецПроцедуры // СвормироватьСообщениеДляОткрытияКарточки()

&НаСервере
Процедура ДозаполнитьАртикулКодПолноеНаименованиеТЧТовары()
	
	//получим тек.колонку номенклатуры
	ТекПереченьНоменклатуры= Товары.Выгрузить(,"Номенклатура");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Данные.Номенклатура
	|ПОМЕСТИТЬ ИсходныеДанные
	|ИЗ
	|	&Данные КАК Данные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(спрНоменклатура.Код, """") КАК Код,
	|	ЕСТЬNULL(спрНоменклатура.Артикул, """") КАК Артикул,
	|	ЕСТЬNULL(спрНоменклатура.НаименованиеПолное, """") КАК НаименованиеПолное
	|ИЗ
	|	ИсходныеДанные КАК ИсходныеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
	|		ПО ИсходныеДанные.Номенклатура = спрНоменклатура.Ссылка";

	ТипНоменклатура = МодульОбъекта().ПолучитьТипЗначенияОбъекта("Номенклатура", , Истина);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "Справочник.Номенклатура", "Справочник." + ТипНоменклатура);
	
	Если НЕ МодульОбъекта().ЕстьНеобходимыеМетаданные("Справочники." + ТипНоменклатура + ".Реквизиты.Артикул") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "спрНоменклатура.Артикул", """""");
	КонецЕсли;
	Если НЕ МодульОбъекта().ЕстьНеобходимыеМетаданные("Справочники." + ТипНоменклатура + ".Реквизиты.НаименованиеПолное") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "спрНоменклатура.НаименованиеПолное", "спрНоменклатура.Наименование");
	КонецЕсли;

	Запрос.УстановитьПараметр("Данные",ТекПереченьНоменклатуры);
	ТЗРеквизитов=Запрос.Выполнить().Выгрузить();
	КолонкаКод=ТЗРеквизитов.ВыгрузитьКолонку("Код");
	КолонкаАртикул=ТЗРеквизитов.ВыгрузитьКолонку("Артикул");
	КолонкаНаименованиеПолное=ТЗРеквизитов.ВыгрузитьКолонку("НаименованиеПолное");
	
	ТЗТовары=РеквизитФормыВЗначение("Товары");
	ТЗТовары.ЗагрузитьКолонку(КолонкаКод,"НоменклатураКод");
	ТЗТовары.ЗагрузитьКолонку(КолонкаАртикул,"Артикул");
	ТЗТовары.ЗагрузитьКолонку(КолонкаНаименованиеПолное,"НаименованиеНоменклатурыПолное");
	
	ЗначениеВРеквизитФормы(ТЗТовары,"Товары");
	
КонецПроцедуры // ДозаполнитьАртикулКодПолноеНаименованиеТЧ()

&НаКлиенте
Процедура ПроанализироватьСостояниеЮЗсчф()
	
	Если (Сообщение.ТипСообщения = "INVOIC" ИЛИ Сообщение.ТипСообщения = "COINVOIC")
		И ЗначениеЗаполнено(Сообщение.СообщениеСсылка) 
		И Сообщение.Направление = "Исходящее"
		И ЗначениеЗаполнено(messageId)
		И ЗначениеЗаполнено(boxId) Тогда 
		
		Состояние("Получение статусов из Диадока...");
		
		СтруктураИдентификаторов = Новый Структура;
		СтруктураИдентификаторов.Вставить("boxId",boxId); //идентификаторы определены при создании на сервере в ПроверитьЗаполнениеПолей - по сути это проверка
		СтруктураИдентификаторов.Вставить("messageId",messageId);
		СтруктураИдентификаторов.Вставить("invoiceId",invoiceId);
		СтруктураИдентификаторов.Вставить("torg12Id",torg12Id);
		СтруктураИдентификаторов.Вставить("UniversalTransferDocumentId",UniversalTransferDocumentId);
		СтруктураИдентификаторов.Вставить("Документ",Документ1С);
		
		ОсновнаяФорма().МодульОбменКлиент().СброситьПроверкиДанныхАвторизации();
		СтатусыЮЗДО = ОсновнаяФорма().МодульОбменКлиент().УзнатьСостояниепоСФ(Отправитель1С,СтруктураИдентификаторов.boxId,СтруктураИдентификаторов.messageId);
		ОсновнаяФорма().МодульОбменКлиент().СброситьПарольКонтейнераДанныхАвторизации();
		
		Если СтатусыЮЗДО.Успешно=Ложь Тогда 
			НоваяСтрока = ТаблицаОшибок.Добавить();
			НоваяСтрока.ТекстОшибки = "Не удалось получить статус из Диадок.";
			НоваяСтрока.Действие 	= "";
			НоваяСтрока.ИмяПоля		= "СтатусДиадок";
			
			НоваяСтрока = ТаблицаОшибок.Добавить();
			НоваяСтрока.ТекстОшибки = СтатусыЮЗДО.ОписаниеОшибки;
			НоваяСтрока.Действие 	= "";
			НоваяСтрока.ИмяПоля		= "СтатусДиадок";

		Иначе
			ВывестиВОшибкиСостояниеЮЗДО(СтатусыЮЗДО);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПроанализироватьСостояниеЮЗсчф()

&НаКлиенте
Процедура ВывестиВОшибкиСостояниеЮЗДО(СтатусыЮЗДО) //они не являются ошибками, это уведомления
	
	КлассификаторСтатусовДД=ОсновнаяФорма().ПолучитьКлассификаторСтатусовДД();
	
	МассивСвойств = Новый СписокЗначений;
	МассивСвойств.Добавить("СтатусСФ","Статус СФ");
	МассивСвойств.Добавить("СтатусТорг12","Статус Торг12");
	МассивСвойств.Добавить("СтатусУПД","Статус");
	МассивСвойств.Добавить("КомментарийКЗапросуУточнения","Комментарий к запросу уточнения");
	МассивСвойств.Добавить("КомментарийКОтказуВПодписи","Комментарий к отказу");
	МассивСвойств.Добавить("КомментарийКЗапросуАннулированияСФ","Запрос аннулирования СФ");
	МассивСвойств.Добавить("КомментарийКЗапросуАннулированияТОРГ12","Запрос аннулирования Торг12");
	МассивСвойств.Добавить("КомментарийКЗапросуАннулированияУПД","Запрос аннулирования УПД");
    МассивСвойств.Добавить("СтатусАннулированияСФ","Статус аннулирования СФ");   
	МассивСвойств.Добавить("СтатусАннулированияТорг12","Статус аннулирования Торг12");
	МассивСвойств.Добавить("СтатусАннулированияУПД","Статус аннулирования УПД");

	Для Каждого СвойствоДокумента Из МассивСвойств Цикл
		ЗначениеСвойства="";
		СтатусыЮЗДО.Свойство(СвойствоДокумента.Значение,ЗначениеСвойства);
		Если ЗначениеСвойства<>"" и ЗначениеСвойства<>Неопределено Тогда
			Если Лев(СвойствоДокумента.Значение,11)= "Комментарий" Тогда
				НоваяСтрока = ТаблицаОшибок.Добавить();
				НоваяСтрока.ТекстОшибки = СвойствоДокумента.Представление+": """+ЗначениеСвойства+"""";
				НоваяСтрока.ИмяПоля		= "СтатусДиадок";
			Иначе
				ПсевдонимСтатуса=КлассификаторСтатусовДД.НайтиПоЗначению(ЗначениеСвойства);
				Если ПсевдонимСтатуса<>Неопределено Тогда 
					НоваяСтрока = ТаблицаОшибок.Добавить();
					НоваяСтрока.ТекстОшибки = СвойствоДокумента.Представление+": "+ПсевдонимСтатуса.Представление;
					//НоваяСтрока.Действие 	= "Открыть счет-фактуру в Диадоке";
					НоваяСтрока.ИмяПоля		= "СтатусДиадок";
				КонецЕсли;
				
			КонецЕсли;
			
		Конецесли;
	КонецЦикла;
	
КонецПроцедуры // ВывестиСостояниеЮЗДО()

&НаСервере   
Процедура УстановитьЗаголовкиРеквизитов()

	Если  МодульОбъекта().ЭтоОбратнаяОтгрузка(Сообщение.ТипСообщения) Тогда
		Элементы.НадписьЮрФизЛицоСвое.Заголовок			= "Контрагент";
		Элементы.НадписьЮрФизЛицоСтороннее.Заголовок	= "Организация";
	Конецесли;
	
КонецПроцедуры  

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	//доступность реквизитов и значения типов установлены при создании на сервере
	//Здесь только мелкие штрихи
	
	ПроанализироватьСостояниеЮЗсчф();//если это INVOIC	
	
	Если СообщениеСодержитДанныеОбУпаковках Тогда
		ОбновитьДанныеРазмещения();
		//Элементы.ТранспортныеУпаковки.Развернуть(ТранспортныеУпаковки.ПолучитьЭлементы()[0].ПолучитьИдентификатор());
	КонецЕсли;
	
	Если Элементы.ГруппаТранспортировка.Видимость = Истина Тогда 
		Сообщить("В заказе указаны врата времени РЦ");
		УстановитьВысотуСпискаВрат();
	КонецЕсли;   	
	
	Если ТаблицаОшибок.Количество() = 0 Тогда
		ЗакрытьПанельОшибокКлиент();
	Иначе
		ОткрытьПанельОшибокКлиент();
	КонецЕсли;
		
	Если ЗначениеЗаполнено(Параметры.ПараметрыАвтотестирования) Тогда
		ПодключитьОбработчикОжидания("ЗапуститьАвтотесты",0.1,Истина);
	КонецЕсли;
	
	ОсновнаяФорма().ПриОткрытииФормы(ЭтаФорма, "ФормаСообщенияУправляемая");
	
КонецПроцедуры

&НаКлиенте
Функция Автотесты_ПолучитьКодСледующегоШага()
	
	Код = Неопределено;
	
	СтруктураПараметров = ПолучитьИзВременногоХранилища(Параметры.ПараметрыАвтотестирования);

	Для Каждого Стр Из СтруктураПараметров.ВыполняемыйКод Цикл
		
		Если Стр.Выполнено Тогда
			Продолжить;
		КонецЕсли;
		Если Стр.ФормаОбработки = "ФормаСообщения" Тогда
			Код = Стр.ВыполняемыйКод;
			Прервать;
		Иначе
			Прервать;//есть невыполненный код в другой форме
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Код;
	
КонецФункции

&НаКлиенте
Процедура Автотесты_ПометитьПоследнийШагКакВыполненный()
	
	Код = Неопределено;
	
	СтруктураПараметров = ПолучитьИзВременногоХранилища(Параметры.ПараметрыАвтотестирования);
	Для Каждого Стр Из СтруктураПараметров.ВыполняемыйКод Цикл
		
		Если Стр.Выполнено Тогда
			Продолжить;
		КонецЕсли;
		Если Стр.ФормаОбработки = "ФормаСообщения" Тогда
			Стр.Выполнено = Истина;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(СтруктураПараметров,Параметры.ПараметрыАвтотестирования);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьАвтотесты()
	
	Пока Истина Цикл
		
		Код = Автотесты_ПолучитьКодСледующегоШага();
		
		Если Код = Неопределено Тогда
			
			Прервать;
			
		Иначе
			
			Выполнить(Код);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПроизвестиПервоначальноеЗаполнениеПолейНаФорме() Экспорт
	
	ОбязательныйКодТовараПокупателя = Ложь;
	Элементы.СтраницаАлкоголь.Видимость = Ложь;
	
	КтоМы = МодульОбъекта().ОпределитьКемМыЯвляемся(Сообщение);
	
	СтатусДляФормированияДействий = ОпределитьСтатусДляФормированияДействий(Сообщение);
	
	НастройкаФормы = МодульОбъекта().ПолучитьНастройкиФормыСообщения(Сообщение.ТипСообщения,Сообщение.Направление,СтатусДляФормированияДействий);
	
	НастройкаСсылокИсправленияОшибок = МодульОбъекта().ПолучитьНастройкуВыводаСсылокНаОбъектыСообщения();
	//ЗначениеВРеквизитФормы(НастройкаСсылокИсправленияОшибокТаблицаЗначений,"НастройкаСсылокИсправленияОшибок");

	ПоместитьТаблицыЗначенийПроизвольныхРеквизитовВХранилище();
	
	МетаданныеСообщения = МодульОбъекта().ПолучитьМетаданныеСообщения(Сообщение.ТипСообщения,Сообщение.Направление);
	
	Если ТолькоПросмотрСообщения Тогда
		НастройкаФормы.ТолькоПросмотрПолей1С = Истина;
		НастройкаФормы.ТолькоПросмотрПолейEDI = Истина;
		ТолькоПросмотр = Истина;
	КонецЕсли;
		
	Если Сообщение.Направление = "Входящее" Тогда
		МодульОбъекта().КонвертироватьСообщениеEDIв1С(Сообщение); //сообщене следует переконвертировать т.к. с момента его загрузки могли измениться настройки соответствий и проч.
	КонецЕсли;
	
	ИспользоватьДопПараметрыУпаковки = (МодульОбъекта().ПолучитьЗначениеСвойстваОбъектаEDI(Сообщение.Партнер, "ИспользоватьДопПараметрыУпаковки") = Истина);

	УстановитьЗаголовкиРеквизитов();
	
	СоздатьКнопки(НастройкаФормы.КнопкиКоманднойПанели);
	СоздатьКнопкиДопРеквизитов(НастройкаФормы);
	
	//Заполним Особенности
	ЗаполнитьОсобенностиСообщенияНаФорме();
	ДобавитьУсловноеОформлениеОсобенностейСообщения();
	НужноКонвертироватьОсобенностиВСообщении = МодульОбъекта().НужноКонвертироватьОсобенностиВСообщении(Сообщение); 
	
	ЗаполнитьСпискиВыбораКолонок();
	ЗаполнитьПоляНаФорме();
	ЗаполнитьОсобенностиСтруктурТоваровСообщенияНаФорме();
	УстановитьТипыПолей1С();
	УстановитьВидимость();
	УстановитьДоступностьПолей();

	МодульОбъекта().ПриОткрытииФормыСообщения(Сообщение,ЭтаФорма);//только вызов ПМ
	
	НетОшибок = Истина;
	
	Если НастройкаФормы.ПроверятьПоляПриОткрытииФормы Тогда
		
		НетОшибок = ПроверитьЗаполнениеПолей();
		
	КонецЕсли;
	
	ЗаполнитьСписокВерсийORDERS();
	
	Если Сообщение.Направление = "Входящее" Тогда
		Элементы.ТоварыНоменклатура.АвтоОтметкаНезаполненного = Истина;
	КонецЕсли;
	
	ДозаполнитьАртикулКодПолноеНаименованиеТЧТовары();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовокФормы_КонтурEDI()
	
	ПредставлениеТипаСообщения = НастройкаФормы.Заголовок;
	ПредставлениеДатыСообщения = МодульОбъекта().ПолучитьПредставлениеДатыСообщения(Сообщение);
	
	ЭтаФорма.Заголовок = ПредставлениеТипаСообщения + " (" + Сообщение.ТипСообщения + ") " + ПредставлениеДатыСообщения;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСпискиВыбораКолонок()
	
	СпискиВыбораКолонок = НастройкаФормы.СпискиВыбораКолонок;
	
	Для каждого ТекСписок Из СпискиВыбораКолонок Цикл
	    ЗаполнитьСписокВыбораЭлемента(Элементы["Товары" + ТекСписок.Ключ], ТекСписок.Значение);
	КонецЦикла;
	
	// Для транспортных упаковок
	СписокЕдиницИзмеренияВеса = Новый СписокЗначений;
	СписокЕдиницИзмеренияВеса.Добавить("KGM", "KGM (кг)");
	СписокЕдиницИзмеренияВеса.Добавить("GRM", "GRM (г)");
	СписокЕдиницИзмеренияВеса.Добавить("MGM", "MGM (мг)");
	
	СписокЕдиницИзмеренияГабаритов = Новый СписокЗначений;
	СписокЕдиницИзмеренияГабаритов.Добавить("MTR", "MTR (м)");
	СписокЕдиницИзмеренияГабаритов.Добавить("DMT", "DMT (дм)");
	СписокЕдиницИзмеренияГабаритов.Добавить("CMT", "CMT (см)");
	СписокЕдиницИзмеренияГабаритов.Добавить("MMT", "MMT (мм)");
	
	СпискиВыбораТранспортныхУпаковок = Новый Структура;
	СпискиВыбораТранспортныхУпаковок.Вставить("КодЕдиницыИзмеренияВесаНетто", 	СписокЕдиницИзмеренияВеса);
	СпискиВыбораТранспортныхУпаковок.Вставить("КодЕдиницыИзмеренияВесаБрутто", 	СписокЕдиницИзмеренияВеса);
	СпискиВыбораТранспортныхУпаковок.Вставить("КодЕдиницыИзмеренияДлины", 		СписокЕдиницИзмеренияГабаритов);
	СпискиВыбораТранспортныхУпаковок.Вставить("КодЕдиницыИзмеренияШирины", 		СписокЕдиницИзмеренияГабаритов);
	СпискиВыбораТранспортныхУпаковок.Вставить("КодЕдиницыИзмеренияВысоты", 		СписокЕдиницИзмеренияГабаритов);
	
	Для каждого ТекСписок Из СпискиВыбораТранспортныхУпаковок Цикл
	    ЗаполнитьСписокВыбораЭлемента(Элементы["ТранспортныеУпаковки" + ТекСписок.Ключ], ТекСписок.Значение);
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораЭлемента(Элемент, Список)
	
	Элемент.СписокВыбора.Очистить();
	
	Для каждого Эл Из Список Цикл
		Элемент.СписокВыбора.Добавить(Эл.Значение, Эл.Представление);
	КонецЦикла;	

КонецПроцедуры

//Заполним список версий, если он есть
	//1. Получить все похожие сообщения - ИЗ МОДУЛЯ ИНТЕГРАЦИИ. 
	//По партнеру, номеру заказа, дате (+- месяц, например)
	//формат: таблица - ссылка, дата, номер (на всякий случай)
	//последнее сообщение должно быть актуальным
	//и пронумеровать их
	//2. Создать кнопки версий
	//каждая кнопка должна открывать нужное сообщение
	//причем остальные кнопки должны быть доступны только в актуальном сообщении! Т.е. создавать документы можно только из актуального
	//и если документы уже были созданы в каком-то старом сообщении, то их надо переподцепить к новому
	//тоже через МОДУЛЬ ИНТЕГРАЦИИ            
&НаСервере
Процедура ЗаполнитьСписокВерсийORDERS()
	
	Если Сообщение.Направление = "Входящее"
		И Сообщение.ТипСообщения = "ORDERS"
		Тогда
		//условие на тип сообщения - ?
		Дубли=МодульОбъекта().ПолучитьДублирующиеСообщения(Сообщение.СообщениеСсылка);
		Если Дубли<>Неопределено 
			И Дубли.Количество()>1 
		Тогда
		
			ЕстьДокумент1С = Ложь;
			Для Каждого Стр Из Дубли Цикл
				Если ЗначениеЗаполнено(Стр.Документ1С) Тогда
					ЕстьДокумент1С = Истина;
				КонецЕсли;	
			КонецЦикла;
			
			Элементы.СписокВерсий.Видимость=Истина;
				
			ГлавнаяКнопка = Неопределено;
			Если Элементы.ОсновноеДействиеФормы1.ИмяКоманды<>"NOP" Тогда 
				ГлавнаяКнопка = Элементы.ОсновноеДействиеФормы1;
			ИначеЕсли Элементы.ОсновноеДействиеФормы2.ИмяКоманды<>"NOP" Тогда
				ГлавнаяКнопка = Элементы.ОсновноеДействиеФормы2;
			ИначеЕсли Элементы.ОсновноеДействиеФормы3.ИмяКоманды<>"NOP" Тогда
				ГлавнаяКнопка = Элементы.ОсновноеДействиеФормы3;
			ИначеЕсли Элементы.ОсновноеДействиеФормы4.ИмяКоманды<>"NOP" Тогда
				ГлавнаяКнопка = Элементы.ОсновноеДействиеФормы4;
			КонецЕсли;
			
			Если ГлавнаяКнопка <> Неопределено Тогда
				ГлавнаяКнопка.Заголовок = "Выбрать текущую версию";
				ГлавнаяКнопка.ИмяКоманды = "КнопкаДействияФормыВыборВерсииНажатие";//подменим на клиентскую команду, которая будет задавать вопрос о дальнейшей судьбе
			КонецЕсли;
			 			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьСписокВерсийORDERS()

&НаСервере
Функция ПроверитьЗаполнениеПолей() Экспорт
	
	ВосстановитьТаблицыЗначенийПроизвольныхРеквизитовИзХранилища();
	
	ОчиститьПометкиПолей();
	
	ТаблицаОшибок.Очистить();
	
	ЕстьОшибкиВПрикрепленных = Ложь;
	
	// смотрим на статусное сообщение
	Если Сообщение.Направление = "Исходящее" Тогда
		
		СтатусноеСообщениеОбОшибке = МодульОбъекта().ПрочитатьСообщение(,Сообщение.ДокументСсылка,Сообщение.ТипСообщения);
		
		Если НЕ СтатусноеСообщениеОбОшибке = Неопределено Тогда
			
			СтатусноеСообщениеОбъект = МодульОбъекта().ПолучитьОбъектСообщения(СтатусноеСообщениеОбОшибке.СообщениеСсылка);
			ТекстОшибки = СтатусноеСообщениеОбъект.ОписаниеОшибки;
			
			ОбработатьТекстОшибки(ТекстОшибки);
			
			РеквизитОписаниеОшибкиALCRPT = СтатусноеСообщениеОбъект.ДополнительныеРеквизиты.Найти("ОшибкаALCRPT","ИмяРеквизита");
			
			Если РеквизитОписаниеОшибкиALCRPT <> Неопределено Тогда
				ТекстОшибки = РеквизитОписаниеОшибкиALCRPT.ЗначениеСтрока;				
				ОбработатьТекстОшибки(ТекстОшибки);       				
			КонецЕсли; 			
			
		КонецЕсли;
		
		//УДАЛИТЬ РАНЕЕ В КАРТОЧКЕ COINVOIC ВЫВОДИЛИ статусы исходной СЧФ теперь самого коинвойка
		//Если Сообщение.ТипСообщения = "COINVOIC" Тогда
		//	boxId = СокрЛП(МодульОбъекта().ПолучитьЗначениеСвойстваОбъектаEDI(Сообщение.СчетФактура1С, "boxId"));
		//	messageId = СокрЛП(МодульОбъекта().ПолучитьЗначениеСвойстваОбъектаEDI(Сообщение.СчетФактура1С, "messageId"));
		//	Если Не ЗначениеЗаполнено(boxId) Или Не ЗначениеЗаполнено(messageId) Тогда
		//		ТекстОшибки = "Не найдены идентификаторы ДД исходного счета-фактуры.";
		//		НоваяСтрока = ТаблицаОшибок.Добавить();
		//		НоваяСтрока.ТекстОшибки = ТекстОшибки;
		//	КонецЕсли;
		//	
		//КонецЕсли;
			
		// попробуем прочитать статус уточнения из Диадока
		Если Сообщение.ТипСообщения = "INVOIC" или Сообщение.ТипСообщения = "COINVOIC" Тогда
			#Если Клиент Тогда
			Состояние("Проверка статусов документа в Диадоке...");
				
			#КонецЕсли
			
		СтруктураИдентификаторовДД = МодульОбъекта().ПолучитьСписокСвойствEDI("boxId,messageId,invoiceId,torg12Id,UniversalTransferDocumentId",Сообщение.ДокументСсылка);
			
			boxId 				= СтруктураИдентификаторовДД.boxId;
			messageId 			= СтруктураИдентификаторовДД.messageId;
			invoiceId 			= СтруктураИдентификаторовДД.invoiceId;
			torg12Id 			= СтруктураИдентификаторовДД.torg12Id;
	UniversalTransferDocumentId = СтруктураИдентификаторовДД.UniversalTransferDocumentId;
	
			// В Диадок по этим идентификаторам пойдем на клиенте
			// При открытии  в процедуре ПроанализироватьСостояниеЮЗсчф используем вызов экспортного клиентского метода ОсновнойУправляемойФормы "ЗаполнитьИдентификаторыЧерезКомпонентуДД"
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СообщениеАлко) Тогда
			
			СтатусноеСообщениеОбОшибке = МодульОбъекта().ПрочитатьСообщение(,СообщениеАлко.ДокументСсылка,СообщениеАлко.ТипСообщения,,Истина);
			
			Если НЕ СтатусноеСообщениеОбОшибке = Неопределено Тогда
				
				ТекстОшибки = СтатусноеСообщениеОбОшибке.СообщениеСсылка.ОписаниеОшибки;
				
				НоваяСтрока = ТаблицаОшибок.Добавить();
				НоваяСтрока.ТекстОшибки = ТекстОшибки;
				
			КонецЕсли;
		КонецЕсли;
		
		Для Каждого Стр ИЗ ТаблицаПрикрепленныхСообщений Цикл
			
			Если ЗначениеЗаполнено(Стр.Примечание) Тогда
				
				НоваяСтрока = ТаблицаОшибок.Добавить();
				НоваяСтрока.ТекстОшибки = "В прикрепленных сообщениях были найдены ошибки: "+СокрЛП(Стр.Примечание);
				ЕстьОшибкиВПрикрепленных = Истина;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;	
	//
	
	// проверка алко полей
	ЕстьАлкоОшибки = Ложь;
	Если ЗначениеЗаполнено(СообщениеАлко) Тогда
		Если СообщениеАлко.СодержитОшибки = Истина Тогда
			
			ЕстьАлкоОшибки=Истина;	
			РезультатПроверки = МодульОбъекта().ПроверитьПоляСообщения(СообщениеАлко,Неопределено);
			Для Каждого Стр Из РезультатПроверки.СписокОшибок Цикл
				НоваяСтрока = ТаблицаОшибок.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока,Стр);
			КонецЦикла;
			
		КонецЕсли;
		//проверки перенесены в МО.ПроверитьПоляСообщения
	КонецЕсли;
	
	ПроверитьЗаполнениеОбязательныхПолей();    
	
	КопияСообщения = МодульОбъекта().ПолучитьКопиюСообщения(Сообщение);
	
	Если Сообщение.Направление = "Входящее" Тогда
		
		ПеренестиПоля1СвСообщение(КопияСообщения);
		
		Если РежимРаботы = "Поставщик"
			И Сообщение.СтатусСсылки = "ВходящийНеПрошелВалидацию" Тогда
			СообщениеОбъект = МодульОбъекта().ПолучитьОбъектСообщения(Сообщение.СообщениеСсылка);
			ОшибкиВалидации = МодульОбъекта().ПолучитьОшибкиВалидацииИзОбъектаСообщения(СообщениеОбъект);
			Для Каждого ОшибкаВалидации Из ОшибкиВалидации Цикл
				НоваяСтрока = ТаблицаОшибок.Добавить();
				НоваяСтрока.ИмяПоля = "ОшибкаВалидации";
				НоваяСтрока.ТекстОшибки = ОшибкаВалидации.Значение;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьДействиеЮрФизЛицаСервер("ЮрФизЛицоСвое",		"Подробнее");
	УстановитьДействиеЮрФизЛицаСервер("ЮрФизЛицоСтороннее",	"Подробнее");
	УстановитьДействиеЮрФизЛицаСервер("Грузоотправитель",		"Подробнее");
	УстановитьДействиеЮрФизЛицаСервер("Грузополучатель",		"Подробнее");
	
	РезультатПроверки = МодульОбъекта().ПроверитьПоляСообщения(КопияСообщения);
	
	Если НЕ РезультатПроверки.Успешно Тогда
		
		Для Каждого Стр Из РезультатПроверки.СписокОшибок Цикл
				
			НоваяСтрока = ТаблицаОшибок.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,Стр);
			
			НайденнаяСтрока = НастройкаСсылокИсправленияОшибок.Найти(Стр.ИмяПоля,"ИмяПоля");
			Если Не НайденнаяСтрока = Неопределено Тогда
				НоваяСтрока.Действие = НайденнаяСтрока.ТекстСсылки;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Стр.ИмяТабличнойЧасти) Тогда
			
				Поле = Элементы.Найти(Стр.ИмяПоля);
				Если Не Поле = Неопределено Тогда
				    Поле.ЦветФона = WebЦвета.ЛососьСветлый;
				КонецЕсли;
				
			Иначе
				
				Если НЕ Стр.ИмяПоля = Стр.ИмяТабличнойЧасти И Стр.ИмяТабличнойЧасти = "Товары" Тогда
					
					ТекКолонка = Элементы.Найти(Стр.ИмяТабличнойЧасти+Стр.ИмяПоля);    //это поле формы а не колонка
					
					Если НЕ ТекКолонка = Неопределено Тогда
						Если ТекКолонка.Видимость = Ложь Тогда
							
							ТекКолонка.Видимость	= Истина;
							ТекКолонка.Доступность	= Истина;
							
						КонецЕсли;
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если Стр.ИмяПоля = "Продавец1С" Тогда
				
				Если КтоМы = "Поставщик" Тогда
					УстановитьДействиеЮрФизЛицаСервер("ЮрФизЛицоСвое",		"Установить соответствие", Истина);
				Иначе
					УстановитьДействиеЮрФизЛицаСервер("ЮрФизЛицоСтороннее",	"Установить соответствие", Истина);
				КонецЕсли;
				
			ИначеЕсли Стр.ИмяПоля = "ПродавецEDI" Тогда
				
				Если КтоМы = "Поставщик" Тогда
					УстановитьДействиеЮрФизЛицаСервер("ЮрФизЛицоСвое",		"Исправить ошибки", Истина);
				Иначе
					УстановитьДействиеЮрФизЛицаСервер("ЮрФизЛицоСтороннее",	"Исправить ошибки", Истина);
				КонецЕсли;
				
			ИначеЕсли Стр.ИмяПоля = "Покупатель1С" Тогда
				
				Если КтоМы = "Поставщик" Тогда
					УстановитьДействиеЮрФизЛицаСервер("ЮрФизЛицоСтороннее",	"Установить соответствие", Истина);
				Иначе
					УстановитьДействиеЮрФизЛицаСервер("ЮрФизЛицоСвое",		"Установить соответствие", Истина);
				КонецЕсли;
				
			ИначеЕсли Стр.ИмяПоля = "ПокупательEDI" Тогда
				
				Если КтоМы = "Поставщик" Тогда
					УстановитьДействиеЮрФизЛицаСервер("ЮрФизЛицоСтороннее",	"Исправить ошибки", Истина);
				Иначе
					УстановитьДействиеЮрФизЛицаСервер("ЮрФизЛицоСвое",		"Исправить ошибки", Истина);
				КонецЕсли;
				
			ИначеЕсли Стр.ИмяПоля = "ГрузополучательEDI" Тогда	
				
				УстановитьДействиеЮрФизЛицаСервер("Грузополучатель",	"Исправить ошибки", Истина);
				
			ИначеЕсли Стр.ИмяПоля = "ГрузоотправительEDI" Тогда	
				
				УстановитьДействиеЮрФизЛицаСервер("Грузоотправитель",	"Исправить ошибки", Истина);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ТаблицаОшибок.Количество()=0 Тогда
		ЗакрытьПанельОшибокСервер();
	Иначе
		ОткрытьПанельОшибокСервер();
	КонецЕсли;
	
	
	Если РезультатПроверки.Успешно Тогда
		РезультатПроверки.Успешно = НЕ ЕстьАлкоОшибки;
	КонецЕсли;
	
	Если РезультатПроверки.Успешно Тогда
		РезультатПроверки.Успешно = НЕ ЕстьОшибкиВПрикрепленных;
	КонецЕсли;
	
	Если НЕ РезультатПроверки.Успешно Тогда
		Если Сообщение.Направление = "Исходящее" Тогда
			ВывестиПанельИнформации("В сообщении найдены ошибки, их необходимо исправить перед отправкой.","Плохо");
		Иначе
			ВывестиПанельИнформации("Для загрузки сообщения проставьте соответствия.","Плохо");
		КонецЕсли;
	Иначе
		Если Сообщение.Направление = "Исходящее" Тогда
			Если Не ТаблицаОшибок.Количество() = 0 Тогда
				ВывестиПанельИнформации("В сообщении найдены ошибки, их необходимо исправить перед отправкой.");		
			Иначе
				ВывестиПанельИнформации("Поздравляем! Все ошибки исправлены, теперь можно отправить сообщение.","Хорошо");
			КонецЕсли;
		Иначе
			ВывестиПанельИнформации("Поздравляем! Все соответствия проставлены, теперь можно загрузить сообщение.","Хорошо");
		КонецЕсли;
	КонецЕсли;
	
	КопияСообщения = Неопределено;
	ПоместитьТаблицыЗначенийПроизвольныхРеквизитовВХранилище();
	УничтожитьТаблицыЗначенийПроизвольныхРеквизитов();
	
	Возврат РезультатПроверки.Успешно;
	
КонецФункции

&НаСервере
Процедура ОбработатьТекстОшибки(ТекстОшибки)
	
	Если СтрДлина(ТекстОшибки) > 0 Тогда
		Если Найти(ТекстОшибки,Символы.ПС)>0 Тогда
			Пока Найти(ТекстОшибки,Символы.ПС)>0 Цикл
				НоваяСтрока = ТаблицаОшибок.Добавить();
				НоваяСтрока.ТекстОшибки = Лев(ТекстОшибки, Найти(ТекстОшибки,Символы.ПС)-1);
				ТекстОшибки = Сред(ТекстОшибки, Найти(ТекстОшибки,Символы.ПС)+1);
			КонецЦикла;
			Если СтрДлина(ТекстОшибки)>0 Тогда
				НоваяСтрока = ТаблицаОшибок.Добавить();
				НоваяСтрока.ТекстОшибки = ТекстОшибки;
			КонецЕсли;
		Иначе
			НоваяСтрока = ТаблицаОшибок.Добавить();
			НоваяСтрока.ТекстОшибки = ТекстОшибки;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ОбработатьТекстОшибки()()

&НаСервере
Процедура ЗакрытьПанельОшибокСервер()
	
	Элементы.ПанельОшибок.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ОткрытьПанельОшибокСервер()
	
	Элементы.ПанельОшибок.Видимость = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьПанельОшибокКлиент()
	
	Элементы.ПанельОшибок.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПанельОшибокКлиент()
	
	Элементы.ПанельОшибок.Видимость = Истина;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДействиеЮрФизЛицаСервер(ИмяПоля,Текст,ЕстьОшибки = Ложь)
	
	Надпись = Элементы["Действие"+ИмяПоля];	
	Надпись.Заголовок = Текст;
	Если ЕстьОшибки Тогда
		Надпись.ЦветТекста = WebЦвета.Коричневый;
	Иначе
		Надпись.ЦветТекста = WebЦвета.СинийСоСтальнымОттенком;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПеренестиПоля1СвСообщение(ТекСообщение)
	
	ТекСообщение.Продавец1С 		= Продавец1С;
	ТекСообщение.Покупатель1С 		= Покупатель1С;
	ТекСообщение.Грузоотправитель1С = Грузоотправитель1С;
	ТекСообщение.Грузополучатель1С	= Грузополучатель1С;
	ТекСообщение.Валюта1С			= Валюта1С;
	
	ТекСообщение.Отправитель1С		= Отправитель1С;
	ТекСообщение.Получатель1С		= Получатель1С;
	
	ПереформироватьЕдиницыИзмерения=Ложь;
	
	ИсключаяСвойства = "СтранаEDI,НомерГТДEDI";
	
	Если ТекСообщение.ТипСообщения = "RECADV" Тогда
		ИсключаяСвойства = ИсключаяСвойства + ",КоличествоКВозвратуИПричинаEDI,КоличествоКВозвратуИПричина1С";	
	КонецЕсли;
		
	сч = 0;
	
	Для каждого Стр Из Товары Цикл
		
		ТекСтрокаТоваров = ТекСообщение.Товары[сч];
 		ЗаполнитьЗначенияСвойств(ТекСтрокаТоваров, Стр, , ИсключаяСвойства);
		Если ТипЗнч(ТекСтрокаТоваров.ЕдиницаИзмерения) <> ТипЗнч(Стр.ЕдиницаИзмерения) Тогда
			ПереформироватьЕдиницыИзмерения = Истина;
		КонецЕсли;
		
		сч = сч+1;
		
	КонецЦикла;
	
	Если ПереформироватьЕдиницыИзмерения Тогда //замена типа единицы измерения интерактивными действиями 
		ТекСообщение.Товары.Колонки.Удалить(ТекСообщение.Товары.Колонки.ЕдиницаИзмерения);
		ТекСообщение.Товары.Колонки.Добавить("ЕдиницаИзмерения");
		сч = 0;
		Для каждого Стр Из Товары Цикл
			ТекСтрокаТоваров = ТекСообщение.Товары[сч];
			ТекСтрокаТоваров.ЕдиницаИзмерения = Стр.ЕдиницаИзмерения;
			сч = сч+1;
		КонецЦикла;
	КонецЕсли;//
	
	
	Если ТекСообщение.Направление = "Входящее" Тогда
		
		ТекСообщение.Партнер = Отправитель1С;
		
	Иначе
		
		ТекСообщение.Партнер = Получатель1С;
		
	КонецЕсли;
	
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьПометкиПолей()
	
	//рефакторинг оживить пометки
	
	//ЭлементыФормы.ПометкаПродавец1С.Видимость = Ложь;
	//ЭлементыФормы.ПометкаПродавецEDI.Видимость = Ложь;
	//ЭлементыФормы.ПометкаПокупатель1С.Видимость = Ложь;
	//ЭлементыФормы.ПометкаПокупательEDI.Видимость = Ложь;
	//
	//ЭлементыФормы.ПометкаГрузоотправитель1С.Видимость = Ложь;
	//ЭлементыФормы.ПометкаГрузоотправительEDI.Видимость = Ложь;
	//ЭлементыФормы.ПометкаГрузополучатель1С.Видимость = Ложь;
	//ЭлементыФормы.ПометкаГрузополучательEDI.Видимость = Ложь;
	//
	//ЭлементыФормы.ПометкаВалюта1С.Видимость = Ложь;
	//ЭлементыФормы.ПометкаВалютаEDI.Видимость = Ложь;
	//
	//ЭлементыФормы.ПометкаСтатус.Видимость = Ложь;
	//ЭлементыФормы.ПометкаКомментарий.Видимость = Ложь;
	//
	//ЭлементыФормы.ПометкаДатаВремяПоставки.Видимость = Ложь;
	//ЭлементыФормы.ПометкаДоговор.Видимость = Ложь;
	//
	//ЭлементыФормы.ПометкаЗаказ1С.Видимость = Ложь;
	//ЭлементыФормы.ПометкаЗаказEDI.Видимость = Ложь;
	//ЭлементыФормы.ПометкаНакладная1С.Видимость = Ложь;
	//ЭлементыФормы.ПометкаНакладнаяEDI.Видимость = Ложь;
	//ЭлементыФормы.ПометкаПриемка1С.Видимость = Ложь;
	//ЭлементыФормы.ПометкаПриемкаEDI.Видимость= Ложь;
	//ЭлементыФормы.ПометкаСчетФактура1С.Видимость = Ложь;
	//ЭлементыФормы.ПометкаСчетФактураEDI.Видимость= Ложь;
	//
	//ЭлементыФормы.ПометкаОтправитель1С.Видимость = Ложь;
	//ЭлементыФормы.ПометкаОтправительEDI.Видимость = Ложь;
	//ЭлементыФормы.ПометкаПолучатель1С.Видимость = Ложь;
	//ЭлементыФормы.ПометкаПолучательEDI.Видимость = Ложь;
	//
	//ЭлементыФормы.ПометкаСуммаВсего.Видимость = Ложь;
	//ЭлементыФормы.ПометкаСуммаВсегоБезНДС.Видимость = Ложь;
	//ЭлементыФормы.ПометкаСуммаВсегоНДС.Видимость = Ложь;
	//
	//ЭлементыФормы.ПометкаДатаОтгрузки.Видимость = Ложь;
	
	
	
КонецПроцедуры

&НаСервере
Процедура СделатьНевидимымЭлементФормы(ИмяЭлементаФормы)
	
	НайденныйЭлемент = Элементы.Найти(ИмяЭлементаФормы);
	Если НЕ НайденныйЭлемент = Неопределено Тогда
		
		НайденныйЭлемент.Видимость = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимость()
	
	//НастройкаФормы была получена в МодульОбъекта().ПолучитьНастройкиФормыСообщения()
	
	НевидимыеПоля = НастройкаФормы.НевидимыеПоля;
	
	Для Каждого Стр ИЗ НевидимыеПоля Цикл
		
		ИмяПоля = Стр.Значение;
		
		// пробуем всевозможные варианты
		СделатьНевидимымЭлементФормы("Надпись"+ИмяПоля);
		СделатьНевидимымЭлементФормы("НадписьНомер"	+ ИмяПоля);
		СделатьНевидимымЭлементФормы("НадписьДата"	+ ИмяПоля);
		СделатьНевидимымЭлементФормы(ИмяПоля);
		СделатьНевидимымЭлементФормы(ИмяПоля+"1С");
		СделатьНевидимымЭлементФормы(ИмяПоля+"EDI");
		СделатьНевидимымЭлементФормы(ИмяПоля+"Номер");
		СделатьНевидимымЭлементФормы(ИмяПоля+"Дата");
		
	КонецЦикла;
	
	НевидимыеКолонки = НастройкаФормы.НевидимыеКолонки;
	
	Для Каждого Колонка ИЗ Элементы.Товары.ПодчиненныеЭлементы Цикл
		
	Если ТипЗнч(Колонка) = Тип("ПолеФормы") Тогда 
		
		НайденнаяКолонка = НевидимыеКолонки.НайтиПоЗначению(Сред(Колонка.Имя,7));
		
		Если НайденнаяКолонка = Неопределено Тогда
			Колонка.Видимость	= Истина;
			Колонка.Доступность = Истина;
		Иначе
			Колонка.Видимость	= Ложь;
			Колонка.Доступность	= Ложь;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Колонка) = Тип("ГруппаФормы") Тогда
		
		Для Каждого Колонка ИЗ Колонка.ПодчиненныеЭлементы Цикл
			
		НайденнаяКолонка = НевидимыеКолонки.НайтиПоЗначению(Сред(Колонка.Имя,7));
		
			Если НайденнаяКолонка = Неопределено Тогда
				Колонка.Видимость	= Истина;
				Колонка.Доступность = Истина;
			Иначе
				Колонка.Видимость	= Ложь;
				Колонка.Доступность	= Ложь;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	КонецЦикла;
	
	Если НЕ Элементы.Заказ1С.Видимость И НЕ Элементы.Накладная1С.Видимость И НЕ Элементы.Приемка1С.Видимость И НЕ Элементы.СчетФактура1С.Видимость Тогда
		Элементы.ГруппаСвязанныеДокументы.Видимость = Ложь;
	КонецЕсли;
	
	Если ОбязательныйКодТовараПокупателя = Истина Тогда
		КолонкаКода = Элементы.ТоварыКодТовараПокупателя;
		КолонкаКода.Видимость = Истина;
		КолонкаКода.ТолькоПросмотр = Ложь;
		КолонкаКода.Доступность = Истина;
	КонецЕсли;
	
	Если (Сообщение.ТипСообщения = "ORDERS" ИЛИ Сообщение.ТипСообщения = "RECADV" ИЛИ Сообщение.ТипСообщения = "RETANN") 
		И Сообщение.Направление = "Входящее" Тогда
		Элементы.ТоварыНеЗагружать.Видимость = Истина;
	Иначе
		Элементы.ТоварыНеЗагружать.Видимость = Ложь;
	КонецЕсли;
	
	Если Сообщение.ТипСообщения = "ORDRSP"
		ИЛИ Сообщение.ТипСообщения = "RECADV" Тогда
		Элементы.ТоварыКомментарий.Видимость = Истина;
		Элементы.ТоварыКомментарий.Доступность = Истина;
	КонецЕсли;
	
	Элементы.ТоварыКомментарий.ТолькоПросмотр = НЕ (Сообщение.ТипСообщения = "ORDRSP");
	
	Если (Сообщение.ТипСообщения = "DESADV" И Сообщение.Направление = "Исходящее")
		 ИЛИ (Сообщение.ТипСообщения = "ORDRSP" И Сообщение.Направление = "Исходящее")
		 ИЛИ (Сообщение.ТипСообщения = "PORDERS" И Сообщение.Направление = "Исходящее")
	Тогда
		Элементы.ДействиеДатаПоставки.Видимость = Истина;
	КонецЕсли;
	
	Если Сообщение.ТипСообщения = "INVOIC" Тогда
		
		Если НЕ ЗначениеЗаполнено(ДатаПоставки) Тогда
			Элементы.ДействиеДатаПоставки.Видимость = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Сообщение.ТипСообщения = "ORDERS" И Сообщение.Направление = "Входящее" Тогда
		Если Сообщение.Свойство("ОбратныйЗаказEDI") И ЗначениеЗаполнено(Сообщение.ОбратныйЗаказEDI.Номер) Тогда
            Элементы.ТоварыНеЗагружать.Доступность = Ложь;
			Элементы.ТоварыНеЗагружать.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	//устанавливаем видимой закладку "Данные об упаковках"
	//если у партнера установлена соответствующая настройка и в сообщении есть раздел для хранения данных об упаковках
	СообщениеМожетСодержатьДанныеОбУпаковках = Сообщение.Свойство("ТранспортныеУпаковки") И Сообщение.ТранспортныеУпаковки.Строки.Количество() > 0;
	Элементы.СтраницаТранспортныеУпаковки.Видимость = (ОтправлятьДанныеОбУпаковках = Истина И Сообщение.Направление = "Исходящее") ИЛИ (СообщениеМожетСодержатьДанныеОбУпаковках И Сообщение.Направление = "Входящее");

	//для упаковок SSCC добавились параметры: вес, габариты, температурный режим
	Если Элементы.СтраницаТранспортныеУпаковки.Видимость Тогда
		ВидимостьДопПараметровУпаковки = ИспользоватьДопПараметрыУпаковки ИЛИ Сообщение.Направление = "Входящее";
		ТипУпаковкиПоУмолчанию = "201";
		ВыбраннаяУпаковкаЗаголовок = "евро-палету (201)";
		УстановитьЗаголовокВыбраннойУпаковкиСервер(ВыбраннаяУпаковкаЗаголовок);
		
		Элементы.ТранспортныеУпаковкиГруппаВесНетто.Видимость 			= ВидимостьДопПараметровУпаковки;		
		Элементы.ТранспортныеУпаковкиГруппаВесБрутто.Видимость 			= ВидимостьДопПараметровУпаковки;		
		Элементы.ТранспортныеУпаковкиГруппаДлина.Видимость 				= ВидимостьДопПараметровУпаковки;		
		Элементы.ТранспортныеУпаковкиГруппаШирина.Видимость 			= ВидимостьДопПараметровУпаковки;		
		Элементы.ТранспортныеУпаковкиГруппаВысота.Видимость 			= ВидимостьДопПараметровУпаковки;		
		Элементы.ТранспортныеУпаковкиГруппаТемпературныйРежим.Видимость = ВидимостьДопПараметровУпаковки;		
	КонецЕсли;
	
	//Врата X5
	Если  ((Сообщение.ТипСообщения = "ORDERS" И Сообщение.Направление = "Входящее") ИЛИ (Сообщение.ТипСообщения = "ORDRSP" И Сообщение.Направление = "Исходящее"))
		и Сообщение.Свойство("Транспортировка")
		и Сообщение.Транспортировка<>Неопределено
		и Сообщение.Транспортировка.Свойство("ДатаВремяПрибытия") 
		и Сообщение.Транспортировка.ДатаВремяПрибытия<>неопределено 
		и Сообщение.Транспортировка.ДатаВремяПрибытия.Количество()>0
		Тогда 
		Элементы.ГруппаТранспортировка.Видимость = истина;
	КонецЕсли;
	
	ОтображатьКонтакты = ложь;
	Если (Сообщение.ТипСообщения = "ORDERS" или Сообщение.ТипСообщения = "ORDRSP" или Сообщение.ТипСообщения = "DESADV") и 
		Сообщение.Свойство("КонтактноеЛицоФИО") и ЗначениеЗаполнено(Сообщение.КонтактноеЛицоФИО) Тогда
		ОтображатьКонтакты = Истина;
	КонецЕсли;
			
	Элементы.ГруппаКонтакты.Видимость = ОтображатьКонтакты;		
	Элементы.ДействиеДатаПоставки.Ширина = ?(ОтображатьКонтакты, 8, Элементы.ДействиеЮрФизЛицоСвое.Ширина);

КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьПолей()
	
	// пока опускаем грузоотправителя
	Элементы.Грузоотправитель1С.ТолькоПросмотр = Истина;
	
	ТолькоПросмотрПолей1С = НастройкаФормы.ТолькоПросмотрПолей1С;
	
	//Если НастройкаФормы.ТолькоПросмотрПолей1С Тогда
	
	Элементы.Документ1С.ТолькоПросмотр = ТолькоПросмотрПолей1С;
	
	Элементы.Покупатель1С.ТолькоПросмотр = ТолькоПросмотрПолей1С;
	Элементы.Продавец1С.ТолькоПросмотр = ТолькоПросмотрПолей1С;
	Элементы.Грузоотправитель1С.ТолькоПросмотр = ТолькоПросмотрПолей1С;
	Элементы.Грузополучатель1С.ТолькоПросмотр = ТолькоПросмотрПолей1С;
	Элементы.Договор.ТолькоПросмотр = ТолькоПросмотрПолей1С;
	Элементы.Валюта1С.ТолькоПросмотр = ТолькоПросмотрПолей1С;
	
	Элементы.Возврат1С.ТолькоПросмотр = ТолькоПросмотрПолей1С;
	Элементы.Заказ1С.ТолькоПросмотр = ТолькоПросмотрПолей1С;
	Элементы.Накладная1С.ТолькоПросмотр = ТолькоПросмотрПолей1С;
	Элементы.Приемка1С.ТолькоПросмотр = ТолькоПросмотрПолей1С;
	Элементы.СчетФактура1С.ТолькоПросмотр = ТолькоПросмотрПолей1С;
	
	Элементы.Отправитель1С.ТолькоПросмотр = ТолькоПросмотрПолей1С;
	Элементы.Получатель1С.ТолькоПросмотр = ТолькоПросмотрПолей1С;
	
	Элементы.ТоварыНоменклатура.ТолькоПросмотр = ТолькоПросмотрПолей1С;
	Элементы.ТоварыХарактеристикаНоменклатуры.ТолькоПросмотр = ТолькоПросмотрПолей1С;
	Элементы.ТоварыЕдиницаИзмерения.ТолькоПросмотр = ТолькоПросмотрПолей1С;
	Элементы.ТоварыСтавкаНДС1С.ТолькоПросмотр = ТолькоПросмотрПолей1С;
	Элементы.ТоварыСтрана1С.ТолькоПросмотр = ТолькоПросмотрПолей1С;
	Элементы.ТоварыНомерГТД1С.ТолькоПросмотр = ТолькоПросмотрПолей1С;
	Элементы.ТоварыРешениеПоВозврату.ТолькоПросмотр = ТолькоПросмотрПолей1С;	
	
	Элементы.ТоварыПричинаРешения.ТолькоПросмотр = ТолькоПросмотрСообщения;
	
	//КонецЕсли;
	
	ТолькоПросмотрПолейEDI = НастройкаФормы.ТолькоПросмотрПолейEDI;
	
	Элементы.ПокупательEDI.ТолькоПросмотр = ТолькоПросмотрПолейEDI;
	Элементы.ПродавецEDI.ТолькоПросмотр = ТолькоПросмотрПолейEDI;
	Элементы.ГрузоотправительEDI.ТолькоПросмотр = ТолькоПросмотрПолейEDI;
	Элементы.ГрузополучательEDI.ТолькоПросмотр = ТолькоПросмотрПолейEDI;
	
	Элементы.ВалютаEDI.ТолькоПросмотр = ТолькоПросмотрПолейEDI;
	
	Элементы.ВозвратНомер.ТолькоПросмотр = ТолькоПросмотрПолейEDI;
	Элементы.ВозвратДата.ТолькоПросмотр = ТолькоПросмотрПолейEDI;
	Элементы.ОбратныйЗаказНомер.ТолькоПросмотр = ТолькоПросмотрПолейEDI;
	Элементы.ЗаказНомер.ТолькоПросмотр = ТолькоПросмотрПолейEDI;
	Элементы.ЗаказДата.ТолькоПросмотр = ТолькоПросмотрПолейEDI;
	Элементы.НакладнаяНомер.ТолькоПросмотр = ТолькоПросмотрПолейEDI;
	Элементы.НакладнаяДата.ТолькоПросмотр = ТолькоПросмотрПолейEDI;
	Элементы.ПриемкаНомер.ТолькоПросмотр = ТолькоПросмотрПолейEDI;
	Элементы.ПриемкаДата.ТолькоПросмотр = ТолькоПросмотрПолейEDI;
	Элементы.СчетФактураНомер.ТолькоПросмотр = ТолькоПросмотрПолейEDI;
	Элементы.СчетФактураДата.ТолькоПросмотр = ТолькоПросмотрПолейEDI;
	
	Элементы.ОтправительEDI.ТолькоПросмотр = ТолькоПросмотрПолейEDI;
	Элементы.ПолучательEDI.ТолькоПросмотр = ТолькоПросмотрПолейEDI;
	
	Элементы.ТоварыGTIN.ТолькоПросмотр = ТолькоПросмотрПолейEDI;
	Если ОбязательныйКодТовараПокупателя = Истина Тогда
		Элементы.ТоварыКодТовараПокупателя.ТолькоПросмотр = Ложь;
	Иначе
		Элементы.ТоварыКодТовараПокупателя.ТолькоПросмотр = ТолькоПросмотрПолейEDI;
	КонецЕсли;
	Элементы.ТоварыКодТовараПоставщика.ТолькоПросмотр = ТолькоПросмотрПолейEDI;
	Элементы.ТоварыНаименование.ТолькоПросмотр = ТолькоПросмотрПолейEDI;
	Элементы.ТоварыКодЕдиницыИзмеренияEDI.ТолькоПросмотр = ТолькоПросмотрПолейEDI;
	
	Элементы.ТоварыСтавкаНДСEDI.ТолькоПросмотр = ТолькоПросмотрПолейEDI;
	Элементы.ТоварыСтранаEDI.ТолькоПросмотр = ТолькоПросмотрПолейEDI;
	Элементы.ТоварыНомерГТДEDI.ТолькоПросмотр = ТолькоПросмотрПолейEDI;
	Элементы.ТоварыКоличествоКВозвратуИПричинаEDI.ТолькоПросмотр = ТолькоПросмотрПолейEDI;
	Элементы.ТоварыКоличествоКВозвратуИПричина1С.ТолькоПросмотр = ТолькоПросмотрПолейEDI;
	
	//КонецЕсли;
	
	Элементы.ТоварыКоличество.ТолькоПросмотр = Истина;
	Элементы.ТоварыЦенаСНДС.ТолькоПросмотр = Истина;
	Элементы.ТоварыЦенаБезНДС.ТолькоПросмотр = Истина;
	Элементы.ТоварыСуммаСНДС.ТолькоПросмотр = Истина;
	Элементы.ТоварыСуммаБезНДС.ТолькоПросмотр = Истина;
	Элементы.ТоварыСуммаНДС.ТолькоПросмотр = Истина;
	
	Элементы.ТоварыКоличествоСверхпоставки.ТолькоПросмотр = Истина;
	Элементы.ТоварыКоличествоНедопоставки.ТолькоПросмотр = Истина;
	
	Если ТолькоПросмотрСообщения ИЛИ Сообщение.Направление = "Входящее" Тогда 
		
		Элементы.ТоварныеПозиции.ТолькоПросмотр = Истина;
		Элементы.ТранспортныеУпаковки.ТолькоПросмотр = Истина;
		
		Элементы.ТранспортныеУпаковки.КоманднаяПанель.Доступность 	= Ложь;
		Элементы.ГруппаРазделительИУправляющиеКнопки.Доступность	= Ложь;
		
	КонецЕсли;
	
	Элементы.Особенности.ТолькоПросмотр = Параметры.ТолькоПросмотр;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТипПоля1С(ИмяПоля)
	
	Если МетаданныеСообщения=Неопределено Тогда 
		МетаданныеСообщения=МодульОбъекта().ПолучитьМетаданныеСообщения(Сообщение.ТипСообщения,Сообщение.Направление);
	КонецЕсли;
	
	СтрокаМетаданных = МетаданныеСообщения.Найти(ИмяПоля,"ИмяПоля");
	
	Если НЕ СтрокаМетаданных = Неопределено Тогда
		
		Тип1С = СтрокаМетаданных.Тип1С;
		Если ЗначениеЗаполнено(Тип1С) Тогда
			
			Если СтрокаМетаданных.Принадлежность = "1С" ИЛИ СтрокаМетаданных.Принадлежность = "=" Тогда
				ИмяНаФорме = ИмяПоля;
			Иначе
				ИмяНаФорме = ИмяПоля+"1С";
			КонецЕсли;
			
			ПолеФормы	= Элементы[ИмяНаФорме];
			ТипПоля		= МодульОбъекта().ПолучитьТипЗначенияОбъекта(Тип1С);
			
			Если ТипПоля = Неопределено ИЛИ (ВнешнееХранилище И Найти(ТипПоля,"КонтурEDI_")>0) Тогда
				//Сообщить("Не задан тип объекта 1С для поля с типом "+Тип1С);
			Иначе	
				
				Элементы[ИмяНаФорме].ОграничениеТипа = Новый ОписаниеТипов(ТипПоля);
				                     
				Если НЕ ЗначениеЗаполнено(Сообщение[ИмяНаФорме]) Тогда
					
					ЭтаФорма[ИмяНаФорме] = МодульОбъекта().ПолучитьПустуюСсылкуОбъекта(Тип1С);
					
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура УстановитьТипКолонки1С(ИмяПоля)
	
	СвойстваПоля = МодульОбъекта().ПолучитьСвойстваПоля(ИмяПоля,МетаданныеСообщения);
	
	Если ЗначениеЗаполнено(СвойстваПоля.ИмяНаФорме1С) И ЗначениеЗаполнено(СвойстваПоля.Тип1С) Тогда
		
		ПолеФормы	= Элементы["Товары"+СвойстваПоля.ИмяНаФорме1С];
		ТипПоля		= МодульОбъекта().ПолучитьТипЗначенияОбъекта(СвойстваПоля.Тип1С);
		
		Если ТипПоля = Неопределено Тогда
			
			//Сообщить("Не задан тип объекта 1С для поля с типом "+СвойстваПоля.Тип1С);
			
		Иначе	
			
			ПолеФормы.ОграничениеТипа = Новый ОписаниеТипов(ТипПоля);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТипыПолей1С()
	
	УстановитьТипПоля1С("Документ");
	
	УстановитьТипПоля1С("Продавец");
	УстановитьТипПоля1С("Покупатель");
	УстановитьТипПоля1С("Грузоотправитель");
	УстановитьТипПоля1С("Грузополучатель");
	УстановитьТипПоля1С("Договор");
	УстановитьТипПоля1С("Валюта");
	УстановитьТипПоля1С("Отправитель");
	УстановитьТипПоля1С("Получатель");
	УстановитьТипПоля1С("Заказ");
	УстановитьТипПоля1С("Накладная");
	УстановитьТипПоля1С("Приемка");
	
	УстановитьТипКолонки1С("Номенклатура");
	УстановитьТипКолонки1С("ХарактеристикаНоменклатуры");
	УстановитьТипКолонки1С("ЕдиницаИзмерения");
	УстановитьТипКолонки1С("СтавкаНДС");
	УстановитьТипКолонки1С("Страна");
	УстановитьТипКолонки1С("НомерГТД");
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьПрикрепленныеСообщения()
	
	ТаблицаПрикрепленныхСообщений.Очистить();
	
	ОтправкаТолькоALCRPT = Ложь;
	
	Накладная = Неопределено;

	Если Найти("\DESADV\INVOIC\",Сообщение.ТипСообщения) > 0 Тогда
		Если Сообщение.ТипСообщения = "DESADV" И МодульОбъекта().ПолучитьЗначениеСвойстваОбъектаEDI(Сообщение.Получатель1С,"ОтправлятьALCRPT") = Истина Тогда
			 Накладная = Сообщение.Документ1С;
		ИначеЕсли Сообщение.ТипСообщения = "INVOIC" И МодульОбъекта().ПолучитьЗначениеСвойстваОбъектаEDI(Сообщение.Получатель1С,"ОтправлятьALCRPTсINVOIC") = Истина Тогда
			 Накладная = МодульОбъекта().ПолучитьНакладнуюСчетаФактуры(Сообщение.Документ1С);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Накладная) Тогда // Нужно отправить ALCRPT
		
		Элементы.СтраницаАлкоголь.Видимость = Истина;
		
		АлкоТовары.Очистить();
		
		СообщениеАлко = МодульОбъекта().ПодготовитьИсходящееСообщение("ALCRPT",Накладная);
		//!! если сделать произвольным реквизитом при передаче на клиент упадет! или чистить ТЗ у неё
		
		Для каждого Стр Из СообщениеАлко.Товары Цикл
			
			НоваяСтрока = АлкоТовары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,Стр);
			
			НоваяСтрока.АлкоЛицензияПоставщика	= МодульОбъекта().ПолучитьПредставлениеАлкоЛицензии(Стр.АлкоЛицензияПоставщика); 
			НоваяСтрока.АлкоСертификатПродукции = МодульОбъекта().ПолучитьПредставлениеАлкоЛицензии(Стр.АлкоСертификатПродукции); 
			
		КонецЦикла;
		
		НомерТТН	= СообщениеАлко.ТранспортнаяНакладнаяEDI.Номер;
		ДатаТТН		= СообщениеАлко.ТранспортнаяНакладнаяEDI.Дата;
		
		//отправляем только алко если desadv уже дошел и явно не указано что это переотправка desadv
		Если Сообщение.ТипСообщения = "DESADV" Тогда
			Если МодульОбъекта().ПолучитьСтатусСообщения(, Накладная, "DESADV") = "Доставлен" 
				И НЕ ЗначениеЗаполнено(Параметры.ПереотправляемоеСообщениеСсылка) Тогда
				ОтправкаТолькоALCRPT = Истина;
			КонецЕсли;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаПрикрепленныхСообщений.Добавить();
		НоваяСтрока.СообщениеПредставление = "Данные об алкогольной продукции (ALCRPT)";
		НоваяСтрока.Документ = Сообщение.ДокументСсылка;
		
	КонецЕсли;
	
	Если Сообщение.ТипСообщения = "DESADV" Тогда
		
		Если МодульОбъекта().ПолучитьЗначениеСвойстваОбъектаEDI(Сообщение.Получатель1С,"ОтправлятьINVOICсDESADV") = Истина Тогда
			
			СчетФактура = МодульОбъекта().ПолучитьСчетФактуруНакладной(Сообщение.Документ1С);
			//!! если сделать произвольным реквизитом при передаче на клиент упадет! или чистить ТЗ у неё
			
			Если ЗначениеЗаполнено(СчетФактура) Тогда
				СообщениеСФ = МодульОбъекта().ПодготовитьИсходящееСообщение("INVOIC",СчетФактура);
				
				НоваяСтрока = ТаблицаПрикрепленныхСообщений.Добавить();
				НоваяСтрока.СообщениеПредставление = "Счет-фактура (INVOIC)";
				НоваяСтрока.Документ = СчетФактура;
				
			Иначе
				
				НоваяСтрока = ТаблицаПрикрепленныхСообщений.Добавить();
				НоваяСтрока.СообщениеПредставление = "Счет-фактура (INVOIC)";
				НоваяСтрока.Документ = СчетФактура;
				НоваяСтрока.Примечание = "Не найден счет-фактура!";
				НоваяСтрока.ЕстьОшибки = Истина;
				
			КонецЕсли;
			
		КонецЕсли
		
	КонецЕсли;
	
	Если ТаблицаПрикрепленныхСообщений.Количество()=0 Тогда
		
		Элементы.ПанельДопИнформации.ТекущаяСтраница = Элементы.ПанельДопИнформации.ПодчиненныеЭлементы.СтраницаНаименований;
		
	Иначе
		
		Элементы.ПанельДопИнформации.ТекущаяСтраница = Элементы.ПанельДопИнформации.ПодчиненныеЭлементы.СтраницаПрикрепленныхСообщений;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура  ЗаполнитьДеревоТранспортныхУпаковокИзСообщения(ДеревоТранспортныхУпаковок, ТранспортныеУпаковки)
	
	Для каждого СтрокаТранспортныхУпаковокИзСообщения Из ТранспортныеУпаковки.Строки Цикл
		СтрокаДереваУпаковок =  ДеревоТранспортныхУпаковок.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаДереваУпаковок,СтрокаТранспортныхУпаковокИзСообщения);
		ЗаполнитьДеревоТранспортныхУпаковокИзСообщения(СтрокаДереваУпаковок, СтрокаТранспортныхУпаковокИзСообщения)
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоляНаФорме()
	
	Если НЕ Сообщение = Неопределено Тогда
		
		Документ1С			= Сообщение.Документ1С;
		
		ПолучитьПрикрепленныеСообщения();
		
		НадписьЗаголовок = Элементы.НадписьОткрытьДокумент;
		
		Если ЗначениеЗаполнено(Документ1С) Тогда
			НадписьЗаголовок.Заголовок = СокрЛП(Документ1С);
			НадписьЗаголовок.Гиперссылка = Истина;
			НадписьЗаголовок.ЦветТекста = WebЦвета.СинийСоСтальнымОттенком;
		Иначе
			Если Сообщение.ТипСообщения = "ORDERS" Тогда
				НадписьЗаголовок.Заголовок = "не создан";
			Иначе
				НадписьЗаголовок.Заголовок = "не привязан";
			КонецЕсли;
			НадписьЗаголовок.Гиперссылка = Ложь;
			НадписьЗаголовок.ЦветТекста = Элементы.НадписьЮрФизЛицоСвое.ЦветТекста;
		КонецЕсли;
		
		ДокументНомер		= Сообщение.ДокументEDI.Номер;
		ДокументДата		= Сообщение.ДокументEDI.Дата;
		
		ПродавецEDI			= Сообщение.ПродавецEDI.GLN;
		ПокупательEDI		= Сообщение.ПокупательEDI.GLN;
		ГрузоотправительEDI = Сообщение.ГрузоотправительEDI.GLN;
		ГрузополучательEDI	= Сообщение.ГрузополучательEDI.GLN;
		ВалютаEDI			= Сообщение.ВалютаEDI;
		
		ОтправительEDI	= Сообщение.ОтправительEDI.GLN;
		ПолучательEDI	= Сообщение.ПолучательEDI.GLN;
		
		Продавец1С			= Сообщение.Продавец1С;
		Покупатель1С		= Сообщение.Покупатель1С;
		Грузополучатель1С	= Сообщение.Грузополучатель1С;
		Грузоотправитель1С  = Сообщение.Грузоотправитель1С;
		
		Элементы.НадписьПримечание.Видимость = Ложь;
		
		ТекНомерСерииЗаказов = "";
		Если Сообщение.Свойство("НомерСерииЗаказов",ТекНомерСерииЗаказов) Тогда
			Если ЗначениеЗаполнено(ТекНомерСерииЗаказов) Тогда
				Элементы.НадписьПримечание.Заголовок = "№ серии заказов: " + СокрЛП(ТекНомерСерииЗаказов);
				Элементы.НадписьПримечание.Видимость = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если Сообщение.Свойство("Комментарий") Тогда
			ТипПоставки = МодульОбъекта().ТипПоставкиСообщения(Сообщение);
			Комментарий = ""+Сообщение.Комментарий + ?(ТипПоставки = "",""," Тип поставки: "+ТипПоставки);
		КонецЕсли;
		
		Если Сообщение.Свойство("ПромоНомер") Тогда
			ПромоНомер	= Сообщение.ПромоНомер;
		КонецЕсли;
		
		Если Сообщение.Свойство("Контракт") Тогда
			КонтрактНомер	= Сообщение.Контракт.Номер;
			КонтрактДата	= Сообщение.Контракт.Дата;
			Если Не ЗначениеЗаполнено(КонтрактНомер) и не ЗначениеЗаполнено(КонтрактДата) Тогда
				НастройкаФормы.НевидимыеПоля.Добавить("Контракт");;	
			КонецЕсли;
		КонецЕсли;
		
		// представления юр\физ лиц
		Если КтоМы=Неопределено Тогда 
			КтоМы = МодульОбъекта().ОпределитьКемМыЯвляемся(Сообщение);
		КонецЕсли;

		Если КтоМы = "Поставщик" Тогда
			Элементы.ПредставлениеЮрФизЛицоСвое.Заголовок		= МодульОбъекта().ПолучитьПредставлениеЮрФизЛица(Сообщение.ПродавецEDI, Сообщение.Продавец1С);
			Элементы.ПредставлениеЮрФизЛицоСтороннее.Заголовок = МодульОбъекта().ПолучитьПредставлениеЮрФизЛица(Сообщение.ПокупательEDI, Сообщение.Покупатель1С);
		Иначе
			Элементы.ПредставлениеЮрФизЛицоСвое.Заголовок		= МодульОбъекта().ПолучитьПредставлениеЮрФизЛица(Сообщение.ПокупательEDI, Сообщение.Покупатель1С);
			Элементы.ПредставлениеЮрФизЛицоСтороннее.Заголовок = МодульОбъекта().ПолучитьПредставлениеЮрФизЛица(Сообщение.ПродавецEDI, Сообщение.Продавец1С);
		КонецЕсли;
		
		Элементы.ПредставлениеГрузополучатель.Заголовок	= МодульОбъекта().ПолучитьПредставлениеЮрФизЛица(Сообщение.ГрузополучательEDI,	Сообщение.Грузополучатель1С,	Истина);
		Элементы.ПредставлениеГрузоотправитель.Заголовок	= МодульОбъекта().ПолучитьПредставлениеЮрФизЛица(Сообщение.ГрузоотправительEDI,	Сообщение.Грузоотправитель1С);
	
		Если Элементы.ПредставлениеЮрФизЛицоСвое.Заголовок = "не указан" Тогда
			Элементы.ПредставлениеЮрФизЛицоСвое.Заголовок = "не указана";
			Элементы.ДействиеЮрФизЛицоСвое.Видимость = Ложь;
		Иначе
			Элементы.ДействиеЮрФизЛицоСвое.Видимость = Истина;
		КонецЕсли;
		Если Элементы.ПредставлениеЮрФизЛицоСтороннее.Заголовок = "не указан" Тогда
			Элементы.ДействиеЮрФизЛицоСтороннее.Видимость = Ложь;
		Иначе
			Элементы.ДействиеЮрФизЛицоСтороннее.Видимость = Истина;
		КонецЕсли;
		Если Элементы.ПредставлениеГрузоотправитель.Заголовок = "не указан" Тогда
			Элементы.ДействиеГрузоотправитель.Видимость = Ложь;
		Иначе
			Элементы.ДействиеГрузоотправитель.Видимость = Истина;
		КонецЕсли;
		Если Элементы.ПредставлениеГрузополучатель.Заголовок = "не указан" Тогда
			Элементы.ДействиеГрузополучатель.Видимость = Ложь;
		Иначе
			Элементы.ДействиеГрузополучатель.Видимость = Истина;
		КонецЕсли;
		
		//+для АТАК
		Если Сообщение.Свойство("ДатаПоставкиФактическая") 
			И ЗначениеЗаполнено(Сообщение.ДатаПоставкиФактическая) Тогда
			Элементы.ПредставлениеДатыПоставки.Заголовок		= МодульОбъекта().ПолучитьПредставлениеДатыПоставки_КонтурEDI(Сообщение.ДатаПоставкиФактическая)	
		//-
		Иначе
			Элементы.ПредставлениеДатыПоставки.Заголовок		= МодульОбъекта().ПолучитьПредставлениеДатыПоставки_КонтурEDI(Сообщение.ДатаПоставки);
			Если Сообщение.ТипСообщения = "ORDRSP" Тогда
				Результат = ПроверитьИзменениеДатыПоставки();
				Если Результат.Изменение Тогда
					Элементы.ПредставлениеДатыПоставки.ЦветТекста = WebЦвета.ТемноОранжевый;
				Иначе
					Элементы.ПредставлениеДатыПоставки.ЦветТекста = WebЦвета.ТемноЗеленый;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если Сообщение.Свойство("КонтактноеЛицоФИО") 
			И ЗначениеЗаполнено(Сообщение.КонтактноеЛицоФИО) Тогда
			Элементы.ПредставлениеКонтакты.Заголовок = ПолучитьПредставлениеКонтактногоЛицаПоЗаказу();
		КонецЕсли;

		Отправитель1С		= Сообщение.Отправитель1С;
		Получатель1С		= Сообщение.Получатель1С;
		
		Валюта1С			= Сообщение.Валюта1С;
		
		Если Не ЗначениеЗаполнено(Сообщение.Договор) Тогда
			Сообщение.Договор = МодульОбъекта().ПолучитьДоговорКонтрагентаДляСообщения(Сообщение,МодульОбъекта().ОпределитьКемМыЯвляемся(Сообщение));
		КонецЕсли;
		Договор				= Сообщение.Договор;
		
		Статус				= ПеревестиСтатус(Сообщение.Статус);
		
		Заказ1С				= Сообщение.Заказ1С;
		Накладная1С			= Сообщение.Накладная1С;
		Приемка1С			= Сообщение.Приемка1С;
		Если Сообщение.Свойство("СчетФактура1С") Тогда
			СчетФактура1С		= Сообщение.СчетФактура1С;
		КонецЕсли;
		
		Если Сообщение.Свойство("ОбратныйЗаказEDI") Тогда
			ОбратныйЗаказНомер  = Сообщение.ОбратныйЗаказEDI.Номер;
		КонецЕсли;
		
		ЗаказНомер			= Сообщение.ЗаказEDI.Номер;
		ЗаказДата			= Сообщение.ЗаказEDI.Дата;
		НакладнаяНомер		= Сообщение.НакладнаяEDI.Номер;
		НакладнаяДата		= Сообщение.НакладнаяEDI.Дата;
		ПриемкаНомер		= Сообщение.ПриемкаEDI.Номер;
		ПриемкаДата			= Сообщение.ПриемкаEDI.Дата;
		Если Сообщение.Свойство("СчетФактураEDI") Тогда
			СчетФактураНомер	= Сообщение.СчетФактураEDI.Номер;
			СчетФактураДата		= Сообщение.СчетФактураEDI.Дата;
		КонецЕсли;
		
		Если (Сообщение.ТипСообщения = "INVOIC") ИЛИ (Сообщение.ТипСообщения = "COINVOIC") Тогда
			
			Если Сообщение.Исправление = Истина Тогда
				
				Исправление = Сообщение.Исправление;
				НомерИсправления = Сообщение.ДокументEDI.НомерИсправления;
				
				Элементы.ГруппаСтатусИсправление.Видимость=Истина;
				
			КонецЕсли;
			
		КонецЕсли;	

		Если Сообщение.ТипСообщения = "COINVOIC" Тогда
			
			СуммаВсегоУвеличение		= Сообщение.СуммаВсегоУвеличение;
			СуммаВсегоНДСУвеличение		= Сообщение.СуммаВсегоНДСУвеличение;
			СуммаВсегоБезНДСУвеличение	= Сообщение.СуммаВсегоБезНДСУвеличение;
			
			СуммаВсегоУменьшение		= Сообщение.СуммаВсегоУменьшение;
			СуммаВсегоНДСУменьшение		= Сообщение.СуммаВсегоНДСУменьшение;
			СуммаВсегоБезНДСУменьшение	= Сообщение.СуммаВсегоБезНДСУменьшение;
			
			Элементы.ПанельСумм.ТекущаяСтраница = Элементы.ГруппаИтогиТабличнойЧастиПлюсМинус;
			
		Иначе
			
			СуммаВсего			= Сообщение.СуммаВсего;
			СуммаВсегоНДС		= Сообщение.СуммаВсегоНДС;
			СуммаВсегоБезНДС	= Сообщение.СуммаВсегоБезНДС;
			
		КонецЕсли;
		
		КоличествоСтрок		= Сообщение.Товары.Количество();
		
		Если КоличествоСтрок = 0 Тогда
			НадписьТовары = "Товары";
		Иначе
			НадписьТовары = "Товары ("+КоличествоСтрок+")";
		КонецЕсли;
		
		Элементы.СтраницаТовары.Заголовок = НадписьТовары;
		
		//ПустаяСсылкаНоменклатуры				= МодульОбъекта().ПолучитьПустуюСсылкуОбъекта("Номенклатура");
		//ПустаяСсылкаХарактеристикиНоменклатуры	= МодульОбъекта().ПолучитьПустуюСсылкуОбъекта("ХарактеристикаНоменклатуры");
		//ПустаяСсылкаЕдиницыИзмерения			= МодульОбъекта().ПолучитьПустуюСсылкуОбъекта("ЕдиницаИзмерения");
		//ПустаяСсылкаСтавкиНДС					= МодульОбъекта().ПолучитьПустуюСсылкуОбъекта("СтавкаНДС");
		//ПустаяСсылкаСтрана						= МодульОбъекта().ПолучитьПустуюСсылкуОбъекта("Страна");
		//ПустаяСсылкаНомерГТД					= МодульОбъекта().ПолучитьПустуюСсылкуОбъекта("НомерГТД");
		
		Товары.Очистить();
		ТоварныеПозиции.Очистить();
		НомСтр = 1;
				
		ПробоватьСчитатьУпаковки 			= Ложь;
		ПоказыватьКоличествоУпаковок 		= Ложь;
		ПоказыватьКоличествоВОдномМесте 	= Ложь;
		ЕстьКонечныйГрузополучательВТоварах = Ложь;
		ЕстьКоличествоКВозвратуИПричина 	= Ложь;
		ЕстьКоличествоСверхпоставки 		= Ложь;
		ЕстьКоличествоНедопоставки 			= Ложь;
		
		Если Сообщение.ТипСообщения = "ORDERS" Тогда
			Если НЕ Сообщение.Товары.Колонки.Найти("КоличествоВОдномМесте")=Неопределено Тогда
				ПробоватьСчитатьУпаковки = Истина;
			КонецЕсли;
		КонецЕсли;
		
		ОтправлятьДанныеОбУпаковках 		= Ложь;
		СообщениеСодержитДанныеОбУпаковках 	= Ложь;
		Если КтоМы = "Поставщик" Тогда
			Если Сообщение.Направление = "Исходящее" Тогда
				Если ЗначениеЗаполнено(Сообщение.Получатель1С) Тогда
					ОбязательныйКодТовараПокупателя = (МодульОбъекта().ПолучитьЗначениеСвойстваОбъектаEDI(Сообщение.Получатель1С, "ОбязательныйКодТовараТорговойСети") = Истина);
					Если Сообщение.ТипСообщения = "DESADV" ИЛИ Сообщение.ТипСообщения = "INVOIC"  Тогда
						ОтправлятьДанныеОбУпаковках = (МодульОбъекта().ПолучитьЗначениеСвойстваОбъектаEDI(Сообщение.Получатель1С, "ОтправлятьДанныеОбУпаковках") = Истина);
						Если Сообщение.Свойство("ТранспортныеУпаковки") И Сообщение.ТранспортныеУпаковки<>Неопределено И Не Сообщение.ТранспортныеУпаковки.Строки.Количество() = 0 Тогда
							СообщениеСодержитДанныеОбУпаковках  = Истина;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			Иначе
				СообщениеСодержитДанныеОбУпаковках = Сообщение.Свойство("ТранспортныеУпаковки") И Сообщение.ТранспортныеУпаковки.Строки.Количество() > 0;
			КонецЕсли;
		ИначеЕсли Сообщение.Направление = "Входящее" Тогда
			СообщениеСодержитДанныеОбУпаковках = Сообщение.Свойство("ТранспортныеУпаковки") И Сообщение.ТранспортныеУпаковки.Строки.Количество() > 0;
		КонецЕсли;
		
		Для каждого Стр Из Сообщение.Товары Цикл
			
			НоваяСтрока = Товары.Добавить();
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока,Стр);
			НоваяСтрока.НомерСтроки = НомСтр;
			НомСтр = НомСтр + 1;
			
			Если НЕ ЗначениеЗаполнено(Стр.Номенклатура) Тогда
				Если ТипЗнч(Стр.СписокНоменклатуры) = Тип("СписокЗначений") И Стр.СписокНоменклатуры.Количество() > 0 Тогда
					НоваяСтрока.Номенклатура					= "Несколько позиций: " + Строка(Стр.СписокНоменклатуры);
					НоваяСтрока.НаименованиеНоменклатурыПолное	= НоваяСтрока.Номенклатура;
				//Иначе
					//НоваяСтрока.Номенклатура = ПустаяСсылкаНоменклатуры;
				КонецЕсли;
			КонецЕсли;
			//Если НЕ ЗначениеЗаполнено(Стр.ХарактеристикаНоменклатуры) Тогда
			//	НоваяСтрока.ХарактеристикаНоменклатуры = ПустаяСсылкаХарактеристикиНоменклатуры;
			//КонецЕсли;
			//Если НЕ ЗначениеЗаполнено(Стр.ЕдиницаИзмерения) Тогда
			//	НоваяСтрока.ЕдиницаИзмерения = ПустаяСсылкаЕдиницыИзмерения;
			//КонецЕсли;
			//Если НЕ Сообщение.ТипСообщения = "COINVOIC" Тогда
			//	Если НЕ ЗначениеЗаполнено(Стр.СтавкаНДС1С) Тогда
			//		НоваяСтрока.СтавкаНДС1С = ПустаяСсылкаСтавкиНДС;
			//	КонецЕсли;
			//КонецЕсли;
			//Если НЕ ЗначениеЗаполнено(Стр.Страна1С) Тогда
			//	НоваяСтрока.Страна1С = ПустаяСсылкаСтрана;
			//КонецЕсли;
			//Если НЕ ЗначениеЗаполнено(Стр.НомерГТД1С) Тогда
			//	НоваяСтрока.НомерГТД1С = ПустаяСсылкаНомерГТД;
			//КонецЕсли;
			
			Если ПробоватьСчитатьУпаковки Тогда
				
				Если НоваяСтрока.КодЕдиницыИзмеренияEDI = "CT" ИЛИ НоваяСтрока.КодЕдиницыИзмеренияEDI = "PA" Тогда
					НоваяСтрока.КоличествоУпаковок = НоваяСтрока.Количество;
				Иначе
					Если ЗначениеЗаполнено(НоваяСтрока.КоличествоВОдномМесте) И ЗначениеЗаполнено(НоваяСтрока.Количество) Тогда
						НоваяСтрока.КоличествоУпаковок = НоваяСтрока.Количество/НоваяСтрока.КоличествоВОдномМесте;
					КонецЕсли;						
				КонецЕсли;
				
				Если ЗначениеЗаполнено(НоваяСтрока.КоличествоУпаковок) Тогда
					ПоказыватьКоличествоУпаковок = Истина;
				КонецЕсли;					
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(НоваяСтрока.КоличествоВОдномМесте) Тогда
				ПоказыватьКоличествоВОдномМесте = Истина;
			КонецЕсли;
			
			//заполняем товарные позиции (sscc) только для создаваемых (новых) сообщений (элемент справочника "Сообщения" отсутствует)
			ДобавитьТоварнуюПозицию(НоваяСтрока);
			
			Если Сообщение.ТипСообщения = "ORDERS" И Сообщение.Товары.Колонки.Найти("КонечныйГрузополучательGLN")<>Неопределено  и Значениезаполнено(Стр.КонечныйГрузополучательGLN) Тогда
				НоваяСтрока.КонечныйГрузополучательПредставление = Стр.КонечныйГрузополучательGLN;
				ЕстьКонечныйГрузополучательВТоварах=Истина;
			иначе
				НоваяСтрока.КонечныйГрузополучательПредставление = "";
			КонецЕсли;
			
			Если Сообщение.ТипСообщения = "RECADV" Тогда
				Если Сообщение.Товары.Колонки.Найти("КоличествоКВозвратуИПричина1С") <> Неопределено
					И ЗначениеЗаполнено(НоваяСтрока.КоличествоКВозвратуИПричина1С) Тогда
					ЕстьКоличествоКВозвратуИПричина = Истина;
				КонецЕсли;
				
				Если Сообщение.Товары.Колонки.Найти("КоличествоСверхпоставки") <> Неопределено
					И ЗначениеЗаполнено(НоваяСтрока.КоличествоСверхпоставки) Тогда
					ЕстьКоличествоСверхпоставки = Истина;
				КонецЕсли;
				
				Если Сообщение.Товары.Колонки.Найти("КоличествоНедопоставки") <> Неопределено
					И ЗначениеЗаполнено(НоваяСтрока.КоличествоНедопоставки) Тогда
					ЕстьКоличествоНедопоставки = Истина;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
		НастройкаФормы.НевидимыеКолонки.Добавить("КоличествоКВозвратуИПричинаEDI");
		Если НЕ ЕстьКоличествоКВозвратуИПричина Тогда
			НастройкаФормы.НевидимыеКолонки.Добавить("КоличествоКВозвратуИПричина1С");
		КонецЕсли;
		Если НЕ ЕстьКоличествоСверхпоставки Тогда
			НастройкаФормы.НевидимыеКолонки.Добавить("КоличествоСверхпоставки");
		КонецЕсли;
		Если НЕ ЕстьКоличествоНедопоставки Тогда
			НастройкаФормы.НевидимыеКолонки.Добавить("КоличествоНедопоставки");
		КонецЕсли;
		
		Если НЕ ЕстьКонечныйГрузополучательВТоварах Тогда
			НастройкаФормы.НевидимыеКолонки.Добавить("КонечныйГрузополучательПредставление");
		КонецЕсли;
		Если НЕ ПоказыватьКоличествоВОдномМесте Тогда
			НастройкаФормы.НевидимыеКолонки.Добавить("КоличествоВОдномМесте");
		КонецЕсли;
		Если НЕ ПоказыватьКоличествоУпаковок Тогда
			НастройкаФормы.НевидимыеКолонки.Добавить("КоличествоУпаковок");
		КонецЕсли;
		
		КоличествоВсего	= Товары.Итог("Количество");
		
		Если СуммаВсего = 0 Тогда
			СуммаВсего = Товары.Итог("СуммаСНДС");
		КонецЕсли;
		Если СуммаВсегоБезНДС = 0 Тогда
			СуммаВсегоБезНДС = Товары.Итог("СуммаБезНДС");
		КонецЕсли;
		Если СуммаВсегоНДС = 0 Тогда
			СуммаВсегоНДС = Товары.Итог("СуммаНДС");
		КонецЕсли;	
		
		Если Сообщение.ТипСообщения = "ORDERS" 
			И Сообщение.Направление = "Входящее" Тогда
			
			//вызов ПМ по товарной группе товаров
			ЗаполнитьТоварнуюГруппуТаблицыТоваровВызовСервера();
		Иначе
			НастройкаФормы.НевидимыеКолонки.Добавить("ТоварнаяГруппа");
			//еще можно скрывать, если товарные группы не используются или если все товары вошли в одну группу.
		КонецЕсли;
		
		//если партнер может отправлять данные об упаковках и есть элемент справочника "Сообщения" (т.е. сообщение открыто на просмотр) и транспортные упаковки присутствуют в сообщении
		//тогда заполняем данные о распределении товаров в упаковках
		//иначе очищаем данные об упаковках
		Если СообщениеСодержитДанныеОбУпаковках Тогда
			ДеревоТранспортныхУпаковок = РеквизитФормыВЗначение("ТранспортныеУпаковки");
			ДеревоТранспортныхУпаковок.Строки.Очистить();
			
			ЗаполнитьДеревоТранспортныхУпаковокИзСообщения(ДеревоТранспортныхУпаковок,Сообщение.ТранспортныеУпаковки);
			
			ЗначениеВРеквизитФормы(ДеревоТранспортныхУпаковок,"ТранспортныеУпаковки");		
		Иначе
			ТранспортныеУпаковки.ПолучитьЭлементы().Очистить();
		КонецЕсли;
		
		//Врата времени Х5
		Если ((Сообщение.ТипСообщения = "ORDERS" И Сообщение.Направление = "Входящее") ИЛИ (Сообщение.ТипСообщения = "ORDRSP" И Сообщение.Направление = "Исходящее"))
			и Сообщение.Свойство("Транспортировка") и Сообщение.Транспортировка<>Неопределено Тогда 
			
			ОригинальныйСписок = Сообщение.Транспортировка.ДатаВремяПрибытия.Скопировать();
			МассивУдаляемыхСтрок = Новый Массив;
			
			Для Каждого Стр ИЗ ОригинальныйСписок Цикл
				Если Стр.Значение = "" Тогда
					МассивУдаляемыхСтрок.Добавить(Стр);
				КонецЕсли;
			КонецЦикла;
			
			Для Каждого УдаляемаяСтрока Из МассивУдаляемыхСтрок Цикл
				ОригинальныйСписок.Удалить(УдаляемаяСтрока);	
			КонецЦикла;
			
			Транспортировка = ОригинальныйСписок;
			для каждого Элемент из Транспортировка цикл
				Элемент.Представление = МодульОбъекта().ДатаXML_в_ДатаВремя1С(Элемент.Значение);
			конеццикла;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПеревестиСтатус(Статус) 
	
	СтатусВерхнийРегистр = ВРег(Статус);
	
	Если СтатусВерхнийРегистр = "CANCELED" Тогда
	    Статус = "Отмена";
	ИначеЕсли СтатусВерхнийРегистр = "REPLACE" Тогда
		Статус = "Замена";
	КонецЕсли;
	
	Возврат Статус;
	
КонецФункции

&НаСервере
Функция ПроверитьИзменениеДатыПоставки()
	
	РезультатПроверки = Новый Структура("Изменение", Ложь);
	
	Партнер = МодульОбъекта().ОпределитьПартнера(Новый Структура("ТипЗначения,ЮрФизЛицо","ЮрФизЛицо",Сообщение.Покупатель1С));
	_СтатусChangedДляИзмененнойДатыПоставкиORDRSP = МодульОбъекта().ПолучитьЗначениеСвойстваОбъектаEDI(Партнер, "СтатусChangedДляИзмененнойДатыПоставкиORDRSP");
	
	Если Не _СтатусChangedДляИзмененнойДатыПоставкиORDRSP = Истина Тогда
		Возврат РезультатПроверки;
	КонецЕсли;
	
	ВходящийЗаказ = МодульОбъекта().ПрочитатьСообщение(,Сообщение.Заказ1С,"ORDERS","Входящее");
	
	Если Не ЗначениеЗаполнено(ВходящийЗаказ) Тогда
		Возврат РезультатПроверки;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВходящийЗаказ.ДатаПоставки) И Не Сообщение.ДатаПоставки = ВходящийЗаказ.ДатаПоставки Тогда
		РезультатПроверки.Вставить("Изменение", Истина);
		РезультатПроверки.Вставить("Значение",  ВходящийЗаказ.ДатаПоставки);
	КонецЕсли;
	
	Возврат РезультатПроверки;
	
КонецФункции

//добавление / изменение / удаление данных
&НаСервере
Процедура ДобавитьТоварнуюПозицию(Строка)
	
	//заполняем товарные позиции (sscc) если:
	//1. (у партнера установлена галка отправлять данные по SSCC)
	//    И
	//2. (Сообщение не содержит транспортные упаковки (например, была отправка без sscc, а теперь переотправка))
	Если ОтправлятьДанныеОбУпаковках И Не СообщениеСодержитДанныеОбУпаковках Тогда
		 //И 
		 //( Не ЗначениеЗаполнено(Сообщение.СообщениеСсылка)
		 //  И Сообщение.Свойство("ТранспортныеУпаковки") 
		 //  И Сообщение.ТранспортныеУпаковки.Строки.Количество() = 0 ) Тогда
	    НоваяСтрока = ТоварныеПозиции.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		Если Не ЗначениеЗаполнено(НоваяСтрока.Номенклатура) Тогда
			НоваяСтрока.Номенклатура = Строка.Наименование;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьКнопкиДопРеквизитов(НастройкаФормы)
	
	КнопкиДопРеквизитов = НастройкаФормы.КнопкиДопРеквизитов;
	
	Если ЗначениеЗаполнено(КнопкиДопРеквизитов) Тогда
		КнопкиЕщеДопРеквизиты = Элементы.ЕщеДополнительныеРеквизиты;			
		
		Для каждого Кнопка Из КнопкиДопРеквизитов Цикл 				
			НоваяКоманда = ЭтаФорма.Команды.Добавить(Кнопка.ИмяКнопки);
			НоваяКоманда.Действие = "ЕщеДополнительныеРеквизиты_Нажатие";
			НоваяКоманда.Заголовок = Кнопка.Представление;
			
			НовыйЭлемент = Элементы.Добавить(Кнопка.ИмяКнопки, Тип("КнопкаФормы"),КнопкиЕщеДопРеквизиты);
			НовыйЭлемент.ИмяКоманды = Кнопка.ИмяКнопки; 			
		КонецЦикла; 	
	КонецЕсли;
	
	НастройкаФормы.КнопкиДопРеквизитов = Неопределено;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьКнопки(НастройкаКнопок)
	
	КнопкиДобавлены = Ложь;
	КоличествоДобавленныхКнопок = 0;
	счКнопок = 0;
	
	Если НЕ ТолькоПросмотрСообщения Тогда
		Для Каждого ОписаниеКнопки Из НастройкаКнопок Цикл
			
			КоличествоДобавленныхКнопок = КоличествоДобавленныхКнопок + 1;
			
			Если ОписаниеКнопки.ВидКнопки = "Кнопка" Тогда
				
				счКнопок = счКнопок + 1;
				
				// TODO это уже похоже на склад костылей
				// результат того, что в ПолучитьКнопкиКоманднойПанелиФормыСообщения не хватает параметров?
				
				Если (ОписаниеКнопки.Имя = "ЗагрузитьВозврат" 
					ИЛИ ОписаниеКнопки.Имя = "ЗагрузитьУведомлениеОбОтгрузкеВозврата") 
					И МодульОбъекта().НужноСоздатьКорректировкуИзВозврата(Сообщение) Тогда 
					
					ОписаниеКнопки.Представление = "Создать документ ""Корректировка реализации""";
					
				КонецЕсли;
				
				Если Сообщение.ТипСообщения = "ORDRSP" Тогда
					Если Сообщение.Статус = "Отклонен" Тогда
						Если ОписаниеКнопки.Имя = "ПоказатьРасхожденияЗаказаПокупателя" Тогда
							счКнопок = счКнопок - 1; //такая кнопка не в счет - т.к. её не показываем
							Продолжить;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
				Если Сообщение.ТипСообщения = "ORDERS" Тогда
					Если Сообщение.Свойство("ОбратныйЗаказ1С") И ЗначениеЗаполнено(Сообщение.ОбратныйЗаказ1С) Тогда
						Если ОписаниеКнопки.Имя = "СоздатьЗаказ" Тогда
							ОписаниеКнопки.Представление = "Загрузить входящий заказ";
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
				Если Сообщение.Направление = "Исходящее" 
					И ОписаниеКнопки.КнопкаПоУмолчанию Тогда 
					
					Если (МодульОбъекта().НужноПропуститьИсходящееСообщение(Сообщение)
						И НЕ ОписаниеКнопки.Имя = "ПропуститьИсходящееСообщение")
						ИЛИ (НЕ МодульОбъекта().НужноПропуститьИсходящееСообщение(Сообщение)
						И ОписаниеКнопки.Имя = "ПропуститьИсходящееСообщение") Тогда
						// не добавляем кнопку
						счКнопок = счКнопок - 1;
						Продолжить;
					КонецЕсли;
					
				КонецЕсли;
				
				НоваяКнопка = Неопределено;
				Выполнить("НоваяКнопка = Элементы.ОсновноеДействиеФормы" + счКнопок);
				
				НоваяКнопка.Заголовок = ОписаниеКнопки.Представление;
				НоваяКнопка.ИмяКоманды = ОписаниеКнопки.Имя; //команда формы будет называться так же как имя кнопки и как дальнейший ключ в модуле обхекта
				НоваяКнопка.Видимость = ОписаниеКнопки.Видимость;
				
				Если ОписаниеКнопки.КнопкаПоУмолчанию Тогда
					НоваяКнопка.КнопкаПоУмолчанию = Истина;
				КонецЕсли;
				
				//проверим, не был ли заказ отклонен, и отключим возможность создания документа по данному ORDERS
				_СообщениеСсылка = Сообщение.СообщениеСсылка;
				Если ВнешнееХранилище 
					И ТипЗнч(_СообщениеСсылка)<>Тип("COMОбъект")
					И ЗначениеЗаполнено(_СообщениеСсылка)
					Тогда
				КонецЕсли;
				
				Если ложь //(ВнешнееХранилище И СоединениеСХранилищем.ЗначениеЗаполнено(_СообщениеСсылка))
					Или (Не ВнешнееХранилище И ЗначениеЗаполнено(_СообщениеСсылка))
				Тогда
					Если _СообщениеСсылка.ОтклоненоОтправителем
						И ОписаниеКнопки.Имя = "СоздатьЗаказ" Тогда
						НоваяКнопка.Доступность = Ложь;
						НоваяКнопка.Текст = "Заказ отклонен отправителем";
					КонецЕсли;	
				КонецЕсли;
				
			ИначеЕсли ОписаниеКнопки.ВидКнопки = "Разделитель" Тогда
				//	 нет разделителей на уф     			//счКнопок=счКнопок+0;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
    //Уберем не потребовавшиеся кнопари
	Пока счКнопок < 7 Цикл
		счКнопок = счКнопок + 1;
		НоваяКнопка = Неопределено;
		Выполнить("НоваяКнопка = Элементы.ОсновноеДействиеФормы" + счКнопок);
		НоваяКнопка.Видимость = Ложь;
		//НоваяКнопка.ИмяКоманды = "";
	КонецЦикла;
	
	//Кнопки подменю ЕЩЕ сделаны иначе чем в ОФ, просто регулируем видимость
	ВидимостьДополнительныхДействийФормы(КнопкиДобавлены);	
	
КонецПроцедуры

//Кнопки подменю ЕЩЕ сделаны иначе чем в ОФ, просто регулируем видимость
&НаСервере
Процедура ВидимостьДополнительныхДействийФормы(КнопкиДобавлены)

	Если НЕ КнопкиДобавлены Тогда

		Если ЗначениеЗаполнено(Сообщение.Документ1С) Тогда
			Элементы.ПривязатьСообщение.Видимость=Ложь;
			
			Если Сообщение.Направление = "Входящее" Тогда
				Элементы.ОтвязатьДокумент.Видимость = Истина;
				Если Сообщение.ТипСообщения = "ORDERS" 
					ИЛИ Сообщение.ТипСообщения = "RETANN"
					ИЛИ Сообщение.ТипСообщения = "RETDES" Тогда
					Элементы.ПерезаполнитьДокумент.Видимость = Истина;
				КонецЕсли;
			КонецЕсли;	
			
		Иначе
			Элементы.ПерезаполнитьДокумент.Видимость=Ложь;
			Элементы.ОтвязатьДокумент.Видимость=Ложь;
			Если Сообщение.ТипСообщения = "ORDERS" 
				ИЛИ Сообщение.ТипСообщения = "DESADV"
				ИЛИ Сообщение.ТипСообщения = "RETANN"
				ИЛИ Сообщение.ТипСообщения = "RETDES" Тогда
				Если Сообщение.Направление = "Входящее" Тогда
					Элементы.ПривязатьСообщение.Видимость = Истина;
				КонецЕсли;
			КонецЕсли;
				
		КонецЕсли;
		
		Если МодульОбъекта().ПолучитьЗначениеСвойстваОбъектаEDI(Сообщение.Партнер,"ORDRSP")=Истина 
			И ((Сообщение.ТипСообщения = "ORDERS" 
			И Сообщение.Направление = "Входящее" 
			И Сообщение.СтатусСсылки <> "Отклонен") 
			ИЛИ (Сообщение.ТипСообщения = "ORDRSP" 
			И Сообщение.Направление = "Исходящее" 
			И Сообщение.Статус <> "Отклонен")) Тогда
			
			Элементы.ОтклонитьЗаказПолностью.Видимость=Истина;
			
		КонецЕсли;
		
		Если 	(Сообщение.ТипСообщения = "RETANN" И Сообщение.Направление = "Входящее" И Сообщение.СтатусСсылки <> "Отклонен" И МодульОбъекта().ПолучитьЗначениеСвойстваОбъектаEDI(Сообщение.Партнер,"RETINS")=Истина) 
			ИЛИ (Сообщение.ТипСообщения = "RETINS" И Сообщение.Направление = "Исходящее" И Сообщение.Статус <> "Отклонен")
			ИЛИ (Сообщение.ТипСообщения = "RETDES" И Сообщение.Направление = "Входящее" И Сообщение.СтатусСсылки <> "Отклонен" И МодульОбъекта().ПолучитьЗначениеСвойстваОбъектаEDI(Сообщение.Партнер,"RETREC")=Истина) 
			ИЛИ (Сообщение.ТипСообщения = "RETREC" И Сообщение.Направление = "Исходящее" И Сообщение.Статус <> "Отклонен") Тогда
						
			Элементы.ОтклонитьВозвратПолностью.Видимость=Истина;
			
		КонецЕсли;    		
		
		
		Если МодульОбъекта().МожноПрекратитьОбработкуВходящегоСообщения(Сообщение) Тогда
			
			Элементы.ПрекратитьОбработкуВходящегоСообщения.Видимость = Истина;
			
		КонецЕсли;
		
		Если МодульОбъекта().МожноПрекратитьОбработкуИсходящегоСообщения(Сообщение) Тогда
			
			Элементы.ПрекратитьОбработкуИсходящегоСообщения.Видимость = Истина;
			
		КонецЕсли;

		Элементы.ОткрытьФайл.Видимость = Истина;
		Элементы.НайтиСвязанныеСообщения.Видимость = Истина;
		
		Если ЗначениеЗаполнено(Сообщение.СообщениеСсылка) Тогда
			
			Если Сообщение.Направление = "Исходящее" Тогда
				
				Если Найти("\DESADV\INVOIC\",Сообщение.ТипСообщения)>0 Тогда
					Если ЗначениеЗаполнено(Сообщение.Получатель1С) Тогда
						ПоказыватьКнопкуПереотправкиАлко = Ложь;
						Партнер = МодульОбъекта().ПолучитьЭлементСправочника("Партнеры",Сообщение.Получатель1С);
						Если Сообщение.ТипСообщения = "DESADV" И Партнер.Свойство("ОтправлятьALCRPT") И Партнер.ОтправлятьALCRPT = Истина Тогда
							ПоказыватьКнопкуПереотправкиАлко = Истина;
						ИначеЕсли Сообщение.ТипСообщения = "INVOIC" И Партнер.Свойство("ОтправлятьALCRPTсINVOIC") И Партнер.ОтправлятьALCRPTсINVOIC = Истина Тогда
							ПоказыватьКнопкуПереотправкиАлко = Истина;
						КонецЕсли;
						Если ПоказыватьКнопкуПереотправкиАлко Тогда
							Элементы.ПереотправитьALCRPT.Видимость=Истина;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
						
				Если Найти("\ORDRSP\DESADV\INVOIC\",Сообщение.ТипСообщения)>0 Тогда
					Элементы.ПереотправитьСообщение.Видимость=Истина;
				КонецЕсли;
				
			КонецЕсли;
		
			Элементы.УдалитьСообщение.Видимость=Истина;
		Иначе
			
			Если Сообщение.ТипСообщения = "INVOIC" и Сообщение.СуммаВсего = 0 и МодульОбъекта().ЕстьКоррИспрСФ Тогда
		
				ДокОснование = МодульОбъекта().ПолучитьЗначениеСвойстваОбъектаМетаданныхПоИмени("ДокументОснование", Сообщение.Документ1С);
				Если ТипЗнч(ДокОснование) = Тип(МодульОбъекта().ПолучитьТипЗначенияОбъекта("ИсходящийКСФ")) Тогда
					Элементы.ПрекратитьОбработкуНулевойСФ.Видимость = Истина;
				КонецЕсли;
				
			КонецЕсли;

		КонецЕсли;
		
		ПечатныеФормы_Структура = МодульОбъекта().КэшПечатныхФормСообщений();

		Если ТипЗнч(ПечатныеФормы_Структура) = Тип("Структура") И ПечатныеФормы_Структура.Свойство("ПечатнаяФорма_" + Сообщение.ТипСообщения) Тогда
			Элементы.НапечататьСообщение.Видимость = Истина;
		КонецЕсли; 
		
	КонецЕсли;

КонецПроцедуры // ВидимостьДополнительныхДейсвийФормы()

&НаСервере
Процедура ПроверитьЗаполнениеОбязательныхПолей()
	
	ВосстановитьТаблицыЗначенийПроизвольныхРеквизитовИзХранилища();
	
	Для Каждого Стр Из НастройкаФормы.ОбязательныеПоля Цикл
		
		Если НЕ ЗначениеЗаполнено(Стр.ИмяТабличнойЧасти) Тогда
			
			Если ЗначениеЗаполнено(ЭтаФорма[Стр.ИмяПоля]) Тогда
				
			//	ПометкаПоля = ЭлементыФормы["Пометка"+Стр.ИмяПоля];
			//
			//	ПометкаПоля.Картинка = ЭлементыФормы.КартинкаГалочкаМаленькая.Картинка;
			//	ПометкаПоля.Видимость = Истина;
			
			//подкрасим в зеленый лосось :Р
				Поле = Элементы[Стр.ИмяПоля];
			
				//Поле.Картинка = ЭлементыФормы.КартинкаГалочкаМаленькая.Картинка; //(маленькая зеленая галочка)
				//Не уверен что будем добавять пометки галочки как в ОФ т.к. они слопают очень много места, особенно в такси! пока просто не будем ничего устанавливать
			КонецЕсли;
			
			Элементы[Стр.ИмяПоля].АвтоОтметкаНезаполненного = Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ВывестиПанельИнформации(Текст,Вид = Неопределено)
	
	// Возможные виды:
	//	- Хорошо
	//	- Плохо
	//	- Нейтрально
	
	Если Вид = "Хорошо" Тогда
		
		//Элементы.ПанельИнформации.ЦветФона		= WebЦвета.Роса;
		Элементы.ПанельИнформации.ЦветТекста		= WebЦвета.ЦианТемный;
		//Элементы.ПанельИнформации.ЦветРамки	= WebЦвета.ЗеленыйЛес;
		
		//ЭлементыФормы.ИконкаПанелиИнформации.Картинка = ПолучитьКартинкуEDI("ЭлементФормы","КартинкаГалочка");
		
	ИначеЕсли Вид = "Плохо" Тогда
		
		//Элементы.ПанельИнформации.ЦветФона		= WebЦвета.ТусклоРозовый;
		Элементы.ПанельИнформации.ЦветТекста		= WebЦвета.Киноварь;
		//Элементы.ПанельИнформации.ЦветРамки	= WebЦвета.Шоколадный;
		
		//ЭлементыФормы.ИконкаПанелиИнформации.Картинка = ПолучитьКартинкуEDI("ЭлементФормы","КартинкаИсправитьОшибки");
		
	Иначе	
		
		//Элементы.ПанельИнформации.ЦветФона		= WebЦвета.СлоноваяКость;
		Элементы.ПанельИнформации.ЦветТекста		= WebЦвета.ТемноОранжевый;
		//Элементы.ПанельИнформации.ЦветРамки	= WebЦвета.РыжеватоКоричневый;
		
		//Элементы.ИконкаПанелиИнформации.Картинка = ПолучитьКартинкуEDI("ЭлементФормы","КартинкаИнформация");
		
	КонецЕсли;
		
	Элементы.ПанельИнформации.Заголовок = СокрЛП(Текст);
				 
КонецПроцедуры

&НаСервере
Функция ПодготовитьСтруктуруИзмененныхРеквизитов()
	
	ИзмененныеРеквизиты = Новый Структура;
	
	Для Каждого Реквизит Из ТаблицаИзмененныхРеквизитов Цикл
		ИзмененныеРеквизиты.Вставить(Реквизит.Наименование, Реквизит.Значение); 
	КонецЦикла;
	
	Возврат ИзмененныеРеквизиты;
	
КонецФункции

//пробегает по таблице значений Дубли и смотрит, были ли созданы по этим сообщениям документы
//если такой документ был создан, и в единственном экземпляре, то функция перепривяжет его к последнему сообщению
//а все остальные пометит как уже загруженные. В списке документов все равно будет светиться только одно из них
&НаСервере
Функция ПерепривязатьСообщенияИнтерактивно()
	
	ВосстановитьТаблицыЗначенийПроизвольныхРеквизитовИзХранилища();
	
	//вызвать магическую заполнялку
	МодульОбъекта().МагическиОбработатьДублиORDERS(Сообщение,"НоваяВерсия");
	//нужен ли именно этот статус? Или ставить "ВходящийОжидаетУстановкиСоответствий"?
	
	_Сообщение = МодульОбъекта().ПрочитатьСообщение(Сообщение.СообщениеСсылка,,Сообщение.ТипСообщения,"Входящее");
	
	Если ЗначениеЗаполнено(_Сообщение) Тогда
		
		//МодульИнтеграции_Новый.КонвертироватьСообщениеEDIв1С(_Сообщение);
		
		_ТекущиеТовары = Товары.Выгрузить();
		//_Сообщение.СообщениеСсылка = _СообщениеСсылка;
		//Сообщение=ЗначениеИзСтрокиВнутр(ЗначениеВСтрокуВнутр(_Сообщение));//копия сообщения
		Сообщение=_Сообщение;//копия сообщения
		//Сообщение.Документ1С=_СообщениеСсылка.Документ;
		Сообщение.Документ1С=Сообщение.ДокументСсылка;
		
		//ПриСозданииНаСервере("","");  //видимо ранее здесь инициировалось перезаполнение формы. Это лишнее.
		
		//костыль - до полной переработки данной формы
		//скопировать бы товары заново
		Товары.Загрузить(_ТекущиеТовары);
		
		ПоместитьТаблицыЗначенийПроизвольныхРеквизитовВХранилище();
		УничтожитьТаблицыЗначенийПроизвольныхРеквизитов();
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция ПерезаполнитьДокумент1С()
	
	ПервоначальнаяЗагрузкаСообщения = Ложь;
	//в случае, если документов несколько, то мы можем только пометить на удаление старые, и создать вместо них новые.
	
	Если ЗначениеЗаполнено(Документ1С) Тогда
		ТабВиртуальныхСообщений = МодульОбъекта().ПолучитьВиртуальныеСвязанныеORDERS(Документ1С);
		Если ТабВиртуальныхСообщений.Количество()>1 Тогда
			
			//ТекстВопроса = "Следующие документы будут помечены на удаление:";
			//Для Каждого Стр Из ТабВиртуальныхСообщений Цикл
			//	ТекстВопроса = ТекстВопроса+Символы.ПС+Стр.Документ;
			//КонецЦикла;	
			//ТекстВопроса = ТекстВопроса+Символы.ПС+"Вместо них будут созданы новые документы. Продолжить?";
			//
			Если ложь тогда //Вопрос(ТекстВопроса,РежимДиалогаВопрос.ДаНет,0,КодВозвратаДиалога.Нет,"Контур.EDI") = КодВозвратаДиалога.Нет Тогда
				
				Возврат Ложь;
			Иначе
				//как будем с этим жить?
				//обязательно надо учесть, что текущее сообщение тоже будет удалено.
				НачатьТранзакцию();
				Попытка
					
					Для Каждого Стр Из ТабВиртуальныхСообщений Цикл
						Стр.Документ.ПолучитьОбъект().УстановитьПометкуУдаления(Истина);
						СообщениеОбъект_ORDERS = МодульОбъекта().ПолучитьОбъектСообщения(Стр.ORDERS);
						Если Стр.ЭтоТекущийДокумент Тогда
							СообщениеОбъект_ORDERS.Документ = Неопределено;
							//статус?
							МодульОбъекта().СохранитьОбъектСообщения(СообщениеОбъект_ORDERS);
						Иначе
							СообщениеОбъект_ORDERS.Удалить();
						КонецЕсли;
						
						//заодно уберем ORDRSP, чтобы его переотправить потом с новыми данными
						Если ЗначениеЗаполнено(Стр.ORDRSP) Тогда
							СообщениеОбъект_ORDRSP = МодульОбъекта().ПолучитьОбъектСообщения(Стр.ORDRSP);
							СообщениеОбъект_ORDRSP.Удалить();
						КонецЕсли;
						
					КонецЦикла;	
					Документ1С = Неопределено;
					Сообщение.Документ1С = Неопределено;
					Сообщение.ДокументСсылка = Неопределено;
					
					ПервоначальнаяЗагрузкаСообщения = Истина;//костыль
					
				    Если ТранзакцияАктивна() Тогда
				        ЗафиксироватьТранзакцию();
					КонецЕсли;
					
				Исключение
					
				    Если ТранзакцияАктивна() Тогда
				        ОтменитьТранзакцию();
					КонецЕсли;
					
					ТекстОшибки = "Не удалось перезаполнить документ 1С - " + ОписаниеОшибки();
					МодульОбъекта().ЗаписьЖурналаРегистрации_КонтурEDI(УровеньЖурналаРегистрации.Ошибка, ТекстОшибки);
					ВызватьИсключение ТекстОшибки;				
					
				КонецПопытки;
				
			КонецЕсли;
		КонецЕсли;	
	Иначе
		ПервоначальнаяЗагрузкаСообщения = Истина;
	КонецЕсли;	
			
	ВосстановитьТаблицыЗначенийПроизвольныхРеквизитовИзХранилища();
	
	ПеренестиПоля1СвСообщение(Сообщение);
	
	Если НЕ ЗначениеЗаполнено(Сообщение.ДокументСсылка) Тогда
		Сообщение.ДокументСсылка = Сообщение.Документ1С;
	КонецЕсли;
	
	Результат = МодульОбъекта().ПринятьВходящееСообщение(Сообщение,ПервоначальнаяЗагрузкаСообщения);
	
	ПоместитьТаблицыЗначенийПроизвольныхРеквизитовВХранилище();
	УничтожитьТаблицыЗначенийПроизвольныхРеквизитов();

	Если Результат.Успешно Тогда
		
		Если Сообщение.ТипСообщения = "ORDERS" Тогда
			//костыль для версионности ORDERS
			МодульОбъекта().УстановитьСтатусСообщения(Сообщение.СообщениеСсылка,Сообщение.ДокументСсылка,Сообщение.ТипСообщения,"ВходящийЗагружен");
		КонецЕсли;
		
		Возврат Истина;
	Иначе
		Сп=Новый СообщениеПользователю;
		Сп.Текст="Не удалось перезаполнить документ!";
		Сп.Сообщить();
		Возврат Ложь;
	КонецЕсли;	
	
КонецФункции	

&НаСервере
Процедура УничтожитьТаблицыЗначенийПроизвольныхРеквизитов()
	
	НастройкаСсылокИсправленияОшибок     = Неопределено;  
	НастройкаФормы.КнопкиКоманднойПанели = Неопределено;
	НастройкаФормы.КнопкиДопРеквизитов 	 = Неопределено;
	НастройкаФормы.ОбязательныеПоля		 = Неопределено;
	Сообщение.Товары 					 = Неопределено;
	Если ТипЗнч(Сообщение) = Тип("Структура") И Сообщение.Свойство("ТранспортныеУпаковки") Тогда
		Сообщение.ТранспортныеУпаковки = неопределено;
	КонецЕсли;
	Если ТипЗнч(Сообщение) = Тип("Структура") И Сообщение.Свойство("Транспортировка") Тогда
		Сообщение.Транспортировка.ДатаВремяПрибытия = неопределено;
	КонецЕсли;
	Если ТипЗнч(Сообщение) = Тип("Структура") И Сообщение.Свойство("ТочкиСамовывоза") Тогда
		Сообщение.ТочкиСамовывоза = неопределено;
	КонецЕсли;
	Если ТипЗнч(Сообщение) = Тип("Структура") И Сообщение.Свойство("Фасовка") Тогда
		Сообщение.Фасовка = неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПоместитьТаблицыЗначенийПроизвольныхРеквизитовВХранилище()
	
	Отказ = Истина;
	
	//сохраним только значащие реквизиты
	СтруктураВременногоХранилища = новый Структура;
	Если НастройкаФормы.КнопкиКоманднойПанели<>Неопределено Тогда 
		СтруктураВременногоХранилища.Вставить("НастройкаФормыКнопкиКоманднойПанели",НастройкаФормы.КнопкиКоманднойПанели);
		Отказ = Ложь;
	КонецЕсли;
	
	Если НастройкаФормы.КнопкиДопРеквизитов<>Неопределено Тогда 
		СтруктураВременногоХранилища.Вставить("НастройкаФормыКнопкиДопРеквизитов",НастройкаФормы.КнопкиДопРеквизитов);
		Отказ = Ложь;
	КонецЕсли;

	
	Если НастройкаФормы.ОбязательныеПоля<>Неопределено Тогда 
		СтруктураВременногоХранилища.Вставить("НастройкаФормыОбязательныеПоля",НастройкаФормы.ОбязательныеПоля);
		Отказ = Ложь;
	КонецЕсли;
	
	Если Сообщение.Свойство("ТранспортныеУпаковки") Тогда
		СтруктураВременногоХранилища.Вставить("СообщениеТранспортныеУпаковки", Сообщение.ТранспортныеУпаковки);
	КонецЕсли;
	
	Если Сообщение.Свойство("ТочкиСамовывоза") И Сообщение.ТочкиСамовывоза<>Неопределено Тогда 
		СтруктураВременногоХранилища.Вставить("СообщениеТочкиСамовывоза",Сообщение.ТочкиСамовывоза);
		Отказ = Ложь;
	КонецЕсли;
	
	Если Сообщение.Свойство("Фасовка") И НЕ Сообщение.Фасовка = Неопределено Тогда 
		СтруктураВременногоХранилища.Вставить("СообщениеФасовка", Сообщение.Фасовка);
		Отказ = Ложь;
	КонецЕсли;
	
	Если Сообщение.Товары<>Неопределено Тогда 
		СтруктураВременногоХранилища.Вставить("СообщениеТовары",Сообщение.Товары);
		Отказ = Ложь;
	КонецЕсли;
	
	Если НастройкаСсылокИсправленияОшибок<>Неопределено Тогда 
		СтруктураВременногоХранилища.Вставить("НастройкаСсылокИсправленияОшибок",НастройкаСсылокИсправленияОшибок);
		Отказ = Ложь;
	КонецЕсли;
	
	Если не Отказ Тогда 
		АдресВХПроизвольныеРеквизиты = ПоместитьВоВременноеХранилище(СтруктураВременногоХранилища,ЭтаФорма.УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВосстановитьТаблицыЗначенийПроизвольныхРеквизитовИзХранилища()
	
	//извлечем из ВХ если не инициализированы реквизиты и при этом там хоть что-то есть
	
	Если НЕ ПустаяСтрока(АдресВХПроизвольныеРеквизиты)
		И (НастройкаСсылокИсправленияОшибок 	 = Неопределено
		ИЛИ НастройкаФормы.ОбязательныеПоля 	 = Неопределено
		ИЛИ НастройкаФормы.КнопкиКоманднойПанели = Неопределено
		ИЛИ НастройкаФормы.КнопкиДопРеквизитов 	 = Неопределено)
		ИЛИ Сообщение.Товары					 = Неопределено
		ИЛИ (Сообщение.Свойство("Фасовка") И Сообщение.Фасовка = Неопределено)  
		ИЛИ (Сообщение.Свойство("ТранспортныеУпаковки") И Сообщение.ТранспортныеУпаковки = Неопределено)  
		ИЛИ (Сообщение.Свойство("ТочкиСамовывоза") И Сообщение.ТочкиСамовывоза = Неопределено) 
		Тогда 
		
		СтруктураВременногоХранилища = ПолучитьИзВременногоХранилища(АдресВХПроизвольныеРеквизиты);
		Если СтруктураВременногоХранилища = Неопределено Тогда 
			ВызватьИсключение "Сервер потерял временное хранилище (откройте форму заново)";
		КонецЕсли;
		
		НастройкаСсылокИсправленияОшибок		= СтруктураВременногоХранилища.НастройкаСсылокИсправленияОшибок;
		НастройкаФормы.КнопкиКоманднойПанели	= СтруктураВременногоХранилища.НастройкаФормыКнопкиКоманднойПанели;
		НастройкаФормы.КнопкиДопРеквизитов		= СтруктураВременногоХранилища.НастройкаФормыКнопкиДопРеквизитов;
		НастройкаФормы.ОбязательныеПоля			= СтруктураВременногоХранилища.НастройкаФормыОбязательныеПоля;
		
		Сообщение.Товары						= СтруктураВременногоХранилища.СообщениеТовары; 		
		ПриВосстановленииТоваровСообщения(); 
		
		Если Сообщение.Свойство("ТранспортныеУпаковки") Тогда
			Сообщение.ТранспортныеУпаковки = РеквизитФормыВЗначение("ТранспортныеУпаковки");
		КонецЕсли;
		
		Если ТипСообщения = "ORDERS" ИЛИ ТипСообщения = "ORDRSP" Тогда
			Если Сообщение.Свойство("ТранспортныеУпаковки") Тогда
				Сообщение.ТранспортныеУпаковки = СтруктураВременногоХранилища.СообщениеТранспортныеУпаковки;
			КонецЕсли;
		КонецЕсли;
		
		Если Сообщение.Свойство("ТочкиСамовывоза") И СтруктураВременногоХранилища.Свойство("СообщениеТочкиСамовывоза") Тогда //могли загрузить сообщение предыдущей версией модуля тогда в хранилище будет пусто
			Сообщение.ТочкиСамовывоза = СтруктураВременногоХранилища.СообщениеТочкиСамовывоза;
		КонецЕсли;
		
		Если Сообщение.Свойство("Фасовка") И СтруктураВременногоХранилища.Свойство("СообщениеФасовка") Тогда //могли загрузить сообщение предыдущей версией модуля тогда в хранилище будет пусто
			Сообщение.Фасовка = СтруктураВременногоХранилища.СообщениеФасовка;
		КонецЕсли;
		
		Если Сообщение.Свойство("Транспортировка") Тогда
			Сообщение.Транспортировка.ДатаВремяПрибытия = Транспортировка;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриВосстановленииТоваровСообщения()
		
	Если Сообщение.ТипСообщения = "ORDRSP" Тогда	
		
		ИндексСтроки = 0;
		
		Для каждого Стр Из Товары Цикл 
			
			СтрокаСообщения = Сообщение.Товары.Получить(ИндексСтроки);
			
			СтрокаСообщения.Комментарий = Стр.Комментарий; 
			
			ИндексСтроки = ИндексСтроки + 1;	
			
		КонецЦикла;	
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура КнопкаВыборВерсииНажатие(ИмяКоманды)
	
	Состояние("Ищу версии сообщений",50,,БиблиотекаКартинок.НайтиВСодержании);
	
	СписокКнопок = ПолучитьСписокИменКнопокДублей();	
	ПараметрыОбработчика = Неопределено;
	
	МодульОбъектаКлиент().ВыбратьИзМеню_КонтурEDI("ОбработчикВыбораВерсии", ЭтаФорма, ПараметрыОбработчика, СписокКнопок, Элементы.СуммаВсего);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикВыбораВерсии(ВыбраннаяВерсия = Неопределено,ДопПараметр = Неопределено)Экспорт
	
	Если ВыбраннаяВерсия<>Неопределено Тогда
		
		Если Лев(ВыбраннаяВерсия.Значение,6)="Версия" Тогда
			
			Состояние("Собираю выбранное сообщение",50,,БиблиотекаКартинок.СформироватьОтчет);
			//откроем карточку выбранной версии
			ГУИДСообщения=СтрЗаменить(Сред(ВыбраннаяВерсия.Значение,8),"_","-");
			ОбработчикВыбораВерсииСервер(ГУИДСообщения);
			ПриОткрытии(ложь);
			
		ИначеЕсли ВыбраннаяВерсия.Значение = "СравнитьВерсии" Тогда
			
			Состояние("Сравниваю сообщеиня",50,,БиблиотекаКартинок.СформироватьОтчет);
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("ВидРасхождения", 		"РасхожденияВерсийЗаказа");
			ПараметрыФормы.Вставить("Заказ",				Сообщение.СообщениеСсылка);
			ПараметрыФормы.Вставить("Параметры",			Неопределено);
			
			МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Сервис_ПросмотрРасхожденийУправляемая", , ПараметрыФормы, ЭтаФорма, , , , РежимОткрытияОкнаФормы.Независимый);
			
		КонецЕсли;
	КонецЕсли;	

КонецПроцедуры // ОбработчикВыбораВерсии()

&НаСервере
Процедура ОбработчикВыбораВерсииСервер(ГУИДСообщения)
	
	_СообщениеСсылка=Справочники.КонтурEDI_Сообщения.ПолучитьСсылку(Новый УникальныйИдентификатор(ГУИДСообщения));
	_Сообщение = МодульОбъекта().ПрочитатьСообщение(_СообщениеСсылка,,Сообщение.ТипСообщения,"Входящее");
	
	Если ЗначениеЗаполнено(_Сообщение) Тогда
		
		МодульОбъекта().КонвертироватьСообщениеEDIв1С(_Сообщение);    //взято из оф - а в тут наверно можно выпилить
		
		_Сообщение.СообщениеСсылка = _СообщениеСсылка;
		Сообщение =_Сообщение;
		Если ВнешнееХранилище Тогда
			Если ЗначениеЗаполнено(_Сообщение.ДокументСсылка) Тогда
				Сообщение.Документ1С=_Сообщение.ДокументСсылка;
			Иначе
				Сообщение.Документ1С=Неопределено;
			КонецЕсли;
		Иначе
			Сообщение.Документ1С=_СообщениеСсылка.Документ;
		КонецЕсли;	
		
		Параметры.СообщениеСсылка	= _СообщениеСсылка; 
		Параметры.Документ1С		= _СообщениеСсылка.Документ;
		
		ПроизвестиПервоначальноеЗаполнениеПолейНаФорме();
		ПоместитьТаблицыЗначенийПроизвольныхРеквизитовВХранилище();	
		УничтожитьТаблицыЗначенийПроизвольныхРеквизитов();
		ДозаполнитьАртикулКодПолноеНаименованиеТЧТовары();
		
	КонецЕсли;
	
КонецПроцедуры // ()

&НаСервере
Функция ПолучитьСписокИменКнопокДублей()
	Дубли=МодульОбъекта().ПолучитьДублирующиеСообщения(Сообщение.СообщениеСсылка);
	
	СписокКнопок = новый СписокЗначений;
	
	//оживить
	Для Каждого Стр Из Дубли Цикл
		//добавить кнопку
		ГУИДСообщения=СтрЗаменить(Стр.УникальныйИдентификатор,"-","_");
		
		_ИмяКнопки = "Версия "+Стр.Номерстроки+" ("+Стр.ДатаДокумента+")"+?(Лев(Стр.ТипСообщения,1)="#"," (архив)","");
		
		Нов=СписокКнопок.Добавить("Версия_"+ГУИДСообщения,_ИмяКнопки,,);
		Если Стр.Ссылка=Сообщение.СообщениеСсылка Тогда
			нов.Картинка = БиблиотекаКартинок.СформироватьОтчет;
		КонецЕсли;
	КонецЦикла;
	
	Если Дубли.Количество()>1 Тогда 
		//Добавим кнопку показать расхождения
		Нов=СписокКнопок.Добавить("СравнитьВерсии",	"Сравнить версии",,);
		Если Стр.Ссылка=Сообщение.СообщениеСсылка Тогда
			Нов.Картинка=БиблиотекаКартинок.Отчет;
		КонецЕсли;
	КонецЕсли;
	
	Возврат СписокКнопок;		
КонецФункции

&НаКлиенте
Процедура КнопкаДействияФормыВыборВерсииНажатие(Команда)
	
	ПерепривязатьСообщенияИнтерактивно();  
	
	//далее спросим о действии с сообщением
	СписокВыбора = Новый СписокЗначений;
	СписокВыбора.Добавить("Перезаполнить заказ");
	СписокВыбора.Добавить("Игнорировать новую версию");
	
	ПараметрыОбработчика = Неопределено;
	
	МодульОбъектаКлиент().ВыбратьИзСписка_КонтурEDI("ОбработчикВыбораДействияНадДокументом1С", ЭтаФорма, ПараметрыОбработчика, СписокВыбора, Элементы.Пробел2);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикВыбораДействияНадДокументом1С(ВыбранноеЗначение=неопределено,ДополнительныйПараметр=Неопределено) Экспорт
	
	Если ВыбранноеЗначение = Неопределено Тогда
		
		//пользователь еще подумает
		
	ИначеЕсли ВыбранноеЗначение.Значение = "Перезаполнить заказ" Тогда
				
		ПерезаполнитьДокумент(Команды.Найти("ПерезаполнитьДокумент"));
		
	ИначеЕсли ВыбранноеЗначение.Значение = "Игнорировать новую версию" Тогда
		
		ОбработатьИгнорированиеНовойВерсии();
		ЗакрытиеРазрешено=Истина;
		ЭтаФорма.Закрыть(неопределено);
		
	Иначе
		//пользователь еще подумает
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИгнорированиеНовойВерсии()
	ВосстановитьТаблицыЗначенийПроизвольныхРеквизитовИзХранилища();
	ВиртуальныеORDERS = МодульОбъекта().ПолучитьВиртуальныеСвязанныеORDERS(Сообщение.ДокументСсылка);
	Для Каждого Стр Из ВиртуальныеORDERS Цикл
		МодульОбъекта().УстановитьСтатусСообщения(Стр.ORDERS,Стр.Документ,"ORDERS","ВходящийЗагружен");
	КонецЦикла;	
	
	УничтожитьТаблицыЗначенийПроизвольныхРеквизитов();
	
КонецПроцедуры

 &НаСервере
Процедура ДобавитьПроизвольныеПараметрыФормы(ПараметрыДействия)
	 
	 ПроизвольныеПараметры = Параметры.ПроизвольныеПараметры;
	 
	 Если НЕ ПроизвольныеПараметры = Неопределено Тогда
		 
		 Для Каждого ПроизвольныйПараметр Из ПроизвольныеПараметры Цикл
			 ПараметрыДействия.Вставить(ПроизвольныйПараметр.Ключ,	ПроизвольныйПараметр.Значение);
		 КонецЦикла;
		 
	 КонецЕсли;
 
 КонецПроцедуры // ДобавитьПроизвольныеПараметрыФормы() 

//Обработчики команд нажатия кнопок - реализация иная чем в ОФ в связи с тем что в обработчик передается имя команды а не элемент формы
//so, создадим все возможные команды которые могут случиться в форме и в них пропишем обработчики а выводить или нет саму кнопку будем решать при создании на сервере
//точно так же будем придумыва
&НаКлиенте
Процедура КнопкаДействияФормыНажатие_Клиент(ИмяКоманды)
	
	РезультатДействия = КнопкаДействияФормыНажатие(ИмяКоманды);
	
	//для печати паллетных листов
	Если ТипЗнч(РезультатДействия) = Тип("ТабличныйДокумент") Тогда
		
		РезультатДействия.Показать();
		
	ИначеЕсли РезультатДействия <> Неопределено 
		и РезультатДействия.ЗакрытьФорму
		и ЭтаФорма.Открыта() Тогда
		
		ЗакрытиеРазрешено = Истина;
		ЭтаФорма.Закрыть(Параметры.СообщениеСсылка);
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция КнопкаДействияФормыНажатие(ИмяКоманды)
	
	РезультатДействия = Новый Структура("ЗакрытьФорму", Ложь);
	
	СвойстваКнопки = ПолучитьСвойстваКнопки(ИмяКоманды); 
	
	ПроверкаПолей = (НЕ СвойстваКнопки = Неопределено);
	
	Если ПроверкаПолей И СвойстваКнопки.ПроверятьПоля Тогда
		
		Если НЕ ПроверитьЗаполнениеПолей() Тогда
			
			ОткрытьПанельОшибокСервер();  			
			ПроверкаПолей = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПроверкаПолей Тогда 		
		
		ВосстановитьТаблицыЗначенийПроизвольныхРеквизитовИзХранилища();
		
		Если СвойстваКнопки.СохранятьСоответствия Тогда
			
			ПеренестиПоля1СвСообщение(Сообщение); 
			
		КонецЕсли; 		
		
		Если ОтправлятьДанныеОбУпаковках 
			И (ИмяКоманды = "ОтправитьУведомлениеОбОтгрузке" ИЛИ ИмяКоманды = "Сохранить" ИЛИ ИмяКоманды = "ОтправитьСчетФактуру") Тогда
			
			ПромаркироватьУпаковкиSSCCКодом();                                                  //присваиваем SSCC-коды и присваиваем имена палетам (с SSCC-кодом)
			УстановитьНомерПоследнейУпаковки(); 				   								//сохраняем последний SSCC-код
			Сообщение.ТранспортныеУпаковки = РеквизитФормыВЗначение("ТранспортныеУпаковки"); 	//записываем в сообщение данные о транспортных упаковках			
			//SSCC-кода юр.лица и фиксируем код заключительной упаковки сообщения, как последний   			
		КонецЕсли; 						
				
		Действие = ОпределитьДействиеПоОсновнойКнопке(ИмяКоманды);    
		
		Если Действие = "ПерезаполнитьДокумент1С" Тогда
			
			Если ПерезаполнитьДокумент1С() Тогда 				
				РезультатДействия.ЗакрытьФорму = Истина;  								
			КонецЕсли;  
			
		ИначеЕсли Действие = "ВыбратьТекущуюВерсию" Тогда
			
			ПеренестиПоля1СвСообщение(Сообщение);
			МодульОбъекта().СохранитьСообщение(Сообщение);
			
			ПерепривязатьСообщенияИнтерактивно();
			
			//теперь перезаполним заказ - уже спршивали и пользователь так решил
			//в этот момент документ/документы уже должны быть привязаны к текущему сообщению, открытому в окне
			Если ПерезаполнитьДокумент1С() Тогда
				
				РезультатДействия.ЗакрытьФорму = Истина;
				
			КонецЕсли;
			
		ИначеЕсли Действие = "ОсновноеДействие" Тогда 
			
			РезультатДействия = МодульОбъекта().КнопкаФормыСообщенияНажатие(ИмяКоманды,Сообщение, ПараметрыДействия); //КонвертироватьПарамтрыФормыВСтруктуруСервер());
			
			Если РезультатДействия.СозданДокумент = Истина Тогда
				
				Документ1С = РезультатДействия.СсылкаНаДокумент;
				
			КонецЕсли;  			
			
		КонецЕсли;
		
		ПоместитьТаблицыЗначенийПроизвольныхРеквизитовВХранилище();
		УничтожитьТаблицыЗначенийПроизвольныхРеквизитов(); 
		
	КонецЕсли;
	
	Возврат РезультатДействия;
	
КонецФункции

&НаСервере
Функция ОпределитьДействиеПоОсновнойКнопке(Знач ИмяКоманды)
	
	Действие = "ОсновноеДействие";
	
	Элемент = ОпределитьКнопкуЭлементФормыПоИмениКоманды(ИмяКоманды);
	
	Если ИмяКоманды = "СоздатьЗаказ" 
		И Элемент <> Неопределено
		И Элемент.Заголовок = "Перезаполнить заказ" Тогда
		
		Действие = "ПерезаполнитьДокумент1С";
		
	КонецЕсли;
	
	Если ИмяКоманды = "СоздатьЗаказ"
		И Элемент <> Неопределено
		И Элемент.Заголовок = "Выбрать текущую версию" Тогда
		
		Действие = "ВыбратьТекущуюВерсию";
		
	КонецЕсли;
	
	Если НЕ ТипЗнч(ПараметрыДействия) = Тип("Структура") Тогда
		ПараметрыДействия = Новый Структура();
	КонецЕсли;		
	
	ПараметрыДействия.Вставить("ОтправитьСообщениеИзФормы",	Истина);
	ПараметрыДействия.Вставить("Сообщение",					Сообщение);
	ПараметрыДействия.Вставить("Объект",					МодульОбъекта());
	ПараметрыДействия.Вставить("ОтправкаТолькоALCRPT",		ОтправкаТолькоALCRPT);
	ПараметрыДействия.Вставить("ИзмененныеРеквизиты",		ПодготовитьСтруктуруИзмененныхРеквизитов());
	
	ДобавитьПроизвольныеПараметрыФормы(ПараметрыДействия);
	
	Если ИмяКоманды = "ОтправитьОтветНаЗаказ" и ПараметрыДействия.Свойство("ВходящийЗаказ")Тогда  //костылек на отмену заказа через спец кнопку
		
		ПараметрыДействия.ВходящийЗаказ = МодульОбъекта().ПрочитатьСообщение(ПараметрыДействия.ВходящийЗаказ,,"ORDERS","Входящее");
		
	КонецЕсли;       			
	
	СтандартнаяОбработкаEDI = Истина;
	РезультатДействия = МодульОбъекта().ОбработкаСобытияПодключаемогоМодуля("КнопкаФормыСообщенияНажатие",СтандартнаяОбработкаEDI,Новый Структура("ИмяКоманды,Сообщение,Параметры,Форма,РезультатКлиента",ИмяКоманды,Сообщение,Параметры,ЭтаФорма,Неопределено));
	
	Если СтандартнаяОбработкаEDI = Ложь Тогда
		Действие = "НеСтандартнаяОбработкаДействия";
	КонецЕсли;
	
	Возврат Действие;

КонецФункции

&НаСервере
Функция ПолучитьСвойстваКнопки(ИмяКоманды)
	
	Перем КнопкиКоманднойПанели, СвойстваКнопки;
	
	СтруктураВременногоХранилища = ПолучитьИзВременногоХранилища(АдресВХПроизвольныеРеквизиты);
	КнопкиКоманднойПанели	= СтруктураВременногоХранилища.НастройкаФормыКнопкиКоманднойПанели;
	
	СвойстваКнопки = КнопкиКоманднойПанели.Найти(ИмяКоманды,"Имя");
	Возврат СвойстваКнопки;

КонецФункции

&НаСервере
Функция КонвертироватьПарамтрыФормыВСтруктуруСервер()
    //только ключевые
	Сконвертированнаятруктура=Новый Структура;
	
	Сконвертированнаятруктура.Вставить("Документ1С",Параметры.Документ1С);
	Сконвертированнаятруктура.Вставить("КодДействия",Параметры.КодДействия);
	Сконвертированнаятруктура.Вставить("МодальностьЗапрещена",Объект.ПараметрыКлиентСервер.МодальностьЗапрещена);
	Сконвертированнаятруктура.Вставить("РежимРаботы",Параметры.РежимРаботы);
	Сконвертированнаятруктура.Вставить("СообщениеСсылка",Параметры.СообщениеСсылка);
	Сконвертированнаятруктура.Вставить("ТипСообщения",Параметры.ТипСообщения);
	Сконвертированнаятруктура.Вставить("ОтправкаТолькоALCRPT",ОтправкаТолькоALCRPT);    //добавим сервисный реквизит, костыльно смотрится - //метка рефакторинг
	//добавить произвольные параметры если там структура - опять же смотрится костыльно - //метка рефакторинг
	
	Если Параметры.ПроизвольныеПараметры<>Неопределено Тогда
		Если Параметры.ПроизвольныеПараметры.Свойство("Статус") Тогда 
			Сконвертированнаятруктура.Вставить("Статус",		Параметры.ПроизвольныеПараметры.Статус);
		КонецЕсли;
		Если Параметры.ПроизвольныеПараметры.Свойство("ВходящийЗаказ") Тогда
			
			СообщениеЗаказ = МодульОбъекта().ПрочитатьСообщение(Параметры.ПроизвольныеПараметры.ВходящийЗаказ,,"ORDERS","Входящее");
			МодульОбъекта().КонвертироватьСообщениеEDIв1С(СообщениеЗаказ);
			
			Сконвертированнаятруктура.Вставить("ВходящийЗаказ",	СообщениеЗаказ);
		КонецЕсли;
	КонецЕсли;
	
	
	Возврат Сконвертированнаятруктура;
	
КонецФункции // КонвертироватьПарамтрыФормыВСтруктуру()

&НаКлиенте
Процедура КнопкаДополнительныеДействияФормыНажатие(Команда)
	
	КнопкаДействияФормыНажатие_Клиент(Команда.Имя);   	
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗаказ(Команда)
	
	КнопкаДействияФормыНажатие_Клиент(Команда.Имя);  
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗаказПоставщику(Команда)
	
	КнопкаДействияФормыНажатие_Клиент(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьВозврат(Команда)
	
	КнопкаДействияФормыНажатие_Клиент(Команда.Имя);

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьНовуюВерсиюВозврата(Команда)
	
	Если Сообщение.ТипСообщения = "RETANN" И Сообщение.Свойство("Статус") Тогда
		Если ВРег(Сообщение.Статус) = "CANCELED" ИЛИ ВРег(Сообщение.Статус) = "ОТМЕНА" ИЛИ ВРег(Сообщение.Статус) = "REPLACE" ИЛИ ВРег(Сообщение.Статус) = "ЗАМЕНА" Тогда
			СписокДействий = Новый СписокЗначений;
			Текст = "";
			
			Если ВРег(Сообщение.Статус) = "CANCELED" ИЛИ ВРег(Сообщение.Статус) = "ОТМЕНА" Тогда
				
				Текст = "Партнер отменил возврат. Что делать?";  				
				
				СписокДействий.Добавить("ПометитьНаУдаление",		"Пометить документ на удаление");								
				СписокДействий.Добавить("ОтметитьКакОбработанное",	"Отметить сообщение как обработанное");
				
			ИначеЕсли ВРег(Сообщение.Статус) = "REPLACE" ИЛИ ВРег(Сообщение.Статус) = "ЗАМЕНА" Тогда
				
				Текст = "Партнер отправил новую версию возврата. Что делать?";
														
				СписокДействий.Добавить("ЗагрузитьВозврат",			"Перезаполнить документ возврата"); 				
				СписокДействий.Добавить("ОтметитьКакОбработанное",	"Игнорировать новую версию");
				
			КонецЕсли;
			
			СписокДействий.Добавить("Отмена","Отмена");
			
			ПараметрыВопроса = Новый Структура("Вопрос","НоваяВерсияВозврата");
			
			Если Объект.ПараметрыКлиентСервер.МодальностьЗапрещена Тогда
				ЗадатьВопрос = "";
				ЗадатьВопрос = ЗадатьВопрос + Символы.ПС + "Оповещение = Новый ОписаниеОповещения(""ПослеЗакрытияВопроса"", ЭтаФорма,ПараметрыВопроса);"; 
				ЗадатьВопрос = ЗадатьВопрос + Символы.ПС + "ПоказатьВопрос(Оповещение, Текст, СписокДействий);"; 	 
				Выполнить(ЗадатьВопрос);
			Иначе 				
				Ответ = Вопрос(Текст, СписокДействий, 0);
				
				ПослеЗакрытияВопроса(Ответ,ПараметрыВопроса);  				
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры  

&НаКлиенте
Процедура ПослеЗакрытияВопроса(Ответ, ДопПараметры = Неопределено) Экспорт

	Если ДопПараметры <> Неопределено Тогда  		
		Если ДопПараметры.Свойство("Вопрос") Тогда
			Если ДопПараметры.Вопрос = "НоваяВерсияВозврата" Тогда
				Если  Ответ = "Отмена" Тогда
					//Ничего не делаем
				ИначеЕсли Ответ = "ОтклонитьВозвратПолностью" Тогда
					ОтклонитьВозвратПолностью(ЭтаФорма.Команды.ОтклонитьВозвратПолностью)
				Иначе
					
					КнопкаДействияФормыНажатие_Клиент(Ответ);  
					
				КонецЕсли;
			КонецЕсли;   
		КонецЕсли; 				
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьОтветНаЗаказ(Команда)
	
	КнопкаДействияФормыНажатие_Клиент(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьУведомлениеОбОтгрузке(Команда)
	
	КнопкаДействияФормыНажатие_Клиент(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьУведомлениеОбОтгрузкеВозврата(Команда)
	
	Если НЕ ЗначениеЗаполнено(Документ1С) Тогда
		
		СписокКнопок = Новый СписокЗначений;
		СписокКнопок.Добавить("Создать документ");
		СписокКнопок.Добавить("Привязать документ");
		СписокКнопок.Добавить("Отмена");
		
		ТекстВопроса = "Не найден документ возврата, создать новый или привязать существующий?" ;
		ИмяОбработчика = "ОбработчикВопросаСоздатьИлиПривязатьДокументПриЗагрузкиУведомленияОбОтгрузкеВозврата";
		ПараметрыОбработчика = Неопределено;
		
		МодульОбъектаКлиент().ПоказатьВопрос_КонтурEDI(ИмяОбработчика, ЭтаФорма, ПараметрыОбработчика, ТекстВопроса, СписокКнопок);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикВопросаСоздатьИлиПривязатьДокументПриЗагрузкиУведомленияОбОтгрузкеВозврата(РезультатВопроса, ДопПараметрДляПередачиВОбработчик = Неопределено) Экспорт
	
	Если РезультатВопроса = "Создать документ" Тогда
		
		КнопкаДействияФормыНажатие_Клиент("ЗагрузитьУведомлениеОбОтгрузкеВозврата");
		
	ИначеЕсли РезультатВопроса = "Привязать документ" Тогда
		
		ПривязатьСообщение(Неопределено);	
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьРасхожденияЗаказаПокупателя(Команда)
	
	//собрать параметры и показать расхождения
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВидРасхождения", 	"РасхожденияЗаказа");
	ПараметрыФормы.Вставить("Заказ",			Документ1С);
	
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Сервис_ПросмотрРасхожденийУправляемая", , ПараметрыФормы, ЭтаФорма, , , , РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаКлиенте
Процедура ПропуститьИсходящееСообщение(Команда)
	
	КнопкаДействияФормыНажатие_Клиент(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьУведомлениеОбОтгрузке(Команда)
	
	КнопкаДействияФормыНажатие_Клиент(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьУведомлениеОПриемке(Команда)
	
	КнопкаДействияФормыНажатие_Клиент(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьОтветНаУведомлениеОВозврате(Команда)
	
	КнопкаДействияФормыНажатие_Клиент(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьУведомлениеОПриемкеВозврата(Команда)
	
	КнопкаДействияФормыНажатие_Клиент(Команда.Имя);

КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗаказ(Команда)
	
	КнопкаДействияФормыНажатие_Клиент(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьОтветНаЗаказ(Команда)
	
	КнопкаДействияФормыНажатие_Клиент(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьСчетФактуру(Команда)
	
	КнопкаДействияФормыНажатие_Клиент(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	КнопкаДействияФормыНажатие_Клиент(Команда.Имя);
	
КонецПроцедуры

&НаСервере
Функция ОпределитьКнопкуЭлементФормыПоИмениКоманды(ИмяКоманды)
	Если Этаформа.КоманднаяПанель.ПодчиненныеЭлементы.ОсновноеДействиеФормы1.ИмяКоманды = ИмяКоманды Тогда 
		Возврат Этаформа.КоманднаяПанель.ПодчиненныеЭлементы.ОсновноеДействиеФормы1;
	ИначеЕсли Этаформа.КоманднаяПанель.ПодчиненныеЭлементы.ОсновноеДействиеФормы2.ИмяКоманды = ИмяКоманды Тогда 
		Возврат Этаформа.КоманднаяПанель.ПодчиненныеЭлементы.ОсновноеДействиеФормы2; 
	ИначеЕсли Этаформа.КоманднаяПанель.ПодчиненныеЭлементы.ОсновноеДействиеФормы3.ИмяКоманды = ИмяКоманды Тогда 
		Возврат Этаформа.КоманднаяПанель.ПодчиненныеЭлементы.ОсновноеДействиеФормы3; 
	ИначеЕсли Этаформа.КоманднаяПанель.ПодчиненныеЭлементы.ОсновноеДействиеФормы4.ИмяКоманды = ИмяКоманды Тогда 
		Возврат Этаформа.КоманднаяПанель.ПодчиненныеЭлементы.ОсновноеДействиеФормы4; 
	иначе
		Возврат Неопределено;
	КонецЕсли;		
КонецФункции // ОпределитьКнопкуЭлементФормыПоИмениКоманды()


//прямые обработчики дополнтельных кнопок - расширить
&НаКлиенте
Процедура ОтклонитьЗаказПолностью(Команда)
	
	ЗакрытиеРазрешено = Истина;
	
	ПараметрыФормы = Новый Структура("СообщениеСсылка",		Параметры.СообщениеСсылка); 
	ПараметрыФормы.Вставить("Документ1С",					Параметры.Документ1С);
	ПараметрыФормы.Вставить("ТипСообщения",					"ORDRSP");
	ПараметрыФормы.Вставить("Направление",					"Исходящее");
	ПараметрыФормы.Вставить("ТолькоПросмотр",				Ложь);
	ПараметрыФормы.Вставить("КодДействия",					"Отправить_ORDRSP");
	ПараметрыФормы.Вставить("РежимРаботы",					Параметры.РежимРаботы);
	ПараметрыФормы.Вставить("АдресХранилища",				Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект);
	ПараметрыФормы.Вставить("ПараметрыАвтотестирования",	Параметры.ПараметрыАвтотестирования);
	
	ДанныеДляОтклоненияЗаказаПолностью = Новый Структура;
	ДанныеДляОтклоненияЗаказаПолностью.Вставить("ВходящийЗаказСсылка", 				Неопределено);
	ДанныеДляОтклоненияЗаказаПолностью.Вставить("ПереотправляемоеСообщениеСсылка", 	Неопределено);
	
	Если ЗначениеЗаполнено(Параметры.Документ1С) Тогда
		ЗаполнитьДанныеДляОтклоненияЗаказаПолностью(ДанныеДляОтклоненияЗаказаПолностью, Параметры.Документ1С);
	КонецЕсли;	
	
	Если ДанныеДляОтклоненияЗаказаПолностью.ПереотправляемоеСообщениеСсылка <> Неопределено Тогда
		ПараметрыФормы.Вставить("ПереотправляемоеСообщениеСсылка",	
								ДанныеДляОтклоненияЗаказаПолностью.ПереотправляемоеСообщениеСсылка);
	КонецЕсли;	
	
	ПроизвольныеПараметры = Новый Структура();
	ПроизвольныеПараметры.Вставить("Статус", "Отклонить");
	
	Если Параметры.ТипСообщения = "ORDERS" Тогда
		ВходящийЗаказСсылка = Параметры.СообщениеСсылка;
	ИначеЕсли Параметры.ТипСообщения = "ORDRSP" Тогда
		ВходящийЗаказСсылка = ДанныеДляОтклоненияЗаказаПолностью.ВходящийЗаказСсылка;
	КонецЕсли;
	
	ПроизвольныеПараметры.Вставить("ВходящийЗаказ", 	ВходящийЗаказСсылка);
	ПараметрыФормы.Вставить("ПроизвольныеПараметры",	ПроизвольныеПараметры);
	
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("ФормаСообщенияУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбработчикПослеЗакрытияКарточкиПереотправкиСообщения", ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ОтклонитьВозвратПолностью(Команда)
	
	ЗакрытиеРазрешено = Истина;
	
	ПараметрыФормы = Новый Структура("СообщениеСсылка",		Параметры.СообщениеСсылка); 
	ПараметрыФормы.Вставить("Документ1С",					Параметры.Документ1С);
	
	Если Параметры.ТипСообщения = "RETANN" ИЛИ Параметры.ТипСообщения = "RETINS"  Тогда
		ПараметрыФормы.Вставить("ТипСообщения",					"RETINS");
		ПараметрыФормы.Вставить("КодДействия",					"Отправить_RETINS"); 
	Иначе
		ПараметрыФормы.Вставить("ТипСообщения",					"RETREC");
		ПараметрыФормы.Вставить("КодДействия",					"Отправить_RETREC"); 
	КонецЕсли;
	
	ПараметрыФормы.Вставить("Направление",					"Исходящее");
	ПараметрыФормы.Вставить("ТолькоПросмотр",				Ложь);
	ПараметрыФормы.Вставить("РежимРаботы",					Параметры.РежимРаботы);
	ПараметрыФормы.Вставить("АдресХранилища",				Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект);
	ПараметрыФормы.Вставить("ПараметрыАвтотестирования",	Параметры.ПараметрыАвтотестирования);
	
	ДанныеДляОтклоненияВозвратаПолностью = Новый Структура; 	
	ДанныеДляОтклоненияВозвратаПолностью.Вставить("ВходящийВозвратСсылка", 				Неопределено);
	ДанныеДляОтклоненияВозвратаПолностью.Вставить("ПереотправляемоеСообщениеСсылка", 	Неопределено);
	
	Если ЗначениеЗаполнено(Параметры.Документ1С) Тогда
		ЗаполнитьДанныеДляОтклоненияВозратаПолностью(ДанныеДляОтклоненияВозвратаПолностью, Параметры.Документ1С,Параметры.ТипСообщения);
	КонецЕсли;	
	
	Если ДанныеДляОтклоненияВозвратаПолностью.ПереотправляемоеСообщениеСсылка <> Неопределено Тогда
		ПараметрыФормы.Вставить("ПереотправляемоеСообщениеСсылка",	
								ДанныеДляОтклоненияВозвратаПолностью.ПереотправляемоеСообщениеСсылка);
	КонецЕсли;	
	
	ПроизвольныеПараметры = Новый Структура();
	ПроизвольныеПараметры.Вставить("Статус", "Отклонить");
	
	Если Параметры.ТипСообщения = "RETANN" ИЛИ Параметры.ТипСообщения = "RETDES" Тогда 		
		ВходящийВозвратСсылка = Параметры.СообщениеСсылка;  		
	ИначеЕсли Параметры.ТипСообщения = "RETINS" ИЛИ Параметры.ТипСообщения = "RETREC" Тогда 		
		ВходящийВозвратСсылка = ДанныеДляОтклоненияВозвратаПолностью.ВходящийВозвратСсылка;		
	КонецЕсли;
	
	ПроизвольныеПараметры.Вставить("ВходящийВозврат", 	ВходящийВозвратСсылка);

	ПараметрыФормы.Вставить("ПроизвольныеПараметры",	ПроизвольныеПараметры);
	
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("ФормаСообщенияУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбработчикПослеЗакрытияКарточкиПереотправкиСообщения", ЭтаФорма);
	
КонецПроцедуры  

//Объединим в одну функцию, чтобы не ходить дважды на сервер
&НаСервере
Процедура ЗаполнитьДанныеДляОтклоненияЗаказаПолностью(СтруктураДанные, ДокументСсылка)
	
	Док_ORDERS = МодульОбъекта().НайтиСвязанныйДокументПоТипуСообщения(ДокументСсылка,"ORDERS");
	СтруктураДанные.ВходящийЗаказСсылка = МодульОбъекта().НайтиСообщениеДокумента(Док_ORDERS, "ORDERS", "Входящее");
	СтруктураДанные.ПереотправляемоеСообщениеСсылка = МодульОбъекта().НайтиСообщениеДокумента(ДокументСсылка, "ORDRSP", "Исходящее");
	
КонецПроцедуры //

&НаСервере
Процедура ЗаполнитьДанныеДляОтклоненияВозратаПолностью(СтруктураДанные, ДокументСсылка, ТипСообщения)
	
	Если Параметры.ТипСообщения = "RETANN" ИЛИ Параметры.ТипСообщения = "RETINS"  Тогда 
		СтруктураДанные.ВходящийВозвратСсылка = МодульОбъекта().НайтиСообщениеДокумента(ДокументСсылка, "RETANN", "Входящее"); 
		СтруктураДанные.ПереотправляемоеСообщениеСсылка = МодульОбъекта().НайтиСообщениеДокумента(ДокументСсылка, "RETINS", "Исходящее");
	Иначе
		СтруктураДанные.ВходящийВозвратСсылка = МодульОбъекта().НайтиСообщениеДокумента(ДокументСсылка, "RETDES", "Входящее");
		СтруктураДанные.ПереотправляемоеСообщениеСсылка = МодульОбъекта().НайтиСообщениеДокумента(ДокументСсылка, "RETREC", "Исходящее");
	КонецЕсли; 	 		
	
КонецПроцедуры // 

&НаКлиенте
Процедура ПерезаполнитьДокумент(Команда)
	
	ТекстВопроса = "Документ " + СокрЛП(Сообщение.Документ1С) + " будет перезаполнен. Продолжить?";
	КнопкиВопроса = Новый СписокЗначений;
	КнопкиВопроса.Добавить("Да, перезаполнить документ");
	КнопкиВопроса.Добавить("Отмена");
	ПараметрыОбработчика = Неопределено;
	ИмяОбработчика = "ОбработчикСогласияПерезаполненияДокумента";
	
	МодульОбъектаКлиент().ПоказатьВопрос_КонтурEDI(ИмяОбработчика, ЭтаФорма, ПараметрыОбработчика, ТекстВопроса, КнопкиВопроса);

КонецПроцедуры
 
&НаКлиенте
Процедура ОбработчикСогласияПерезаполненияДокумента(РезультатВопроса,ДопПараметрПереданныйВОбработчик=Неопределено) Экспорт
	
	Если РезультатВопроса =	"Да, перезаполнить документ" Тогда 
		ДокументПерезаполнен=ПерезаполнитьДокумент1С();
		Если ДокументПерезаполнен = Истина Тогда 
			ЗакрытиеРазрешено=Истина;
			ЭтаФорма.Закрыть(неопределено);
		Иначе
			Сообщить("Не удалось перезаполнить документ");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтвязатьДокумент(Команда)
	
	Если НЕ ЭтоПоследнееСообщениеВЦепочкеВызовСервера() Тогда
		
		ТекстПредупреждения = "Можно отвязывать только последнее сообщение в цепочке!";
		МодульОбъектаКлиент().Предупреждение_КонтурEDI(ТекстПредупреждения);
		Возврат;
		
	КонецЕсли;
	
	ПараметрыАвтотестирования = Неопределено;
	
	Если НЕ ЗначениеЗаполнено(ПараметрыАвтотестирования) Тогда
		
		ТекстВопроса = "Сообщение будет отвязано от документа "+СокрЛП(Сообщение.Документ1С)+". Продолжить?";
		КнопкиВопроса = Новый СписокЗначений;
		КнопкиВопроса.Добавить("Да, отвязать сообщение");
		КнопкиВопроса.Добавить("Отмена");
		ПараметрыОбработчика = Неопределено;
		ИмяОбработчика = "ОбработчикПодтвержденияОтвязкиДокумента";
		
		МодульОбъектаКлиент().ПоказатьВопрос_КонтурEDI(ИмяОбработчика, ЭтаФорма, ПараметрыОбработчика, ТекстВопроса, КнопкиВопроса);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикПодтвержденияОтвязкиДокумента(РезультатВопроса,ДопПараметрПереданныйВОбработчик=Неопределено) Экспорт
	
	//Действия, связанные с обработкой результата ответа на вопрос
	Если РезультатВопроса= "Да, отвязать сообщение" Тогда 
		
		ОтвязатьСообщениеОтДокументаВызовСервера();
		
		ТолькоПросмотрСообщения = Ложь;
		
		Сообщение.ДокументСсылка	= Неопределено;
		Сообщение.Документ1С		= Неопределено;
		
		Если Сообщение.ТипСообщения = "RECADV" Тогда
			Сообщение.Накладная1С = Неопределено;
		КонецЕсли;
		
		ВыполнитьСерверныедействияПослеОтвязыванияСообщения();
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ВыполнитьСерверныедействияПослеОтвязыванияСообщения()
	
    ВосстановитьТаблицыЗначенийПроизвольныхРеквизитовИзХранилища();
	ПроизвестиПервоначальноеЗаполнениеПолейНаФорме();
	УничтожитьТаблицыЗначенийПроизвольныхРеквизитов();
	
КонецПроцедуры

&НаСервере
Процедура ОтвязатьСообщениеОтДокументаВызовСервера()
	
    ВосстановитьТаблицыЗначенийПроизвольныхРеквизитовИзХранилища();
	МодульОбъекта().ОтвязатьСообщениеОтДокумента(Сообщение, Сообщение.ДокументСсылка);	
	УничтожитьТаблицыЗначенийПроизвольныхРеквизитов();
	
КонецПроцедуры // ОтвязатьСообщениеОтДокументаВызовСервера()

&НаКлиенте
Процедура ОткрытьФайл(Команда)
	
	//РезультатКонвертации = ОткрытьФайлВызовСервера();
	//ЗапуститьПриложение(РезультатКонвертации.ПутьКФайлу);
	
	#Если Не ВебКлиент Тогда
		
	ТекстФайла = ОткрытьФайлВызовСервера();
	
	Если ТекстФайла = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекДок = Новый ТекстовыйДокумент;
	ТекДок.УстановитьТекст(ТекстФайла);
	ИмяВремФайла = ПолучитьИмяВременногоФайла("xml");
	
	ТекДок.Записать(ИмяВремФайла,КодировкаТекста.UTF8);
	
	ЗапуститьПриложение(ИмяВремФайла);
		
	#КонецЕсли
		
КонецПроцедуры

&НаСервере
Функция ОткрытьФайлВызовСервера()
	
	Если Параметры.ПроизвольныеПараметры=Неопределено Тогда
		Параметры.ПроизвольныеПараметры = Новый Структура;
	КонецЕсли;
	
	Параметры.ПроизвольныеПараметры.Вставить("ИзмененныеРеквизиты",		ПодготовитьСтруктуруИзмененныхРеквизитов());

	Если Сообщение.Направление = "Входящее" 
		ИЛИ Параметры.ТолькоПросмотр = истина 
		Тогда //на входящих сообщениях и открытых только на просмотр у нас уже есть сообщение
		
		времСообщение = МодульОбъекта().ПрочитатьСообщение(Параметры.СообщениеСсылка,Параметры.Документ1С);//,Параметры.ТипСообщения,Параметры.СообщениеСсылка.Направление); //уточнять тип и направление не следует т.к. мы могли просматривать сообщение из строки с треб действием по созданию документа 
	Иначе
		СохраненноесообщениеФормы = Сообщение;
		
		ВосстановитьТаблицыЗначенийПроизвольныхРеквизитовИзХранилища();
		ПеренестиПоля1СвСообщение(Сообщение);
		
		времСообщение = ЗначениеИзСтрокиВнутр(ЗначениеВСтрокуВнутр(Сообщение));
		Сообщение = СохраненноесообщениеФормы;
		
		//ранее готовили сообщение
		//времСообщение = МодульОбъекта().ПодготовитьИсходящееСообщение(Параметры.ТипСообщения, Параметры.Документ1С,Параметры.ПроизвольныеПараметры);

	КонецЕсли;
	
	Результат = МодульОбъекта().КонвертироватьИсходящееСообщениеПоНовому(времСообщение);
	
	//Возврат Результат;
	
	Если НЕ Результат.Успешно Тогда 
		Для каждого СтрокаОшибки из Результат.ТаблицаОшибок Цикл
			МодульОбъекта().Сообщить_КонтурEDI(СтрокаОшибки.ТекстОшибки);
		КонецЦикла;
		Возврат Неопределено;
	КонецЕсли;
	
	ТекДок = Новый ТекстовыйДокумент;
	ТекДок.Прочитать(Результат.ПутьКФайлу,КодировкаТекста.UTF8);
	
	УничтожитьТаблицыЗначенийПроизвольныхРеквизитов();
	Возврат ТекДок.ПолучитьТекст();
	
КонецФункции

&НаКлиенте
Процедура ПерезаполнитьДокументКлиент(Команда)
	// Вставить содержимое обработчика.
КонецПроцедуры

//подготовка / поиск данных

&НаКлиенте
Функция НайтиРекурсивноНижнююСтроку(Строки)
	
	НижняяСтрока = Строки[Строки.Количество()-1];
	
	Если НижняяСтрока.ПолучитьЭлементы().Количество() > 0 Тогда
		НижняяСтрока = НайтиРекурсивноНижнююСтроку(НижняяСтрока.ПолучитьЭлементы());
	КонецЕсли;
	
	Возврат НижняяСтрока;
	
КонецФункции

&НаКлиенте
Функция ПолучитьОбщееКоличествоТоварныхПозиций(Строка)
	
	Количество 		= 0;
	НайденнаяСтрока = Неопределено;
	
	МассивНайденныхСтрок = Товары.НайтиСтроки(Новый Структура("НомерСтроки", Строка.НомерСтроки));
	Если Не МассивНайденныхСтрок.Количество() = 0 Тогда
		НайденнаяСтрока = МассивНайденныхСтрок[0];
	КонецЕсли;
	
	Если Не НайденнаяСтрока = Неопределено Тогда
		Количество = НайденнаяСтрока.Количество;	
	КонецЕсли;
		
	Возврат Количество;
	
КонецФункции

&НаКлиенте
Функция ПолучитьРазмещенноеКоличество(Строка)
	
	Количество = 0;

	//для вложений не более одного: верхний уровень - упаковки, нижний - товары
	Для Каждого Упаковка Из ТранспортныеУпаковки.ПолучитьЭлементы() Цикл
		Для Каждого Товар Из Упаковка.ПолучитьЭлементы() Цикл
			Если Товар.НомерСтроки = Строка.НомерСтроки Тогда
				Количество = Количество + Товар.Количество;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Количество;
	
КонецФункции

//учет упаковок по sscc

&НаКлиенте
Функция ПолучитьТипУпаковки(КодТипа)
	
	ТипУпаковки = "Упаковка";
	
	Если КодТипа = "201" Тогда 
		ТипУпаковки = "Евро-палета"; 
	ИначеЕсли КодТипа = "202" Тогда 
		ТипУпаковки = "Стандартная палета";
	ИначеЕсли КодТипа = "CS" Тогда
		ТипУпаковки = "Коробка";
	КонецЕсли; 
	
	Возврат ТипУпаковки;
	
КонецФункции

&НаКлиенте
Функция ПолучитьУпаковкуВерхнегоУровня(Строка)
	
	РодительСтроки = Строка.ПолучитьРодителя();
	
	Если РодительСтроки = Неопределено Тогда 
		Возврат Строка;
	Иначе
		Возврат ПолучитьУпаковкуВерхнегоУровня(РодительСтроки);
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ПолучитьТипУпаковкиСервер(КодТипа)
	
	ТипУпаковки = "Упаковка";
	
	Если КодТипа = "201" Тогда
		ТипУпаковки = "Европалета";
	КонецЕсли;
	
	Возврат ТипУпаковки;
	
КонецФункции

&НаКлиенте
Функция ПолучитьРазвернутоеНаименованиеТовара(Строка)
	
	Наименование = "";
	
	Номенклатура = Строка(Строка.Номенклатура); 
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		Наименование = Наименование + Номенклатура;
	КонецЕсли;
	
	ХарактеристикаНоменклатуры = Строка(Строка.ХарактеристикаНоменклатуры); 
		
	ЕдиницаИзменения = Строка(Строка.ЕдиницаИзмерения);
	
	Если ЗначениеЗаполнено(ХарактеристикаНоменклатуры) 
		 ИЛИ ЗначениеЗаполнено(ЕдиницаИзменения) Тогда
		 
		Если ЗначениеЗаполнено(ХарактеристикаНоменклатуры)
			 И ЗначениеЗаполнено(ЕдиницаИзменения) Тогда
			Наименование = Наименование + " " + "( " + ХарактеристикаНоменклатуры + "," + " " + ЕдиницаИзменения + " )";
		Иначе
			Если ЗначениеЗаполнено(ХарактеристикаНоменклатуры) Тогда
				Наименование = Наименование + " " + "( " + ХарактеристикаНоменклатуры + " )";
			ИначеЕсли ЗначениеЗаполнено(ЕдиницаИзменения) Тогда
				Наименование = Наименование + " " + "( " + ЕдиницаИзменения + " )";
			КонецЕсли;				 
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Наименование;
	
КонецФункции

//интерфейсная обработка

&НаКлиенте
Процедура ВыбратьТипУпаковки(Команда)
	
	Если Команда.Имя = "ВыбратьУпаковкуЕвропалета" Тогда
		ТипУпаковкиПоУмолчанию = "201";
		ВыбраннаяУпаковкаЗаголовок = "евро-палету (201)";
	ИначеЕсли Команда.Имя = "ВыбратьУпаковкуСтандартнаяПалета" Тогда
		ТипУпаковкиПоУмолчанию = "202";
		ВыбраннаяУпаковкаЗаголовок = "стандартную палету (202)";
	КонецЕсли;
	
	УстановитьЗаголовокВыбраннойУпаковкиСервер(ВыбраннаяУпаковкаЗаголовок);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовокВыбраннойУпаковкиСервер(ВыбраннаяУпаковкаЗаголовок)

	Элементы.ТранспортныеУпаковкиВыбратьТипУпаковки.Заголовок = ВыбраннаяУпаковкаЗаголовок;	

КонецПроцедуры

&НаКлиенте
Процедура ВыделитьСтроку(Строка)
	
	Элементы.ТранспортныеУпаковки.ТекущаяСтрока = Строка.ПолучитьИдентификатор();
	Элементы.ТранспортныеУпаковки.ТекущийЭлемент = Элементы.ТранспортныеУпаковки.ПодчиненныеЭлементы.ТранспортныеУпаковкиКоличество;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыделитьЯчейку(Строка)
	
	Элементы.ТранспортныеУпаковки.ТекущаяСтрока = Строка.ПолучитьИдентификатор();
	Элементы.ТранспортныеУпаковки.ТекущийЭлемент = Элементы.ТранспортныеУпаковки.ПодчиненныеЭлементы.ТранспортныеУпаковкиКоличество;
	Элементы.ТранспортныеУпаковки.ИзменитьСтроку();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеРазмещения()

	//заполнили "Товарные позиции" заново
	ТоварныеПозиции.Очистить();
	Для Каждого Строка Из Товары Цикл
		НоваяСтрока = ТоварныеПозиции.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		Если Не ЗначениеЗаполнено(НоваяСтрока.Номенклатура) Тогда
			НоваяСтрока.Номенклатура = Строка.Наименование;
		КонецЕсли;
	КонецЦикла;
	
	//уменьшаем товарные позиции на распределенные количества
	Для Каждого Упаковка Из ТранспортныеУпаковки.ПолучитьЭлементы() Цикл
		Для Каждого Товар Из Упаковка.ПолучитьЭлементы() Цикл
			
			Отбор = Новый Структура("НомерСтроки",Товар.НомерСтроки);
			МассивТоварныхПозиций = ТоварныеПозиции.НайтиСтроки(Отбор);
			
			Если МассивТоварныхПозиций.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаТоварнойПозиции = МассивТоварныхПозиций.Получить(0);
			
			СтрокаТоварнойПозиции.Количество = СтрокаТоварнойПозиции.Количество - Товар.Количество;  				
			
			Если СтрокаТоварнойПозиции.Количество = 0 Тогда
				ТоварныеПозиции.Удалить(СтрокаТоварнойПозиции);
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;	
	
		
	ВычислитьКоличествоВУпаковках(ТранспортныеУпаковки.ПолучитьЭлементы());
		
	СортироватьТоварныеПозиции();
		
	ПронумероватьУпаковкиСквознымПорядковымНомером();
	ПроименоватьУпаковки(ТранспортныеУпаковки.ПолучитьЭлементы());
	РазвернутьВсеУровниТранспортныхУпаковок();
	
	Если ТаблицаОшибок.НайтиСтроки(Новый Структура("ИмяТабличнойЧасти", "ТранспортныеУпаковки")).Количество() > 0 Тогда
		ПодключитьОбработчикОжидания("ОбработчикПерепроверитьСообщение", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьВсеУровниТранспортныхУпаковок()

	Для каждого Строка Из ТранспортныеУпаковки.ПолучитьЭлементы() Цикл
		ИдСтроки = Строка.ПолучитьИдентификатор();
		Элементы.ТранспортныеУпаковки.Развернуть(ИдСтроки, Истина);
	КонецЦикла;	

КонецПроцедуры

&НаКлиенте
Процедура ВычислитьКоличествоВУпаковках(Упаковки)
	
	Для Каждого Упаковка Из Упаковки Цикл
		Количество = 0;
		Для Каждого Товар Из Упаковка.ПолучитьЭлементы() Цикл
			Количество = Количество + Товар.Количество;
		КонецЦикла;
		Упаковка.Количество = Количество;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СортироватьТоварныеПозиции()
	
	ТоварныеПозиции.Сортировать("НомерСтроки");	
	
КонецПроцедуры

&НаСервере
Функция ПолучитьНомераГТДПоСтроке(НомерСтроки)
	НомераГТД="";
	
	ВосстановитьТаблицыЗначенийПроизвольныхРеквизитовИзХранилища();
	СтрокаТоваров = Сообщение.Товары[НомерСтроки-1].НомерГТДEDI;
	Для каждого СтрокаГТД Из СтрокаТоваров Цикл
		НомераГТД = ?(НомераГТД="",НомераГТД,НомераГТД+Символы.ПС) + СтрокаГТД.НомерГТД;
	КонецЦикла;
	УничтожитьТаблицыЗначенийПроизвольныхРеквизитов();
	
	Возврат НомераГТД;
	
КонецФункции

&НаСервере
Функция ПолучитьКоличествоКВозвратуИПричины1С(НомерСтроки)
	
	КоличествоКВозвратуИПричины1С = "";
	
	ВосстановитьТаблицыЗначенийПроизвольныхРеквизитовИзХранилища();
	КоличествоИПричина = Сообщение.Товары[НомерСтроки-1].КоличествоКВозвратуИПричина1С;
	Для каждого Строка Из КоличествоИПричина Цикл
		КоличествоКВозвратуИПричины1С = КоличествоКВозвратуИПричины1С + Строка.ПричинаВозврата + "-" + Строка.КоличествоКВозврату + Символы.ПС;
	КонецЦикла;
	УничтожитьТаблицыЗначенийПроизвольныхРеквизитов();
	
	Возврат КоличествоКВозвратуИПричины1С;
	
КонецФункции

// нумерация упаковок и товаров

&НаКлиенте
Процедура ПронумероватьУпаковкиСквознымПорядковымНомером()
	
	//на будущее: когда появятся вложения - тогда здесь рекурсивно проваливаться вниз
	СоответствиеРодительскихУпаковок = ПолучитьСоответствиеРодительскихУпаковок(ТранспортныеУпаковки.ПолучитьЭлементы());
	//делаем +1 - поскольку в формате имеется один или несколько корневых узлов, начинаем нумеровать упаковки с 2-ки
	НомерУпаковки = СоответствиеРодительскихУпаковок.Количество() + 1; 
	
	Для Каждого Строка Из ТранспортныеУпаковки.ПолучитьЭлементы() Цикл
		
		Если Не ЗначениеЗаполнено(Строка.ТипУпаковки) Тогда
			Продолжить;
		КонецЕсли;
		
		Строка.НомерУпаковки = НомерУпаковки; 
		ПронумероватьТоварыУпаковки(Строка);
		НомерУпаковки = НомерУпаковки + 1;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьСоответствиеРодительскихУпаковок(КоллекцияУпаковок)
	
	Результат = Новый Соответствие;
	
	Для Каждого Строка Из КоллекцияУпаковок Цикл
		Если Не ЗначениеЗаполнено(Строка.ТипУпаковки) Тогда
			Продолжить;
		КонецЕсли;
		
		РодительскаяУпаковка = Результат[Строка.ТипУпаковки];
		Если РодительскаяУпаковка = Неопределено Тогда
			Результат.Вставить(Строка.ТипУпаковки);
			Результат[Строка.ТипУпаковки] = Новый Соответствие;
			Результат[Строка.ТипУпаковки].Вставить("КоличествоУпаковок", 1);
		Иначе
			РодительскаяУпаковка["КоличествоУпаковок"] = РодительскаяУпаковка["КоличествоУпаковок"] + 1;
		КонецЕсли;
	КонецЦикла;
	
	ТекУровеньИерархии = 1;
	Для каждого Эл Из Результат Цикл
		Эл.Значение.Вставить("УровеньИерархии", ТекУровеньИерархии);
		ТекУровеньИерархии = ТекУровеньИерархии + 1;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПронумероватьТоварыУпаковки(Упаковка)
	
	Для Каждого Товар Из Упаковка.ПолучитьЭлементы() Цикл
		
		Товар.НомерУпаковки = Упаковка.НомерУпаковки;	
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроименоватьУпаковки(ТранспортныеУпаковкиСтроки)
	
	Для Каждого Строка Из ТранспортныеУпаковкиСтроки Цикл
		
		ПроименоватьУпаковки(Строка.ПолучитьЭлементы());
		
		Если Не ЗначениеЗаполнено(Строка.ТипУпаковки) Тогда
			Продолжить;
		КонецЕсли;
		
		УстановитьСоставУпаковки(Строка);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСоставУпаковки(Упаковка)
	
	РасшифровкаТипУпаковки = ПолучитьТипУпаковки(Упаковка.ТипУпаковки);
	
	Если Упаковка.ТипУпаковки = "201" ИЛИ Упаковка.ТипУпаковки = "202" Тогда
		ТекстSSCC = ?(ЗначениеЗаполнено(Упаковка.SSCC), " (" + Упаковка.SSCC + ")", "");
		Упаковка.СоставУпаковки = РасшифровкаТипУпаковки + " " + "№" + Строка(Упаковка.НомерУпаковки-1) + ТекстSSCC;
	Иначе
		Упаковка.СоставУпаковки = РасшифровкаТипУпаковки;
	КонецЕсли;		
КонецПроцедуры

&НаСервере
Процедура ПромаркироватьУпаковкиSSCCКодом()
	
	//на будущее: когда появятся вложения - тогда здесь рекурсивно проваливаться вниз
	
	Нумератор = 1;
	
	Для Каждого Строка Из ТранспортныеУпаковки.ПолучитьЭлементы() Цикл
        //маркируем только строки упаковок, в которых не заполнен SSCC-код
		Если Не ЗначениеЗаполнено(Строка.ТипУпаковки) или ЗначениеЗаполнено(Строка.SSCC) Тогда  
			Продолжить;
		КонецЕсли;
		
		РасшифровкаТипУпаковки	= ПолучитьТипУпаковкиСервер(Строка.ТипУпаковки);
		
		Строка.SSCC 			= ПолучитьSSCCКодУпаковки(Нумератор);
		//делаем -1 - поскольку в формате имеется корневой узел, начинаем нумеровать упаковки с 2-ки
		Строка.СоставУпаковки 	= РасшифровкаТипУпаковки + " " + "№" + Строка(Строка.НомерУпаковки-1) + " " + "(" + Строка.SSCC + ")";
		Нумератор = Нумератор + 1;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьSSCCКодУпаковки(НомерУпаковкиТекущейОтгрузки)
	
	SSCC = "";
	Продавец = Сообщение.Продавец1С;
	GLNПродавца = Сообщение.ПродавецEDI.GLN;
			
	НомерПоследнейОтгруженнойУпаковки = ПолучитьНомерПоследнейУпаковки(Продавец);
	
	СтрокаНомерФормируемойУпаковки = СтрЗаменить( Строка(НомерПоследнейОтгруженнойУпаковки + НомерУпаковкиТекущейОтгрузки), Символы.НПП, "" );
	
	SSCC = СгенерироватьSSCC(GLNПродавца, СтрокаНомерФормируемойУпаковки); 
	
	Возврат SSCC;			
	
КонецФункции

&НаСервере
Функция ПолучитьНомерПоследнейУпаковки(Продавец)
	
	НомерПоследнейУпаковки = 0;
	
	РезервнаяСхемаSSCC = (МодульОбъекта().ПолучитьКонстантуEDI("РезервнаяСхемаSSCC")=Истина);
	ПоследнийSSCCКод = МодульОбъекта().ПолучитьЗначениеСвойстваОбъектаEDI(Продавец, "ПоследнийSSCCКод");
	
	НачалоНомераУпаковки 				= ?(РезервнаяСхемаSSCC,14,11);  
	КоличествоСимволовНомераУпаковки 	= ?(РезервнаяСхемаSSCC,4,7);
		
	Если ПоследнийSSCCКод = Неопределено Тогда
		НомерПоследнейУпаковки = 0;
	Иначе 
		Попытка
			НомерПоследнейУпаковки = Число(Сред(ПоследнийSSCCКод,НачалоНомераУпаковки,КоличествоСимволовНомераУпаковки));
		Исключение
			НомерПоследнейУпаковки = 0;
		КонецПопытки;
	КонецЕсли;
		
	Возврат НомерПоследнейУпаковки;
	
КонецФункции

&НаСервере
Процедура УстановитьНомерПоследнейУпаковки()
	
	Продавец = Сообщение.Продавец1С;

	ПоследнийSSCCКодТекущейПоставкиЧисло  = 0;
	ПоследнийSSCCКодТекущейПоставкиСтрока = 0;
	
	Для Каждого Строка Из ТранспортныеУпаковки.ПолучитьЭлементы() Цикл
		Если ЗначениеЗаполнено(Строка.SSCC) И Число(Строка.SSCC) > ПоследнийSSCCКодТекущейПоставкиЧисло Тогда
			ПоследнийSSCCКодТекущейПоставкиЧисло  = Число(Строка.SSCC);
			ПоследнийSSCCКодТекущейПоставкиСтрока = Строка.SSCC;
		КонецЕсли;
	КонецЦикла;
	
	ПоследнийSSCCКодВсехПоставок = МодульОбъекта().ПолучитьЗначениеСвойстваОбъектаEDI(Продавец, "ПоследнийSSCCКод");
    Если ПоследнийSSCCКодВсехПоставок = Неопределено Тогда
        ПоследнийSSCCКодВсехПоставок = "0";
    КонецЕсли;	
	
	Если ЗначениеЗаполнено(ПоследнийSSCCКодТекущейПоставкиСтрока) и Число(ПоследнийSSCCКодВсехПоставок) < ПоследнийSSCCКодТекущейПоставкиЧисло Тогда	
		МодульОбъекта().УстановитьЗначениеСвойстваОбъекта(Продавец, "ПоследнийSSCCКод", ПоследнийSSCCКодТекущейПоставкиСтрока);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СгенерироватьSSCC(GLNПродавца, НомерУпаковки)
	
	SSCC = "";
	
	РезервнаяСхемаSSCC = (МодульОбъекта().ПолучитьКонстантуEDI("РезервнаяСхемаSSCC")=Истина);
	
	ПрефиксПродавца = Лев(GLNПродавца,?(РезервнаяСхемаSSCC,12,9));
	ДлинаSSCCКода 	= 17;
	СуммаЧетных 	= 0;
	СуммаНечетных 	= 0;
	
	//добиваем нулями номер паллеты
	КоличествоНулей 	= ?(РезервнаяСхемаSSCC,4,7) - СтрДлина(НомерУпаковки);
	СтрокаНомерУпаковки = "";
	Для К = 1 По КоличествоНулей Цикл
		СтрокаНомерУпаковки = СтрокаНомерУпаковки + "0";	
	КонецЦикла;
	СтрокаНомерУпаковки = СтрокаНомерУпаковки + НомерУпаковки;
	
	Шаблон = "1" + ПрефиксПродавца + СтрокаНомерУпаковки;
	
	//вычисляем контрольный разряд
	КопияШаблон = Шаблон;
	Для Позиция = 1 По ДлинаSSCCКода Цикл
		Фрагмент   	= Число( Прав( КопияШаблон, 1 ) );
		КопияШаблон = Лев( Шаблон, ДлинаSSCCКода - Позиция);
		Если (Позиция/2) = Цел(Позиция/2) Тогда
			СуммаНечетных = СуммаНечетных + Фрагмент;
		Иначе	
			СуммаЧетных = СуммаЧетных + Фрагмент;
		КонецЕсли;			
	КонецЦикла;
	КонтрольнаяСумма = Строка(СуммаЧетных*3 + СуммаНечетных);
	КонтрольныйРазряд = 10 - Число( Прав( КонтрольнаяСумма,1 ) );
	Если КонтрольныйРазряд = 10 Тогда
		КонтрольныйРазряд = 0;
	КонецЕсли;	
	SSCC = Шаблон + Строка(КонтрольныйРазряд);
	
	Возврат SSCC;	
	
КонецФункции

&НаКлиенте
Процедура ТаблицаОшибокПриАктивизацииЯчейки(Элемент)
	
	ТекущаяСтрока = Элементы.ТаблицаОшибок.ТекущиеДанные;
	Если НЕ ТекущаяСтрока = Неопределено Тогда
		СделатьАктивнымПоле(ТекущаяСтрока.ИмяПоля,ТекущаяСтрока.ИмяТабличнойЧасти,ТекущаяСтрока.НомерСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СделатьАктивнымПоле(ИмяПоля,ИмяТабличнойЧасти,НомерСтроки)//
	
	Если ЗначениеЗаполнено(ИмяТабличнойЧасти) Тогда
		
		ТабличнаяЧасть = Элементы[ИмяТабличнойЧасти];
		
		ЭтаФорма.ТекущийЭлемент = ТабличнаяЧасть;
		
		Если НЕ НомерСтроки = 0 Тогда
			
			ТабличнаяЧасть.ТекущийЭлемент	= Элементы.Найти(ИмяТабличнойЧасти+ИмяПоля);
			Выполнить("ТабличнаяЧасть.ТекущаяСтрока	= "+ИмяТабличнойЧасти+"[НомерСтроки-1].ПолучитьИдентификатор()");
			
		КонецЕсли;
		
	Иначе	
		
		Если ЗначениеЗаполнено(ИмяПоля) Тогда
			
			Если Найти(ИмяПоля,"Грузоотправитель") > 0 Тогда
				
				ЭтаФорма.ТекущийЭлемент = Элементы.ДействиеГрузоотправитель;
				
			ИначеЕсли Найти(ИмяПоля,"Грузополучатель") > 0 Тогда
				
				ЭтаФорма.ТекущийЭлемент = Элементы.ДействиеГрузополучатель;
				
			ИначеЕсли Найти(ИмяПоля,"Продавец") > 0 Тогда
				
				Если КтоМы = "Поставщик" Тогда
					ЭтаФорма.ТекущийЭлемент = Элементы.ДействиеЮрФизЛицоСвое;
				Иначе
					ЭтаФорма.ТекущийЭлемент = Элементы.ДействиеЮрФизЛицоСтороннее;
				КонецЕсли;
				
			ИначеЕсли Найти(ИмяПоля,"Покупатель") > 0 Тогда
				
				Если КтоМы = "Поставщик" Тогда
					ЭтаФорма.ТекущийЭлемент = Элементы.ДействиеЮрФизЛицоСтороннее;
				Иначе
					ЭтаФорма.ТекущийЭлемент = Элементы.ДействиеЮрФизЛицоСвое;
				КонецЕсли;
				
			ИначеЕсли Найти(ИмяПоля,"ЗаказEDI") > 0 Тогда
				
				
			ИначеЕсли Найти(ИмяПоля,"СчетФактураEDI") > 0 Тогда
				
				ЭтаФорма.ТекущийЭлемент = Элементы.СчетФактура1С;
				
			ИначеЕсли Особенности.НайтиСтроки(Новый Структура("Идентификатор",ИмяПоля )).Количество() > 0 Тогда
				
				ТабличнаяЧасть = Элементы.Особенности;
				ЭтаФорма.ТекущийЭлемент = ТабличнаяЧасть;
				
				ТабличнаяЧасть.ТекущаяСтрока	= Особенности.НайтиСтроки(Новый Структура("Идентификатор",ИмяПоля ))[0].ПолучитьИдентификатор();				
				ТабличнаяЧасть.ТекущийЭлемент	= Элементы.ОсобенностиЗначение;
				
			Иначе
				
				НайденноеПоле = Элементы.Найти(ИмяПоля);
				Если НЕ НайденноеПоле = Неопределено Тогда
					ЭтаФорма.ТекущийЭлемент = НайденноеПоле;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаОшибокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ТаблицаОшибокДействие" Тогда
		
		ИмяПоля=Элемент.ТекущиеДанные.ИмяПоля;
		
		НайденнаяСтрока = НайтиСтрокуНастройкиСсылокИсправленияОшибок(Элемент.ТекущиеДанные.ИмяПоля);
		                  
		Если Не НайденнаяСтрока = Неопределено Тогда
			
			ОбработчикИсправленияОшибкиКлиент(НайденнаяСтрока,ИмяПоля,Элемент.ТекущаяСтрока,Элемент.ТекущиеДанные.ТекстОшибки);
			
			ОбработчикПослеИсправленияОшибки(ИмяПоля);	
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НайтиСтрокуНастройкиСсылокИсправленияОшибок(ИмяПоля)
	
	ВосстановитьТаблицыЗначенийПроизвольныхРеквизитовИзХранилища();
	НайденнаяСтрока = НастройкаСсылокИсправленияОшибок.Найти(ИмяПоля, "ИмяПоля");
	Если НайденнаяСтрока <> Неопределено Тогда
		СтруктураВозврата = Новый Структура;
		СтруктураВозврата.Вставить("ИмяПоля",НайденнаяСтрока.ИмяПоля);
		СтруктураВозврата.Вставить("ТекстСсылки",НайденнаяСтрока.ТекстСсылки);
		СтруктураВозврата.Вставить("ТипСсылки",НайденнаяСтрока.ТипСсылки);
	Иначе
		СтруктураВозврата = неопределено;
	КонецЕсли;
	УничтожитьТаблицыЗначенийПроизвольныхРеквизитов();
	
	
	Возврат СтруктураВозврата;
КонецФункции

&НаКлиенте
Процедура ОбработчикПослеИсправленияОшибки(ИмяПоля,ДопПараметр=неопределено) Экспорт//
	
	
	Если Сообщение.Направление = "Исходящее" Тогда
		ПерезаполнитьИсходящееСообщение();
	Иначе
		Если ИмяПоля = "Грузополучатель1С"
			Или ИмяПоля = "Покупатель1С"
			Тогда
			
			//Сообщение[ИмяПоля] = КопияСообщения[ИмяПоля];     //включить, когда будет передаваться копия сообщения
			//ЭтаФорма[ИмяПоля] = Сообщение[ИмяПоля];
			
			ПроверитьЗаполнениеПолей();
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
	
&НаКлиенте
Процедура ОбработчикИсправленияОшибкиКлиент(НайденнаяСтрока,ИмяПоля,НомерСтроки,ТекстОшибки) Экспорт//
		//Сообщить("Функционал в разработке, исправьте вручную");
		//КопияСообщения = МодульИнтеграции_Новый.ПолучитьКопиюСообщения(Сообщение);
		КопияСообщения=Неопределено;
		ВыполнитьДействиеДляИсправлениеОшибкиСообщения(ИмяПоля,НайденнаяСтрока.ТипСсылки,НомерСтроки,ТекстОшибки);

КонецПроцедуры

&НаКлиенте
Процедура ОбработчикРедактированияТочкиДоставкиСторонней(Параметр1,ДопПараметр=Неопределено) Экспорт
	ПроверитьЗаполнениеПолей();	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьДействиеДляИсправлениеОшибкиСообщения_Сервер(СтандартнаяОбработкаEDI, ИмяПоля, ТипСсылки, НомерСтроки = Неопределено, ТекстОшибки = Неопределено) Экспорт 
	
	СтандартнаяОбработкаEDI = Истина;
	
	МодульОбъекта().ОбработкаСобытияПодключаемогоМодуля("ВыполнитьДействиеДляИсправлениеОшибки", СтандартнаяОбработкаEDI, Новый Структура("ИмяПоля, ТипСсылки, КопияСообщения, ОригиналСообщения, ФормаСообщения", ИмяПоля, ТипСсылки, Сообщение, Сообщение, ЭтаФорма));
	
	Если Не СтандартнаяОбработкаEDI Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте                                             
Функция ВыполнитьДействиеДляИсправлениеОшибкиСообщения(ИмяПоля,ТипСсылки,НомерСтроки = Неопределено,ТекстОшибки = Неопределено) Экспорт //Только для ОФ.
	
	СтандартнаяОбработкаEDI = Истина;
	
	ВыполнитьДействиеДляИсправлениеОшибкиСообщения_Сервер(СтандартнаяОбработкаEDI, ИмяПоля, ТипСсылки ,НомерСтроки, ТекстОшибки);
	
	Если Не СтандартнаяОбработкаEDI Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	//В УФ Неопределено //Для УФ эта клиентская функция определена в ФормаСообщенияУправляемая
	Если ТипСсылки = "КарточкаКонтрагента" Тогда
		
		Если ИмяПоля = "Грузополучатель1С" Тогда
			Если ЭтаФорма.Сообщение.ТипСообщения = "ORDERS" Тогда
				Если Найти(ТекстОшибки,"не указана связь с точкой доставк")>0 или Найти(ТекстОшибки,"не указана связь с юр.")>0 Тогда
					
					//Открыть форму ТД
					Состояние("Открываю точку доставки",75);
					
					ПараметрыФормы = Новый Структура;
					ПараметрыФормы.Вставить("СсылкаТочкиДоставки", ЭтаФорма.Сообщение.Грузополучатель1С);
					МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("ТочкиДоставкиСторонние_ЭлементУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбработчикРедактированияТочкиДоставкиСторонней", ЭтаФорма);
					
					Возврат Неопределено;
					
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		//выше - была ТД но без соотв.
		
		//ниже - не было самой ТД
		Если Прав(ИмяПоля,3) = "EDI" Тогда
			ИмяПоляКонтрагента = Лев(ИмяПоля,СтрДлина(ИмяПоля)-3);
		ИначеЕсли Прав(ИмяПоля,2) = "1С" Тогда
			ИмяПоляКонтрагента = Лев(ИмяПоля,СтрДлина(ИмяПоля)-2);
		Иначе 
			ИмяПоляКонтрагента = ИмяПоля;
		КонецЕсли;
		
		//открыть форму контрагента для интерактивных действий пользователя
		ОткрытьФормуКонтрагента(ИмяПоляКонтрагента);
		
	ИначеЕсли ТипСсылки = "Документ1С" Тогда
		
		//Сообщение.ДокументСсылка.ПолучитьФорму().ОткрытьМодально();
		
	ИначеЕсли ТипСсылки = "НоменклатураСсылка" Тогда
		
		ТекстВопроса = "Необходимо проверить настройку соотвествия номенклатуры для этого партнера.";
		ЗадатьВопросОткрытьОнлайнСправку(ТекстВопроса, "СопоставлениеНоменклатуры");
		
	//	Если Сообщение.ТипСообщения = "PORDERS" Тогда
	//		
	//		Если НомерСтроки > 0 Тогда
	//			Номенклатура  		= Сообщение.Товары[НомерСтроки-1].Номенклатура;
	//			ЕдиницаИзмерения 	= Сообщение.Товары[НомерСтроки-1].ЕдиницаИзмерения;
	//			Если ЗначениеЗаполнено(Номенклатура) и ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
	//				ХарактеристикаНоменклатуры = Сообщение.Товары[НомерСтроки-1].ХарактеристикаНоменклатуры;
	//				Штрихкод = ПолучитьШтрихкодНоменклатуры(Новый Структура("Номенклатура,ХарактеристикаНоменклатуры,ЕдиницаИзмерения",Номенклатура,ХарактеристикаНоменклатуры,ЕдиницаИзмерения));
	//				ФормаНоменклатуры = ПолучитьФорму("ФормаНоменклатура_Элемент");
	//				ФормаНоменклатуры.Партнер				= Сообщение.Получатель1С;
	//				ФормаНоменклатуры.Наименование			= Номенклатура.Наименование;
	//				ФормаНоменклатуры.GTIN					= Штрихкод;
	//				ФормаНоменклатуры.КодТовараВнутренний 	= Номенклатура.Код;
	//				СписокНоменклатуры = ФормаНоменклатуры.СписокНоменклатуры;
	//				НоваяСтрока = СписокНоменклатуры.Добавить();
	//				НоваяСтрока.Номенклатура 				= Номенклатура;
	//				НоваяСтрока.ХарактеристикаНоменклатуры 	= ХарактеристикаНоменклатуры;
	//				НоваяСтрока.ЕдиницаИзмерения 			= ЕдиницаИзмерения;
	//				ФормаНоменклатуры.ОткрытьМодально();
	//			КонецЕсли;				
	//		КонецЕсли;
	//		
	//	Иначе
	//	
	//		Если НомерСтроки>0 Тогда
	//			Номенклатура = Сообщение.Товары[НомерСтроки-1].Номенклатура;
	//			Если ЗначениеЗаполнено(Номенклатура) Тогда
	//				Номенклатура.ПолучитьФорму().ОткрытьМодально();
	//			КонецЕсли;
	//		КонецЕсли;
	//		
	//	КонецЕсли;
	//	
	//ИначеЕсли ТипСсылки = "ДокументДиадок" Тогда
	//	
	//	Если Не Найти(ТекстОшибки, "Отказ в подписи накладной") = 0 Тогда
	//		
	//		boxId = СокрЛП(ПолучитьЗначениеСвойстваОбъектаEDI(Сообщение.ДокументСсылка, "boxId"));
	//		messageId = СокрЛП(ПолучитьЗначениеСвойстваОбъектаEDI(Сообщение.ДокументСсылка, "messageId"));
	//		torg12Id = СокрЛП(ПолучитьЗначениеСвойстваОбъектаEDI(Сообщение.ДокументСсылка, "torg12Id"));
	//		Если ЗначениеЗаполнено(boxId) и ЗначениеЗаполнено(messageId) и ЗначениеЗаполнено(torg12Id) Тогда
	//			ЗапуститьПриложение("https://diadoc.kontur.ru/"+boxId+"/Document/Show?letterId="+messageId+"&documentId="+torg12Id);
	//		КонецЕсли;
	//		
	//	ИначеЕсли Не Найти(ТекстОшибки, "Запрос на уточнение счета-фактуры") = 0 Тогда
	//		
	//		boxId = СокрЛП(ПолучитьЗначениеСвойстваОбъектаEDI(Сообщение.ДокументСсылка, "boxId"));
	//		messageId = СокрЛП(ПолучитьЗначениеСвойстваОбъектаEDI(Сообщение.ДокументСсылка, "messageId"));
	//		invoiceId = СокрЛП(ПолучитьЗначениеСвойстваОбъектаEDI(Сообщение.ДокументСсылка, "invoiceId"));
	//		Если ЗначениеЗаполнено(boxId) и ЗначениеЗаполнено(messageId) и ЗначениеЗаполнено(invoiceId) Тогда
	//			ЗапуститьПриложение("https://diadoc.kontur.ru/"+boxId+"/Document/Show?letterId="+messageId+"&documentId="+invoiceId);
	//		КонецЕсли;
	//		
	//	КонецЕсли;
		
	КонецЕсли;
	
	
КонецФункции

&НаКлиенте
Процедура ЗадатьВопросОткрытьОнлайнСправку(ТекстВопроса = "Открыть справку по работе в модуле Контур.EDI?", ИмяРазделаСправки = "Главная")
	
	СписокКнопок = Новый СписокЗначений;
	СписокКнопок.Добавить("Открыть онлайн справку");
	СписокКнопок.Добавить("Я знаю, что делать дальше");
	
	Если Параметры.МодальностьЗапрещена Тогда 
		Выполнить("ПоказатьВопрос(Новый ОписаниеОповещения(""ОбработчикВопросаОткрытьОнлайнСправку"", ЭтаФорма, ИмяРазделаСправки), ТекстВопроса, СписокКнопок,,,""Контур.EDI"")");
	Иначе
		РезультатВопроса = Вопрос(ТекстВопроса, СписокКнопок,,,"Контур.EDI");
		Выполнить("ОбработчикВопросаОткрытьОнлайнСправку(РезультатВопроса,ИмяРазделаСправки)");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикВопросаОткрытьОнлайнСправку(РезультатВопроса, ИмяРазделаСправки = Неопределено) Экспорт
	
	Если РезультатВопроса = "Открыть онлайн справку" 
		И ЗначениеЗаполнено(ИмяРазделаСправки) Тогда
		
		СписокРазделовСправки = ПолучитьСписокРазделовСправкиСервер();
		ЭлементСписка = СписокРазделовСправки.НайтиПоЗначению(ИмяРазделаСправки);
		Если НЕ ЭлементСписка = Неопределено Тогда
			ЗапуститьПриложение(ЭлементСписка.Представление);	
		КонецЕсли;	
		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСписокРазделовСправкиСервер()

	Возврат МодульОбъекта().ЗагрузитьКлассификаторИзМакета("ОнлайнСправка");	

КонецФункции // ()

&НаСервере
Процедура ОбработчикИсправленияОшибкиСервер()//
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьСтруктуруИдентификаторовТовара(ВыбраннаяСтрока, ТипСообщения, КтоМы)
	
	Структура = Новый Структура;
	
	Структура.Вставить("GTIN", ВыбраннаяСтрока.GTIN);
	
	// supplier code & buyer code поменяны местами EDI_An-354
	Если ТипСообщения = "RETDES" 
		ИЛИ КтоМы = "Покупатель" Тогда
		
		Структура.Вставить("КодТовараПартнера", ВыбраннаяСтрока.КодТовараПоставщика);
		Структура.Вставить("КодТовараВнутренний", ВыбраннаяСтрока.КодТовараПокупателя);
		
	Иначе
		
		Структура.Вставить("КодТовараПартнера", ВыбраннаяСтрока.КодТовараПокупателя);
		Структура.Вставить("КодТовараВнутренний", ВыбраннаяСтрока.КодТовараПоставщика);
		
	КонецЕсли;
	
	Возврат Структура;
	
КонецФункции

&НаКлиенте
Процедура ТоварыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)//!!
	
	ИмяКолонки = Поле.Имя;
	
	Если Сообщение.Направление = "Входящее" Тогда
		Если ИмяКолонки = "ТоварыНоменклатура" ИЛИ ИмяКолонки = "ТоварыЕдиницаИзмерения" ИЛИ ИмяКолонки = "ТоварыХарактеристикаНоменклатуры" Тогда
			
			СтандартнаяОбработка = Ложь;
			
			ИдентификаторыТовара = ПолучитьСтруктуруИдентификаторовТовара(Элементы.Товары.ТекущиеДанные, Сообщение.ТипСообщения, КтоМы);
			
			СписокВыбора = СоставитьСписокВыбораПоСтрокеТоваровСервер(ИдентификаторыТовара.GTIN, ИдентификаторыТовара.КодТовараПартнера, Отправитель1С);
			ПараметрыОбработчика = Неопределено;
			МодульОбъектаКлиент().ВыбратьИзМеню_КонтурEDI("ОбработчикВыбораСоответствияИзМеню", ЭтаФорма, ПараметрыОбработчика, СписокВыбора, Элемент);
			
		КонецЕсли;
	КонецЕсли;
	
	Если ИмяКолонки = "ТоварыНомерГТДEDI" Тогда
		Если ЗначениеЗаполнено(Элемент.ТекущиеДанные.НомерГТДEDI) Тогда 
			Попытка
				ТекстГТД = ПолучитьНомераГТДПоСтроке(Элемент.ТекущиеДанные.НомерСтроки);
				Если Параметры.МодальностьЗапрещена Тогда 
					Выполнить("ПоказатьПредупреждение(,ТекстГТД,,""Номера ГТД"")");
				Иначе
					Предупреждение(ТекстГТД,,"Номера ГТД");
				КонецЕсли;
			Исключение
			УничтожитьТаблицыЗначенийПроизвольныхРеквизитов();
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;

	Если ИмяКолонки = "ТоварыКоличествоКВозвратуИПричина1С" Тогда
		Если ЗначениеЗаполнено(Элемент.ТекущиеДанные.КоличествоКВозвратуИПричина1С) Тогда 
			Попытка
				ТекстКоличествоКВозвратуИПричины1С = ПолучитьКоличествоКВозвратуИПричины1С(Элемент.ТекущиеДанные.НомерСтроки);
				Если Параметры.МодальностьЗапрещена Тогда 
					Выполнить("ПоказатьПредупреждение(,ТекстКоличествоКВозвратуИПричины1С,,""Количество к возврату"")");
				Иначе
					Предупреждение(ТекстКоличествоКВозвратуИПричины1С,,"Количество к возврату");
				КонецЕсли;
			Исключение
				УничтожитьТаблицыЗначенийПроизвольныхРеквизитов();
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;

	Если ИмяКолонки = "ТоварыНеЗагружать" Тогда
		
		Элемент.ТекущиеДанные.НеЗагружать = НЕ Элемент.ТекущиеДанные.НеЗагружать;
		ПроверитьЗаполнениеПолей();

	КонецЕсли;
	
	Если НЕ ТолькоПросмотрСообщения Тогда
		// покажем меню выбора для списка значений	
		
		Если ИмяКолонки = "ТоварыРешениеПоВозврату" Тогда
			
			СтандартнаяОбработка = Ложь;
			МодульОбъектаКлиент().ВыбратьИзМеню_КонтурEDI("ОбработчикВыбораРешенияПоВозврату", ЭтаФорма, , Поле.СписокВыбора, Поле);
			
		ИначеЕсли МодульОбъектаКлиент().ПеременнаяСодержитСвойство(Поле, "СписокВыбора") 
			И ЗначениеЗаполнено(Поле.СписокВыбора) Тогда
			// здесь должны быть только особенности, но проверим на всякий случай
			
			Если НЕ Найти(Поле.Подсказка, "Эта колонка добавлена из-за особенности партнера") = 0 Тогда
				
				СтандартнаяОбработка = Ложь;
				
				ДопПараметры = Новый Структура;
				ДопПараметры.Вставить("ИмяКолонки", ИмяКолонки);
				МодульОбъектаКлиент().ВыбратьИзМеню_КонтурEDI("ОбработчикВыбораОсобенностиТоваров", ЭтаФорма, ДопПараметры, Поле.СписокВыбора, Поле);
				
			КонецЕсли;

		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикВыбораОсобенностиТоваров(ВыбранноеЗначение, ДопПараметры = Неопределено) Экспорт
	
	Если ВыбранноеЗначение = Неопределено
		ИЛИ ДопПараметры = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекДанные = Элементы.Товары.ТекущиеДанные;
	ИмяКолонки = Сред(ДопПараметры.ИмяКолонки, 7);
	ТекДанные[ИмяКолонки] =  ВыбранноеЗначение.Значение;
	ПеренестиОсобенностиИПерепроверитьСообщениеВызовСервера();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикВыбораРешенияПоВозврату(ВыбранноеЗначение, Параметры = Неопределено) Экспорт
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекДанные = Элементы.Товары.ТекущиеДанные;
	ТекДанные.РешениеПоВозврату = ВыбранноеЗначение.Значение;
	ТекДанные.Количество 		= ?(ТекДанные.РешениеПоВозврату = "В возврате отказано", 0, ТекДанные.КоличествоПредложеноКВозврату);
	ОбработчикВыбораРешенияПоВозвратуСервер();
	
КонецПроцедуры

&НаСервере
Процедура ОбработчикВыбораРешенияПоВозвратуСервер()
	
	ВосстановитьТаблицыЗначенийПроизвольныхРеквизитовИзХранилища();
	
	ВремТовары = РеквизитФормыВЗначение("Товары");
	Сообщение.Товары.ЗагрузитьКолонку(ВремТовары.ВыгрузитьКолонку("РешениеПоВозврату"), "РешениеПоВозврату");
	Сообщение.Товары.ЗагрузитьКолонку(ВремТовары.ВыгрузитьКолонку("Количество"), 		"Количество");
	
	ПоместитьТаблицыЗначенийПроизвольныхРеквизитовВХранилище();
	УничтожитьТаблицыЗначенийПроизвольныхРеквизитов();
	
	Сообщение.СодержитОшибки = НЕ ПроверитьЗаполнениеПолей();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикПослеИнтерактивнойНастройкиСоответствий(ВыбранноеЗначение, Параметры) Экспорт
	
	ТекСтрока = Элементы.Товары.ТекущиеДанные;
	ИдентификаторТекСтроки = ТекСтрока.ПолучитьИдентификатор();
	
	Если НЕ ЗначениеЗаполнено(ТекСтрока.Номенклатура) 
		ИЛИ НЕ ЗначениеЗаполнено(ТекСтрока.ЕдиницаИзмерения) Тогда
		
		ОбработчикПослеИнтерактивнойНастройкиСоответствийСервер(ИдентификаторТекСтроки);
		
	Иначе
		//уже была какая-то номенклатура выбрана в поле, но возможно у неё поменяли артикул...
		ДозаполнитьАртикулКодПолноеНаименованиеТЧТовары();
	КонецЕсли;
	
	ЗаполнитьТоварнуюГруппуТаблицыТоваровВызовСервера();
		
КонецПроцедуры

&НаСервере
Процедура ОбработчикПослеИнтерактивнойНастройкиСоответствийСервер(ИдентификаторТекСтроки)
	
	ВыбраннаяСтрока = Товары.НайтиПоИдентификатору(ИдентификаторТекСтроки);
	ИдентификаторыТовара = ПолучитьСтруктуруИдентификаторовТовара(ВыбраннаяСтрока, Сообщение.ТипСообщения, КтоМы);
	
	СоответствиеТоваров = МодульОбъекта().СоответствиеТоваров_НайтиНоменклатуру(ВыбраннаяСтрока.GTIN, ИдентификаторыТовара.КодТовараПартнера, Отправитель1С);
	
	Для каждого Стр Из СоответствиеТоваров Цикл
		Если Стр.Основной Тогда
			
			ВыбраннаяСтрока.Номенклатура				= Стр.Номенклатура;
			ВыбраннаяСтрока.ХарактеристикаНоменклатуры	= Стр.ХарактеристикаНоменклатуры;
			ВыбраннаяСтрока.ЕдиницаИзмерения			= Стр.ЕдиницаИзмерения;
			ВыбраннаяСтрока.КоэффициентEDIВ1С			= Стр.КоэффициентEDIВ1С;
			ВыбраннаяСтрока.ТипЕдиницы					= Стр.ТипЕдиницы;
			
			ПроверитьЗаполнениеПолей();
			
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	//дозаполним код, артикул, ПолноеНаименование
	ДозаполнитьАртикулКодПолноеНаименованиеТЧТовары();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикВыбораСоответствияИзМеню(ВыбранноеЗначение, ДопПараметры) Экспорт//!
	
	Если НЕ ВыбранноеЗначение = Неопределено Тогда
		Если ВыбранноеЗначение.Значение = "Настроить соответствие товаров" Тогда
			
			ТекСтрока = Элементы.Товары.ТекущиеДанные;
			ИдентификаторыТовара = ПолучитьСтруктуруИдентификаторовТовара(ТекСтрока, Сообщение.ТипСообщения, КтоМы);
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Партнер", Отправитель1С);
			ПараметрыФормы.Вставить("Наименование", ТекСтрока.Наименование);
			ПараметрыФормы.Вставить("GTIN", ИдентификаторыТовара.GTIN);
			ПараметрыФормы.Вставить("КодТовараПартнера", ИдентификаторыТовара.КодТовараПартнера);
			ПараметрыФормы.Вставить("КодТовараВнутренний", ИдентификаторыТовара.КодТовараВнутренний);
			
			Если ЗначениеЗаполнено(ТекСтрока.ТипЕдиницы) Тогда
				ПараметрыФормы.Вставить("ТипЕдиницы", ТекСтрока.ТипЕдиницы);
			КонецЕсли;
			
			МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Номенклатура_ЭлементУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбработчикПослеИнтерактивнойНастройкиСоответствий", ЭтаФорма);
			
		Иначе
			
			СтруктураТовара = ВыбранноеЗначение.Значение;
			
			ЗаполнитьЗначенияСвойств(Элементы.Товары.ТекущиеДанные,СтруктураТовара,"Номенклатура,ХарактеристикаНоменклатуры,ЕдиницаИзмерения,КоэффициентEDIВ1С,ТипЕдиницы");
			
			ОчиститьСообщения();//на рефакторинг - перетащить к пометкам
			ПроверитьЗаполнениеПолей();
			
			//дозаполним код, артикул, ПолноеНаименование
			ДозаполнитьАртикулКодПолноеНаименованиеТЧТовары();
			
			ЗаполнитьТоварнуюГруппуТаблицыТоваровВызовСервера();
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТоварнуюГруппуТаблицыТоваровВызовСервера()

	МодульОбъекта().ОбработкаСобытияПодключаемогоМодуля("ЗаполнитьТоварнуюГруппуТаблицыТоваров",,Новый Структура("Сообщение,Товары",Сообщение,Товары));

КонецПроцедуры // ЗаполнитьТоварнуюГруппуТаблицыТоваровВызовСервера()


&НаКлиенте
Процедура ОбработчикПослеВводаДаты(ВыбраннаяДата, ПараметрыЗакрытия) Экспорт
	Если НЕ ВыбраннаяДата = Неопределено Тогда
		
		//+для АТАК
		Если Сообщение.Свойство("ДатаПоставкиФактическая") Тогда
			ДатаПоставкиФактическая = ВыбраннаяДата;
			Сообщение.ДатаПоставкиФактическая = ВыбраннаяДата;
			Элементы.ПредставлениеДатыПоставки.Заголовок = МодульОбъектаКлиент().ПолучитьПредставлениеДатыПоставки_КонтурEDI(Сообщение.ДатаПоставкиФактическая);
		КонецЕсли;
		//-
			
		Если Сообщение.ТипСообщения = "ORDRSP" 
			И Не ВыбраннаяДата = Сообщение.ДатаПоставки
			И ЗначениеЗаполнено(Сообщение.ДокументСсылка) Тогда
			ДополнитьТаблицуИзмененныхРеквизитов("ДатаПоставки", ВыбраннаяДата);
			ПерезаполнитьИсходящееСообщение();
		Иначе	
			ДатаПоставки = ВыбраннаяДата;
			Сообщение.ДатаПоставки = ВыбраннаяДата;
			Элементы.ПредставлениеДатыПоставки.Заголовок = МодульОбъектаКлиент().ПолучитьПредставлениеДатыПоставки_КонтурEDI(Сообщение.ДатаПоставки);
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция СоставитьСписокВыбораПоСтрокеТоваровСервер(GTIN, КодТовараПартнера, Партнер)
	
	СписокВыбора = МодульОбъекта().СоставитьСписокВыбораСоответствийПоТовару(GTIN, КодТовараПартнера, Партнер);
	Возврат СписокВыбора;
	
КонецФункции

&НаКлиенте
Процедура ТоварыНеЗагружатьПриИзменении(Элемент)
		ПроверитьЗаполнениеПолей();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПередУдалением(Элемент, Отказ)
	Отказ=Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДействиеДатаПоставкиНажатие(Элемент)
	
	ВыбраннаяДата = ДокументДата;
	Если ЗначениеЗаполнено(НакладнаяДата) Тогда
		ВыбраннаяДата = НакладнаяДата;
	ИначеЕсли ЗначениеЗаполнено(ПриемкаДата) Тогда
		ВыбраннаяДата = ПриемкаДата;
	КонецЕсли;
	
	Подсказка = "Укажите дату поставки";
	ЧастьДаты = ЧастиДаты.ДатаВремя;
	
	Если Параметры.МодальностьЗапрещена Тогда
		Оповещение = Неопределено;
		Выполнить("Оповещение = Новый ОписаниеОповещения(""ОбработчикПослеВводаДаты"",ЭтаФорма, Параметры)");
		Выполнить("ПоказатьВводДаты(Оповещение, ВыбраннаяДата, Подсказка, ЧастьДаты)");
	Иначе
		Если ВвестиДату(ВыбраннаяДата,Подсказка,ЧастьДаты) Тогда 			
			ОбработчикПослеВводаДаты(ВыбраннаяДата, Неопределено);

		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнитьТаблицуИзмененныхРеквизитов(Наименование, Значение)

	//проверим наличие добавляемого реквизита в таблице
	ОтборСтрок = Новый Структура("Наименование",Наименование);
	
	НайденныеСтроки = ТаблицаИзмененныхРеквизитов.НайтиСтроки(ОтборСтрок);
	//НайденнаяСтрока = ТаблицаИзмененныхРеквизитов.Найти(Наименование, "Наименование");
	Если НайденныеСтроки.Количество() = 0 Тогда
		НовыйРеквизит = ТаблицаИзмененныхРеквизитов.Добавить();
		НовыйРеквизит.Наименование  = Наименование;
		НовыйРеквизит.Значение		= Значение;		
	Иначе
		Для Каждого НайденнаяСтрока из НайденныеСтроки Цикл
			НайденнаяСтрока.Значение	= Значение;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ОпределитьСтатусДляФормированияДействий(Сообщение)

	СтатусДляФормированияДействий = "";
	Если Сообщение.Свойство("Статус") Тогда
		Если НЕ (ВРег(Сообщение.Статус) = "REPLACE" И Не ЗначениеЗаполнено(Сообщение.ДокументСсылка)) Тогда
			 СтатусДляФормированияДействий = Сообщение.Статус;
		КонецЕсли;
	КонецЕсли;
	
	Возврат СтатусДляФормированияДействий;

КонецФункции // ОпределитьСтатусДляФормированияДействий() 

&НаСервере
Процедура ПерезаполнитьИсходящееСообщение()
	
	ПараметрыПерезаполнения = Новый Структура("ИзмененныеРеквизиты", ПодготовитьСтруктуруИзмененныхРеквизитов());
	
	ПереотправляемоеСообщениеСсылка = Неопределено;
	Сообщение.Свойство("ПереотправляемоеСообщениеСсылка",ПереотправляемоеСообщениеСсылка);
	
	ВосстановитьТаблицыЗначенийПроизвольныхРеквизитовИзХранилища();
	
	МодульОбъекта().АктуализироватьСообщение(Сообщение, ПараметрыПерезаполнения);
	
	Если ПереотправляемоеСообщениеСсылка<>Неопределено Тогда
		Сообщение.Вставить("ПереотправляемоеСообщениеСсылка",ПереотправляемоеСообщениеСсылка);
	КонецЕсли;
	                                                                                        
	ПолучитьПрикрепленныеСообщения();
	
	ЗаполнитьПоляНаФорме();
	
	СтатусДляФормированияДействий = ОпределитьСтатусДляФормированияДействий(Сообщение);   
	
	НастройкаФормы						= МодульОбъекта().ПолучитьНастройкиФормыСообщения(Сообщение.ТипСообщения,Сообщение.Направление,СтатусДляФормированияДействий);
	НастройкаСсылокИсправленияОшибок	= МодульОбъекта().ПолучитьНастройкуВыводаСсылокНаОбъектыСообщения();
	
	ПоместитьТаблицыЗначенийПроизвольныхРеквизитовВХранилище();		
	НетОшибок = ПроверитьЗаполнениеПолей();	
	УничтожитьТаблицыЗначенийПроизвольныхРеквизитов(); 	
	ДозаполнитьАртикулКодПолноеНаименованиеТЧТовары();    	
		
	Если НетОшибок И ТаблицаОшибок.Количество() = 0 Тогда
		
		ЗакрытьПанельОшибокСервер();
		
		Если КтоМы = "Поставщик" Тогда
			ВывестиПанельИнформации("В этой форме вы можете проверить данные, которые будут отправлены в торговую сеть.");
		Иначе
			ВывестиПанельИнформации("В этой форме вы можете проверить данные, которые будут отправлены поставщику.");
		КонецЕсли;
		
	Иначе

		ОткрытьПанельОшибокСервер();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура НайтиСвязанныеСообщения(Команда)
	
	Если ЗначениеЗаполнено(Сообщение.Документ1С) Тогда
		ДокументСсылка = Сообщение.Документ1С;
	ИначеЕсли ЗначениеЗаполнено(Сообщение.Заказ1С) Тогда
		ДокументСсылка = Сообщение.Заказ1С;
	ИначеЕсли ЗначениеЗаполнено(Сообщение.Накладная1С) Тогда
		ДокументСсылка = Сообщение.Накладная1С;
	ИначеЕсли ЗначениеЗаполнено(Сообщение.Приемка1С) Тогда
		ДокументСсылка = Сообщение.Приемка1С;
	ИначеЕсли ЗначениеЗаполнено(Сообщение.СчетФактура1С) Тогда
		ДокументСсылка = Сообщение.СчетФактура1С;
	Иначе
		ТекстПредупреждения = "Не найден документ 1С для поиска связанных сообщений";
		МодульОбъектаКлиент().Предупреждение_КонтурEDI(ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДокументСсылка",		ДокументСсылка);
	ПараметрыФормы.Вставить("СообщениеСсылка",		Сообщение.СообщениеСсылка);
	ПараметрыФормы.Вставить("МодальностьЗапрещена",	Параметры.МодальностьЗапрещена);
	
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Сервис_СвязанныеСообщенияУправляемая", , ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура Документ1СНажатие(Элемент, СтандартнаяОбработка)
	НадписьОткрытьДокументНажатие(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура НадписьОткрытьДокументНажатие(Элемент)
	Если ЗначениеЗаполнено(Документ1С) Тогда
		МодульОбъектаКлиент().ОткрытьФормуЗначения_КонтурEDI(Документ1С, "ОбработкаЗакрытияДокумента1С_КонтурEDI", ЭтаФорма);
	КонецЕсли;
КонецПроцедуры  

&НаКлиенте
Процедура ОбработкаЗакрытияДокумента1С_КонтурEDI(ОсновныеПараметры, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Сообщение.Направление = "Исходящее" И НЕ ЗначениеЗаполнено(Сообщение.СообщениеСсылка) Тогда
		ПерезаполнитьИсходящееСообщение();  
	КонецЕсли;

КонецПроцедуры    

&НаКлиенте
Процедура ДействиеЮрФизЛицоСтороннееНажатие(Элемент)
	
	Если КтоМы = "Поставщик" Тогда
		ОткрытьФормуКонтрагента("Покупатель");
	Иначе
		ОткрытьФормуКонтрагента("Продавец");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДействиеЮрФизЛицоСвоеНажатие(Элемент)
	
	Если КтоМы = "Поставщик" Тогда
		ОткрытьФормуКонтрагента("Продавец");
	Иначе
		ОткрытьФормуКонтрагента("Покупатель");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуКонтрагента(ИмяПоляСообщения)
	
	АдресСообщенияВВХ = ПоместитьКопиюСообщенияВВХ(); //сервер
	
	//открыть форму
	ПараметрыФормы = Новый Структура; 
	ПараметрыФормы.Вставить("АдресСообщенияВВХ",	АдресСообщенияВВХ);
	ПараметрыФормы.Вставить("ИмяПоляСообщения",		ИмяПоляСообщения);
	
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("ФормаКонтрагентаУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбработчикПослеЗакрытияКарточкиКонтрагента", ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Функция ПоместитьКопиюСообщенияВВХ()

	АдресВВХ = Неопределено;
	
	КопияСообщения = МодульОбъекта().ПолучитьКопиюСообщения(Сообщение);
	АдресВВХ = ПоместитьВоВременноеХранилище(КопияСообщения,новый УникальныйИдентификатор);
	
	Возврат АдресВВХ;	

КонецФункции // ПоместитьКопиюСообщенияВВХ()

&НаКлиенте
Процедура ОбработчикПослеЗакрытияКарточкиКонтрагента(Результат=Неопределено,ДопПараметр=Неопределено) Экспорт

	Если Результат<>Неопределено и Результат.Свойство("Успешно") и Результат.Успешно = Истина Тогда
	Сообщение = ПолучитьИзВременногоХранилища(Результат.АдресВВХСообщения);	
		
		ЭтаФорма[Результат.ИмяПоля+"1С"] = Результат.ЮрФизЛицо1С;
		
		Если Сообщение.Направление = "Исходящее" Тогда
			
			Сообщение[Результат.ИмяПоля+"EDI"] = Результат.ЮрФизЛицо;
			
		Иначе
			Сообщение[Результат.ИмяПоля+"1С"] = Результат.ЮрФизЛицо1С;
		КонецЕсли;
		
		ПроверитьЗаполнениеПолей();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДействиеГрузополучательНажатие(Элемент)
	
	ОткрытьФормуКонтрагента("Грузополучатель");
	
КонецПроцедуры

&НаКлиенте
Процедура ДействиеГрузоотправительНажатие(Элемент)
	ОткрытьФормуКонтрагента("Грузоотправитель");
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьОбратныйЗаказ(Команда)
	
	КнопкаДействияФормыНажатие_Клиент(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ДействиеКонтактыНажатие(Элемент)
	
	ТекстПредупреждения = ПолучитьПредставлениеКонтактногоЛицаПоЗаказу();
	Если Параметры.МодальностьЗапрещена Тогда 
		Выполнить("ПоказатьПредупреждение(,ТекстПредупреждения,,""Контакты по заказу"")");
	Иначе
		Предупреждение(ТекстПредупреждения,,"Контакты по заказу");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПредставлениеКонтактногоЛицаПоЗаказу()
	Возврат МодульОбъекта().ПолучитьПредставлениеКонтактногоЛицаПоЗаказу(Сообщение);
КонецФункции

//sscc -------------------------------------------------------------------------------

//обработчики таблицы "Товарные позиции"

&НаКлиенте
Процедура ТоварныеПозицииВыбор(Элемент, ВыбраннаяСтрокаИдентификатор, Поле, СтандартнаяОбработка)
	
	Если ТолькоПросмотрСообщения ИЛИ Сообщение.Направление = "Входящее" Тогда
		Возврат;
	КонецЕсли;
	
	Значение = Новый Массив;
	Значение.Добавить(ТоварныеПозиции.НайтиПоИдентификатору(ВыбраннаяСтрокаИдентификатор));
	
	ПараметрыПеретаскивания = Новый Структура;
	ПараметрыПеретаскивания.Вставить("Действие",			ДействиеПеретаскивания.Перемещение);
	ПараметрыПеретаскивания.Вставить("ДопустимыеДействия",	ДопустимыеДействияПеретаскивания.КопированиеИПеремещение);
	ПараметрыПеретаскивания.Вставить("Значение",			Значение);
	
	СтрокаТранспортнойУпаковки = Неопределено;
	Если Не Элементы.ТранспортныеУпаковки.ТекущаяСтрока = Неопределено Тогда
		СтрокаТранспортнойУпаковки 	= ТранспортныеУпаковки.НайтиПоИдентификатору(Элементы.ТранспортныеУпаковки.ТекущаяСтрока);
	КонецЕсли;
	КолонкаТранспортнойУпаковки	= Элементы.ТранспортныеУпаковки.ПодчиненныеЭлементы.ТранспортныеУпаковкиСоставУпаковки;
	
	ТранспортныеУпаковкиПеретаскивание(Элементы.ТоварныеПозиции, ПараметрыПеретаскивания, Истина, СтрокаТранспортнойУпаковки, КолонкаТранспортнойУпаковки)	
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварныеПозицииПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
	ПеретаскиваемаяСтрока = ПараметрыПеретаскивания.Значение[0];
	Если ТипЗнч(ПараметрыПеретаскивания.Значение[0]) = Тип("Число") Тогда
		ПеретаскиваемаяСтрока = ТоварныеПозиции.НайтиПоИдентификатору(ПараметрыПеретаскивания.Значение[0]);
	КонецЕсли; 
	
	ВнутреннееПеретаскивание = ПеретаскиваемаяСтрока.Свойство("Номенклатура");
	
	Если ВнутреннееПеретаскивание Тогда
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
	КонецЕсли;
	
	Если СообщениеСодержитДанныеОбУпаковках Тогда
		СтандартнаяОбработка = Истина;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ТоварныеПозицииПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	Для Каждого Строка Из ПараметрыПеретаскивания.Значение Цикл
		УдалитьСтрокуТранспортнойУпаковки(Строка);
	КонецЦикла;
	
	ОбновитьДанныеРазмещения();
	
КонецПроцедуры

//обработчики таблицы "Транспортные упаковки"

&НаКлиенте
Процедура ТранспортныеУпаковкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если ТолькоПросмотрСообщения ИЛИ Сообщение.Направление = "Входящее" Тогда
		Возврат;
	КонецЕсли;
	
	ТекДанные = Элемент.ТекущиеДанные;
	
	Если Поле.Имя = "ТранспортныеУпаковкиСоставУпаковки" И Поле.Доступность Тогда
		УдаляемаяСтрока = ТранспортныеУпаковки.НайтиПоИдентификатору(Элементы.ТранспортныеУпаковки.ТекущаяСтрока);
		УдалитьСтрокуТранспортнойУпаковки(УдаляемаяСтрока);
		ОбновитьДанныеРазмещения();
	ИначеЕсли Поле.Имя = "ТранспортныеУпаковкиКоличество" 
		И (ТекДанные.ТипУпаковки = "201" ИЛИ ТекДанные.ТипУпаковки = "202") Тогда
		СтандартнаяОбработка = Ложь;
	ИначеЕсли НЕ (ТекДанные.ТипУпаковки = "201" ИЛИ ТекДанные.ТипУпаковки = "202") 
		И НЕ (Поле.Имя = "ТранспортныеУпаковкиКоличество" 
		ИЛИ Поле.Имя = "ТранспортныеУпаковкиДопИдентификатор") Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортныеУпаковкиТемпературныйРежимПриИзменении(Элемент)
	
	Элементы.ТранспортныеУпаковки.ТекущиеДанные.ТребуетсяТемпературныйРежим = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортныеУпаковкиТребуетсяТемпературныйРежимПриИзменении(Элемент)
	
	ТекДанные = Элементы.ТранспортныеУпаковки.ТекущиеДанные;
	
	Если НЕ ЗначениеЗаполнено(ТекДанные.ТипУпаковки) Тогда
		ТекДанные.ТребуетсяТемпературныйРежим = Ложь;	
	КонецЕсли;
	
	Если НЕ ТекДанные.ТребуетсяТемпературныйРежим Тогда
		ТекДанные.ТемпературныйРежим = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортныеУпаковкиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортныеУпаковкиПередУдалением(Элемент, Отказ)
	
	//отказываемся от стандартного удаления из-за падения платформы в некоторых случаях
	Отказ = Истина;
	
	//вместо стандартного удаления рекурсивно удаляем с нижнего уровня наверх
	ВыделенныеСтроки = Элементы.ТранспортныеУпаковки.ВыделенныеСтроки;
	Количество = ВыделенныеСтроки.Количество();
	Для К = 1 По Количество Цикл
		УдалитьСтрокуТранспортнойУпаковки(ТранспортныеУпаковки.НайтиПоИдентификатору(ВыделенныеСтроки[К-1]));
	КонецЦикла;
	ОбновитьДанныеРазмещения();
	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортныеУпаковкиПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	ОбновитьДанныеРазмещения();
	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортныеУпаковкиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ТекущаяСтрока = ТранспортныеУпаковки.НайтиПоИдентификатору(Элемент.ТекущаяСтрока);
	
	НовоеКоличество = ТекущаяСтрока.Количество;
	
	//проверим не ввел ли пользователь 0
	Если НовоеКоличество = 0 И ТекущаяСтрока.SSCC = "" Тогда
		УдалитьСтрокуТранспортнойУпаковки(ТекущаяСтрока);
		ОбновитьДанныеРазмещения();
		Возврат;
	КонецЕсли;
	
	ОбщееКоличество 		= ПолучитьОбщееКоличествоТоварныхПозиций(ТекущаяСтрока);
	РазмещенноеКоличество   = ПолучитьРазмещенноеКоличество(ТекущаяСтрока) - НовоеКоличество;
	ДоступноеКоличество		= ОбщееКоличество - РазмещенноеКоличество;
	
	Если НовоеКоличество > ДоступноеКоличество Тогда
		ТекущаяСтрока.Количество = ДоступноеКоличество;
	Иначе
		ТекущаяСтрока.Количество = НовоеКоличество;
	КонецЕсли;
	
	ОбновитьДанныеРазмещения();
	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортныеУпаковкиПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	пСтрока = ?(ТипЗнч(Строка)=Тип("Число"),ТранспортныеУпаковки.НайтиПоИдентификатору(Строка),Строка);
	
	СтрокаПриемник = пСтрока;
	
	//еще не добавлена ни одна упаковка
	Если ТранспортныеУпаковки.ПолучитьЭлементы().Количество() = 0 Тогда
		СтрокаПриемник = ДобавитьНовуюУпаковку();
	КонецЕсли;
	
	//пользователь разместил товарную позицию в пустое место (не попал ни в одну строку)
	Если СтрокаПриемник = Неопределено Тогда
		СтрокаПриемник = НайтиРекурсивноНижнююСтроку(ТранспортныеУпаковки.ПолучитьЭлементы());		
	КонецЕсли;
	
	//не позволим добавить товар в товар
	Если Не ЗначениеЗаполнено(СтрокаПриемник.ТипУпаковки) Тогда
		СтрокаПриемник = СтрокаПриемник.ПолучитьРодителя();
	КонецЕсли;
	
	МассовоеДействие = (ПараметрыПеретаскивания.Значение.Количество() > 1);
	Для Каждого Значение Из ПараметрыПеретаскивания.Значение Цикл
		пЗначение = ?(ТипЗнч(Значение)=Тип("Число"),ТранспортныеУпаковки.НайтиПоИдентификатору(Значение),Значение);
		ПеремещениеМеждуУпаковками = пЗначение.Свойство("ТипУпаковки");
		Если ПеремещениеМеждуУпаковками Тогда
			ПереместитьТоварнуюПозицию(пЗначение, СтрокаПриемник, МассовоеДействие);	
		Иначе
		    РазместитьТоварнуюПозицию(пЗначение, СтрокаПриемник, МассовоеДействие);
		КонецЕсли;
	КонецЦикла;
	
	ОбновитьДанныеРазмещения();
	 	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортныеУпаковкиПослеУдаления(Элемент)
	
	ОбновитьДанныеРазмещения();	
	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортныеУпаковкиПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	//заменяем переменную "Строка" на "пСтрока" = ?(ТипЗнч(Строка)=Тип("Число"),ТранспортныеУпаковки.НайтиПоИдентификатору(Строка),Строка)
	//поскольку при перетаскивании из Товарных позиций = это ДанныеФормыЭлементДерева, а в рамках Транспортных упаковок - идентификатор
	
	Перем пСтрока;
	
	СтандартнаяОбработка = Ложь;
	
	пСтрока = ?(ТипЗнч(Строка)=Тип("Число"),ТранспортныеУпаковки.НайтиПоИдентификатору(Строка),Строка);
	
	ПеретаскиваемаяСтрока = ПараметрыПеретаскивания.Значение[0];
	Если ТипЗнч(ПараметрыПеретаскивания.Значение[0]) = Тип("Число") Тогда
		ПеретаскиваемаяСтрока = ТранспортныеУпаковки.НайтиПоИдентификатору(ПараметрыПеретаскивания.Значение[0]);
	КонецЕсли;	
	
	ВнутреннееПеретаскивание = ПеретаскиваемаяСтрока.Свойство("ТипУпаковки");
	
	//запрещаем перетаскивание групп
	Если ВнутреннееПеретаскивание И ЗначениеЗаполнено(ПеретаскиваемаяСтрока.ТипУпаковки) Тогда                                                                                                         
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
	КонецЕсли;
	
	//запрещаем перетаскивание в свою же группу
	Если ВнутреннееПеретаскивание И Не пСтрока = Неопределено И ЗначениеЗаполнено(пСтрока.ТипУпаковки) Тогда
		Если ПеретаскиваемаяСтрока.ПолучитьРодителя() = пСтрока Тогда
			ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
		КонецЕсли;                                                                                                                                
    КонецЕсли;
		
	//запрещаем перетаскивание в товар своей же группы
	Если ВнутреннееПеретаскивание И Не пСтрока = Неопределено И Не ЗначениеЗаполнено(пСтрока.ТипУпаковки) Тогда
		Если ПеретаскиваемаяСтрока.ПолучитьРодителя() = пСтрока.ПолучитьРодителя() Тогда
			ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
 
//команды страницы "Данные по упаковкам"                                                                                                                     

&НаКлиенте
Процедура ДобавитьУпаковку(Команда)
	
	ДобавитьНовуюУпаковку();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВложеннуюУпаковку(Команда)
	
	ТекСтрока = Элементы.ТранспортныеУпаковки.ТекущиеДанные;
	
	Если ТекСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекСтрока.ТипУпаковки) Тогда
		СтрокаПалеты = ПолучитьУпаковкуВерхнегоУровня(ТекСтрока);
		Если СтрокаПалеты.ПолучитьЭлементы().Количество() = 1 Тогда
			ТекСтрока = СтрокаПалеты.ПолучитьЭлементы()[0];
		Иначе
			ТекстПредупреждения = "Выберите строку товара для создания вложенной упаковки";
			Если Параметры.МодальностьЗапрещена = Истина Тогда
				Выполнить("ПоказатьПредупреждение(,ТекстПредупреждения)");
			Иначе
				Предупреждение(ТекстПредупреждения);
			КонецЕсли;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	Если НЕ ТекСтрока.ПолучитьЭлементы().Количество() = 0 Тогда
		// пока пусть будет только одна вложенная упаковка
		Возврат;
	КонецЕсли;

	ТипВложеннойУпаковки = "CS"; // заменить на функцию при необходимости
	
	НоваяСтрока = ДобавитьНовуюУпаковку(ТекСтрока, ТипВложеннойУпаковки);
	ВыделитьЯчейку(НоваяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьКоличествоВУпаковке(Команда)
	
	ТекСтрока = Элементы.ТранспортныеУпаковки.ТекущиеДанные;
	
	Если ТекСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если ЗначениеЗаполнено(ТекСтрока.ТипУпаковки) Тогда
		Возврат
	КонецЕсли;
	
	ВыделитьЯчейку(ТекСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьУпаковку(Команда)
	
	ТекСтрока = Элементы.ТранспортныеУпаковки.ТекущиеДанные;
	
	Если ТекСтрока = Неопределено ИЛИ ТоварныеПозиции.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(ТекСтрока.ТипУпаковки) Тогда
		КопируемаяУпаковка = ТекСтрока.ПолучитьРодителя();
	Иначе
		КопируемаяУпаковка = ТекСтрока;
	КонецЕсли;
	
	КопируемыеСтроки = КопируемаяУпаковка.ПолучитьЭлементы();
	НоваяУпаковкаДобавлена = Ложь;
	
	// найти строки с таким товаром в ТоварныеПозиции
	Для каждого Стр Из КопируемыеСтроки Цикл
		
		НайденныеСтроки = ТоварныеПозиции.НайтиСтроки(Новый Структура("НомерСтроки", Стр.НомерСтроки));
		
		Если НЕ НайденныеСтроки.Количество() = 0 Тогда
			
			Если НЕ НоваяУпаковкаДобавлена Тогда
				НоваяУпаковка = ДобавитьНовуюУпаковку(, , КопируемаяУпаковка);
				НоваяУпаковкаДобавлена = Истина;
			КонецЕсли;
			
			Если НайденныеСтроки[0].Количество > Стр.Количество Тогда
				РазместитьТоварнуюПозицию(НайденныеСтроки[0], НоваяУпаковка, ,Стр.Количество);
			Иначе
				РазместитьТоварнуюПозицию(НайденныеСтроки[0], НоваяУпаковка, ,НайденныеСтроки[0].Количество);
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Если НоваяУпаковкаДобавлена Тогда
		ОбновитьДанныеРазмещения();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьУпаковку(Команда)
	
	ВыделенныеСтроки = Элементы.ТранспортныеУпаковки.ВыделенныеСтроки;
	Количество = ВыделенныеСтроки.Количество();
	Для К = 1 По Количество Цикл
		УдаляемаяСтрока = ТранспортныеУпаковки.НайтиПоИдентификатору(ВыделенныеСтроки[К-1]);
		УдалитьСтрокуТранспортнойУпаковки(УдаляемаяСтрока);
	КонецЦикла;
	
	ОбновитьДанныеРазмещения();
	
КонецПроцедуры

&НаКлиенте
Процедура НапечататьПалетныйЛист(Команда)
	
	Справка = "Палетные листы можно напечатать после сохранения или отправки уведомления об отгрузке. 
			  |
			  |Для групповой печати необходимо:
			  |		1. Закрыть карточку сообщения - перейти в основной список модуля.
			  |		2. Выделить строки с сообщениями, по которым необходимо распечатать палетные листы.
			  |		3. В верхней панели кнопок модуля нажать ""Еще"" -> ""Распечатать палетные листы"".
			  |
			  |Для печати палетных листов из карточки сообщения необходимо установить расширение 
			  |""Печать палетных листов из карточки DESADV"".
			  |Подробнее: Торговая сеть -> Настройки -> Данные об упаковках (SSCC).";
			  
 	Если Параметры.МодальностьЗапрещена = Истина Тогда
		Выполнить("ПоказатьПредупреждение(,Справка)");
	Иначе
		Предупреждение(Справка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаПеренестиВправо(Команда)
	
	ВыделенныеСтроки = Элементы.ТоварныеПозиции.ВыделенныеСтроки;
	КоличествоВыделенныхСтрок = ВыделенныеСтроки.Количество();
	
	Для К = 1 По КоличествоВыделенныхСтрок Цикл
		ТоварныеПозицииВыбор(Элементы.ТоварныеПозиции, ВыделенныеСтроки[К-1], Элементы.ТоварныеПозиции.ПодчиненныеЭлементы.ТоварныеПозицииНомерСтроки, Истина);	
	КонецЦикла;
	
	// уберем фокус с поля количество, чтобы не предлагать редактировать количество
	Если КоличествоВыделенныхСтрок > 1 Тогда
		КолонкаФокуса = Элементы.ТранспортныеУпаковки.ПодчиненныеЭлементы.ТранспортныеУпаковкиСоставУпаковки;
		Элементы.ТранспортныеУпаковки.ТекущийЭлемент = КолонкаФокуса;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаПеренестиВсеВправо(Команда)
	
	КоличествоТоварныхПозиций = ТоварныеПозиции.Количество();
	
	Пока Не ТоварныеПозиции.Количество() = 0 Цикл
		ТоварныеПозицииВыбор(Элементы.ТоварныеПозиции, ТоварныеПозиции[0].ПолучитьИдентификатор(), Элементы.ТоварныеПозиции.ПодчиненныеЭлементы.ТоварныеПозицииНомерСтроки, Истина);	
	КонецЦикла;
	
	// уберем фокус с поля количество, чтобы не предлагать редактировать количество
	Если КоличествоТоварныхПозиций > 1 Тогда
		КолонкаФокуса = Элементы.ТранспортныеУпаковки.ПодчиненныеЭлементы.ТранспортныеУпаковкиСоставУпаковки;
		Элементы.ТранспортныеУпаковки.ТекущийЭлемент = КолонкаФокуса;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура КнопкаПеренестиВлево(Команда)
	
	ВыделенныеСтроки = Элементы.ТранспортныеУпаковки.ВыделенныеСтроки;
	Количество = ВыделенныеСтроки.Количество();
	Для К = 1 По Количество Цикл
		УдаляемаяСтрока = ТранспортныеУпаковки.НайтиПоИдентификатору(ВыделенныеСтроки[К-1]);
		УдалитьСтрокуТранспортнойУпаковки(УдаляемаяСтрока);
	КонецЦикла;
	
	ОбновитьДанныеРазмещения();
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаПеренестиВсеВлево(Команда)
	
	Количество = ТранспортныеУпаковки.ПолучитьЭлементы().Количество();
	Для К = 1 По Количество Цикл
		УдалитьСтрокуТранспортнойУпаковки(ТранспортныеУпаковки.ПолучитьЭлементы()[0]);	
	КонецЦикла;	
	ОбновитьДанныеРазмещения();	
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаРазделитьПоПалетам(Команда)
	
	ТекДанные = Элементы.ТоварныеПозиции.ТекущиеДанные;
	
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НомерСтроки = ТекДанные.НомерСтроки;
	КоличествоРаспределить = ТекДанные.Количество;
	КоличествоНаПалете = 0;
	
	ТекстПодсказка = "Сколько на палете?";
	ДопПараметрДляПередачиВОбработчик = Новый Структура("НомерСтроки,КоличествоРаспределить", НомерСтроки, КоличествоРаспределить);
	
	МодульОбъектаКлиент().ПоказатьВводЧисла_КонтурEDI("ОбработчикВводаКоличествоНаПалете", ЭтаФорма, ДопПараметрДляПередачиВОбработчик, КоличествоНаПалете, ТекстПодсказка, 12, 0);

КонецПроцедуры

&НаКлиенте
Процедура ОбработчикВводаКоличествоНаПалете(КоличествоНаПалете, ДопПараметрОбработчика = Неопределено) Экспорт
	
	Если КоличествоНаПалете = Неопределено 
		ИЛИ КоличествоНаПалете <= 0 Тогда
		
		Возврат;
		
	КонецЕсли;
	
	НомерСтроки = ДопПараметрОбработчика.НомерСтроки;
	КоличествоРаспределить = ДопПараметрОбработчика.КоличествоРаспределить;	
	
	Пока КоличествоРаспределить > 0 Цикл
		
		НайденныеСтроки = ТоварныеПозиции.НайтиСтроки(Новый Структура("НомерСтроки", НомерСтроки));
		
		Если НЕ НайденныеСтроки.Количество() = 0 Тогда
			
			НоваяУпаковка 	= ДобавитьНовуюУпаковку();
			
			Если КоличествоРаспределить > КоличествоНаПалете Тогда
				РазместитьТоварнуюПозицию(НайденныеСтроки[0], НоваяУпаковка, ,КоличествоНаПалете);
			Иначе
				РазместитьТоварнуюПозицию(НайденныеСтроки[0], НоваяУпаковка, ,КоличествоРаспределить);
			КонецЕсли;
			
			КоличествоРаспределить = КоличествоРаспределить - КоличествоНаПалете;
		КонецЕсли;
	КонецЦикла;
	
	ОбновитьДанныеРазмещения();
	
КонецПроцедуры

&НаКлиенте
Функция ДобавитьНовуюУпаковку(Родитель = Неопределено, ТипУпаковки = Неопределено, КопируемаяУпаковка = Неопределено)
	
	Если Родитель = Неопределено Тогда
		НоваяСтрока = ТранспортныеУпаковки.ПолучитьЭлементы().Добавить()
	Иначе
		НоваяСтрока = Родитель.ПолучитьЭлементы().Добавить();
	КонецЕсли;
	
	Если КопируемаяУпаковка = Неопределено Тогда
		
		Если ТипУпаковки = Неопределено Тогда
			НоваяСтрока.ТипУпаковки	= ТипУпаковкиПоУмолчанию;	
		Иначе
			НоваяСтрока.ТипУпаковки = ТипУпаковки;
		КонецЕсли;
		
	Иначе
		
		СвойстваДляКопирования = 
		"ВесБрутто," + 
		"ВесНетто," + 
		"Высота," + 
		"Длина," + 
		"КодЕдиницыИзмеренияВесаБрутто," + 
		"КодЕдиницыИзмеренияВесаНетто," + 
		"КодЕдиницыИзмеренияВысоты," +
		"КодЕдиницыИзмеренияДлины," +
		"КодЕдиницыИзмеренияШирины," +
		"ТемпературныйРежим," +
		"ТипУпаковки," +
		"ТребуетсяТемпературныйРежим," +
		"Ширина";
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, КопируемаяУпаковка, СвойстваДляКопирования);
				
	КонецЕсли;
	
	УстановитьРазмерыУпаковки(НоваяСтрока);
	ПронумероватьУпаковкиСквознымПорядковымНомером();
	ПроименоватьУпаковки(ТранспортныеУпаковки.ПолучитьЭлементы());
	
	ВыделитьСтроку(НоваяСтрока);
		
	Возврат НоваяСтрока;
		
КонецФункции

&НаКлиенте
Процедура УстановитьРазмерыУпаковки(Строка)
	
	Если НЕ ИспользоватьДопПараметрыУпаковки Тогда
		Возврат;
	КонецЕсли;
	
	// евро-палета
	Если Строка.ТипУпаковки = "201" Тогда
		Строка.Длина = 1.2;
		Строка.КодЕдиницыИзмеренияДлины = "MTR";
		Строка.Ширина = 0.8;
		Строка.КодЕдиницыИзмеренияШирины = "MTR";
	// стандартная палета
	ИначеЕсли Строка.ТипУпаковки = "202" Тогда
		Строка.Длина = 1.2;
		Строка.КодЕдиницыИзмеренияДлины = "MTR";
		Строка.Ширина = 1;
		Строка.КодЕдиницыИзмеренияШирины = "MTR";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РазместитьТоварнуюПозицию(Позиция, Упаковка, МассовоеДействие = Истина, РазмещаемоеКоличество = 0)
	
	НоваяСтрока = Неопределено;
	Для Каждого Строка Из Упаковка.ПолучитьЭлементы() Цикл
		Если Строка.НомерСтроки = Позиция.НомерСтроки Тогда
			НоваяСтрока = Строка;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	РазмещаемоеКоличество = ?(РазмещаемоеКоличество = 0, Позиция.Количество, РазмещаемоеКоличество);
		
	Если НоваяСтрока = Неопределено Тогда
		НоваяСтрока = Упаковка.ПолучитьЭлементы().Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Позиция);
		НоваяСтрока.СоставУпаковки = ПолучитьРазвернутоеНаименованиеТовара(Позиция);
		НоваяСтрока.Количество = РазмещаемоеКоличество;
	Иначе
		НоваяСтрока.Количество = НоваяСтрока.Количество + РазмещаемоеКоличество; 
	КонецЕсли;
	
	Если МассовоеДействие Тогда
		ВыделитьСтроку(НоваяСтрока);
	Иначе
		ВыделитьЯчейку(НоваяСтрока);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьТоварнуюПозицию(Позиция, Упаковка, МассовоеДействие = Истина)
	
	Для Каждого Товар Из Упаковка.ПолучитьЭлементы() Цикл
		Если Товар.НомерСтроки = Позиция.НомерСтроки Тогда
			НоваяСтрока = Товар;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если НоваяСтрока = Неопределено Тогда
		НоваяСтрока = Упаковка.ПолучитьЭлементы().Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Позиция);
	Иначе
		НоваяСтрока.Количество = НоваяСтрока.Количество + Позиция.Количество;
	КонецЕсли;
	
	Если Позиция.ПолучитьРодителя() = Неопределено Тогда
		ТранспортныеУпаковки.ПолучитьЭлементы().Удалить(Позиция);
	Иначе
		Позиция.ПолучитьРодителя().ПолучитьЭлементы().Удалить(Позиция);
	КонецЕсли;
		
	ВыделитьСтроку(НоваяСтрока);
			
КонецПроцедуры

&НаКлиенте
Функция ДобавитьНераспределеннуюТоварнуюПозицию(Строка)
	
	НоваяСтрока = ТоварныеПозиции.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока,Строка);
	
	Возврат НоваяСтрока;
	
КонецФункции

&НаКлиенте
Функция УдалитьСтрокуТранспортнойУпаковки(УдаляемаяСтрока = Неопределено, ЗапроситьПодтверждение = Ложь)
	
	МожноУдалятьСтроку = Истина;
	
	Если УдаляемаяСтрока = Неопределено Тогда
		СП = Новый СообщениеПользователю;
		СП.Текст = "Не выбрана удаляемая строка.";
		СП.Поле = "ТранспортныеУпаковки";
		СП.Сообщить();
		МожноУдалятьСтроку = Ложь;
		Возврат МожноУдалятьСтроку;
	КонецЕсли;
	
	//проверим, существует ли строка, не удалена ли она уже
	ЭтоУпаковка 	 = Истина;
	
	Попытка
		ЭтоУпаковка = ЗначениеЗаполнено(УдаляемаяСтрока.ТипУпаковки);
		СтрокаСуществует = Истина;
	Исключение
		СтрокаСуществует = Ложь;	
	КонецПопытки;
	
	Если Не СтрокаСуществует Тогда
		Возврат Истина;		
	КонецЕсли;
	
	Если ЗапроситьПодтверждение Тогда
		Ответ = Вопрос("Удалить выбранную " + ?(ЭтоУпаковка, "упаковку", "товарную позицию") + "?",РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет, "Контур.EDI");
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			МожноУдалятьСтроку = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если МожноУдалятьСтроку Тогда
		УдалитьСтрокуРекурсивно(УдаляемаяСтрока);
	КонецЕсли;
		
	Возврат МожноУдалятьСтроку;
			
КонецФункции

&НаКлиенте
Процедура УдалитьСтрокуРекурсивно(ТекущаяСтрока)
	
	//проваливаемся вложенные строки
	Количество  = ТекущаяСтрока.ПолучитьЭлементы().Количество() - 1;
	Для К = 0 По Количество Цикл
		Строка = ТекущаяСтрока.ПолучитьЭлементы()[0];
		Если Не Строка.ПолучитьЭлементы().Количество() = 0 Тогда
			УдалитьСтрокуРекурсивно(Строка);
		Иначе
			//удаляем текущую строку
			Если Не Строка.ПолучитьРодителя() = Неопределено Тогда
				ВеткаСтроки = Строка.ПолучитьРодителя().ПолучитьЭлементы();
			Иначе
				ВеткаСтроки = ТранспортныеУпаковки.ПолучитьЭлементы();
			КонецЕсли;
			ВеткаСтроки.Удалить(Строка);
			Продолжить;
		КонецЕсли;
	КонецЦикла;
	
	//удаляем родительскую строку
	Если Не ТекущаяСтрока.ПолучитьРодителя() = Неопределено Тогда
		ВеткаСтроки = ТекущаяСтрока.ПолучитьРодителя().ПолучитьЭлементы();
	Иначе
		ВеткаСтроки = ТранспортныеУпаковки.ПолучитьЭлементы();
	КонецЕсли;
	ВеткаСтроки.Удалить(ТекущаяСтрока);
	
КонецПроцедуры

//Удаление сообщения
&НаКлиенте
Процедура УдалитьСообщение(Команда)
	
	Если НЕ ЭтоПоследнееСообщениеВЦепочкеВызовСервера() Тогда
		ТекстПредупреждения="Можно удалять только последнее сообщение в цепочке!";
		ДопПараметрДляПередачиВОбработчик=Неопределено;
		Если Параметры.МодальностьЗапрещена Тогда 
			Выполнить("ПоказатьПредупреждение(Новый ОписаниеОповещения(""ОбработчикНеПоследнееСообщениеОзнакомление"", ЭтаФорма, ДопПараметрДляПередачиВОбработчик),ТекстПредупреждения,,""Контур.EDI"")");
		Иначе
			Предупреждение(ТекстПредупреждения,,"Контур.EDI");
			ОбработчикНеПоследнееСообщениеОзнакомление(ДопПараметрДляПередачиВОбработчик);
		КонецЕсли;
	Иначе
		ВерныйПароль = Ложь;
		ТекПароль = "";
		ТекстПросьбы = "Введите пароль от учетной записи";
		ДопПараметрДляПередачиВОбработчик=Неопределено;
		
		Если Параметры.МодальностьЗапрещена Тогда 
			Выполнить("ПоказатьВводСтроки(Новый ОписаниеОповещения(""ОбработчикВводаПароля"", ЭтаФорма, ДопПараметрДляПередачиВОбработчик), ТекПароль, ТекстПросьбы, , ложь)"); 
		Иначе
			Если ВвестиСтроку(ТекПароль,ТекстПросьбы) Тогда
				ОбработчикВводаПароля(ТекПароль);	
			иначе
				//nop
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикНеПоследнееСообщениеОзнакомление(ДопПараметр=Неопределено)Экспорт

//nop	

КонецПроцедуры // ОбработчикПоследнееСообщениеОзнакомление()

&НаКлиенте
Процедура ОбработчикВводаПароля(ТекПароль,ДопПараметрОбработчика=Неопределено) Экспорт
	
	ТекущийПарольПользователя=ПолучитьПарольВызовСервера();
	
	    ВерныйПароль=Ложь;
	 	Если СокрЛП(ТекПароль) = СокрЛП(ТекущийПарольПользователя) или СокрЛП(ТекПароль)="******" Тогда
			ВерныйПароль = Истина;
		КонецЕсли;
		Если НЕ ВерныйПароль и ТекПароль=Неопределено Тогда
			Возврат;  //отказ от ввода
		ИначеЕсли НЕ ВерныйПароль Тогда
			Сообщить("Неверный пароль");
			Возврат;
		КонецЕсли;
		
		ТекстВопроса ="Текущее сообщение будет навсегда удалено из 1С. Продолжить?";
		КнопкиВопроса=новый СписокЗначений;
		КнопкиВопроса.Добавить("Да, удалить сообщение");
		КнопкиВопроса.Добавить("Нет, не удалять");
		ДопПараметрДляПередачиВОбработчик=Неопределено;
		РезультатВопроса = Неопределено;
		
		Если Параметры.МодальностьЗапрещена Тогда
			Выполнить("ПоказатьВопрос(Новый ОписаниеОповещения(""ОбработчикСогласияУдаленияСообщения"", ЭтаФорма, ДопПараметрДляПередачиВОбработчик), ТекстВопроса, КнопкиВопроса,,,""Контур.EDI"")");
		Иначе
			РезультатВопроса = Вопрос(ТекстВопроса, КнопкиВопроса,,,"Контур.EDI");
			ОбработчикСогласияУдаленияСообщения(РезультатВопроса,ДопПараметрДляПередачиВОбработчик);
		КонецЕсли;
	

КонецПроцедуры // ОбработчикВводаПароля()

&НаКлиенте
Процедура ОбработчикСогласияУдаленияСообщения(РезультатВопроса,ДопПараметр=Неопределено) Экспорт

	Если РезультатВопроса = "Да, удалить сообщение" Тогда 
		УдалитьСообщениеВызовСервера();
		ЗакрытиеРазрешено=Истина;
		ЭтаФорма.Закрыть(неопределено);
	КонецЕсли;

КонецПроцедуры // ОбработчикСогласияУдаленияСообщения()

&НаСервере
Функция ПолучитьПарольВызовСервера()

Возврат МодульОбъекта().ПараметрыПользователяEDI.Пароль;	

КонецФункции // ПолучитьПарольВызовСервера()

&НаСервере
Процедура УдалитьСообщениеВызовСервера()
	
	Если НЕ ЗначениеЗаполнено(Сообщение.ДокументСсылка) Тогда
		Сообщение.ДокументСсылка = Сообщение.Документ1С;
	КонецЕсли;
	
	МодульОбъекта().УдалитьСообщение(Сообщение);

КонецПроцедуры // УдалитьСообщениеВызовСервера()

&НаСервере
Функция ЭтоПоследнееСообщениеВЦепочкеВызовСервера()

	Возврат МодульОбъекта().ЭтоПоследнееСообщениеВЦепочке(Сообщение.СообщениеСсылка, Сообщение.ДокументСсылка) = Истина;	

КонецФункции // ЭтоПоследнееСообщениеВЦепечкеВызовСервера()

//конец Удаление сообщения

&НаКлиенте
Процедура Накладная1СПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Накладная1С) Тогда
		
		РезультатПроверкиНакладной = ПриПопыткеИзмененияНакладнойВСообщенииВызовСервера();
		
		Если НЕ РезультатПроверкиНакладной.Успешно Тогда
			
			ТекстПредупреждения=РезультатПроверкиНакладной.ТекстОшибки;
			ДопПараметрДляПередачиВОбработчик=Неопределено;
			Если Параметры.МодальностьЗапрещена Тогда 
				Выполнить("ПоказатьПредупреждение(,ТекстПредупреждения,,""Контур.EDI"")");
			Иначе
				Предупреждение(ТекстПредупреждения,,"Контур.EDI");
			КонецЕсли;
			
			Накладная1С = Неопределено;
			Сообщение.Накладная1С = Неопределено;

			УстановитьТипПоля1С("Накладная");
			Возврат;	
		КонецЕсли;
		
	КонецЕсли;
	
	Сообщение.Накладная1С = Накладная1С;
	
	//сбросить цвет
	Элемент.ЦветФона = новый Цвет();
	ПроверитьЗаполнениеПолей();
	
КонецПроцедуры

&НаСервере
Функция ПриПопыткеИзмененияНакладнойВСообщенииВызовСервера()

Результат=МодульОбъекта().ПриПопыткеИзмененияНакладнойВСообщении(Сообщение,Накладная1С);

Возврат Результат;

КонецФункции // ПриПопыткеИзмененияНакладнойВСообщенииВызовСервера()

&НаКлиенте
Процедура Накладная1СНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если МыЯвляемсяПродавцомНаСервере(Сообщение) Тогда
		Организация = Сообщение.Продавец1С;
		Контрагент  = Сообщение.Покупатель1С;
	Иначе
		Организация = Сообщение.Покупатель1С;
		Контрагент  = Сообщение.Продавец1С;
	КонецЕсли;
		
	СтруктураОтбора = Новый Структура("Организация,Контрагент", Организация, Контрагент);
	
	СтруктураПараметров	= Новый Структура;
	СтруктураПараметров.Вставить("Отбор",              СтруктураОтбора);
	СтруктураПараметров.Вставить("РежимВыбора",        Истина);
	СтруктураПараметров.Вставить("МножественныйВыбор", Ложь);
	
	ТипДокумента = ОпределитьИмяТипаДокументаСервер(Документ1С, Сообщение);	

	ОткрытьФорму("Документ." + ТипДокумента + ".ФормаВыбора", СтруктураПараметров, Элемент);

КонецПроцедуры

&НаСервере
Функция МыЯвляемсяПродавцомНаСервере(Сообщение)
	
	Возврат МодульОбъекта().МыЯвляемсяПродавцом(Сообщение);
	
КонецФункции


&НаКлиенте
Процедура Заказ1СПриИзменении(Элемент)
	ПроверитьЗаполнениеПолей();
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьУведомлениеОПриемке(Команда)
	
	КнопкаДействияФормыНажатие_Клиент(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ПривязатьСообщение(Команда)
	//Перевод на УФ
	//Понять тип
	
	ТипДокумента = ОпределитьИмяТипаДокументаСервер(Документ1С, Сообщение);	
		//открыть отбор
	СтандартнаяОбработка=Ложь;
	
	Если Сообщение.ТипСообщения = "RETDES" ИЛИ (Сообщение.Направление = "Входящее" И Сообщение.ТипСообщения = "DESADV") Тогда
		ОрганизацияВСообщении = Сообщение.Покупатель1С;
		КонтрагентВСообщении = Сообщение.Продавец1С;
	Иначе
		ОрганизацияВСообщении = Сообщение.Продавец1С;
		КонтрагентВСообщении = Сообщение.Покупатель1С;
	КонецЕсли;
	
	СтруктураОтбора = Новый Структура("Организация,Контрагент", ОрганизацияВСообщении, КонтрагентВСообщении);
	
	СтруктураПараметров	= Новый Структура;
	СтруктураПараметров.Вставить("Отбор",              СтруктураОтбора);
	СтруктураПараметров.Вставить("РежимВыбора",        Истина);
	СтруктураПараметров.Вставить("МножественныйВыбор", Ложь);
	
	ИмяФормыВыбора = "Документ." + ТипДокумента + ".ФормаВыбора";
	
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI(ИмяФормыВыбора, "", СтруктураПараметров, ЭтаФорма, "Документ1СПриИзменении", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура Документ1СПриИзменении(ВыбранноеЗначение, ДопПараметр = Неопределено) Экспорт
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РезультатПривязки = СвязатьСообщениеСДокументомВызовСервера(ВыбранноеЗначение); 
	
	Если РезультатПривязки.Успешно = Ложь Тогда
		
		МодульОбъектаКлиент().Предупреждение_КонтурEDI(РезультатПривязки.ОписаниеОшибки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СвязатьСообщениеСДокументомВызовСервера(ВыбранноеЗначение)
	
	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("Успешно",Ложь);
	СтруктураРезультата.Вставить("ОписаниеОшибки","");
	
	Если НЕ МодульОбъекта().НайтиСообщениеДокумента(ВыбранноеЗначение,Сообщение.ТипСообщения) = Неопределено Тогда
		
		СтруктураРезультата.ОписаниеОшибки = "К данному документу уже привязано другое сообщение " + Сообщение.ТипСообщения;
		СтруктураРезультата.Успешно = Ложь;
		
		Возврат СтруктураРезультата;
		
	КонецЕсли;
		
	РезультатПроверки = МодульОбъекта().ОбработкаСобытияПодключаемогоМодуля("ПередСвязываниемСообщенияСДокументомЧерезФорму", ,
																			Новый Структура("Документ, Сообщение, Форма", 
																							ВыбранноеЗначение, Сообщение, ЭтаФорма));
							
	Если РезультатПроверки <> Неопределено
		И НЕ РезультатПроверки.Успешно Тогда
		
		СтруктураРезультата.ОписаниеОшибки = РезультатПроверки.ОписаниеОшибки;
		
		Возврат СтруктураРезультата;
		
	КонецЕсли;
							
	Документ1С = ВыбранноеЗначение;
	ВосстановитьТаблицыЗначенийПроизвольныхРеквизитовИзХранилища();
	
	МодульОбъекта().СвязатьСообщениеСДокументом(Сообщение,Документ1С);
	ТолькоПросмотрСообщения = Истина;
	Сообщение.ДокументСсылка	= Документ1С;
	Сообщение.Документ1С		= Документ1С;
	
	ПроизвестиПервоначальноеЗаполнениеПолейНаФорме();

	ПоместитьТаблицыЗначенийПроизвольныхРеквизитовВХранилище();
	УничтожитьТаблицыЗначенийПроизвольныхРеквизитов();
	
	СтруктураРезультата.Успешно = Истина;
	
	Возврат СтруктураРезультата;
	
КонецФункции

&НаСервере
Функция ОпределитьИмяТипаДокументаСервер(Ссылка, Сообщение)
	
	ТипДокумента = МодульОбъекта().ОпределитьИмяТипаДокумента(Ссылка, Сообщение);

	Возврат ТипДокумента;	

КонецФункции

&НаКлиенте
Процедура ПереотправитьСообщение(Команда)
	
	ПараметрыФормы = Новый Структура("СообщениеСсылка",			Сообщение.СообщениеСсылка); 
	ПараметрыФормы.Вставить("Документ1С",						Сообщение.Документ1С);
	ПараметрыФормы.Вставить("ТипСообщения",						Сообщение.ТипСообщения);
	ПараметрыФормы.Вставить("Направление",						Сообщение.Направление);
	ПараметрыФормы.Вставить("ТолькоПросмотр",					Ложь);
	ПараметрыФормы.Вставить("КодДействия",						"Отправить_");
	ПараметрыФормы.Вставить("РежимРаботы",						Параметры.РежимРаботы);
	ПараметрыФормы.Вставить("АдресХранилища",					Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект);
	ПараметрыФормы.Вставить("ПараметрыАвтотестирования",		Параметры.ПараметрыАвтотестирования);
	ПараметрыФормы.Вставить("ПереотправляемоеСообщениеСсылка",	Сообщение.СообщениеСсылка);
	
	Если НЕ Параметры.ПроизвольныеПараметры = Неопределено Тогда 
		ПараметрыФормы.Вставить("ПроизвольныеПараметры",	Параметры.ПроизвольныеПараметры);
	КонецЕсли;
	
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("ФормаСообщенияУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбработчикПослеЗакрытияКарточкиПереотправкиСообщения", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикПослеЗакрытияКарточкиПереотправкиСообщения(Параметр = Неопределено, ДопПараметр = Неопределено)Экспорт

	ЭтаФорма.Закрыть(неопределено);

КонецПроцедуры // ОбработчикПослеЗакрытияКарточкиПереотправкиСообщения()

&НаКлиенте
Процедура ПрекратитьОбработкуВходящегоСообщения(Команда)
	
	ТекстВопроса = "Обработка сообщения будет прекращена. Продолжить?";
	КнопкиВопроса = Новый СписокЗначений;
	КнопкиВопроса.Добавить("Да, прекратить обработку");
	КнопкиВопроса.Добавить("Отмена");
	ДопПараметрДляПередачиВОбработчик = Неопределено;
	РезультатВопроса = Неопределено;
	
	Если Параметры.МодальностьЗапрещена Тогда
		Выполнить("ПоказатьВопрос(Новый ОписаниеОповещения(""ОбработчикСогласияОстановкиОбработки"", ЭтаФорма, ДопПараметрДляПередачиВОбработчик), ТекстВопроса, КнопкиВопроса,,,""Контур.EDI"")");
	Иначе
		РезультатВопроса = Вопрос(ТекстВопроса, КнопкиВопроса,,,"Контур.EDI");
		ОбработчикСогласияОстановкиОбработки(РезультатВопроса,ДопПараметрДляПередачиВОбработчик);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикСогласияОстановкиОбработки(РезультатВопроса,ДопПараметрПереданныйВОбработчик = Неопределено) Экспорт
	
    Если РезультатВопроса = "Да, прекратить обработку" Тогда 
		ПрекратитьОбработкуВходящегоСообщенияВызовСервера();
		ЗакрытиеРазрешено = Истина;
    	ЭтаФорма.Закрыть(неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрекратитьОбработкуВходящегоСообщенияВызовСервера()

	МодульОбъекта().ПрекратитьОбработкуВходящегоСообщения(Сообщение.СообщениеСсылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрекратитьОбработкуНулевойСФ(Команда)
	
	ТекстВопроса ="Обработка счет-фактуры будет прекращена. Продолжить?";
	КнопкиВопроса=новый СписокЗначений;
	КнопкиВопроса.Добавить("Да, прекратить обработку");
	КнопкиВопроса.Добавить("Отмена");
	ДопПараметрДляПередачиВОбработчик=Неопределено;
	РезультатВопроса = Неопределено;
	
	Если Параметры.МодальностьЗапрещена Тогда
		Выполнить("ПоказатьВопрос(Новый ОписаниеОповещения(""ОбработчикСогласияОстановкиОбработкиНулевойСФ"", ЭтаФорма, ДопПараметрДляПередачиВОбработчик), ТекстВопроса, КнопкиВопроса,,,""Контур.EDI"")");
	Иначе
		РезультатВопроса = Вопрос(ТекстВопроса, КнопкиВопроса,,,"Контур.EDI");
		ОбработчикСогласияОстановкиОбработкиНулевойСФ(РезультатВопроса,ДопПараметрДляПередачиВОбработчик);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработчикСогласияОстановкиОбработкиНулевойСФ(РезультатВопроса,ДопПараметрПереданныйВОбработчик=Неопределено) Экспорт
	
	Если РезультатВопроса = "Да, прекратить обработку" Тогда 
		ПрекратитьОбработкуНулевойСФВызовСервера();
		ЗакрытиеРазрешено = Истина;
    	ЭтаФорма.Закрыть(неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрекратитьОбработкуНулевойСФВызовСервера()

	МодульОбъекта().ПрекратитьОбработкуНулевойСФ(Сообщение);
	УничтожитьТаблицыЗначенийПроизвольныхРеквизитов();
	
КонецПроцедуры // ПрекратитьОбработкуСообщенияВызовСервера()

&НаКлиенте
Процедура ЗакрытьФормуБезПримененияДействия(Команда)
	ЗакрытиеРазрешено=Истина;
	ЭтаФорма.Закрыть(Параметры.СообщениеСсылка);//возвращаем ту же самую ссылку что и пришла на входе - обработчик не будет вычеркивать строку
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	Если ЗакрытиеРазрешено=Неопределено Тогда
		СтандартнаяОбработка=Ложь;
		Отказ=Истина;
		ЗакрытиеРазрешено=Истина;
		ЭтаФорма.Закрыть(Параметры.СообщениеСсылка);//возвращаем ту же самую ссылку что и пришла на входе - обработчик не будет вычеркивать строку
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	ОсновнаяФорма().ПриЗакрытииФормы(ЭтаФорма, "ФормаСообщенияУправляемая");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСчетФактуру(Команда)
	
	КнопкаДействияФормыНажатие_Клиент(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортировкаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ=истина;
КонецПроцедуры

&НаКлиенте
Процедура ТранспортировкаПередУдалением(Элемент, Отказ)
	Отказ=истина;
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВрата(Команда)
	
	ВыбраннаяДата=Сообщение.ДатаПоставки;
	ПоказатьВводВрат(ВыбраннаяДата);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВрата(Команда)
	//ТекущиеВрата= определить!!!
	
	
	ТекущиеВрата= Сообщение.ДатаПоставки;
	
	ДопПараметры = новый Структура;
	ДопПараметры.Вставить("СтрокаСпискаВрат",Элементы.Транспортировка.ТекущаяСтрока);

	ПоказатьВводВрат(ТекущиеВрата,ДопПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВводВрат(ИсходнаяДата,ДопПараметры=неопределено)
	
	ВыбраннаяДата=ИсходнаяДата;
	Подсказка = "Укажите новые врата времени";
	ЧастьДаты = ЧастиДаты.ДатаВремя;
	//ДопПараметры = новый Структура;
	//ДопПараметры.Вставить("СтрокаСпискаВрат",неопределено);
	Если Параметры.МодальностьЗапрещена Тогда
		Оповещение = Неопределено;
		Выполнить("Оповещение = Новый ОписаниеОповещения(""ОбработчикПослеВводаВрат"",ЭтаФорма, ДопПараметры)");
		Выполнить("ПоказатьВводДаты(Оповещение, ВыбраннаяДата, Подсказка, ЧастьДаты)");
	Иначе
		Если ВвестиДату(ВыбраннаяДата,Подсказка,ЧастьДаты) Тогда 			
			ОбработчикПослеВводаВрат(ВыбраннаяДата, ДопПараметры);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // ПоказатьВводВрат()

&НаКлиенте
Процедура ОбработчикПослеВводаВрат(ВыбраннаяДата, ДопПараметры=Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(ВыбраннаяДата) Тогда
		Если ДопПараметры=Неопределено или ДопПараметры.СтрокаСпискаВрат=Неопределено Тогда 
			Транспортировка.Добавить(ВызовСервера_Дата1С_в_ДатаВремяXML(ВыбраннаяДата),ВыбраннаяДата,истина);
		Иначе
			Транспортировка[ДопПараметры.СтрокаСпискаВрат].Пометка=Истина;
			Транспортировка[ДопПараметры.СтрокаСпискаВрат].Значение=ВызовСервера_Дата1С_в_ДатаВремяXML(ВыбраннаяДата);
			Транспортировка[ДопПараметры.СтрокаСпискаВрат].Представление=ВыбраннаяДата;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьВысотуСпискаВрат()

КонецПроцедуры // ОбработчикПослеВводаВрат()

&НаСервере
Функция ВызовСервера_Дата1С_в_ДатаВремяXML(Дата)

Возврат МодульОбъекта().Дата1С_в_ДатаВремяXML(Дата);	

КонецФункции // ВызовСервера_Дата1С_в_ДатаВремяXML()

&НаКлиенте
Процедура УдалитьВрата(Команда)
	
	ВыделенныеСтроки=Элементы.Транспортировка.ВыделенныеСтроки;
	
	Количество = ВыделенныеСтроки.Количество();
	
	Если Количество=0 Тогда 
		Сообщить("Не выделены строки врат времени для удаления");	
	КонецЕсли;
	
	Для К = 1 По Количество Цикл
		УдаляемаяСтрока = Транспортировка.НайтиПоИдентификатору(ВыделенныеСтроки[К-1]);
		Транспортировка.Удалить(УдаляемаяСтрока);
	КонецЦикла;
	
	УстановитьВысотуСпискаВрат();
	
	Если Транспортировка.Количество() = 0 Тогда 
		ТекстПредупреждения="Внимание, для этого заказа необходимо указать хотя бы 1 врата времени."+Символы.ПС+"В противном случае торговая сеть не получит сведения о времени прибытия машины.";
		ДопПараметрДляПередачиВОбработчик=Неопределено;
		Если Параметры.МодальностьЗапрещена Тогда 
			Выполнить("ПоказатьПредупреждение(,ТекстПредупреждения,,""Контур.EDI"")");
		Иначе
			Предупреждение(ТекстПредупреждения,,"Контур.EDI");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВысотуСпискаВрат()

	//Пересчитать высоту
	Элементы.Транспортировка.Высота=Транспортировка.Количество()+1;
	Если  Элементы.Транспортировка.Высота<=1 Тогда 
		Элементы.Транспортировка.Высота = 2;
	КонецЕсли;

КонецПроцедуры // УстановитьВысотуСпискаВрат()

&НаКлиенте
Процедура ОтправитьКорректировочныйСчетФактуру(Команда)
	
	КнопкаДействияФормыНажатие_Клиент(Команда.Имя);   
	
КонецПроцедуры

&НаКлиенте
Процедура ПрекратитьОбработкуИсходящегоСообщения(Команда)

	ТекстВопроса = "Сообщение будет помечено как успешно доставленное. Продолжить?";
	КнопкиВопроса = Новый СписокЗначений;
	КнопкиВопроса.Добавить("Да, пометить сообщение как доставленное");
	КнопкиВопроса.Добавить("Отмена");
	ДопПараметрДляПередачиВОбработчик = Неопределено;
	РезультатВопроса = Неопределено;
	
	Если Параметры.МодальностьЗапрещена Тогда
		Выполнить("ПоказатьВопрос(Новый ОписаниеОповещения(""ОбработчикСогласияПометитьСообщениеКакДоставленное"", ЭтаФорма, ДопПараметрДляПередачиВОбработчик), ТекстВопроса, КнопкиВопроса,,,""Контур.EDI"")");
	Иначе
		РезультатВопроса = Вопрос(ТекстВопроса, КнопкиВопроса,,,"Контур.EDI");
		ОбработчикСогласияПометитьСообщениеКакДоставленное(РезультатВопроса,ДопПараметрДляПередачиВОбработчик);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикСогласияПометитьСообщениеКакДоставленное(РезультатВопроса,ДопПараметрПереданныйВОбработчик=Неопределено) Экспорт
	
    Если РезультатВопроса = "Да, пометить сообщение как доставленное" Тогда 
		ПометитьСообщениеКакДоставленноеВызовСервера();
		ЗакрытиеРазрешено = Истина;
    	ЭтаФорма.Закрыть(неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПометитьСообщениеКакДоставленноеВызовСервера()
	
	Если(Сообщение.Свойство("ПереотправляемоеСообщениеСсылка") 
		ИЛИ Сообщение.Свойство("СообщениеСсылка")) Тогда 
			Если Сообщение.Свойство("СообщениеСсылка") И ЗначениеЗаполнено(Сообщение.СообщениеСсылка) Тогда
				ТекСсылкаНаСообщение=Сообщение.СообщениеСсылка;
			ИначеЕсли Сообщение.Свойство("ПереотправляемоеСообщениеСсылка") И ЗначениеЗаполнено(Сообщение.ПереотправляемоеСообщениеСсылка) Тогда 
				ТекСсылкаНаСообщение=Сообщение.ПереотправляемоеСообщениеСсылка;
			КонецЕсли;
		
		МодульОбъекта().УстановитьСтатусСообщения(ТекСсылкаНаСообщение, , , "ИсходящийДоставлен"); // условие на наличие этого свойства есть в процедуре СоздатьКнопки()
	Иначе
		Сообщить("Не удалось прекратить обработку сообщения, возможно запись была изменена или удалена");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварныеПозицииНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
	
	Если Параметры.ТолькоПросмотр ИЛИ Сообщение.Направление = "Входящее" Тогда
		Выполнение = Ложь;
		Возврат;
	КонецЕсли;
	
	ПараметрыПеретаскивания.Действие 			= ДействиеПеретаскивания.Перемещение;
	ПараметрыПеретаскивания.ДопустимыеДействия 	= ДопустимыеДействияПеретаскивания.КопированиеИПеремещение;
	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортныеУпаковкиНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
	
	Если Параметры.ТолькоПросмотр ИЛИ Сообщение.Направление = "Входящее" Тогда
		Выполнение = Ложь;
		Возврат;
	КонецЕсли;
	 
	ПараметрыПеретаскивания.Действие 			= ДействиеПеретаскивания.Перемещение;
	ПараметрыПеретаскивания.ДопустимыеДействия 	= ДопустимыеДействияПеретаскивания.КопированиеИПеремещение;
	
КонецПроцедуры

//точки самовывоза ТС Магнит
&НаКлиенте
Процедура ОбработчикОтказОтСамовывоза(РезультатВопроса,ДопПараметрПереданныйВОбработчик=Неопределено) Экспорт

	ОтказОтСамовывозаСервер(РезультатВопроса);
	
КонецПроцедуры

&НаСервере
Процедура ОтказОтСамовывозаСервер(РезультатВопроса)
	
	ВосстановитьТаблицыЗначенийПроизвольныхРеквизитовИзХранилища(); //потребуется для ПроверитьПоляСообщения
	
	Если РезультатВопроса="Самостоятельно" Тогда 
		Сообщение.ОтветственныйЗаДоставку = "TransportBySupplier";
	ИначеЕсли РезультатВопроса="Силами стороннего перевозчика" Тогда 
		Сообщение.ОтветственныйЗаДоставку = "TransportByLogisticProvider";
	Иначе
		Сообщение.ОтветственныйЗаДоставку = "TransportByCustomer";
	КонецЕсли;
	
	РезультатПроверки = МодульОбъекта().ПроверитьПоляСообщения(Сообщение);
	Сообщение.СодержитОшибки =  НЕ РезультатПроверки.Успешно;
	
	ПроверитьЗаполнениеПолей();
	
	УничтожитьТаблицыЗначенийПроизвольныхРеквизитов();
	
КонецПроцедуры // ОтказОтСамовывозаСервер()

&НаКлиенте
Процедура ВыбратьТочкуСамовывоза()
	
	Если ЗначениеЗаполнено(Параметры.СообщениеСсылка) Тогда 
		АдресТаблицыТочекСамовывоза=ПоместитьТочкиСамовывозаВВХ();
		Если АдресТаблицыТочекСамовывоза=Неопределено Тогда 
			Сообщить("Не указаны точки самовывоза"); 
		иначе
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("АдресТаблицыВХранилище",АдресТаблицыТочекСамовывоза);
			МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("УниверсальнаяФормаВыбораУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбработчикПослеВыбораТочкиСамовывоза", ЭтаФорма, ПараметрыФормы);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработчикПослеВыбораТочкиСамовывоза(Параметр=Неопределено,ДопПараметрПереданныйВОбработчик=Неопределено) Экспорт
	Если Параметр=Неопределено Тогда
		//так и не выбрали
	Иначе
		ОбработчикПослеВыбораТочкиСамовывозаСервер(Параметр);	
	КонецЕсли;
	
КонецПроцедуры 

&НаСервере
Процедура ОбработчикПослеВыбораТочкиСамовывозаСервер(АдресВХ)

	//поместить выбранное в сообщение
	ВосстановитьТаблицыЗначенийПроизвольныхРеквизитовИзХранилища();
	
	ТаблицаВыбранныхТочекСамовывоза=ПолучитьИзВременногоХранилища(АдресВХ);
	НоваяТЗТочкиСамовывоза=ТаблицаВыбранныхТочекСамовывоза;	
	ВсегоТочекСамовывоза=НоваяТЗТочкиСамовывоза.Количество();
	
	Для й=1 По ВсегоТочекСамовывоза Цикл
		Если НоваяТЗТочкиСамовывоза[ВсегоТочекСамовывоза-й].Выбор=Ложь Тогда
			НоваяТЗТочкиСамовывоза.Удалить(ВсегоТочекСамовывоза-й);
		КонецЕсли;
	КонецЦикла;
	
	Сообщение.ОтветственныйЗаДоставку = "TransportByCustomer";
	Сообщение.ТочкиСамовывоза=НоваяТЗТочкиСамовывоза;
	ПоместитьТаблицыЗначенийПроизвольныхРеквизитовВХранилище();
	
	//перепроверить сообщение
	РезультатПроверки = МодульОбъекта().ПроверитьПоляСообщения(Сообщение);
	Сообщение.СодержитОшибки =  НЕ РезультатПроверки.Успешно;
	
	ПроверитьЗаполнениеПолей();
	
	УничтожитьТаблицыЗначенийПроизвольныхРеквизитов();
	
КонецПроцедуры // ОбработчикПослеВыбораТочкиСамовывозаСервер()

&НаКлиенте
Процедура ОтказОтСамовывоза()
	
	Если ЗначениеЗаполнено(Параметры.СообщениеСсылка) Тогда 
		ТекстВопроса ="Доставка будет осуществляться:";
		КнопкиВопроса=новый СписокЗначений;
		КнопкиВопроса.Добавить("Самостоятельно");
		КнопкиВопроса.Добавить("Силами стороннего перевозчика");
		КнопкиВопроса.Добавить("ОТМЕНА операции");
		ДопПараметрДляПередачиВОбработчик=Неопределено;
		РезультатВопроса = Неопределено;
		
		Если Параметры.МодальностьЗапрещена Тогда
			Выполнить("ПоказатьВопрос(Новый ОписаниеОповещения(""ОбработчикОтказОтСамовывоза"", ЭтаФорма, ДопПараметрДляПередачиВОбработчик), ТекстВопроса, КнопкиВопроса,,,""Контур.EDI"")");
		Иначе
			РезультатВопроса = Вопрос(ТекстВопроса, КнопкиВопроса,,,"Контур.EDI");
			ОбработчикОтказОтСамовывоза(РезультатВопроса,ДопПараметрДляПередачиВОбработчик);
		КонецЕсли;	
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПоместитьТочкиСамовывозаВВХ()

	СообщениеССамовывозом = МодульОбъекта().ПрочитатьСообщение(Параметры.СообщениеСсылка,,Параметры.ТипСообщения,Параметры.СообщениеСсылка.Направление);
	
	Если СообщениеССамовывозом.Свойство("ТочкиСамовывоза") И ЗначениеЗаполнено(СообщениеССамовывозом.ТочкиСамовывоза) И СообщениеССамовывозом.ТочкиСамовывоза.Количество()>0 Тогда 
		АдресвВХ=ПоместитьВоВременноеХранилище(СообщениеССамовывозом.ТочкиСамовывоза);
		Возврат АдресвВХ;
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ОбработчикПерепроверитьСообщение() Экспорт
	
	ПерепроверитьСообщениеСервер();
	
КонецПроцедуры 

&НаСервере
Процедура ПерепроверитьСообщениеСервер() Экспорт
	
	ВосстановитьТаблицыЗначенийПроизвольныхРеквизитовИзХранилища();
	
	РезультатПроверки = МодульОбъекта().ПроверитьПоляСообщения(Сообщение);
	
	Сообщение.СодержитОшибки =  НЕ РезультатПроверки.Успешно;
	
	ПроверитьЗаполнениеПолей();
	
	УничтожитьТаблицыЗначенийПроизвольныхРеквизитов();
	
КонецПроцедуры

//Универсальный Акцептор Нажатия Доавбленной Кнопки
&НаКлиенте
Процедура АкцепторДобавленнойКнопки(Команда)
	
	//действия, которые нужно выполнить на клиенте, например что-то спросить
	КлиентскийКодОбработчика=ПолучитьКлиентскийКодОбработчикаНажатияКнопки(Команда.Имя);
	
	Если КлиентскийКодОбработчика<>неопределено Тогда 
		//в этот моент мы знаем Команда.Имя и можем выполнить нужный код внутри КлиентскийКодОбработчика смотри описание функции ПолучитьКлиентскийКодОбработчикаНажатияКнопки
		//для дальнейшей передачи на сервер для обработки Использовать  ОбработчикИнтерактивногоДействияДобавленнойкнопки(РезультатДействия,ИмяКоманды)
		Выполнить(КлиентскийКодОбработчика); 	
	КонецЕсли;
	
	//Вне зависимости от наличия клиентского обработчика выполняем серверную процедуру	
	АкцепторДобавленнойКнопкиСервер(Команда.Имя);
	
КонецПроцедуры

//////Пример кода события "ПолучениеКодаКлиентскогоОбработчика"
//Если Команда.Имя = "ПроверитьАдекватность" Тогда
//	
//	ТекстВопроса ="Сколько будет 3 + 1000000 ?";
//	КнопкиВопроса=новый СписокЗначений;
//	КнопкиВопроса.Добавить("не знаю");
//	КнопкиВопроса.Добавить("1000003");
//	ДопПараметрДляПередачиВОбработчик=Команда.Имя;
//	РезультатВопроса = Неопределено;
//	Если Параметры.МодальностьЗапрещена Тогда                     
//		Выполнить("ПоказатьВопрос(Новый ОписаниеОповещения(""ОбработчикИнтерактивногоДействияДобавленнойкнопки"", ЭтаФорма, ДопПараметрДляПередачиВОбработчик), ТекстВопроса, КнопкиВопроса,,,""Контур.EDI"")");
//	Иначе
//		РезультатВопроса = Вопрос(ТекстВопроса, КнопкиВопроса,,,"Контур.EDI");
//		ОбработчикИнтерактивногоДействияДобавленнойкнопки(РезультатВопроса,ДопПараметрДляПередачиВОбработчик); 
//	КонецЕсли;
//КонецЕсли;
&НаСервере
Функция ПолучитьКлиентскийКодОбработчикаНажатияКнопки(ИмяКоманды)
	
	СтандартнаяОбработкаEDI=Истина;	
	КлиентскийКодОбработчика = МодульОбъекта().ОбработкаСобытияПодключаемогоМодуля("ПолучениеКодаКлиентскогоОбработчика",СтандартнаяОбработкаEDI,Новый Структура("ИмяКоманды,Сообщение,Параметры,Форма",ИмяКоманды,Сообщение,Параметры,ЭтаФорма));
	
	Если ЗначениеЗаполнено(СокрЛП(КлиентскийКодОбработчика)) Тогда 
		Возврат КлиентскийКодОбработчика;
	Иначе
		Возврат неопределено;
	КонецЕсли;
	
КонецФункции // ПолучитьКлиентскийКодОбработчикаНажатияКнопки()

//если мы хотим в режиме запрета модальности спросить что-то у пользователя, то его ответ требуется положить в обработчик
&НаКлиенте
Процедура ОбработчикИнтерактивногоДействияДобавленнойкнопки(РезультатКлиента,ИмяКоманды) Экспорт
	
	АкцепторДобавленнойКнопкиСервер(ИмяКоманды,РезультатКлиента);	
	
КонецПроцедуры // ОбработчикИнтерактивногоДействияДобавленнойкнопки()

&НаСервере
Процедура АкцепторДобавленнойКнопкиСервер(ИмяКоманды, РезультатКлиента = Неопределено)
	
	ВосстановитьТаблицыЗначенийПроизвольныхРеквизитовИзХранилища();   //нужны, например для перепроверки таб части
	СтандартнаяОбработкаEDI=истина;

	МодульОбъекта().ОбработкаСобытияПодключаемогоМодуля("КнопкаФормыСообщенияНажатие",СтандартнаяОбработкаEDI,Новый Структура("ИмяКоманды,Сообщение,Параметры,Форма,РезультатКлиента",ИмяКоманды,Сообщение,Параметры,ЭтаФорма,РезультатКлиента));
	УничтожитьТаблицыЗначенийПроизвольныхРеквизитов();
	
КонецПроцедуры

&НаКлиенте                                
Процедура ОбработкаОповещения(ИмяСобытияОповещения, Параметр, Источник)
	                                      //ждем  Параметр.ИмяКоманды, Параметр.Результат
	Если ИмяСобытияОповещения = "ОповещениеИзПодключаемогоМодуля" Тогда 
		АкцепторДобавленнойКнопкиСервер(Параметр.ИмяКоманды,Параметр.Результат)
	КонецЕсли;
	
КонецПроцедуры


//типы ТС Магнит
&НаКлиенте
Процедура ВыбратьТипТС()
	
	Если ЗначениеЗаполнено(Параметры.СообщениеСсылка) Тогда 
		//список типов тс
		ОригинальныйСписок=ПолучитьСписокТиповТССообщения(Сообщение);
		
		Если ОригинальныйСписок=Неопределено или ОригинальныйСписок.Количество() = 0 Тогда 
			Сообщить("Не указаны типы ТС"); 
		иначе
			Если Параметры.МодальностьЗапрещена Тогда
				Выполнить("ОригинальныйСписок.ПоказатьОтметкуЭлементов(Новый ОписаниеОповещения(""ОбработчикПослеВыбораТипаТС"", ЭтаФорма),""Выберите тип транспортного средства"")");
			Иначе
				Если ОригинальныйСписок.ОтметитьЭлементы("Выберите тип транспортного средства") Тогда 			
					ОбработчикПослеВыбораТипаТС(ОригинальныйСписок, Неопределено)
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ПолучитьСписокТиповТССообщения(Сообщение)
	
	ВходящийЗаказДокумент1С = МодульОбъекта().НайтиСвязанныйДокументПоТипуСообщения(Сообщение.Документ1С,"ORDERS");
	СообщениеORDERS = МодульОбъекта().ПрочитатьСообщение(,ВходящийЗаказДокумент1С,"ORDERS","Входящее",,);
	//открытие формы для выбора и добавления даты, времени прибытия.
	ОригинальныйСписок=СообщениеORDERS.Транспортировка.ТипТранспортногоСредства.Скопировать();
	ТекущийСписок=Сообщение.Транспортировка.ТипТранспортногоСредства.Скопировать();
	Если ТекущийСписок.Количество()>0 Тогда 
		ОригинальныйСписок=ТекущийСписок;
	КонецЕсли;
	
	Возврат ОригинальныйСписок;
	
КонецФункции // ПолучитьСписокТиповТССообщения()

&НаКлиенте
Процедура ОбработчикПослеВыбораТипаТС(Параметр=Неопределено,ДопПараметрПереданныйВОбработчик=Неопределено) Экспорт
	Если Параметр=Неопределено Тогда
		//так и не выбрали
	Иначе
		Сообщение.Транспортировка.ТипТранспортногоСредства=Параметр;
	КонецЕсли;
	
	ОбработчикПослеВыбораТипаТССервер();
	
КонецПроцедуры 

&НаСервере
Процедура ОбработчикПослеВыбораТипаТССервер()

	ПерепроверитьСообщениеСервер();

КонецПроцедуры // ОбработчикПослеВыбораТочкиСамовывозаСервер()

//Блок Особенности
&НаКлиенте
Процедура ОсобенностиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ОсобенностиПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОсобенностиСообщенияНаФорме()

	Особенности.Очистить();
	
	ОсобенностиСообщений = ОсобенностиСообщений();
	//штатный обход таблицы особенностей
	Для каждого ОсобенностьСообщения Из ОсобенностиСообщений Цикл
		
		Если НЕ МодульОбъекта().ОсобенностьВыводитсяВКарточкуСообщения(ОсобенностьСообщения, Сообщение) Тогда
			Продолжить;
		КонецЕсли;
		
		Если МодульОбъекта().ЭтоОсобенностьТоваров(ОсобенностьСообщения) Тогда
			
			// Пока не умеем выводить сложные особенности в товарах
			Если МодульОбъекта().ЭтоОсобенностьСложногоТипа(ОсобенностьСообщения.ТипЗначенияEDI) Тогда
				Продолжить;
			КонецЕсли;
			ДобавитьОсобенностьТоваров(ОсобенностьСообщения);
			
		Иначе
			
			ДобавитьОсобенностьШапки(ОсобенностьСообщения);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если Особенности.Количество() = 0 Тогда
		Элементы.СтраницаОсобенности.Видимость = Ложь;
	Иначе
		Элементы.СтраницаОсобенности.Видимость = Истина;
		Элементы.СтраницаОсобенности.Заголовок = "Особенности ("+Особенности.Количество()+")";
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОсобенностиСтруктурТоваровСообщенияНаФорме()
	
	Для каждого ОсобенностьСообщения Из ОсобенностиСообщений Цикл
		
		Если НЕ МодульОбъекта().ОсобенностьВыводитсяВКарточкуСообщения(ОсобенностьСообщения, Сообщение) Тогда
			Продолжить;
		КонецЕсли;
		Если НЕ МодульОбъекта().ЭтоОсобенностьТоваров(ОсобенностьСообщения) Тогда
			Продолжить;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(ОсобенностьСообщения.ИмяСтруктуры) Тогда
			Продолжить;
		КонецЕсли;
		Если ОсобенностьСообщения.ЭтоСтруктура Тогда
			Продолжить;
		КонецЕсли;
		
		ПолныйИдентификаторОсобенности = МодульОбъекта().ПолныйИдентификаторОсобенностиТоваров(ОсобенностьСообщения);

		Для Сч = 0 По Сообщение.Товары.Количество() - 1 Цикл
			ЗначениеОсобенности = Неопределено;
			Если ЗначениеЗаполнено(ОсобенностьСообщения.ИмяСтруктуры)
				И ЗначениеЗаполнено(ОсобенностьСообщения.ИмяТаблицы)
				И МодульОбъекта().ПроверитьСвойствоПоляВСтруктуре(Сообщение.Товары[Сч][ОсобенностьСообщения.ИмяТаблицы], ОсобенностьСообщения.Идентификатор, ЗначениеОсобенности) Тогда 
				Товары[Сч][ПолныйИдентификаторОсобенности] = ЗначениеОсобенности;
            КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьОсобенностьШапки(Особенность) //Особенность - строка таблицы Особенностей
	
	//в таблицу Особенности
	НоваяОсобенность = Особенности.Добавить();
	НоваяОсобенность.Наименование 	= Особенность.Наименование;
	НоваяОсобенность.Идентификатор 	= Особенность.Идентификатор;
	НоваяОсобенность.ИмяТаблицы 	= Особенность.ИмяТаблицы;
	НоваяОсобенность.Подсказка 		= Особенность.ПодробноеОписание;
	
	НоваяОсобенность.ЭтоСтруктура 	= Особенность.ЭтоСтруктура;
	НоваяОсобенность.ИмяСтруктуры 	= Особенность.ИмяСтруктуры;
	
	НоваяОсобенность.НеСохранятьВСообщение 	= Особенность.НеСохранятьВСообщение;
	
	ПолныйПутьОсобенности = МодульОбъекта().ПолучитьПолныйПутьОсобенностиСообщения(Особенность);
	
	ЗначениеСвойстваСообщения = Неопределено;
	МодульОбъекта().ПроверитьСвойствоПоляВСтруктуре(Сообщение, ПолныйПутьОсобенности, ЗначениеСвойстваСообщения); 
	
	Если Не ЗначениеЗаполнено(ЗначениеСвойстваСообщения) Тогда
		Дефолт = МодульОбъекта().ПолучитьПустоеЗначениеОсобенности(Особенность);
		Если НЕ Дефолт = Неопределено Тогда
			ЗначениеСвойстваСообщения = Дефолт;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Особенность.СписокВыбора) Тогда
		НоваяОсобенность.СписокВыбора = Особенность.СписокВыбора;
	КонецЕсли;
	
	ТипОсобенности = МодульОбъекта().ОпределитьИмяТипаЗначенияСложногоТипа(ЗначениеСвойстваСообщения);
	Если НЕ ТипОсобенности = Неопределено Тогда
		НоваяОсобенность.ТипЗначения = ТипОсобенности;
	Иначе
		НоваяОсобенность.Значение = ЗначениеСвойстваСообщения;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьОсобенностьТоваров(Особенность)
	
	//создать колонку и привязать данные
	
	ТипЗначенияКолонкиОсобенность = МодульОбъекта().ПолучитьТипОсобенности(Особенность);
	
	Если ЗначениеЗаполнено(Особенность.ИмяСтруктуры) Тогда
		ПолныйИдентификаторОсобенности = МодульОбъекта().ПолныйИдентификаторОсобенностиТоваров(Особенность);
	Иначе
		ПолныйИдентификаторОсобенности = Особенность.Идентификатор;
	КонецЕсли;
	
	Если Особенность.Ревенство1СEDI = Неопределено Тогда
		ПолныйИдентификаторОсобенности = ПолныйИдентификаторОсобенности + "1С";			
	КонецЕсли;
	
	РеквизитДобавлен = Ложь;
	Для каждого РеквизитТоваров Из ЭтаФорма.ПолучитьРеквизиты("Товары") Цикл
		Если РеквизитТоваров.Имя = ПолныйИдентификаторОсобенности Тогда
			РеквизитДобавлен = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ РеквизитДобавлен Тогда	
		НовыеРеквизиты = Новый Массив;
		НовыеРеквизиты.Добавить(Новый РеквизитФормы(ПолныйИдентификаторОсобенности, ТипЗначенияКолонкиОсобенность, "Товары", Особенность.Наименование));
		ИзменитьРеквизиты(НовыеРеквизиты);
	КонецЕсли;
	
	КолонкаНаФорме = Элементы.Найти("Товары" + ПолныйИдентификаторОсобенности);
	Если КолонкаНаФорме = Неопределено Тогда
		
		НоваяКолонкаНаФорме = Элементы.Добавить("Товары" + ПолныйИдентификаторОсобенности, Тип("ПолеФормы"), Элементы.Товары);
		Если ТипЗначенияКолонкиОсобенность = Новый ОписаниеТипов("Булево") Тогда
			НоваяКолонкаНаФорме.Вид = ВидПоляФормы.ПолеФлажка; 
		Иначе
			НоваяКолонкаНаФорме.Вид = ВидПоляФормы.ПолеВвода; 
			НоваяКолонкаНаФорме.Ширина = 15;
		КонецЕсли;
		
		НоваяКолонкаНаФорме.ПутьКДанным = "Товары." + ПолныйИдентификаторОсобенности;
		НоваяКолонкаНаФорме.Заголовок	= Особенность.Наименование;
		НоваяКолонкаНаФорме.Подсказка 	= "Эта колонка добавлена из-за особенности партнера: """+Особенность.Наименование+""""; // по этому полю будем определять особенность в обработке выбора
		НоваяКолонкаНаФорме.КартинкаШапки = Элементы.СтраницаОсобенности.Картинка;	
		НоваяКолонкаНаФорме.УстановитьДействие("ПриИзменении", "ПеренестиОсобенностиИПерепроверитьСообщение");
		
		Если ЗначениеЗаполнено(Особенность.СписокВыбора) Тогда
			ШиринаКолонки = 15;
			Для каждого Эл Из Особенность.СписокВыбора Цикл
				НоваяКолонкаНаФорме.СписокВыбора.Добавить(Эл.Значение, Эл.Представление);
				Если ЗначениеЗаполнено(Эл.Представление) Тогда
					ШиринаКолонки = Макс(СтрДлина(Эл.Представление), ШиринаКолонки);
				ИначеЕсли ТипЗнч(Эл.Значение) = Тип("Строка") Тогда
					ШиринаКолонки = Макс(СтрДлина(Эл.Значение), ШиринаКолонки);
				КонецЕсли;
			КонецЦикла;
			НоваяКолонкаНаФорме.Ширина = Мин(ШиринаКолонки, 40);	
		КонецЕсли;
		
	КонецЕсли;
	
	//значение туда положит механизм  ЗаполнитьПоляНаФорме()
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьУсловноеОформлениеОсобенностейСообщения()
	
	Для каждого ОсобенностьСообщения Из ОсобенностиСообщений Цикл
		
		Если НЕ МодульОбъекта().ОсобенностьВыводитсяВКарточкуСообщения(ОсобенностьСообщения,Сообщение) Тогда
			Продолжить;
		КонецЕсли;
		
		Если МодульОбъекта().ЭтоОсобенностьТоваров(ОсобенностьСообщения) Тогда
			ПолныйИдентификаторОсобенности = МодульОбъекта().ПолныйИдентификаторОсобенностиТоваров(ОсобенностьСообщения);
			ИмяПоляОформления 	= "Товары" + ПолныйИдентификаторОсобенности;
			ИмяПоляОтбора 		= "Товары." + ПолныйИдентификаторОсобенности;
		Иначе
			ИмяПоляОформления 	= "ОсобенностиЗначение";
			ИмяПоляОтбора 		= "Особенности.Значение";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОсобенностьСообщения.СписокВыбора) Тогда
			
			Для каждого Эл Из ОсобенностьСообщения.СписокВыбора Цикл
			
				Элемент = УсловноеОформление.Элементы.Добавить();
				ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
				ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ИмяПоляОформления);
				
				ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоляОтбора);
				ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
				ОтборЭлемента.ПравоеЗначение = Эл.Значение;
				
				Элемент.Оформление.УстановитьЗначениеПараметра("Текст", Эл.Представление);
		
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ПеренестиОсобенностиИПерепроверитьСообщение(Заглушка=Неопределено) //чтобы можно было привязать ПриИзменении
	
	//на сервер
	ПеренестиОсобенностиИПерепроверитьСообщениеВызовСервера();
	
КонецПроцедуры

&НаСервере
Процедура ПеренестиОсобенностиИПерепроверитьСообщениеВызовСервера()

	ВосстановитьТаблицыЗначенийПроизвольныхРеквизитовИзХранилища();	

	ПеренестиОсобенностиВСообщение();
	Если НужноКонвертироватьОсобенностиВСообщении Тогда
		КонвертироватьОсобенностиСообщения();
		ЗаполнитьОсобенностиСообщенияНаФорме();
	КонецЕсли;

	ПоместитьТаблицыЗначенийПроизвольныхРеквизитовВХранилище();
	
	ОшибокБольшеНет = ПроверитьЗаполнениеПолей();	
	Если ОшибокБольшеНет = Истина Тогда
		Сообщение.СодержитОшибки = Ложь;
	КонецЕсли;
	
	УничтожитьТаблицыЗначенийПроизвольныхРеквизитов();

КонецПроцедуры // ПеренестиОсобенностиИПерепроверитьСообщениеВызовСервера()

&НаСервере
Процедура ПеренестиОсобенностиВСообщение()

	МодульОбъекта().ОбработкаСобытияПодключаемогоМодуля("ПеренестиОсобенностиВСообщение",,
														Новый Структура("Сообщение, Особенности, Форма", 
																		Сообщение, Особенности, ЭтаФорма));
										
	ОсобенностиСообщений = ОсобенностиСообщений();
	ВремТовары = РеквизитФормыВЗначение("Товары");
	ВремОсобенности = РеквизитФормыВЗначение("Особенности");
	
	Для каждого ОсобенностьСообщения Из ОсобенностиСообщений Цикл
		
		Если НЕ МодульОбъекта().ОсобенностьВыводитсяВКарточкуСообщения(ОсобенностьСообщения,Сообщение) Тогда
			Продолжить;
		КонецЕсли;
		Если ОсобенностьСообщения.НеСохранятьВСообщение Тогда
			Продолжить;
		КонецЕсли;
		Если МодульОбъекта().ЭтоОсобенностьСложногоТипа(ОсобенностьСообщения.ТипЗначенияEDI) Тогда
			Продолжить;
		КонецЕсли;

		ПолныйПутьОсобенности = МодульОбъекта().ПолучитьПолныйПутьОсобенностиСообщения(ОсобенностьСообщения);
		
		Если МодульОбъекта().ЭтоОсобенностьТоваров(ОсобенностьСообщения) Тогда
			
			ПолныйИдентификаторОсобенности = МодульОбъекта().ПолныйИдентификаторОсобенностиТоваров(ОсобенностьСообщения);
			
			ИмяПоляСообщения = ПолныйИдентификаторОсобенности;
			Если ОсобенностьСообщения.Ревенство1СEDI = Неопределено Тогда
				ИмяПоляСообщения = ИмяПоляСообщения + "1С";		
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ОсобенностьСообщения.ИмяСтруктуры)
				И ЗначениеЗаполнено(ОсобенностьСообщения.ИмяТаблицы) Тогда
				Для Сч = 0 По ВремТовары.Количество() - 1 Цикл
					ЗначениеОсобенности = ВремТовары[Сч][ПолныйИдентификаторОсобенности];
					МодульОбъекта().ПеренестиЗначениеВПолеСтруктуры(Сообщение.Товары[Сч][ОсобенностьСообщения.ИмяТаблицы], ОсобенностьСообщения.Идентификатор, ЗначениеОсобенности);
				КонецЦикла;
			КонецЕсли;
			
			Сообщение.Товары.ЗагрузитьКолонку(ВремТовары.ВыгрузитьКолонку(ИмяПоляСообщения), ИмяПоляСообщения);
			
		Иначе
			
			ИмяСтруктурыОсобенности 	= ?(ОсобенностьСообщения.ИмяСтруктуры = Неопределено, "", ОсобенностьСообщения.ИмяСтруктуры);
			МассивОсобенностейНаФорме 	= 	ВремОсобенности.НайтиСтроки(Новый Структура("Идентификатор,ИмяСтруктуры", 
																						ОсобенностьСообщения.Идентификатор, 
																						ИмяСтруктурыОсобенности));
			
			Если МассивОсобенностейНаФорме.Количество() = 1 Тогда
				
				НайденнаяОсобенность = МассивОсобенностейНаФорме[0];
				МодульОбъекта().ПеренестиЗначениеВПолеСтруктуры(Сообщение, ПолныйПутьОсобенности, НайденнаяОсобенность.Значение);
				
			КонецЕсли;

		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // ПеренестиОсобенностиВСообщение()

&НаСервере
Процедура КонвертироватьОсобенностиСообщения()

	ОсобенностиСообщений = ОсобенностиСообщений();
	
	Для каждого ОсобенностьСообщения Из ОсобенностиСообщений Цикл
		
		Если НЕ МодульОбъекта().ОсобенностьЕстьВСообщении(ОсобенностьСообщения, Сообщение) Тогда
			Продолжить;
		КонецЕсли;
		Если НЕ МодульОбъекта().ОсобенностьНужноКонвертировать(ОсобенностьСообщения) Тогда
			Продолжить;
		КонецЕсли;
		Если ОсобенностьСообщения.НеСохранятьВСообщение = Истина Тогда
			Продолжить;
		КонецЕсли;
		
		Источник 		= ?(ОсобенностьСообщения.Направление = "Исходящее", "1С", "EDI"); 
		Приемник 		= ?(ОсобенностьСообщения.Направление = "Исходящее", "EDI", "1С");
		ИмяИсточника 	= ОсобенностьСообщения.Идентификатор + Источник;
		ИмяПриемника 	= ОсобенностьСообщения.Идентификатор + Приемник;
		
		Если МодульОбъекта().ЭтоОсобенностьТоваров(ОсобенностьСообщения) Тогда
			
			Для каждого Стр Из Сообщение.Товары Цикл 
				ЗначениеОсобенности = Стр[ИмяИсточника];
				
				Если ОсобенностьСообщения.Направление = "Исходящее" Тогда
					Стр[ИмяПриемника] 	= МодульОбъекта().КонвертироватьЗначение1СвEDI(ЗначениеОсобенности, 
																						ОсобенностьСообщения.ТипЗначения1С, 
																						ОсобенностьСообщения.ТипЗначенияEDI, 
																						Сообщение);
				Иначе
					Стр[ИмяПриемника] 	= МодульОбъекта().КонвертироватьЗначениеEDIв1С(ЗначениеОсобенности, 
																						ОсобенностьСообщения.ТипЗначенияEDI,
																						ОсобенностьСообщения.ТипЗначения1С, 
																						Сообщение);
				КонецЕсли;
			КонецЦикла;
			
		Иначе
			
			ЗначениеОсобенности = Сообщение[ИмяИсточника];
			
			Если ОсобенностьСообщения.Направление = "Исходящее" Тогда
				Сообщение[ИмяПриемника] = МодульОбъекта().КонвертироватьЗначение1СвEDI(ЗначениеОсобенности, 
																						ОсобенностьСообщения.ТипЗначения1С, 
																						ОсобенностьСообщения.ТипЗначенияEDI, 
																						Сообщение);
			Иначе
				Сообщение[ИмяПриемника] = МодульОбъекта().КонвертироватьЗначениеEDIв1С(ЗначениеОсобенности, 
																						ОсобенностьСообщения.ТипЗначенияEDI, 
																						ОсобенностьСообщения.ТипЗначения1С, 
																						Сообщение);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Функция ОсобенностиСообщений()
	//это базовая таблица доступная после инициализации Модуля Объекта
	
	Если ОсобенностиСообщений=Неопределено Тогда
		Возврат МодульОбъекта().ОсобенностиСообщений;
	Иначе
		Возврат ОсобенностиСообщений;
	КонецЕсли;
	
КонецФункции // ОсобенностиСообщений()

&НаКлиенте
Процедура ОсобенностиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ПеренестиОсобенностиИПерепроверитьСообщениеВызовСервера();
	
КонецПроцедуры

&НаКлиенте
Процедура ОсобенностиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущаяСтрока = Элемент.ТекущиеДанные;
	
	Если ЭтоОсобенностьСложногоТипаКлиент(ТекущаяСтрока.ТипЗначения) Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыОсобенности = Новый Структура;
		ПараметрыОсобенности.Вставить("Идентификатор",			ТекущаяСтрока.Идентификатор);
		ПараметрыОсобенности.Вставить("ИмяТаблицы",				ТекущаяСтрока.ИмяТаблицы);
		ПараметрыОсобенности.Вставить("ИмяСтруктуры",			ТекущаяСтрока.ИмяСтруктуры);
		ПараметрыОсобенности.Вставить("ЭтоСтруктура",			ТекущаяСтрока.ЭтоСтруктура);
		ПараметрыОсобенности.Вставить("ТипЗначения",			ТекущаяСтрока.ТипЗначения);
		ПараметрыОсобенности.Вставить("НеСохранятьВСообщение" , ТекущаяСтрока.НеСохранятьВСообщение);
		
		СтруктураАдресаВоВХ = ПоместитьКонтекстДляУниверсальнойФормыВоВХ(ПараметрыОсобенности);
		
		Если СтруктураАдресаВоВХ = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		НастройкиФормы = Новый Структура;
		НастройкиФормы.Вставить("НадписьИнформация", ТекущаяСтрока.Наименование);
		НастройкиФормы.Вставить("ТолькоПросмотр"   , ТолькоПросмотрСообщения);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("НастройкиФормы",			НастройкиФормы);
		ПараметрыФормы.Вставить("ИмяСтруктуры",				ТекущаяСтрока.ИмяСтруктуры);
		ПараметрыФормы.Вставить("ТаблицаМаппингаАдресВоВХ",	СтруктураАдресаВоВХ.ТаблицаМаппингаАдресВоВХ); 
		ПараметрыФормы.Вставить("СообщениеАдресВоВХ",		СтруктураАдресаВоВХ.СообщениеАдресВоВХ);
		ПараметрыФормы.Вставить("ЗначениеАдресВоВХ",		СтруктураАдресаВоВХ.ЗначениеАдресВоВХ);
		
		МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("УниверсальнаяФормаРедактированияУправляемая2", , ПараметрыФормы, ЭтаФорма, "ОбработчикПослеЗакрытияУниверсальнойФормыРедактирования2", ЭтаФорма, ПараметрыОсобенности);
			
	ИначеЕсли НЕ ТолькоПросмотрСообщения
		И Поле.Имя = "ОсобенностиЗначение"
		И ЗначениеЗаполнено(ТекущаяСтрока.СписокВыбора) Тогда
		
		Если Параметры.МодальностьЗапрещена Тогда 
			Выполнить("ПоказатьВыборИзСписка(Новый ОписаниеОповещения(""СписокЗначенийОсобенностиВыборЗавершение"", ЭтаФорма), ТекущаяСтрока.СписокВыбора, Элемент)");
		Иначе
			ВыбранноеЗначение = ВыбратьИзСписка(ТекущаяСтрока.СписокВыбора, Элемент);
			СписокЗначенийОсобенностиВыборЗавершение(ВыбранноеЗначение);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьМассивИменТиповСложныхОбъектовКлиент()

	МассивТипов = Новый Массив;
	МассивТипов.Добавить("Структура");	
	МассивТипов.Добавить("Соответствие");	
	МассивТипов.Добавить("Массив");	
	МассивТипов.Добавить("СписокЗначений");	
	МассивТипов.Добавить("ТаблицаЗначений");	
	МассивТипов.Добавить("ДеревоЗначений");	
	
	Возврат МассивТипов;
	
КонецФункции // ПолучитьМассивИменТиповСложныхОбъектовКлиент()

&НаКлиенте
Функция ЭтоОсобенностьСложногоТипаКлиент(ИмяТипа) Экспорт
	
	МассивИменТипов = ПолучитьМассивИменТиповСложныхОбъектовКлиент();
	ЭтоСложныйТип = НЕ МассивИменТипов.Найти(ИмяТипа) = Неопределено;
	
	Возврат ЭтоСложныйТип;	
	
КонецФункции

&НаСервере
Функция ПоместитьКонтекстДляУниверсальнойФормыВоВХ(ПараметрыОсобенности)
	
	СтруктураАдресаВоВХ = Неопределено;
	
	ВосстановитьТаблицыЗначенийПроизвольныхРеквизитовИзХранилища();
	ЗначениеОсобенности = Неопределено;
	ПолныйПутьОсобенности = МодульОбъекта().ПолучитьПолныйПутьОсобенностиСообщения(ПараметрыОсобенности);
	
	Если МодульОбъекта().ПроверитьСвойствоПоляВСтруктуре(Сообщение, ПолныйПутьОсобенности, ЗначениеОсобенности) Тогда
		
		ТаблицаМаппингаСтруктуры = МодульОбъекта().ПолучитьОписаниеРеквизитовДопСправочника_КонтурEDI(ПараметрыОсобенности.ИмяСтруктуры);
		
		Если НЕ ЗначениеЗаполнено(ТаблицаМаппингаСтруктуры) Тогда
 			ТаблицаМаппингаСтруктуры = МодульОбъекта().ПолучитьМетаданныеОсобенностиСообщения(ПараметрыОсобенности, МетаданныеСообщения);
		КонецЕсли;
		
		МетаданныеСообщения = МодульОбъекта().ПолучитьМетаданныеСообщения(Сообщение.ТипСообщения, Сообщение.Направление);		
		КопияСообщения = ЗначениеИзСтрокиВнутр(ЗначениеВСтрокуВнутр(Сообщение));
		КопияОсобенности = ЗначениеИзСтрокиВнутр(ЗначениеВСтрокуВнутр(ЗначениеОсобенности));
		КопияТаблицыМаппинга = ЗначениеИзСтрокиВнутр(ЗначениеВСтрокуВнутр(ТаблицаМаппингаСтруктуры));
		
		СтруктураАдресаВоВХ = Новый Структура;
		СтруктураАдресаВоВХ.Вставить("ЗначениеАдресВоВХ", ПоместитьВоВременноеХранилище(КопияОсобенности, ЭтаФорма.УникальныйИдентификатор));
		СтруктураАдресаВоВХ.Вставить("СообщениеАдресВоВХ", ПоместитьВоВременноеХранилище(КопияСообщения, ЭтаФорма.УникальныйИдентификатор));
		СтруктураАдресаВоВХ.Вставить("ТаблицаМаппингаАдресВоВХ", ПоместитьВоВременноеХранилище(КопияТаблицыМаппинга, ЭтаФорма.УникальныйИдентификатор));
		
	КонецЕсли;
	
	УничтожитьТаблицыЗначенийПроизвольныхРеквизитов();

	Возврат СтруктураАдресаВоВХ;
	
КонецФункции

&НаКлиенте
Процедура ОбработчикПослеЗакрытияУниверсальнойФормыРедактирования2(РезультатРедактирования = Неопределено, ДопПараметрыОбработчика = Неопределено) Экспорт
	
	Если РезультатРедактирования = Неопределено 
		ИЛИ ДопПараметрыОбработчика = Неопределено
		ИЛИ ДопПараметрыОбработчика.НеСохранятьВСообщение = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Состояние("Переносим особенности в сообщение", 10);
	ОбработчикПослеЗакрытияУниверсальнойФормыРедактирования2Сервер(РезультатРедактирования, ДопПараметрыОбработчика);
	
	Состояние("Проверяем сообщение", 80);
	Если НетОшибок И ТаблицаОшибок.Количество() = 0 Тогда
		ЗакрытьПанельОшибокКлиент();
		
		Если КтоМы = "Поставщик" Тогда
			ВывестиПанельИнформации("В этой форме вы можете проверить данные, которые будут отправлены в торговую сеть.");
		Иначе
			ВывестиПанельИнформации("В этой форме вы можете проверить данные, которые будут отправлены поставщику.");
		КонецЕсли;
		
	Иначе
		ОткрытьПанельОшибокКлиент();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработчикПослеЗакрытияУниверсальнойФормыРедактирования2Сервер(РезультатРедактирования, ДопПараметрыОбработчика)
	
	ВосстановитьТаблицыЗначенийПроизвольныхРеквизитовИзХранилища();	
	ЗначениеОсобенности = ПолучитьИзВременногоХранилища(РезультатРедактирования);
	
	МодульОбъекта().ОбработкаСобытияПодключаемогоМодуля("ПеренестиОсобенностиВСообщение",,
														Новый Структура("Сообщение, Особенности, Форма, ЗначениеОсобенности, ИдентификаторОсобенности", 
																		Сообщение, Особенности, ЭтаФорма, ЗначениеОсобенности, ДопПараметрыОбработчика.Идентификатор));
														
																		
	ПолныйПутьОсобенности = МодульОбъекта().ПолучитьПолныйПутьОсобенностиСообщения(ДопПараметрыОбработчика);
	МодульОбъекта().ПеренестиЗначениеВПолеСтруктуры(Сообщение, ПолныйПутьОсобенности, ЗначениеОсобенности);
	
	ПоместитьТаблицыЗначенийПроизвольныхРеквизитовВХранилище();
	ПроизвестиПервоначальноеЗаполнениеПолейНаФорме();
	УничтожитьТаблицыЗначенийПроизвольныхРеквизитов();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗначенийОсобенностиВыборЗавершение(ВыбранноеЗначение = Неопределено, ДопПараметр = Неопределено) Экспорт

	Если Не ВыбранноеЗначение = Неопределено Тогда
		Элементы.Особенности.ТекущиеДанные.Значение = ВыбранноеЗначение.Значение;
		ПеренестиОсобенностиИПерепроверитьСообщениеВызовСервера();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОсобенностиПередНачаломИзменения(Элемент, Отказ)
	
	Если НЕ Элемент.ТекущиеДанные = Неопределено
		И ЗначениеЗаполнено(Элемент.ТекущиеДанные.СписокВыбора) Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПередНачаломИзменения(Элемент, Отказ)
	
	Если НЕ Элемент.ТекущиеДанные = Неопределено Тогда
		Если НЕ Найти(Элемент.ТекущийЭлемент.Подсказка, "Эта колонка добавлена из-за особенности партнера") = 0
			И ЗначениеЗаполнено(Элемент.ТекущийЭлемент.СписокВыбора) Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Возврат1СПриИзменении(Элемент)
	ПроверитьЗаполнениеПолей();
КонецПроцедуры

&НаКлиенте
Процедура Возврат1СНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПривязатьСообщение(ЭтаФорма.Команды.ПривязатьСообщение);
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьКакОбработанное(Команда)
	
	ИмяКоманды = "ОтметитьКакОбработанное"; 
	
	КнопкаДействияФормыНажатие_Клиент(ИмяКоманды);
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьНаУдаление(Команда)
	
	ИмяКоманды = "ПометитьНаУдаление"; 
	
	КнопкаДействияФормыНажатие_Клиент(ИмяКоманды);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереотправитьALCRPT(Команда)
	ПереотправитьALCRPT_Сервер();
	Сообщить("Теперь выполните обмен"); 
КонецПроцедуры

&НаСервере
Процедура ПереотправитьALCRPT_Сервер()

	МодульОбъекта().ЗарегистрироватьСообщениеALCRPT_К_Отправке(Сообщение);
     
КонецПроцедуры //

// Универсальная форма редактирования реквизитов
&НаСервере
Функция ПолучитьНастройкиДляФормыРедактированияДопРеквизитов(Настройка)
	
	СтрокаНастройки = Настройка[0];
		
	//Подготовим значения настроек
	НедоступныеПоляИКолонки = Новый Массив;
	НедоступныеПоляИКолонки.Добавить("ИмяРеквизита");
	
	ОтборПоДопСправочнику = МодульОбъекта().ПолучитьСтруктуру_ИзТекстаНастройкиРеквизитов(СтрокаНастройки, "ОтборПоДопСправочнику");
		
	/////
	ВключитьОбработчикиСобытий = Новый Соответствие;
	
	УниверсальнаяПроцедураПриИзменении			= КодПроцедурыПриИзмененииВыбранногоЭлементаСправочникаВДереве(СтрокаНастройки.ИмяРеквизита); 
	
	МассивСобытий = Новый Массив;
	МассивСобытий.Добавить(Новый Структура("Имя, Текст", "НачалоВыбора",		КодПроцедурыПолучитьСписокЭлементовСправочникаДляВыбора(СтрокаНастройки.ПутьЗаписиВДопСправочник, ОтборПоДопСправочнику, СтрокаНастройки)));
	МассивСобытий.Добавить(Новый Структура("Имя, Текст", "ПриИзменении",		УниверсальнаяПроцедураПриИзменении));
	МассивСобытий.Добавить(Новый Структура("Имя, Текст", "ОбработкаВыбора",		УниверсальнаяПроцедураПриИзменении));
	
	ВключитьОбработчикиСобытий.Вставить("Значение", МассивСобытий);		
			
	/////
	ВключитьОбработчикиСобытийСеврвер = Новый Соответствие;
	
	МассивСобытий = Новый Массив;
	МассивСобытий.Добавить(Новый Структура("Имя, Текст", "ПриИзменении_Сервер", КодПроцедурыПриИзмененииВыбранногоЭлементаСправочникаВДереве_Сервер()));
	
    ВключитьОбработчикиСобытийСеврвер.Вставить("Значение", МассивСобытий);
	
	/////
	ВключитьОбработчикиСобытийПосле = Новый Соответствие;
	
	МассивСобытий = Новый Массив;
	МассивСобытий.Добавить(Новый Структура("Имя, Текст", "НачалоВыбора",	КодПроцедурыОбработатьВыбранныйЭлемент()));
	
	ВключитьОбработчикиСобытийПосле.Вставить("Значение", МассивСобытий);
	
	//Запишем настройки в структуру
	НастройкиФормыРедактированияДопРеквизитов = Новый Структура;
	
	НастройкиФормыРедактированияДопРеквизитов.Вставить("ИзменятьСоставСтрок",					Ложь);
	НастройкиФормыРедактированияДопРеквизитов.Вставить("НадписьИнформация",						СтрокаНастройки.ПредставлениеРеквизита); 	
	НастройкиФормыРедактированияДопРеквизитов.Вставить("НедоступныеПоляИКолонки",				НедоступныеПоляИКолонки);
	НастройкиФормыРедактированияДопРеквизитов.Вставить("ВключитьОбработчикиСобытий",			ВключитьОбработчикиСобытий);	
	НастройкиФормыРедактированияДопРеквизитов.Вставить("ВключитьОбработчикиСобытийСеврвер",		ВключитьОбработчикиСобытийСеврвер);
	НастройкиФормыРедактированияДопРеквизитов.Вставить("ВключитьОбработчикиСобытийПосле",		ВключитьОбработчикиСобытийПосле);	
		
	Возврат НастройкиФормыРедактированияДопРеквизитов;
	
КонецФункции

&НаСервере  
Функция КодПроцедурыПолучитьСписокЭлементовСправочникаДляВыбора(ИмяСправочника, СтруктураОтбора, СтрокаНастройки)
    	
	Алгоритм = 						   		"ПоискПоСправочнику = Новый Структура;";
	Для каждого ЭлементОтбора Из СтруктураОтбора Цикл
		Алгоритм = Алгоритм + Символы.ПС + 	"ПоискПоСправочнику.Вставить(""" + ЭлементОтбора.Ключ + """," + """"+ЭлементОтбора.Значение + """);";
	КонецЦикла;
	Алгоритм = Алгоритм + Символы.ПС + 		"ВыбратьЭлементДопСправочникаИЗаполнитьПодчиненныеРеквизиты(Элемент, ""Дерево"", """ + ИмяСправочника + """, ПоискПоСправочнику, """ + СтрокаНастройки.ИмяРеквизита + """, СтандартнаяОбработка);"; 	

	Возврат Алгоритм;

КонецФункции // ()

&НаСервере  
Функция КодПроцедурыОбработатьВыбранныйЭлемент()

	Алгоритм = "ОбработатьВыбранныйЭлементДопСправочника_КонтурEDI(Параметр, ДопПараметры);";

	Возврат Алгоритм; 
	
КонецФункции

&НаСервере 
Функция КодПроцедурыПриИзмененииВыбранногоЭлементаСправочникаВДереве(ИмяРеквизита)
	
	Алгоритм =							"ЕщеПараметры = Новый Структура(""Дерево_ИмяЭлемента"", ""Дерево"");";
	Алгоритм = Алгоритм + Символы.ПС + 	"УниверсальныйОбработчикСобытияПриИзменении_Сервер(Элемент.Имя, ЕщеПараметры);";  

	Возврат Алгоритм;

КонецФункции // ()

&НаСервере 
Функция КодПроцедурыПриИзмененииВыбранногоЭлементаСправочникаВДереве_Сервер()

	Алгоритм = "ПриИзмененииВыбранногоЭлементаСправочникаВДереве_Сервер(ЕщеПараметры);";
	
	Возврат Алгоритм;

КонецФункции // ()

&НаКлиенте  
Процедура ЕщеДополнительныеРеквизиты_Нажатие(Команда)
	
	АдресВХ_СтруктураРеквизитов = ПолучитьСтруктуруРеквизитов_И_ВернутьАдресВХ(Сообщение, Команда.Имя);
		
	Если ЗначениеЗаполнено(АдресВХ_СтруктураРеквизитов) Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("АдресВХ_СтруктураРеквизитов",	АдресВХ_СтруктураРеквизитов);
				
		МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("УниверсальнаяФормаРедактированияУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбработчикПослеЗакрытияУниверсальнойФормыРедактирования", ЭтаФорма, ПараметрыФормы);

	КонецЕсли;
		
КонецПроцедуры 

&НаСервере
Процедура ОбработчикПослеЗакрытияУниверсальнойФормыРедактирования(СтруктураРезультата = Неопределено,ДопПараметрПереданныйВОбработчик = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(СтруктураРезультата) Тогда
		
		НастройкаРеквизитов	= МодульОбъекта().ПолучитьНастройкуРеквизитов_КонтурEDI(Новый Структура("ИмяРеквизита", СтруктураРезультата.ИмяРеквизита)); 
		
		РезультатСохраненияРеквизитов = ПолучитьИзВременногоХранилища(СтруктураРезультата.АдресСохраненногоЗначения);
		
		МодульОбъекта().ОбработатьСохраненныйИзУниверсальнойФормыРедактированияРезультат(Сообщение, РезультатСохраненияРеквизитов,	НастройкаРеквизитов); 
		
	КонецЕсли;
	
КонецПроцедуры  

&НаСервере
Функция ПолучитьСтруктуруРеквизитов_И_ВернутьАдресВХ(Сообщение, ИмяРеквизитов)
	
	АдресВХ_СтруктураРеквизитов = Неопределено;
	
	СтруктураРеквизита = Новый Структура;
	СтруктураРеквизита.Вставить("ИмяРеквизита",		ИмяРеквизитов); 
	СтруктураРеквизита.Вставить("ДеревоЗначения",	Неопределено);
	
	МодульОбъекта().ПолучитьДеревоДопРеквизитов_ДляРедактирования(СтруктураРеквизита, Сообщение);

	Если ЗначениеЗаполнено(СтруктураРеквизита.ДеревоЗначения) Тогда
		СтруктураРеквизита.Вставить("НастройкаФормы", ПолучитьНастройкиДляФормыРедактированияДопРеквизитов(СтруктураРеквизита.НастройкаРеквизитов));
		
        АдресВХ_СтруктураРеквизитов			 = ПоместитьВоВременноеХранилище(СтруктураРеквизита);
	КонецЕсли;
	
	Возврат АдресВХ_СтруктураРеквизитов;

КонецФункции // ПолучитьДерервоДопРеквизитовИВернутьАдресВХ()

&НаСервере
Функция ПолучитьПечатнуюФормуСообщения_КарточкаСообщения_Сервер()

	ВосстановитьТаблицыЗначенийПроизвольныхРеквизитовИзХранилища();

	Результат = МодульОбъекта().ПолучитьПечатнуюФормуСообщения("ПечатнаяФорма_" + Сообщение.ТипСообщения, Сообщение);
	
    УничтожитьТаблицыЗначенийПроизвольныхРеквизитов();
	
	Возврат Результат;

КонецФункции // ПолучитьПечатнуюФормуСообщения_КарточкаСообщения_Сервер()
 
&НаКлиенте
Процедура НапечататьСообщение(Команда)
	
	Результат = ПолучитьПечатнуюФормуСообщения_КарточкаСообщения_Сервер();
	
	Если Результат.Успешно Тогда
		
		Результат.ТабДок.Показать();
		
	КонецЕсли;
	
КонецПроцедуры

ВнешнееХранилище = Ложь;