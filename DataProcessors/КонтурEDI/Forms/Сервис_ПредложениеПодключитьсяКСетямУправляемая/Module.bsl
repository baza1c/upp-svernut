&НаСервере
Перем МодульОбъекта;

&НаКлиенте
Перем МодульОбъектаКлиент;

// СЛУЖЕБНЫЕ {

// Инициализация модуля и его экспортных функций
&НаСервере
Функция МодульОбъекта()
	
	Если МодульОбъекта = Неопределено Тогда
		
		Если Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект Тогда
			МодульОбъекта = МодульОбъектаИзКэша();
		КонецЕсли;
		
		Если МодульОбъекта = Неопределено Тогда
			МодульОбъекта = РеквизитФормыВЗначение("Объект");
			МодульОбъекта.ИнициализироватьПодключаемыеМодули(); // TODO Проверить что из этого используется на формах и перенести в ПараметрыКлиентСервер
			МодульОбъекта.ОбновитьКэшМодуляОбъекта();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат МодульОбъекта;
	
КонецФункции

&НаСервере
Функция МодульОбъектаИзКэша()
	
	Результат = Неопределено;
	
	Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда
		АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект;
 		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Структура = ПолучитьИзВременногоХранилища(АдресВХ);
			Если ТипЗнч(Структура) = Тип("Структура") и Структура.Свойство("ОбработкаОбъект") Тогда
			 	Результат = Структура.ОбработкаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульОбъектаКлиент()
	
	Если ТипЗнч(МодульОбъектаКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбъектаКлиент;
	КонецЕсли;
	
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		МодульОбъектаКлиент = ОсновнаяФорма.МодульОбъектаКлиент();
		Возврат МодульОбъектаКлиент;
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
	
	Если ЭтаФорма.ВладелецФормы = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ЭтаФорма.ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;
    	
КонецФункции

&НаКлиенте
Функция НоваяМетрика()
	
	Возврат ОсновнаяФорма().НоваяМетрика();
	
КонецФункции

// } СЛУЖЕБНЫЕ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
	Параметры.Свойство("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	
	Для Каждого Сеть Из Параметры.НеподключенныеСети Цикл
		НоваяСтрока = ТаблицаСетей.Добавить();
		НоваяСтрока.ТорговаяСеть = Сеть.Наименование;
		НоваяСтрока.Кнопка = "Подключиться";
		НоваяСтрока.СсылкаНаСайте = Сеть.СсылкаНаСайте;
		НоваяСтрока.Иконка = 0;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	ЭтаФорма.Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Элементы.ТаблицаСетей.Шапка = Ложь;
	
	ОсновнаяФорма().ПриОткрытииФормы(ЭтаФорма, "Сервис_ПредложениеПодключитьсяКСетямУправляемая");
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСетейПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСетейПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСетейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Элементы.ТаблицаСетей.ТекущиеДанные.СсылкаНаСайте) Тогда
		ТекСсылка = "http://www.r-kontur.ru/clients/"+СокрЛП(Элементы.ТаблицаСетей.ТекущиеДанные.СсылкаНаСайте);
	Иначе
		ТекСсылка = "http://www.r-kontur.ru/price";
	КонецЕсли;
	
	ТекСсылка = ТекСсылка + "?GLN="+СокрЛП(Параметры.ОсновнойGLN);
	
	ЗапуститьПриложение(ТекСсылка);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
   ПередЗакрытиемСервер();
   
КонецПроцедуры

&НаСервере
Процедура ПередЗакрытиемСервер()
	Структура = Новый Структура;
	
	НоваяДатаНапоминания = ТекущаяДата();
	
	Если НеНапоминатьМесяц Тогда
		НоваяДатаНапоминания = ДобавитьМесяц(ТекущаяДата(),1);
	КонецЕсли;
	
	Структура.Вставить("КонтурEDI_ДатаПредложенияПодключенияТС", НоваяДатаНапоминания);
	
	ХранилищеСистемныхНастроек.Сохранить("КонтурEDI_ДатаПредложенияПодключенияТС", , Структура);
	
КонецПроцедуры // ПередЗакрытиемСервер()

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	ОсновнаяФорма().ПриЗакрытииФормы(ЭтаФорма, "Сервис_ПредложениеПодключитьсяКСетямУправляемая");	
	
КонецПроцедуры

