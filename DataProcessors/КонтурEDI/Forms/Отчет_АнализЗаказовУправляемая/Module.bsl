&НаСервере
Перем МодульОбъекта;

&НаКлиенте
Перем МодульОбъектаКлиент;

// СЛУЖЕБНЫЕ {

// Инициализация модуля и его экспортных функций
&НаСервере
Функция МодульОбъекта()
	
	Если МодульОбъекта = Неопределено Тогда
		
		Если Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект Тогда
			МодульОбъекта = МодульОбъектаИзКэша();
		КонецЕсли;
		
		Если МодульОбъекта = Неопределено Тогда
			МодульОбъекта = РеквизитФормыВЗначение("Объект");
			МодульОбъекта.ИнициализироватьПодключаемыеМодули(); // TODO Проверить что из этого используется на формах и перенести в ПараметрыКлиентСервер
			МодульОбъекта.ОбновитьКэшМодуляОбъекта();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат МодульОбъекта;
	
КонецФункции

&НаСервере
Функция МодульОбъектаИзКэша()
	
	Результат = Неопределено;
	
	Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда
		АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект;
 		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Структура = ПолучитьИзВременногоХранилища(АдресВХ);
			Если ТипЗнч(Структура) = Тип("Структура") и Структура.Свойство("ОбработкаОбъект") Тогда
			 	Результат = Структура.ОбработкаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульОбъектаКлиент()
	
	Если ТипЗнч(МодульОбъектаКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбъектаКлиент;
	КонецЕсли;
	
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		МодульОбъектаКлиент = ОсновнаяФорма.МодульОбъектаКлиент();
		Возврат МодульОбъектаКлиент;
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
	
	Если ЭтаФорма.ВладелецФормы = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ЭтаФорма.ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;
    	
КонецФункции

&НаКлиенте
Функция НоваяМетрика()
	
	Возврат ОсновнаяФорма().НоваяМетрика();
	
КонецФункции

// } СЛУЖЕБНЫЕ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
	Параметры.Свойство("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	
	Параметры.МодальностьЗапрещена = МодульОбъекта().МодальностьЗапрещена();
	ПутьКФормам = МодульОбъекта().Метаданные().ПолноеИмя() + ".Форма.";
	
	СхемаКомпоновки = МодульОбъекта().ПолучитьМакет("Отчет_АнализЗаказов");
	
    АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпоновки, УникальныйИдентификатор);
    КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных));
	
	Настройки = СхемаКомпоновки.НастройкиПоУмолчанию;
	КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);
	
	ЕстьРеквизитОрганизация = ПроверитьНаличиеРеквизитаОрганизация();
	
	Если Не ЕстьРеквизитОрганизация Тогда 
		Сообщить("Для формирования отчета необходим реквизит справочника КонтурEDI_Сообщения ""Организация"""+Символы.ПС+"Обновите объекты Контур.EDI");
		
		ЭтаФорма.ТолькоПросмотр=Истина;
		Элементы.СформироватьИсходныеДанные.Доступность=Ложь;
		Элементы.СформироватьИсходныеДанные.Заголовок="Обновите объекты Контур.EDI";
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодДатыПоставки(Команда)
	ОткрытьВыборПериодаДатыПоставки("Поставки");
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодДатыЗаказов(Команда)
	ОткрытьВыборПериодаДатыПоставки("Заказы");
КонецПроцедуры

 &НаКлиенте
Процедура ОткрытьВыборПериодаДатыПоставки(РедактируемыПериод)
	Диалог = Новый ДиалогРедактированияСтандартногоПериода();
	
	Если РедактируемыПериод = "Поставки" Тогда
		Диалог.Период = НастройкаПериодаДатаПоставки;
	Иначе
		Диалог.Период = НастройкаПериодаДатаЗаказов;
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("РедактируемыПериод",РедактируемыПериод);
	
	Если Параметры.МодальностьЗапрещена Тогда 
		Выполнить("Диалог.Показать(Новый ОписаниеОповещения(""ОбработчикВыбораПериодаДатыПоставки"", ЭтаФорма,СтруктураПараметров))");
	Иначе
		Если Диалог.Редактировать() Тогда 
			ОбработчикВыбораПериодаДатыПоставки(Диалог.Период,СтруктураПараметров);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикВыбораПериодаДатыПоставки(Период,СтруктураПараметров) Экспорт 
	
	Если Период<>Неопределено Тогда
		НастройкаПериодаДатаПоставки=Неопределено;
		НастройкаПериодаДатаЗаказов=Неопределено;
		Если СтруктураПараметров.РедактируемыПериод="Поставки" Тогда
			НастройкаПериодаДатаПоставки=Период;
		Иначе
			НастройкаПериодаДатаЗаказов=Период;
		КонецЕсли;
	КонецЕсли;
	Если не ЗначениеЗаполнено(НастройкаПериодаДатаЗаказов) Тогда
		Элементы.НастройкаПериодаДатаЗаказов.Видимость = Ложь;
	КонецЕсли;
	Если не ЗначениеЗаполнено(НастройкаПериодаДатаПоставки) Тогда
		Элементы.НастройкаПериодаДатаПоставки.Видимость = Ложь;
	КонецЕсли;
	
		
	
	
КонецПроцедуры // ОбработчикВыбораПериода()

&НаКлиенте
Процедура СформироватьИсходныеДанные(Команда)
	Состояние("СобираюИсходныеДанные...",50,,);
	
	СформироватьИсходныеДанныеСервер();
	
КонецПроцедуры

&НаСервере
Функция ПроверитьНаличиеРеквизитаОрганизация()

Возврат МодульОбъекта().ЕстьНеобходимыеМетаданные("Справочники.КонтурEDI_Сообщения.Реквизиты.Организация");

КонецФункции // ПроверитьНаличиеРеквизитаОрганизация()

&НаСервере
Процедура СформироватьИсходныеДанныеСервер()
    ИзвлеченныеДанные.Очистить();
	
	НастройкаПериода=?(ЗначениеЗаполнено(НастройкаПериодаДатаПоставки),НастройкаПериодаДатаПоставки,НастройкаПериодаДатаЗаказов);
	
	НачалоПериода = НастройкаПериода.ДатаНачала;
	Если Не ЗначениеЗаполнено(НастройкаПериода.ДатаОкончания) Тогда
		КонецПериода = КонецДня(Дата("39990101"));
	Иначе	
		КонецПериода = КонецДня(НастройкаПериода.ДатаОкончания);
	КонецЕсли;	

	ОтборПоДатеПоставки = ЗначениеЗаполнено(НастройкаПериодаДатаПоставки);
	ЗначениеВРеквизитФормы(МодульОбъекта().Анализ_СформироватьИсходныеДанныеСервер(НачалоПериода,КонецПериода,ОтборПоДатеПоставки),"ИзвлеченныеДанные");
	
	Элементы.ГруппаСформироватьОтчет.Доступность=Истина;
КонецПроцедуры // СформироватьИсходныеДанныеСервер()

&НаКлиенте
Процедура СформироватьОтчет(Команда)
	СформироватьОтчетНаСервере();
	ТекущийЭлемент = Элементы.РезультатОтчета;
	
	РезультатОтчета.ПоказатьУровеньГруппировокСтрок(1);
КонецПроцедуры

&НаСервере
Процедура СформироватьОтчетНаСервере()

	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("ORDERS",РеквизитФормыВЗначение("ИзвлеченныеДанные"));
	
	СхемаКомпоновки = МодульОбъекта().ПолучитьМакет("Отчет_АнализЗаказов");
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных));
	//Настройки = СхемаКомпоновки.НастройкиПоУмолчанию;
	//
	//КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);

	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновки,КомпоновщикНастроек.ПолучитьНастройки(),ДанныеРасшифровки);
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки,ВнешниеНаборыДанных,ДанныеРасшифровки);
	
	//ДокументРезультат = Новый ТабличныйДокумент;
	РезультатОтчета.Очистить();
	
	ПроцессорВывода = новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(РезультатОтчета);
	
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	//Возврат ДокументРезультат;
	
КонецПроцедуры // СформироватьОтчетНаСервере()

&НаКлиенте
Процедура НастроитьОтчет(Команда)
	ТекущийЭлемент = Элементы.КомпоновщикНастроекНастройки;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ОсновнаяФорма().ПриОткрытииФормы(ЭтаФорма, "Отчет_АнализЗаказовУправляемая");
		
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	ОсновнаяФорма().ПриЗакрытииФормы(ЭтаФорма, "Отчет_АнализЗаказовУправляемая");		
	
КонецПроцедуры



