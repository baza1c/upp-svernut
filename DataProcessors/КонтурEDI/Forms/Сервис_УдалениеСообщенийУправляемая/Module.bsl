&НаСервере
Перем МодульОбъекта;

&НаКлиенте
Перем МодульОбъектаКлиент;

// СЛУЖЕБНЫЕ {

// Инициализация модуля и его экспортных функций
&НаСервере
Функция МодульОбъекта()
	
	Если МодульОбъекта = Неопределено Тогда
		
		Если Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект Тогда
			МодульОбъекта = МодульОбъектаИзКэша();
		КонецЕсли;
		
		Если МодульОбъекта = Неопределено Тогда
			МодульОбъекта = РеквизитФормыВЗначение("Объект");
			МодульОбъекта.ИнициализироватьПодключаемыеМодули(); // TODO Проверить что из этого используется на формах и перенести в ПараметрыКлиентСервер
			МодульОбъекта.ОбновитьКэшМодуляОбъекта();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат МодульОбъекта;
	
КонецФункции

&НаСервере
Функция МодульОбъектаИзКэша()
	
	Результат = Неопределено;
	
	Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда
		АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект;
 		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Структура = ПолучитьИзВременногоХранилища(АдресВХ);
			Если ТипЗнч(Структура) = Тип("Структура") и Структура.Свойство("ОбработкаОбъект") Тогда
			 	Результат = Структура.ОбработкаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульОбъектаКлиент()
	
	Если ТипЗнч(МодульОбъектаКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбъектаКлиент;
	КонецЕсли;
	
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		МодульОбъектаКлиент = ОсновнаяФорма.МодульОбъектаКлиент();
		Возврат МодульОбъектаКлиент;
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
	
	Если ЭтаФорма.ВладелецФормы = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ЭтаФорма.ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;
    	
КонецФункции

&НаКлиенте
Функция НоваяМетрика()
	
	Возврат ОсновнаяФорма().НоваяМетрика();
	
КонецФункции

// } СЛУЖЕБНЫЕ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
	Параметры.Свойство("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	
	МодальностьЗапрещена = Параметры.МодальностьЗапрещена;
	РольПользователяEDI = МодульОбъекта().ПараметрыПользователяEDI.РольПользователяEDI;
	
	УстановитьВидимостьДоступностьЭлементов(); 	
	УстановитьТекстКоличествоСообщенийДляУдаленияСервер();

КонецПроцедуры

&НаКлиенте
Процедура КомандаУдалить(Команда)
	
	Если КоличествоСообщенийДляУдаления = 0
		И НЕ КоличествоСообщенийПосчитано Тогда
		КомандаПосчитать(Команда);
	КонецЕсли;
	
	Если КоличествоСообщенийДляУдаления = 0 Тогда
		ТекстПредупреждения = "За выбранный период не найдено сообщений для удаления.";
		
		Если МодальностьЗапрещена Тогда 
			Выполнить("ПоказатьПредупреждение(, ТекстПредупреждения, , ""Контур.EDI"")");
		Иначе
			Предупреждение(ТекстПредупреждения, , "Контур.EDI");
		КонецЕсли;
	Иначе
		ТекстВопроса = "За выбранный период найдено сообщений для удаления: " + КоличествоСообщенийДляУдаления
						+ Символы.ПС + "Вы уверены, что хотите продолжить удаление?";
		КнопкиВопроса = Новый СписокЗначений;
		КнопкиВопроса.Добавить("Да, удалить сообщения");
		КнопкиВопроса.Добавить("Нет");
		ДопПараметрДляПередачиВОбработчик = Неопределено;
		РезультатВопроса = Неопределено;
		
		Если МодальностьЗапрещена Тогда
			Выполнить("ПоказатьВопрос(Новый ОписаниеОповещения(""ОбработчикСогласияУдаленияСообщений"", ЭтаФорма, ДопПараметрДляПередачиВОбработчик), ТекстВопроса, КнопкиВопроса,,,""EDI.Контур"")");
		Иначе
			РезультатВопроса = Вопрос(ТекстВопроса, КнопкиВопроса,,,"EDI.Контур");
			ОбработчикСогласияУдаленияСообщений(РезультатВопроса, ДопПараметрДляПередачиВОбработчик);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработчикСогласияУдаленияСообщений(РезультатВопроса, ДопПараметрОбработчика = Неопределено) Экспорт
	
	Если РезультатВопроса = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатВопроса = "Да, удалить сообщения" Тогда
		ТекПароль = "";
		ТекстПросьбы = "Введите пароль от учетной записи Контур.EDI";
		ДопПараметрДляПередачиВОбработчик = Неопределено;
		
		Если МодальностьЗапрещена Тогда 
			Выполнить("ПоказатьВводСтроки(Новый ОписаниеОповещения(""ОбработчикВводаПароля"", ЭтаФорма, ДопПараметрДляПередачиВОбработчик), ТекПароль, ТекстПросьбы, , ложь)"); 
		Иначе
			Если ВвестиСтроку(ТекПароль, ТекстПросьбы) Тогда
				ОбработчикВводаПароля(ТекПароль);	
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикВводаПароля(ТекПароль, ДопПараметрОбработчика = Неопределено) Экспорт
	
	Если ТекПароль = Неопределено Тогда
		// отказ от ввода
		Возврат;
	КонецЕсли;
	
	ВерныйПароль = Ложь;
	ТекущийПарольПользователя = ПолучитьПарольВызовСервера();
	
	Если СокрЛП(ТекПароль) = СокрЛП(ТекущийПарольПользователя) 
		ИЛИ СокрЛП(ТекПароль) = "******" Тогда
		ВерныйПароль = Истина;
	КонецЕсли;
	
	Если НЕ ВерныйПароль Тогда 
		ТекстПредупреждения = "Указан неверный пароль";
		ДопПараметрДляПередачиВОбработчик = Неопределено;
		Если МодальностьЗапрещена Тогда 
			Выполнить("ПоказатьПредупреждение(,ТекстПредупреждения,,""Контур.EDI"")");
		Иначе
			Предупреждение(ТекстПредупреждения,,"Контур.EDI");
		КонецЕсли;
	Иначе
		ОбработчикПродолженияУдаленияСообщений(НачалоПериода, КонецПериода);
	КонецЕсли;
	
КонецПроцедуры // ОбработчикВводаПароля()

&НаКлиенте
Процедура ОбработчикПродолженияУдаленияСообщений(НачалоПериода, КонецПериода)

	Состояние("Выполняется удаление сообщений (" + КоличествоСообщенийДляУдаления + ") за период" 
				+ ?(ЗначениеЗаполнено(НачалоПериода), " с " + Формат(НачалоПериода, "ДЛФ=Д"), "") 
				+ " по " + Формат(КонецПериода, "ДЛФ=Д"));
				
	КоличествоСообщенийУдалено = УдалитьСообщенияСервер(НачалоПериода, КонецПериода);
	ТекстПредупреждения = "Завершено удаление сообщений: "  + КоличествоСообщенийУдалено;
	
	Если МодальностьЗапрещена Тогда 
		Выполнить("ПоказатьПредупреждение(, ТекстПредупреждения, , ""Контур.EDI"")");
	Иначе
		Предупреждение(ТекстПредупреждения, , "Контур.EDI");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция УдалитьСообщенияСервер(НачалоПериода, КонецПериода)
	
	КоличествоСообщенийУдалено = МодульОбъекта().УдалитьСообщения_КонтурEDI(НачалоПериода, КонецПериода);
	КоличествоСообщенийДляУдаления = КоличествоСообщенийДляУдаления - КоличествоСообщенийУдалено;
	УстановитьТекстКоличествоСообщенийДляУдаленияСервер();
	
	Возврат КоличествоСообщенийУдалено;
	
КонецФункции

&НаСервере
Функция ПолучитьПарольВызовСервера()

	Возврат МодульОбъекта().ПараметрыПользователяEDI.Пароль;	

КонецФункции // ПолучитьПарольВызовСервера()

&НаКлиенте
Процедура КомандаПосчитать(Команда)
	
	Состояние("Расчет количества сообщений для удаления");
	ПосчитатьКоличествоСообщенийДляУдаленияСервер(НачалоПериода, КонецПериода);
	КоличествоСообщенийПосчитано = Истина;
	УстановитьТекстКоличествоСообщенийДляУдаления();

КонецПроцедуры

&НаСервере
Процедура ПосчитатьКоличествоСообщенийДляУдаленияСервер(НачалоПериода, КонецПериода)

	КоличествоСообщенийДляУдаления = МодульОбъекта().ПосчитатьКоличествоСообщенийДляУдаления(НачалоПериода, КонецПериода);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступностьЭлементов()

	Если НЕ РольПользователяEDI = "ПолныеПрава" Тогда
		
		Элементы.НадписьПредупреждениеПраваПользователяEDI.Видимость = Истина;
		Элементы.КнопкаУдалить.Доступность = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТекстКоличествоСообщенийДляУдаления()
	
	Если КоличествоСообщенийДляУдаления = 0 
		И НЕ КоличествоСообщенийПосчитано Тогда
		Элементы.КоличествоСообщенийДляУдаления.Формат = "ЧН='не посчитано'";
	ИначеЕсли КоличествоСообщенийДляУдаления = 0
		И КоличествоСообщенийПосчитано Тогда
		Элементы.КоличествоСообщенийДляУдаления.Формат = "ЧН='0'";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТекстКоличествоСообщенийДляУдаленияСервер()
	
	Если КоличествоСообщенийДляУдаления = 0 
		И НЕ КоличествоСообщенийПосчитано Тогда
		Элементы.КоличествоСообщенийДляУдаления.Формат = "ЧН='не посчитано'";
	ИначеЕсли КоличествоСообщенийДляУдаления = 0
		И КоличествоСообщенийПосчитано Тогда
		Элементы.КоличествоСообщенийДляУдаления.Формат = "ЧН='0'";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СброситьКоличествоСообщенийДляУдаления()
	
	КоличествоСообщенийДляУдаления = 0;
	КоличествоСообщенийПосчитано = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоПериодаПриИзменении(Элемент)
	
	Если НачалоПериода > КонецПериода
		И ЗначениеЗаполнено(КонецПериода) Тогда 
		НачалоПериода = КонецПериода;
	КонецЕсли;
	
	СброситьКоличествоСообщенийДляУдаления();
	УстановитьТекстКоличествоСообщенийДляУдаления();
	
КонецПроцедуры

&НаКлиенте
Процедура КонецПериодаПриИзменении(Элемент)
	
	Если НачалоПериода > КонецПериода Тогда 
		КонецПериода = НачалоПериода;
	КонецЕсли;
	
	СброситьКоличествоСообщенийДляУдаления();
	УстановитьТекстКоличествоСообщенийДляУдаления();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ОсновнаяФорма().ПриОткрытииФормы(ЭтаФорма, "Сервис_УдалениеСообщенийУправляемая");	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	ОсновнаяФорма().ПриЗакрытииФормы(ЭтаФорма, "Сервис_УдалениеСообщенийУправляемая");	
	
КонецПроцедуры
