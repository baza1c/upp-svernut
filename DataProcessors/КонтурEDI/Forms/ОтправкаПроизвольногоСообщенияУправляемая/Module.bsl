&НаСервере
Перем МодульОбъекта;

&НаКлиенте
Перем МодульОбъектаКлиент;

// СЛУЖЕБНЫЕ {

// Инициализация модуля и его экспортных функций
&НаСервере
Функция МодульОбъекта()
	
	Если МодульОбъекта = Неопределено Тогда
		
		Если Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект Тогда
			МодульОбъекта = МодульОбъектаИзКэша();
		КонецЕсли;
		
		Если МодульОбъекта = Неопределено Тогда
			МодульОбъекта = РеквизитФормыВЗначение("Объект");
			МодульОбъекта.ИнициализироватьПодключаемыеМодули(); // TODO Проверить что из этого используется на формах и перенести в ПараметрыКлиентСервер
			МодульОбъекта.ОбновитьКэшМодуляОбъекта();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат МодульОбъекта;
	
КонецФункции

&НаСервере
Функция МодульОбъектаИзКэша()
	
	Результат = Неопределено;
	
	Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда
		АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект;
 		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Структура = ПолучитьИзВременногоХранилища(АдресВХ);
			Если ТипЗнч(Структура) = Тип("Структура") и Структура.Свойство("ОбработкаОбъект") Тогда
			 	Результат = Структура.ОбработкаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульОбъектаКлиент()
	
	Если ТипЗнч(МодульОбъектаКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбъектаКлиент;
	КонецЕсли;
	
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		МодульОбъектаКлиент = ОсновнаяФорма.МодульОбъектаКлиент();
		Возврат МодульОбъектаКлиент;
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
	
	Если ЭтаФорма.ВладелецФормы = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ЭтаФорма.ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;
    	
КонецФункции

&НаКлиенте
Функция НоваяМетрика()
	
	Возврат ОсновнаяФорма().НоваяМетрика();
	
КонецФункции

// } СЛУЖЕБНЫЕ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
	Параметры.Свойство("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	
	Параметры.МодальностьЗапрещена = МодульОбъекта().МодальностьЗапрещена();
	ПутьКФормам = МодульОбъекта().Метаданные().ПолноеИмя() + ".Форма.";
	СписокGLN = ПолучитьСписокGLNДляОтправкиСообщения();
	ЗаполнитьСписокВыбораЭлемента(Элементы.GLN, СписокGLN);

КонецПроцедуры

&НаКлиенте
Процедура Отправить(Команда)
	
	ОчиститьСообщения();
	Успешно=Ложь;
	ФайлОтправкиДвДанн = Новый ДвоичныеДанные(ИмяФайла);	
	ВыполнитьОтправкуСервер(ФайлОтправкиДвДанн,GLN,Успешно);
	Если Успешно Тогда 
		//УдалитьФайл
		Сообщить("Отправлено");
		УдалитьФайлы(ИмяФайла); 
	Иначе
		Сообщить("Ошибка отправки");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьОтправкуСервер(ФайлОтправкиДвДанн,GLN,Успешно)

	ИмяВременного=ПолучитьИмяВременногоФайла("xlm");
	ФайлОтправкиДвДанн.Записать(ИмяВременного);
	
	Результат = МодульОбъекта().ПередатьСообщениеНаСервер(Новый Структура("ПутьКФайлу,GLN,Успешно",ИмяВременного,GLN,Истина));
	
	Если Результат.Успешно Тогда
		Успешно=Истина;
	ИНаче
		Успешно=Ложь;
	КонецЕсли;

КонецПроцедуры // ВыполнитьОтправку()

&НаКлиенте
Процедура ИмяФайлаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Фильтр = "XML|*.XML";
	Если Диалог.Выбрать() Тогда
		ИмяФайла = Диалог.ПолноеИмяФайла;
	КонецЕсли;

КонецПроцедуры

&НаСерверебезКонтекста
Функция ПолучитьСписокGLNДляОтправкиСообщения()
	
	Результат = Новый СписокЗначений;
	
	Запрос = новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	КонтурEDI_ДополнительныеРеквизиты.Значение КАК GLN
	|ИЗ
	|	РегистрСведений.КонтурEDI_ДополнительныеРеквизиты КАК КонтурEDI_ДополнительныеРеквизиты
	|ГДЕ
	|	(КонтурEDI_ДополнительныеРеквизиты.Свойство = ""GLN_Основной""
	|			ИЛИ КонтурEDI_ДополнительныеРеквизиты.Свойство = ""GLN_Организации"")
	|
	|УПОРЯДОЧИТЬ ПО
	|	GLN";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Результат.Добавить(Выборка.GLN);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокВыбораЭлемента(Элемент, Список)
	
	Элемент.СписокВыбора.Очистить();
	
	Для каждого Эл Из Список Цикл
		Элемент.СписокВыбора.Добавить(Эл.Значение, Эл.Представление);
	КонецЦикла;	

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ОсновнаяФорма().ПриОткрытииФормы(ЭтаФорма, "ОтправкаПроизвольногоСообщенияУправляемая");	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	ОсновнаяФорма().ПриЗакрытииФормы(ЭтаФорма, "ОтправкаПроизвольногоСообщенияУправляемая");	
	
КонецПроцедуры
