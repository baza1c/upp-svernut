&НаСервере
Перем МодульОбъекта;

&НаКлиенте
Перем МодульОбъектаКлиент;

// СЛУЖЕБНЫЕ {

// Инициализация модуля и его экспортных функций
&НаСервере
Функция МодульОбъекта()
	
	Если МодульОбъекта = Неопределено Тогда
		
		Если Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект Тогда
			МодульОбъекта = МодульОбъектаИзКэша();
		КонецЕсли;
		
		Если МодульОбъекта = Неопределено Тогда
			МодульОбъекта = РеквизитФормыВЗначение("Объект");
			МодульОбъекта.ИнициализироватьПодключаемыеМодули(); // TODO Проверить что из этого используется на формах и перенести в ПараметрыКлиентСервер
			МодульОбъекта.ОбновитьКэшМодуляОбъекта();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат МодульОбъекта;
	
КонецФункции

&НаСервере
Функция МодульОбъектаИзКэша()
	
	Результат = Неопределено;
	
	Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда
		АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект;
 		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Структура = ПолучитьИзВременногоХранилища(АдресВХ);
			Если ТипЗнч(Структура) = Тип("Структура") и Структура.Свойство("ОбработкаОбъект") Тогда
			 	Результат = Структура.ОбработкаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульОбъектаКлиент()
	
	Если ТипЗнч(МодульОбъектаКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбъектаКлиент;
	КонецЕсли;
	
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		МодульОбъектаКлиент = ОсновнаяФорма.МодульОбъектаКлиент();
		Возврат МодульОбъектаКлиент;
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
	
	Если ЭтаФорма.ВладелецФормы = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ЭтаФорма.ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;
    	
КонецФункции

&НаКлиенте
Функция НоваяМетрика()
	
	Возврат ОсновнаяФорма().НоваяМетрика();
	
КонецФункции

// } СЛУЖЕБНЫЕ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
	Параметры.Свойство("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	
	Параметры.МодальностьЗапрещена = МодульОбъекта().МодальностьЗапрещена();
	ПутьКФормам = МодульОбъекта().Метаданные().ПолноеИмя() + ".Форма.";
	
	СтруктураОрганизаций =  МодульОбъекта().ПолучитьСтруктуруОрганизаций_КонтурEDI();
	
	URL = Параметры.URL;
	
	Если Не ЗначениеЗаполнено(URL) Тогда
		URL = Объект.ПараметрыКлиентСервер.URLАдреса.ИсторияРелизовМодуляEDI;
	Иначе
		
		Заголовок = "Контур.EDI. Помощь";
		
		URL_БезРазрешения = СтрЗаменить(URL, ".html", "");
		
		РазбивкаURL = СтрЗаменить(URL_БезРазрешения, "/", Символы.ПС);
		ПоследняяЧастьURL = СтрПолучитьСтроку(РазбивкаURL, СтрЧислоСтрок(РазбивкаURL));
		
		URL_ВыборОрганизации = СтрЗаменить(URL_БезРазрешения, ПоследняяЧастьURL, "Choice_ID_EDI") + ".html"; 
		
	КонецЕсли;
	
	Если URL = Объект.ПараметрыКлиентСервер.URLАдреса.ИсторияРелизовМодуляEDI Тогда
		КодРаздела = "ПоказатьОбновления_НомерРелиза";
		Заголовок = "Контур.EDI. Новое в релизе";
	КонецЕсли;
		
	HTMLТекст = URL;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	ОсновнаяФорма().ПриЗакрытииФормы(ЭтаФорма, "ФормаПомощникаEDI_Управляемая");	
	
КонецПроцедуры

&НаКлиенте
Процедура HTMLТекстПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	href = ДанныеСобытия.Href;
	name = ДанныеСобытия.Element.outerText;
	
	Если href = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Если Найти(href, "choose_ID_OrganizationEDI") > 0  Тогда
		
		Если СтруктураОрганизаций.Количество() > 1 Тогда
			
			ПерейтиПоСсылкеПослеВыбора	= href;
			ВыборОрганизации			= Истина;
			
			HTMLТекст = URL_ВыборОрганизации;
							
		Иначе
			
			Для Каждого ОрганизацияEDI Из СтруктураОрганизаций Цикл
				
				href = СтрЗаменить(href, "choose_ID_OrganizationEDI", ОрганизацияEDI.Значение.partyId);
				
				ЗапуститьПриложение(href);
									
				Прервать;
				
			КонецЦикла;
			
		КонецЕсли;
		
	ИначеЕсли Найти(href, "insert_ID_OrganizationEDI") > 0  Тогда 			
		
		ТекстСсылки = СтрЗаменить(href , Символы.ПС, " ");
		ТекстСсылки = СтрЗаменить(href , "insert_ID_OrganizationEDI", Символы.ПС);
		ТекстСсылки = СокрЛП(СтрПолучитьСтроку(ТекстСсылки, 2));
		
		ПерейтиПоСсылкеПослеВыбора = СтрЗаменить(ПерейтиПоСсылкеПослеВыбора, "choose_ID_OrganizationEDI", ТекстСсылки);
		
		ЗапуститьПриложение(ПерейтиПоСсылкеПослеВыбора);
		
		HTMLТекст = URL;
		
	Иначе 			
		
		НоваяМетрика().НаФорме(ЭтаФорма).Категория("Помощь").НажатаСсылка(name, href).ДобавитьПеременную("urlПомощника", URL).ПоместитьВОчередь();
		ЗапуститьПриложение(href); 			
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура HTMLТекстДокументСформирован(Элемент)
	
	Если ВыборОрганизации Тогда
		
		ВыборОрганизации	= Ложь;
		
		ДополнитьHTML_ВыбораОрганизации(Элемент.Документ.body.innerHTML);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте   
Процедура ДополнитьHTML_ВыбораОрганизации(ТекстHTML)
	
	Для Каждого ОрганизацияEDI Из СтруктураОрганизаций Цикл
		
		Текст = "";
		Текст = Текст + "</TR><!--Заголовок выпадашки-->" + Символы.ПС;
		Текст = Текст + "<TR class=title>" + Символы.ПС;
		Текст = Текст + "<TD class=name><A href='insert_ID_OrganizationEDI"+ ОрганизацияEDI.Значение.partyId +"insert_ID_OrganizationEDI'>" + ОрганизацияEDI.Значение.Наименование + "</A> </TD>" + Символы.ПС;
		Текст = Текст + "<TD class=info>" + Символы.ПС;
		Текст = Текст + "<P>GLN: " + ОрганизацияEDI.Значение.GLN + "</P></TD></TR>" + Символы.ПС;
		Текст = Текст + "<!--ТекстHTML_ВыбораОрганизации-->" + Символы.ПС;
		
		ТекстHTML = СтрЗаменить(ТекстHTML, "<!--ТекстHTML_ВыбораОрганизации-->", Текст);
				
	КонецЦикла;    	
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОсновнаяФорма().ПриЗакрытииФормы(ЭтаФорма, "ФормаПомощникаEDI_Управляемая");
	
КонецПроцедуры

