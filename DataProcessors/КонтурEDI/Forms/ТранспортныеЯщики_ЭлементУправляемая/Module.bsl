&НаСервере
Перем МодульОбъекта;

&НаКлиенте
Перем МодульОбъектаКлиент;

// СЛУЖЕБНЫЕ {

// Инициализация модуля и его экспортных функций
&НаСервере
Функция МодульОбъекта()
	
	Если МодульОбъекта = Неопределено Тогда
		
		Если Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект Тогда
			МодульОбъекта = МодульОбъектаИзКэша();
		КонецЕсли;
		
		Если МодульОбъекта = Неопределено Тогда
			МодульОбъекта = РеквизитФормыВЗначение("Объект");
			МодульОбъекта.ИнициализироватьПодключаемыеМодули(); // TODO Проверить что из этого используется на формах и перенести в ПараметрыКлиентСервер
			МодульОбъекта.ОбновитьКэшМодуляОбъекта();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат МодульОбъекта;
	
КонецФункции

&НаСервере
Функция МодульОбъектаИзКэша()
	
	Результат = Неопределено;
	
	Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда
		АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект;
 		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Структура = ПолучитьИзВременногоХранилища(АдресВХ);
			Если ТипЗнч(Структура) = Тип("Структура") и Структура.Свойство("ОбработкаОбъект") Тогда
			 	Результат = Структура.ОбработкаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульОбъектаКлиент()
	
	Если ТипЗнч(МодульОбъектаКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбъектаКлиент;
	КонецЕсли;
	
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		МодульОбъектаКлиент = ОсновнаяФорма.МодульОбъектаКлиент();
		Возврат МодульОбъектаКлиент;
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
	
	Если ЭтаФорма.ВладелецФормы = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ЭтаФорма.ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;
    	
КонецФункции

&НаКлиенте
Функция НоваяМетрика()
	
	Возврат ОсновнаяФорма().НоваяМетрика();
	
КонецФункции

// } СЛУЖЕБНЫЕ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
	Параметры.Свойство("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	
	Ссылка = Параметры.Ссылка;
	МодальностьЗапрещена = Параметры.МодальностьЗапрещена;
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		
		СтруктураЭлемента = МодульОбъекта().ПолучитьЭлементСправочника("ТранспортныеЯщики", Ссылка);
		
		Если НЕ СтруктураЭлемента = Неопределено Тогда
		
			id 										= СтруктураЭлемента.id;
			Наименование 							= СтруктураЭлемента.Наименование;
			ДатаИдентификаторПоследнегоСообщения 	= СтруктураЭлемента.lastEventId;
			ОрганизацияAPI 							= СтруктураЭлемента.ОрганизацияAPI;
			НеАктивный	 							= СтруктураЭлемента.НеАктивный;
			Тестовый	 							= СтруктураЭлемента.Тестовый;
			GLNЯщика			 					= СтруктураЭлемента.GLNЯщика;
			ОрганизацияПредставление 				= МодульОбъекта().ПолучитьПредставлениеЭлементаСправочника(ОрганизацияAPI);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТипПоля1С(ИмяНаФорме,Тип1С)
	
	ПолеФормы	= Элементы[ИмяНаФорме];
	ТипПоля		= МодульОбъекта().ПолучитьТипЗначенияОбъекта(Тип1С);
			
	Если ТипПоля = Неопределено Тогда
		
		Сообщить("Не задан тип объекта 1С для поля с типом "+Тип1С);
		
	Иначе	
		
		Элементы[ИмяНаФорме].ОграничениеТипа = Новый ОписаниеТипов(ТипПоля);
		
		Если НЕ ЗначениеЗаполнено(ЭтаФорма[ИмяНаФорме]) Тогда
			
			ЭтаФорма[ИмяНаФорме] = МодульОбъекта().ПолучитьПустуюСсылкуОбъекта(Тип1С);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаВыполнитьНажатие(Команда)
	
	УспешноеСохранение = СохранитьТранспортныйЯщикСервер();

	Если УспешноеСохранение=Истина Тогда 
		Оповестить("КонтурEDI_ОбновитьСписокУчетныхЗаписей");
		
		Соединение = Неопределено;
		
		ЭтаФорма.Закрыть(Истина);
	Иначе
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СохранитьТранспортныйЯщикСервер()
	
	Если НЕ ПроверитьЗначенияФормы() Тогда
		Возврат ложь;
	КонецЕсли;
	
	СтруктураПолей = СоздатьСтруктуруСправочникаДляСохранения();  

	МодульОбъекта().СохранитьЭлементСправочника("ТранспортныеЯщики", Ссылка, СтруктураПолей);
	
    Возврат Истина;
	
КонецФункции

&НаСервере
Функция ПроверитьЗначенияФормы()

	//// 1. инициализируем таблицу вывода ошибок
	//ТаблицаОшибок = МодульОбъекта().ИнициализироватьТаблицуОшибок();
	//
	//// 2. передать структуру для проверки
	//МодульОбъекта().ПроверитьПолеФормы(ТаблицаОшибок, Логин,	"Строка50", Истина, "Логин");
	//МодульОбъекта().ПроверитьПолеФормы(ТаблицаОшибок, Пароль, 	"Строка50",	Истина, "Пароль");
	//
	//Если ТаблицаОшибок.Количество() > 0 Тогда
	//	
	//	Если ПустаяСтрока(Логин) Тогда
	//		
	//		ТекстЗаголовка = "При заполнении учетной записи найдены ошибки.";
	//		
	//	Иначе
	//		
	//		ТекстЗаголовка = "При заполнении учетной записи """+СокрЛП(Логин)+""" найдены ошибки.";
	//		
	//	КонецЕсли;
	//		
	//	//МодульПроверкиДанных.ОткрытьФормуВыводаОшибок(ТекстЗаголовка,ТаблицаОшибок,ЭтаФорма);
	//	
	//	Возврат Ложь;
	//	
	//Иначе
	//	
	//	Возврат Истина;
	//	
	//КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция СоздатьСтруктуруСправочникаДляСохранения()
	
	СтруктураПолей = Новый Структура();
	СтруктураПолей.Вставить("id", 									id);
	СтруктураПолей.Вставить("Наименование", 						id);
	СтруктураПолей.Вставить("ОрганизацияAPI",						ОрганизацияAPI);
	СтруктураПолей.Вставить("lastEventId",							ДатаИдентификаторПоследнегоСообщения);
	СтруктураПолей.Вставить("НеАктивный",							НеАктивный);
	СтруктураПолей.Вставить("GLNЯщика",								GLNЯщика);
	СтруктураПолей.Вставить("Тестовый",								Тестовый);
	
	Возврат СтруктураПолей;
	
КонецФункции

&НаКлиенте
Процедура ДатаИдентификаторПоследнегоСообщенияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДатаПоМск = ТекущаяДатаПоМскСервер();
	ПодсказкаВвода = "Выберите дату и время (МСК) начала загрузки";
	
	МодульОбъектаКлиент().ПоказатьВводДаты_КонтурEDI("ОбработчикВыбораДаты", ЭтаФорма, , ДатаПоМск, ПодсказкаВвода, ЧастиДаты.ДатаВремя);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТекущаяДатаПоМскСервер()
	
	ТриЧаса = 60 * 60 * 3;
	ТекущаяДатаПоМск = ТекущаяУниверсальнаяДата() + ТриЧаса;
	Возврат ТекущаяДатаПоМск;
	
КонецФункции

&НаКлиенте
Процедура ОбработчикВыбораДаты(ВыбраннаяДата, ДопПараметры = Неопределено) Экспорт

	Если ВыбраннаяДата = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДатаИдентификаторПоследнегоСообщения = Формат(ВыбраннаяДата, "ДФ=yyyyMMddHHmmss");

КонецПроцедуры // ОбработчикВыбораДаты()

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ОсновнаяФорма().ПриОткрытииФормы(ЭтаФорма, "ТранспортныеЯщики_ЭлементУправляемая");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	ОсновнаяФорма().ПриЗакрытииФормы(ЭтаФорма, "ТранспортныеЯщики_ЭлементУправляемая");	
	
КонецПроцедуры

