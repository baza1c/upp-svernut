&НаСервере
Перем МодульОбъекта;

&НаКлиенте
Перем МодульОбъектаКлиент;

// СЛУЖЕБНЫЕ {

// Инициализация модуля и его экспортных функций
&НаСервере
Функция МодульОбъекта()
	
	Если МодульОбъекта = Неопределено Тогда
		
		Если Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект Тогда
			МодульОбъекта = МодульОбъектаИзКэша();
		КонецЕсли;
		
		Если МодульОбъекта = Неопределено Тогда
			МодульОбъекта = РеквизитФормыВЗначение("Объект");
			МодульОбъекта.ИнициализироватьПодключаемыеМодули(); // TODO Проверить что из этого используется на формах и перенести в ПараметрыКлиентСервер
			МодульОбъекта.ОбновитьКэшМодуляОбъекта();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат МодульОбъекта;
	
КонецФункции

&НаСервере
Функция МодульОбъектаИзКэша()
	
	Результат = Неопределено;
	
	Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда
		АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект;
 		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Структура = ПолучитьИзВременногоХранилища(АдресВХ);
			Если ТипЗнч(Структура) = Тип("Структура") и Структура.Свойство("ОбработкаОбъект") Тогда
			 	Результат = Структура.ОбработкаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульОбъектаКлиент()
	
	Если ТипЗнч(МодульОбъектаКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбъектаКлиент;
	КонецЕсли;
	
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		МодульОбъектаКлиент = ОсновнаяФорма.МодульОбъектаКлиент();
		Возврат МодульОбъектаКлиент;
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
	
	Если ЭтаФорма.ВладелецФормы = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ЭтаФорма.ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;
    	
КонецФункции

&НаКлиенте
Функция НоваяМетрика()
	
	Возврат ОсновнаяФорма().НоваяМетрика();
	
КонецФункции

// } СЛУЖЕБНЫЕ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
	Параметры.Свойство("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	
	РасширениеСсылка  = Параметры.РасширениеСсылка;
	
	ЗаполнитьДанныеРасширения();
	
	Если Не События.Количество() = 0 Тогда
		ТекущееСобытие = События[0].Имя;
		Код.УстановитьТекст(События[0].Код);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСобытие(Команда)
	
	ТекущаяСтрока = Элементы.События.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ТекущаяСтрокаИндекс = События.Индекс(Элементы.События.ТекущиеДанные);
	
	//удаляем
	События.Удалить(Элементы.События.ТекущиеДанные);
	
	//позиционируем: остаемся в этой же строке или поднимаемся на строку выше
	Количество = События.Количество();
	Если Не Количество = 0 Тогда
		Если Количество > ТекущаяСтрокаИндекс Тогда
			ТекСтрока=События.Получить(ТекущаяСтрокаИндекс);
			Элементы.События.ТекущаяСтрока = ТекСтрока.ПолучитьИдентификатор();
		Иначе
			ПредСтрока=События.Получить(ТекущаяСтрокаИндекс-1);
			Элементы.События.ТекущаяСтрока = ПредСтрока.ПолучитьИдентификатор();
		КонецЕсли;
	Иначе
		Код.УстановитьТекст("");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСобытие(Команда)
	
	//получаем события и просим пользователя выбрать
	СписокВыбора = ПолучитьСписокЗначенийОписаниеСобытийПодключаемогоМодуляРасширенийВызовСервера();
	
	Если Объект.ПараметрыКлиентСервер.МодальностьЗапрещена Тогда 
		Выполнить("ПоказатьВыборИзСписка(Новый ОписаниеОповещения(""ОбработчикВыбораСобытияРасширения"",ЭтаФорма), СписокВыбора)"); 
	Иначе
		ВыбранноеЗначение=ВыбратьИзСписка(СписокВыбора,Элементы.ГруппаСобытий);
		ОбработчикВыбораСобытияРасширения(ВыбранноеЗначение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикВыбораСобытияРасширения(ВыбранноеСобытие,ДопПараметр=Неопределено) Экспорт
	
	РезультатДобавления = Новый Структура("Успешно,ПовторноеДобавление,ОписаниеОшибки,ИмяСобытия",Ложь,Ложь,"","");

	//////////////////////////////////
	Если ВыбранноеСобытие = Неопределено Тогда
		РезультатДобавления.Успешно = Ложь;
		Возврат;
	КонецЕсли;  	
	
	ИмяСобытия = ВыбранноеСобытие.Значение;
	
	Если ИмяСобытия = "<<произвольное событие>>" Тогда
		
		ИмяСобытия = "";
		МодульОбъектаКлиент().ПоказатьВводСтроки_КонтурEDI("ОбработчикПослеВводаИмениСобытия", ЭтаФорма, РезультатДобавления, ИмяСобытия, "Введите имя произвольного события", 50, Ложь);
		
	Иначе
		
		ОбработчикПослеВводаИмениСобытия(ИмяСобытия, РезультатДобавления);
		
	КонецЕсли;		
	 		
КонецПроцедуры // ОбработчикВыбораСобытияРасширения()

&НаКлиенте
Процедура ОбработчикПослеВводаИмениСобытия(ИмяСобытия, РезультатДобавления) Экспорт

	Если НЕ ЗначениеЗаполнено(ИмяСобытия)
		ИЛИ ИмяСобытия = "Введите имя произвольного события"
		ИЛИ Найти(СокрЛП(ИмяСобытия), " ") <> 0 Тогда
		
		РезультатДобавления.Успешно = Ложь;
		Возврат;
		
	КонецЕсли;
	
	//проверяем на присутствие
	ПроверкаНаПрисутствие = Ложь;
	ДобавленноеРанееСобытиеМассив = События.НайтиСтроки( Новый Структура("Имя",ИмяСобытия));
	Если Не ДобавленноеРанееСобытиеМассив.Количество() = 0 Тогда
		
		РезультатДобавления.Успешно = Ложь;
		РезультатДобавления.ПовторноеДобавление = Истина;
		РезультатДобавления.ИмяСобытия = ИмяСобытия;
		//на него и прыгнем
		НовоеСобытиеМассив = События.НайтиСтроки(Новый Структура("Имя",РезультатДобавления.ИмяСобытия));
		Если НовоеСобытиеМассив.Количество()>0 Тогда 
			НовоеСобытие=НовоеСобытиеМассив[0];
			Если Не НовоеСобытие = Неопределено Тогда
				Элементы.События.ТекущаяСтрока = НовоеСобытие.ПолучитьИдентификатор();
			КонецЕсли;
		КонецЕсли;
		
		ПроверкаНаПрисутствие = Истина;
		
	КонецЕсли;
	
	Если ПроверкаНаПрисутствие Тогда
		Возврат;
	КонецЕсли;
	
	//добавляем в список   - поход на сервер неизбежен!!!

	НовоеСобытие = События.Добавить();
	НовоеСобытие.Имя = ИмяСобытия;
	НовоеСобытие.Код = СформироватьКомментарийСобытия(ИмяСобытия);
	
	РезультатДобавления.Успешно = Истина;
	РезультатДобавления.ИмяСобытия = НовоеСобытие.Имя;
	
	//обработка результата добавления
	
	НовоеСобытиеМассив = События.НайтиСтроки(Новый Структура("Имя",РезультатДобавления.ИмяСобытия));
	Если НовоеСобытиеМассив.Количество()>0 Тогда 
		НовоеСобытие=НовоеСобытиеМассив[0];
		Если Не НовоеСобытие = Неопределено Тогда
			Элементы.События.ТекущаяСтрока = НовоеСобытие.ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли; 

КонецПроцедуры 

&НаСервере
Функция СформироватьКомментарийСобытия(ИмяСобытия)
	
	Комментарий = "";
	
	ТаблицаСобытий = МодульОбъекта().ПолучитьОписаниеСобытийПодключаемогоМодуляРасширений();
	СтрокаОписания = ТаблицаСобытий.Найти(ИмяСобытия,"Имя");
	
	Если СтрокаОписания <> Неопределено Тогда
		
		_ОписаниеСобытия = СтрокаОписания.Описание;
		_ОписаниеСобытия = СтрЗаменить(_ОписаниеСобытия,Символы.ПС,Символы.ПС + "// " + Символы.Таб);     	
		
		Комментарий = Комментарий + "// " + "Описание:" + Символы.ПС;
		Комментарий = Комментарий + "// " + Символы.Таб + _ОписаниеСобытия + "." + Символы.ПС;
		Комментарий = Комментарий + "// " + Символы.ПС;
		Комментарий = Комментарий + "// " + "Параметры:" + Символы.ПС;
		
		Для Каждого Параметр Из СтрокаОписания.Параметры Цикл
			
			_ОписаниеПараметра = Параметр.Значение.Описание;
			_ОписаниеПараметра = СтрЗаменить(_ОписаниеПараметра,Символы.ПС,Символы.ПС + "// " + Символы.Таб);
			
			Комментарий = Комментарий + "// " + Символы.Таб + Параметр.Значение.Представление + " - " + Параметр.Значение.Тип + " - " + _ОписаниеПараметра + "." + Символы.ПС;
			
		КонецЦикла;
		
		Если СтрокаОписания.СтандартнаяОбработкаEDI = Истина Тогда
			Комментарий = Комментарий + "// " + Символы.Таб + "СтандартнаяОбработкаEDI" + " - " + "Булево" + " - " + "флаг стандартной обработки" + "." + Символы.ПС;
		КонецЕсли;
		
		Если СтрокаОписания.ВозвращаемоеЗначение.Наличие = Истина Тогда
			
			_ОписаниеВозврата = СтрокаОписания.ВозвращаемоеЗначение.Описание;
			_ОписаниеВозврата = СтрЗаменить(_ОписаниеВозврата,Символы.ПС,Символы.ПС + "// " + Символы.Таб);
			
			Комментарий = Комментарий + "// " + Символы.ПС;
			Комментарий = Комментарий + "// " + "Возвращаемое значение:" + Символы.ПС;
			Комментарий = Комментарий + "// " + Символы.Таб + СтрокаОписания.ВозвращаемоеЗначение.Тип + Символы.ПС;
			Комментарий = Комментарий + "// " + Символы.Таб + _ОписаниеВозврата + Символы.ПС;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Комментарий;
	
КонецФункции

&НаСервере
Функция ПолучитьСписокЗначенийОписаниеСобытийПодключаемогоМодуляРасширенийВызовСервера()

	ТаблицаСобытий = МодульОбъекта().ПолучитьОписаниеСобытийПодключаемогоМодуляРасширений();
	//Вернем СписокЗначений
	СписокСобытий = Новый СписокЗначений;
	
	СписокСобытий.ЗагрузитьЗначения(ТаблицаСобытий.ВыгрузитьКолонку("Имя")); 
	
	Возврат СписокСобытий; 
	
КонецФункции // ПолучитьОписаниеСобытийПодключаемогоМодуляРасширенийВызовСервера()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступность();
	
	Если МодульОбъектаКлиент().ПеременнаяСодержитСвойство(Элементы.ОписаниеПолное, "АвтоМаксимальнаяШирина") Тогда	
		Элементы.ОписаниеПолное.АвтоМаксимальнаяШирина = Ложь;
	КонецЕсли;
	
	ОсновнаяФорма().ПриОткрытииФормы(ЭтаФорма, "ФормаРасширения_ЭлементУправляемая");
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступность()
	
	Если События.Количество() = 0 Тогда
		Элементы.СобытияУдалитьСобытие.Доступность = Ложь;
		Элементы.СобытияОткрытьИсходныйКод.Доступность = Ложь;
	Иначе
		Элементы.СобытияУдалитьСобытие.Доступность = Истина;
		Элементы.СобытияОткрытьИсходныйКод.Доступность = Истина;
	КонецЕсли;
		
	//доступность наименование
	Если Не РасширениеСсылка = Неопределено Тогда
		Элементы.Наименование.Доступность = Ложь;
	КонецЕсли;
	
	//доступность кнопки листинга исходного кода
	ТекущаяСтрока = Элементы.События.ТекущиеДанные;
	
	Если Не ТекущаяСтрока = Неопределено Тогда
		Если ЗначениеЗаполнено(ТекущаяСтрока.ИсходныйКод) Тогда
			Элементы.СобытияОткрытьИсходныйКод.Доступность = Истина;	
		Иначе
			Элементы.СобытияОткрытьИсходныйКод.Доступность = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеРасширения()
		
	Если Не РасширениеСсылка = Неопределено Тогда
		ЗначенияРеквизитовРасширения = МодульОбъекта().ПолучитьЗначенияРеквизитовРасширения(РасширениеСсылка);
		
		Наименование = ЗначенияРеквизитовРасширения.Наименование;

		Для Каждого РеквизитыСобытие Из ЗначенияРеквизитовРасширения.События Цикл
			НовоеСобытие = События.Добавить();
			НовоеСобытие.Имя = РеквизитыСобытие.ИмяСобытия;
			НовоеСобытие.Код = РеквизитыСобытие.ИсполняемыйКод;
			НовоеСобытие.ИсходныйКод = РеквизитыСобытие.ИсходныйИсполняемыйКод;
		КонецЦикла;
		
		ОписаниеПолное = СокрЛП(СтрЗаменить(ЗначенияРеквизитовРасширения.ОписаниеПолное,"¶",""));		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СобытияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СобытияПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СобытияПриАктивизацииЯчейки(Элемент)
	
	СохранитьКодТекущегоСобытия();
	
	//показываем код активизированного события
	ТекущаяСтрока = Элементы.События.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ТекущееСобытие = ТекущаяСтрока.Имя;
	
	Код.УстановитьТекст(ТекущаяСтрока.Код);
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьКодТекущегоСобытия()
	
	//пытаемся сохранить код текущего события
	СтрокиСобытия = События.НайтиСтроки(Новый Структура("Имя",ТекущееСобытие));
	Если СтрокиСобытия.Количество()=1 Тогда
		СтрокиСобытия[0].Код = Код.ПолучитьТекст();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьРасширение(Команда)
	СохранитьКодТекущегоСобытия();
	
	Успешно = СохранитьРасширениеВызовСервера();
	Если Успешно Тогда 
		ЭтаФорма.Закрыть(ИдентификаторНовогоРасширения);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция СохранитьРасширениеВызовСервера()

	РезультатСохранения = МодульОбъекта().СохранитьРасширение(ЭтаФорма,Истина); //Истина Управляемая
	
	Если Не РезультатСохранения.Успешно Тогда
		Если Не РезультатСохранения.ТаблицаОшибокПроверки.Количество() = 0 Тогда 		//не прошли проверку
			Для Каждого СтрокаОшибки Из РезультатСохранения.ТаблицаОшибокПроверки Цикл
				Сообщить(СтрокаОшибки.ОписаниеОшибки);
			КонецЦикла;
		Иначе	                                                                 		//не удалось записать в базу
			Сообщить(РезультатСохранения.ОписаниеОшибки);
		КонецЕсли;
		Возврат Ложь;
	Иначе                                                                        		//запись прошла успешно
		ИдентификаторНовогоРасширения = РезультатСохранения.Идентификатор;
		Возврат Истина;
	КонецЕсли;
	
КонецФункции // СохранитьРасширениеВызовСервера()

&НаСервере
Функция Удалить_СохранитьРасширение()
	                                                                   
	РезультатСохранения = МодульОбъекта().СохранитьРасширение(ЭтаФорма,Истина); //Истина Управляемая
	
	Возврат РезультатСохранения;

	
	//вынесено в МО СохранитьРасширение
	
	//РезультатСохранения = Новый Структура("Успешно,ОписаниеОшибкиСохранения,ТаблицаОшибокПроверки,Идентификатор",Ложь,"","");
	//
	//РезультатПроверки = МодульОбъекта().ПроверитьКорректностьДанныхРасширения(ЭтаФорма,Истина);
	//Если Не РезультатПроверки.Успешно Тогда
	//	РезультатСохранения.ТаблицаОшибокПроверки = РезультатПроверки.ТаблицаОшибок;
	//	Возврат РезультатСохранения;
	//КонецЕсли;
	//
	//ТаблицаДанныеРасширения = МодульОбъекта().ИнициализироватьТаблицуДополнительныхРеквизитовРасширения();
	//
	//ИдентификаторРасширения = Неопределено;
	//
	//Если РасширениеСсылка = Неопределено Тогда
	//	
	//	//добавляем данные нового события
	//	Идентификатор = ТаблицаДанныеРасширения.Добавить();
	//	Идентификатор.ИмяРеквизита 				= "Идентификатор";
	//	Идентификатор.Значение 					= Формат(ТекущаяДата(),"ДФ=ггггММддЧЧммсс");;
	//	НаименованиеРасширения = ТаблицаДанныеРасширения.Добавить();
	//	НаименованиеРасширения.ИмяРеквизита 	= "Наименование";
	//	НаименованиеРасширения.Значение 		= Наименование;
	//	ВерсияРасширения = ТаблицаДанныеРасширения.Добавить();
	//	ВерсияРасширения.ИмяРеквизита 			= "ВерсияРасширения";
	//	ВерсияРасширения.Значение 				= "0.0";
	//	РекомендуемаяВерсияМодуля = ТаблицаДанныеРасширения.Добавить();
	//	РекомендуемаяВерсияМодуля.ИмяРеквизита 	= "РекомендуемаяВерсияМодуля";
	//	РекомендуемаяВерсияМодуля.Значение 		= "0.00.000.00";
	//	Обязательный = ТаблицаДанныеРасширения.Добавить();
	//	Обязательный.ИмяРеквизита 				= "Обязательный";
	//	Обязательный.Значение 					= Ложь;
	//	Интерфейс = ТаблицаДанныеРасширения.Добавить();
	//	Интерфейс.ИмяРеквизита 					= "Интерфейс";
	//	Интерфейс.Значение 						= Ложь;
	//	ПорядокВыполнения = ТаблицаДанныеРасширения.Добавить();
	//	ПорядокВыполнения.ИмяРеквизита 			= "ПорядокВыполнения";
	//	ПорядокВыполнения.Значение 				= МодульОбъекта().ПолучитьСледующийПорядокВыполненияРасширения();
	//	Использование = ТаблицаДанныеРасширения.Добавить();
	//	Использование.ИмяРеквизита 				= "Использование";
	//	Использование.Значение 					= Истина;
	//	
	//	ИдентификаторРасширения	= Идентификатор.Значение;
	//	
	//КонецЕсли;
	//
	////добавляем даные обработчиков событий
	//Для Каждого Событие Из События Цикл
	//	НовыйРеквизит = ТаблицаДанныеРасширения.Добавить();
	//	НовыйРеквизит.ИмяРеквизита 		= "ИсполняемыйКод";
	//	НовыйРеквизит.Значение 			= Событие.Имя;
	//	НовыйРеквизит.ЗначениеСтрока 	= Событие.Код;
	//КонецЦикла;
	//
	////добавляем данные описания
	//НовыйРеквизит = ТаблицаДанныеРасширения.Добавить();
	//НовыйРеквизит.ИмяРеквизита 		= "ОписаниеПолное";
	//НовыйРеквизит.ЗначениеСтрока 	= ОписаниеПолное;
	//
	//МодульОбъекта().СохранитьЭлементСправочника("Расширения",РасширениеСсылка,ТаблицаДанныеРасширения);
	//Если РасширениеСсылка = Неопределено Тогда
	//	РезультатСохранения.ОписаниеОшибкиСохранения = "Не удалось записать расширение в базу данных.";
	//	Возврат РезультатСохранения;
	//КонецЕсли;
	//	
	//РезультатСохранения.Успешно 		= Истина;
	//РезультатСохранения.Идентификатор	= ИдентификаторРасширения;
	
	 
КонецФункции

&НаКлиенте
Процедура ОткрытьИсходныйКод(Команда)
	
	ОткрытьЛистингИсходногоКода();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЛистингИсходногоКода()
	
	ТекущаяСтрокаДанные = Элементы.События.ТекущиеДанные;
	Если ТекущаяСтрокаДанные = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.Очистить();
	ТекстовыйДокумент.УстановитьТекст(ТекущаяСтрокаДанные.ИсходныйКод);
	ТекстовыйДокумент.ТолькоПросмотр = Истина;
	ТекстовыйДокумент.Показать("Исходный код обработчика события """ + ТекущаяСтрокаДанные.Имя + """");
	
КонецПроцедуры

&НаКлиенте
Процедура ОРасширенииНажатие(Элемент)
	
	Если РасширениеСсылка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТабДокумент = Новый ТабличныйДокумент;
	
	ПоказатьСвойства(ТабДокумент);
	
	ТабДокумент.ОтображатьСетку=Ложь;
	ТабДокумент.ТолькоПросмотр=Истина;
	ОбластьЗаголовка=ТабДокумент.Область("R2");
	ОбластьЗаголовка.Шрифт = Новый Шрифт(,14,);
	
	ТабДокумент.Показать();
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьСвойства(ТабДокумент)
	
	МодульОбъекта().ПоказатьСвойстваРасширения(ТабДокумент,РасширениеСсылка);

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	ОсновнаяФорма().ПриЗакрытииФормы(ЭтаФорма, "ФормаРасширения_ЭлементУправляемая");	
	
КонецПроцедуры
