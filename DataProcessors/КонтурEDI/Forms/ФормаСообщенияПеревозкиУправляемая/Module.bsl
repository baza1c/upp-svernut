&НаСервере
Перем МодульОбъекта;

&НаКлиенте
Перем МодульОбъектаКлиент;

&НаСервере
Перем Сообщение;

// СЛУЖЕБНЫЕ {

// Инициализация модуля и его экспортных функций
&НаСервере
Функция МодульОбъекта()
	
	Если МодульОбъекта = Неопределено Тогда
		
		Если Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект Тогда
			МодульОбъекта = МодульОбъектаИзКэша();
		КонецЕсли;
		
		Если МодульОбъекта = Неопределено Тогда
			МодульОбъекта = РеквизитФормыВЗначение("Объект");
			МодульОбъекта.ИнициализироватьПодключаемыеМодули(); // TODO Проверить что из этого используется на формах и перенести в ПараметрыКлиентСервер
			МодульОбъекта.ОбновитьКэшМодуляОбъекта();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат МодульОбъекта;
	
КонецФункции

&НаСервере
Функция МодульОбъектаИзКэша()
	
	Результат = Неопределено;
	
	Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда
		АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект;
 		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Структура = ПолучитьИзВременногоХранилища(АдресВХ);
			Если ТипЗнч(Структура) = Тип("Структура") и Структура.Свойство("ОбработкаОбъект") Тогда
			 	Результат = Структура.ОбработкаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульОбъектаКлиент()
	
	Если ТипЗнч(МодульОбъектаКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбъектаКлиент;
	КонецЕсли;
	
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		МодульОбъектаКлиент = ОсновнаяФорма.МодульОбъектаКлиент();
		Возврат МодульОбъектаКлиент;
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
	
	Если ЭтаФорма.ВладелецФормы = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ЭтаФорма.ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;
    	
КонецФункции

&НаКлиенте
Функция НоваяМетрика()
	
	Возврат ОсновнаяФорма().НоваяМетрика();
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСообщениеИзВХ(АдресВоВХ)
	
	Сообщение = Неопределено;
	
	Если ЭтоАдресВременногоХранилища(АдресВоВХ) Тогда
		Сообщение = ПолучитьИзВременногоХранилища(АдресВоВХ);
	КонецЕсли;
	
	Возврат Сообщение;
	
КонецФункции // ПолучитьСообщениеИзВХ()

&НаСервереБезКонтекста
Функция ПоместитьСообщениеВоВХ(Сообщение, АдресВоВХ)

	Если ТипЗнч(Сообщение) = Тип("Структура") Тогда
		Возврат ПоместитьВоВременноеХранилище(Сообщение, АдресВоВХ);
 	КонецЕсли;	

КонецФункции // ПоместитьСообщениеВоВХ()

&НаСервере
Процедура ПоместитьСообщениеВКэш()
	
	СообщениеАдресВоВХ = ПоместитьСообщениеВоВХ(Сообщение, ЭтаФорма.УникальныйИдентификатор);
	
КонецПроцедуры // ПоместитьСообщениеВКэш()

&НаСервере
Функция ПоместитьСообщениеБезТаблицВКэш()
	
	ПрочитатьСообщениеИзКэша();
	КопияСообщения = ЗначениеИзСтрокиВнутр(ЗначениеВСтрокуВнутр(Сообщение));
	КопияСообщения.Грузополучатели = Неопределено;
	КопияСообщения.Товары = Неопределено;
	
	Возврат ПоместитьСообщениеВоВХ(КопияСообщения, ЭтаФорма.УникальныйИдентификатор);
	
КонецФункции // ПоместитьСообщениеВКэш()

&НаСервере
Процедура ПрочитатьСообщениеИзКэша()
	
	Сообщение = ПолучитьСообщениеИзВХ(СообщениеАдресВоВХ);
	
КонецПроцедуры // ПоместитьСообщениеВКэш()

// } СЛУЖЕБНЫЕ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьРеквизитыФормы();
	СформироватьСообщениеДляОткрытияКарточки();
	ЗаполнитьРеквизитыФормыИзСообщения();
	ПроизвестиПервоначальноеЗаполнениеПолейНаФорме();
	ДобавитьУсловноеОформление();
	УстановитьЗаголовокФормы_КонтурEDI();
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьУсловноеОформление()
	
	ДобавитьУсловноеОформлениеГрузополучателей();
	ДобавитьУсловноеОформлениеДополнительныхРеквизитов();
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьУсловноеОформлениеГрузополучателей()
	
	МассивСложныхПолей = Новый Массив;
	МассивСложныхПолей.Добавить("ГрузополучателиГрузополучательEDI");
	МассивСложныхПолей.Добавить("ГрузополучателиТарифНаПеревозку");
	
	Для каждого ИмяПоля Из МассивСложныхПолей Цикл
	
		ДобавитьУсловноеОформлениеСложногоПоля(ИмяПоля);
	
	КонецЦикла;	
	
	ДобавитьУсловноеОформлениеПоляТипТочкиДоставки();
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьУсловноеОформлениеДополнительныхРеквизитов()
	
	ИмяПоляОформления 	= "ДополнительныеРеквизитыЗначение";
	ИмяПоляОтбора 		= "ДополнительныеРеквизиты.Значение";
	
	ДобавитьУсловноеОформлениеПоляДаты("ДополнительныеРеквизитыЗначение", "ДополнительныеРеквизиты.ИмяРеквизита");	
	
	ИмяПоляОформления 	= "ДополнительныеРеквизитыЗначение";
	ИмяПоляОтбора 		= "ДополнительныеРеквизиты.ИмяРеквизита";
	
	МассивСложныхПолей = Новый Массив;
	МассивСложныхПолей.Добавить("Информация о транспорте");
	МассивСложныхПолей.Добавить("Информация о тарифе на перевозку");
	
	Для каждого ЗначениеПоляОтбора Из МассивСложныхПолей Цикл
	
		ДобавитьУсловноеОформлениеСложногоПоля(ИмяПоляОформления, ИмяПоляОтбора, ЗначениеПоляОтбора);
	
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ДобавитьУсловноеОформлениеСложногоПоля(ИмяПоляОформления, ИмяПоляОтбора = "", ЗначениеПоляОтбора = Неопределено)
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ПолеЭлементаОформления = ЭлементОформления.Поля.Элементы.Добавить();
	ПолеЭлементаОформления.Поле = Новый ПолеКомпоновкиДанных(ИмяПоляОформления);
	
	Если ЗначениеЗаполнено(ИмяПоляОтбора)
		И ЗначениеЗаполнено(ЗначениеПоляОтбора) Тогда
		
		ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных(ИмяПоляОтбора);
		ОтборЭлемента.ВидСравнения 		= ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение 	= ЗначениеПоляОтбора;

	КонецЕсли;
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", 		"Подробнее");
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", 	Новый Цвет(70, 130, 180));
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьУсловноеОформлениеПоляДаты(ИмяПоляОформления, ИмяПоляОтбора)
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ПолеЭлементаОформления = ЭлементОформления.Поля.Элементы.Добавить();
	ПолеЭлементаОформления.Поле = Новый ПолеКомпоновкиДанных(ИмяПоляОформления);
	
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных(ИмяПоляОтбора);
	ОтборЭлемента.ВидСравнения 		= ВидСравненияКомпоновкиДанных.Содержит;
	ОтборЭлемента.ПравоеЗначение 	= "01.01.0001";
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Формат",	Формат("ДЛФ=В"));
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ПолеЭлементаОформления = ЭлементОформления.Поля.Элементы.Добавить();
	ПолеЭлементаОформления.Поле = Новый ПолеКомпоновкиДанных(ИмяПоляОформления);
	
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных(ИмяПоляОтбора);
	ОтборЭлемента.ВидСравнения 		= ВидСравненияКомпоновкиДанных.Содержит;
	ОтборЭлемента.ПравоеЗначение 	= " 0.00.00";
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Формат",	Формат("ДЛФ=Д"));
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыФормыИзСообщения()

	ВремяПодачиТранспорта 	= Сообщение.ДатаПоставки;	
	КоличествоПалет 		= Сообщение.КоличествоУпаковок;	

КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовокФормы_КонтурEDI()
	
	ПредставлениеТипаСообщения = МодульОбъекта().ПолучитьЗаголовокФормыСообщения(ТипСообщения, Направление);
	ПредставлениеДатыСообщения = МодульОбъекта().ПолучитьПредставлениеДатыСообщения(Сообщение);
	
	ЭтаФорма.Заголовок = ПредставлениеТипаСообщения + " (" + ТипСообщения + ") " + ПредставлениеДатыСообщения;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыФормы()
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
	Параметры.Свойство("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	
	Направление 					= Параметры.Направление;
	ПереотправляемоеСообщениеСсылка = Параметры.ПереотправляемоеСообщениеСсылка;
	РежимРаботы                     = Параметры.РежимРаботы;
	СообщениеСсылка                 = Параметры.СообщениеСсылка;
	ТипСообщения                    = Параметры.ТипСообщения;
	ТолькоПросмотрСообщения         = Параметры.ТолькоПросмотр;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьСообщениеДляОткрытияКарточки()

	ПараметрыПодготовкиСообщения = Новый Структура;
	ПараметрыПодготовкиСообщения.Вставить("СообщениеСсылка", СообщениеСсылка);
	
	Если ЗначениеЗаполнено(ПереотправляемоеСообщениеСсылка) Тогда
		ПараметрыПодготовкиСообщения.Вставить("ПереотправляемоеСообщениеСсылка", ПереотправляемоеСообщениеСсылка);	
	КонецЕсли;
	
	// В ТабСообщения направление может не соответствовать сообщению, которое формируем,
	// поэтому будем проверять направление по совокупности факторов: тип сообщения и режим работы
	Направление = МодульОбъекта().ПолучитьНаправлениеСообщения(ТипСообщения, РежимРаботы);

	Если ТолькоПросмотрСообщения
		ИЛИ Направление = "Входящее" Тогда
		
		// открываем уже существующее сообщение по ссылке и это все только для просмотра
		Сообщение = МодульОбъекта().ПрочитатьСообщение(СообщениеСсылка, , ТипСообщения, Направление);
		
	КонецЕсли;
	
	Если Направление = "Исходящее" Тогда 
		
		Сообщение = МодульОбъекта().ПодготовитьИсходящееСообщение(ТипСообщения, , ПараметрыПодготовкиСообщения);
		
	КонецЕсли;
	
	Если Направление = "Входящее" Тогда
		
		// сообщение следует переконвертировать т.к. с момента его 
		// загрузки могли измениться настройки соответствий и проч.
		МодульОбъекта().КонвертироватьСообщениеEDIв1С(Сообщение);
		
	КонецЕсли;
	
	ПоместитьСообщениеВКэш();	

КонецПроцедуры

&НаСервере
Процедура ПроизвестиПервоначальноеЗаполнениеПолейНаФорме()
	
    ЗаполнитьПоляНаФорме();
	ВывестиОписаниеОшибкиСообщения();
	ПроверитьСообщениеСервер();
	УстановитьВидимость();
	НастроитьКнопкиПанели();
	
	Если ТаблицаОшибок.Количество() > 0 Тогда
		ОткрытьПанельОшибокСервер();
	Иначе
		ЗакрытьПанельОшибокСервер();
	КонецЕсли;

КонецПроцедуры

&НаСервере 
Процедура ДобавитьУсловноеОформлениеПоляТипТочкиДоставки()
	
	СписокЗначенийТипТочкиДоставки = МодульОбъекта().ПолучитьСписокЗначенийПеречисленияEDI("ТипТочкиДоставки");
	ИмяПоляОформления 	= "ГрузополучателиТипТочкиДоставки";
	ИмяПоляОтбора 		= "Грузополучатели.ТипТочкиДоставки";
	
	Для каждого Эл Из СписокЗначенийТипТочкиДоставки Цикл
		
		Элемент = УсловноеОформление.Элементы.Добавить();
		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ИмяПоляОформления);
		
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоляОтбора);
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Эл.Значение;
		
		Элемент.Оформление.УстановитьЗначениеПараметра("Текст", Эл.Представление);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьСообщениеСервер()
	
	РезультатПроверки = МодульОбъекта().ПроверитьПоляСообщения(Сообщение);
	СообщениеСодержитОшибки = НЕ РезультатПроверки.Успешно;
	
	Если НЕ РезультатПроверки.Успешно Тогда
		
		Для Каждого Стр Из РезультатПроверки.СписокОшибок Цикл
			
			НоваяСтрока = ТаблицаОшибок.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,Стр);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗакрытьПанельОшибокСервер()
	
	Элементы.ПанельОшибок.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ОткрытьПанельОшибокСервер()
	
	Элементы.ПанельОшибок.Видимость = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьПанельОшибокКлиент()
	
	Элементы.ПанельОшибок.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПанельОшибокКлиент()
	
	Элементы.ПанельОшибок.Видимость = Истина;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимость()

	Если ТолькоПросмотрСообщения Тогда
		Элементы.ДействиеВремяПодачиТранспорта.Видимость = Ложь;
		Элементы.ДействиеКоличествоПалет.Видимость = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура НастроитьКнопкиПанели()
	
	Если ТипСообщения = "IFTMBF"
		ИЛИ ТолькоПросмотрСообщения Тогда
		Элементы.ОтправитьОтветНаЗаказПеревозки.Видимость = Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Сообщение.СообщениеСсылка) Тогда
		Элементы.УдалитьСообщение.Видимость = Ложь;
		Элементы.ПоказатьСвязанныеСообщения.Видимость = Ложь;
	КонецЕсли;
	
	// TODO отключим не реализованный функционал
	Элементы.ПоказатьСвязанныеСообщения.Видимость = Ложь;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоляНаФорме()

	ДокументНомер = Сообщение.ДокументEDI.Номер;
	ДокументДата = Сообщение.ДокументEDI.Дата;
	
	Элементы.ПредставлениеВремяПодачиТранспорта.Заголовок 	= МодульОбъекта().ПолучитьПредставлениеДатыПоставки_КонтурEDI(Сообщение.ДатаПоставки);
	Элементы.ПредставлениеКоличествоПалет.Заголовок 		= Сообщение.КоличествоУпаковок;
	
	УстановитьПредставлениеЮрФизЛицНаФормеСервер();
	
	СписокДопРеквизитов = Новый СписокЗначений;
	СписокДопРеквизитов.Добавить("ЭкспедиционныйЗаказEDI.Номер", 						"Номер экспедиционного заказа");
	СписокДопРеквизитов.Добавить("ЭкспедиционныйЗаказEDI.Дата", 						"Дата экспедиционного заказа");
	СписокДопРеквизитов.Добавить("ДатаВремяСовершенияДоверенности", 					"Дата выдачи доверенности");
	СписокДопРеквизитов.Добавить("КатегорииТовара", 									"Категории товара");
	СписокДопРеквизитов.Добавить("ОсобыеУказания", 										"Особые указания");
	СписокДопРеквизитов.Добавить("ВесУпаковокБрутто.Количество", 						"Вес упаковок брутто");
	СписокДопРеквизитов.Добавить("ИнформацияОТранспорте", 								"Информация о транспорте");
	СписокДопРеквизитов.Добавить("ТарифНаПеревозку", 									"Информация о тарифе на перевозку");
	
	Для каждого Эл Из СписокДопРеквизитов Цикл
		ЗначениеРеквизита = Неопределено;
		Если МодульОбъекта().ПроверитьСвойствоПоляВСтруктуре(Сообщение, Эл.Значение, ЗначениеРеквизита) Тогда
			Если ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
				
				НоваяСтрока = ДополнительныеРеквизиты.Добавить();
				НоваяСтрока.ИмяРеквизита 	= Эл.Представление;
				НоваяСтрока.Значение 		= ЗначениеРеквизита;
				
				Если Эл.Представление = "Вес упаковок брутто" Тогда
					ВесУпаковокБруттоЕдИзм = Неопределено;
					Если МодульОбъекта().ПроверитьСвойствоПоляВСтруктуре(Сообщение, "ВесУпаковокБрутто.КодЕдиницыИзмерения", ВесУпаковокБруттоЕдИзм) Тогда
						НоваяСтрока.ИмяРеквизита = НоваяСтрока.ИмяРеквизита + ", " + ВесУпаковокБруттоЕдИзм;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// грузополучатели
	Для каждого Строка Из Сообщение.Грузополучатели Цикл
	
		НоваяСтрока = Грузополучатели.Добавить();
		СписокСвойств = "ПорядковыйНомер,ТипТочкиДоставки,ДатаВремяДоставки,Грузополучатель1С,Комментарий";
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка, СписокСвойств);
		НоваяСтрока.ГрузополучательПредставление = МодульОбъекта().ПолучитьПредставлениеЮрФизЛица(Строка.ГрузополучательEDI, Сообщение.Грузополучатель1С); 
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПредставлениеЮрФизЛицНаФормеСервер()

	Элементы.ПредставлениеЮрФизЛицоСвое.Заголовок 			= МодульОбъекта().ПолучитьПредставлениеЮрФизЛица(Сообщение.ПродавецEDI, Сообщение.Продавец1С);	
	Элементы.ПредставлениеЮрФизЛицоСтороннее.Заголовок 		= МодульОбъекта().ПолучитьПредставлениеЮрФизЛица(Сообщение.ПокупательEDI, Сообщение.Покупатель1С);	
    Элементы.ПредставлениеГрузоотправитель.Заголовок     	= МодульОбъекта().ПолучитьПредставлениеЮрФизЛица(Сообщение.ГрузоотправительEDI, Сообщение.Грузоотправитель1С);	

КонецПроцедуры

&НаСервере
Процедура ВывестиОписаниеОшибкиСообщения()
	
	ПереотправляемоеСообщениеСсылка = Неопределено;
	СтатусСсылки = "";
	
	Если МодульОбъекта().ПроверитьСвойствоПоляВСтруктуре(Сообщение, "ПереотправляемоеСообщениеСсылка", ПереотправляемоеСообщениеСсылка)
		И МодульОбъекта().ПроверитьСвойствоПоляВСтруктуре(Сообщение, "СтатусСсылки", СтатусСсылки)
		И СтатусСсылки = "Ожидает исправления ошибок" Тогда
		
		СообщениеСсылка = ПереотправляемоеСообщениеСсылка;
		ОписаниеОшибки = СообщениеСсылка.ОписаниеОшибки;
		
		Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
			НоваяСтрока = ТаблицаОшибок.Добавить();
			НоваяСтрока.ТекстОшибки = ОписаниеОшибки;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьОтветНаЗаказПеревозки(Команда)
	
	Если СообщениеСодержитОшибки Тогда
		Возврат;
	КонецЕсли;
	
	ОтправитьОтветНаЗаказПеревозкиСервер();
	Закрыть();

КонецПроцедуры

&НаСервере
Процедура ОтправитьОтветНаЗаказПеревозкиСервер()

	ПрочитатьСообщениеИзКэша();
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("ОтправитьСообщениеИзФормы", Истина);
	ПараметрыОтправки.Вставить("Сообщение", Сообщение);
	
	МодульОбъекта().ОтправитьСообщение("IFTMBC", Неопределено, ПараметрыОтправки);

КонецПроцедуры

&НаКлиенте
Процедура ДействиеВремяПодачиТранспортаНажатие(Элемент)
	
	НачальнаяДата 	= ВремяПодачиТранспорта;
	Подсказка 		= "Укажите новую дату подачи транспорта";
	
	МодульОбъектаКлиент().ПоказатьВводДаты_КонтурEDI("ОбработчикПослеВводаДатыПодачиТранспорта", ЭтаФорма, , НачальнаяДата, Подсказка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикПослеВводаДатыПодачиТранспорта(ВыбраннаяДата = Неопределено, ДопПараметры = Неопределено) Экспорт
	
	Если ВыбраннаяДата = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ВыбраннаяДата = ВремяПодачиТранспорта Тогда
		ВремяПодачиТранспорта = ВыбраннаяДата;
		Элементы.ПредставлениеВремяПодачиТранспорта.Заголовок = МодульОбъектаКлиент().ПолучитьПредставлениеДатыПоставки_КонтурEDI(ВыбраннаяДата);
		
		ОбработчикПослеВводаДатыПодачиТранспортаСервер();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработчикПослеВводаДатыПодачиТранспортаСервер()
	
	ПрочитатьСообщениеИзКэша();
	Сообщение.ДатаПоставки = ВремяПодачиТранспорта;
	ПоместитьСообщениеВКэш();
	
КонецПроцедуры

&НаКлиенте
Процедура ДействиеКоличествоПалетНажатие(Элемент)
	
	НачальноеЧисло 	= КоличествоПалет;
	Подсказка 		= "Укажите новое количество палет";
	ДлинаЧисла 		= 12;
	ТочностьЧисла	= 0;
	
	МодульОбъектаКлиент().ПоказатьВводЧисла_КонтурEDI("ОбработчикПослеВводаКоличестваПалет", ЭтаФорма, , НачальноеЧисло, Подсказка, ДлинаЧисла, ТочностьЧисла);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикПослеВводаКоличестваПалет(ВыбранноеКоличество = Неопределено, ДопПараметры = Неопределено) Экспорт
	
	Если ВыбранноеКоличество = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбранноеКоличество < 0 Тогда
		ТекстПредупреждения = "Количество палет должно быть больше 0";
		МодульОбъектаКлиент().Предупреждение_КонтурEDI(ТекстПредупреждения, , , "ОбработчикПредупрежденияОВводеОтрицательногоКоличестваПалет", ЭтаФорма);
		Возврат;
	КонецЕсли;

	Если Не ВыбранноеКоличество = КоличествоПалет Тогда
		КоличествоПалет = ВыбранноеКоличество;
		Элементы.ПредставлениеКоличествоПалет.Заголовок = Строка(КоличествоПалет);
		
		ОбработчикПослеВводаКоличестваПалетСервер();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикПредупрежденияОВводеОтрицательногоКоличестваПалет(ДопПараметры = Неопределено) Экспорт

	ДействиеКоличествоПалетНажатие(Неопределено);	

КонецПроцедуры // ОбработчикПослеВводаОтрицательногоКоличестваПалет()

&НаСервере
Процедура ОбработчикПослеВводаКоличестваПалетСервер()
	
	ПрочитатьСообщениеИзКэша();
	Сообщение.КоличествоУпаковок = КоличествоПалет;
	ПоместитьСообщениеВКэш();
	
КонецПроцедуры

&НаКлиенте
Процедура ДействиеЮрФизЛицоСвоеНажатие(Элемент)
	
	ОткрытьФормуКонтрагента("Продавец");
	
КонецПроцедуры

&НаКлиенте
Процедура ДействиеЮрФизЛицоСтороннееНажатие(Элемент)
	
	ОткрытьФормуКонтрагента("Покупатель");
	
КонецПроцедуры

&НаКлиенте
Процедура ДействиеГрузоотправительНажатие(Элемент)
	
	ОткрытьФормуКонтрагента("Грузоотправитель");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуКонтрагента(ИмяПоляСообщения)
	
	//открыть форму
	ПараметрыФормы = Новый Структура; 
	ПараметрыФормы.Вставить("АдресСообщенияВВХ",	ПоместитьСообщениеБезТаблицВКэш());
	ПараметрыФормы.Вставить("ИмяПоляСообщения",		ИмяПоляСообщения);
	
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("ФормаКонтрагентаУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбработчикПослеЗакрытияКарточкиКонтрагента", ЭтаФорма, ИмяПоляСообщения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикПослеЗакрытияКарточкиКонтрагента(Результат = Неопределено, ДопПараметр = Неопределено) Экспорт

	Если Результат <> Неопределено 
		И Результат.Свойство("Успешно") 
		И Результат.Успешно = Истина Тогда
		
		ОбработчикПослеЗакрытияКарточкиКонтрагентаСервер(Результат);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработчикПослеЗакрытияКарточкиКонтрагентаСервер(Результат)
	
	ПрочитатьСообщениеИзКэша();
	
	Если Сообщение.Направление = "Исходящее" Тогда
		Сообщение[Результат.ИмяПоля+"EDI"] = Результат.ЮрФизЛицо;
	Иначе
		Сообщение[Результат.ИмяПоля+"1С"] = Результат.ЮрФизЛицо1С;
	КонецЕсли;
		
	ПоместитьСообщениеВКэш();
	УстановитьПредставлениеЮрФизЛицНаФормеСервер();

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьУниверсальнуюФормуРедактирования(СтруктураПараметров)
	
	СтруктураАдресаВоВХ = ПоместитьКонтекстДляУниверсальнойФормыВоВХ(СтруктураПараметров);
	
	Если СтруктураАдресаВоВХ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиФормы = Новый Структура;
	НастройкиФормы.Вставить("НадписьИнформация", СтруктураПараметров.НадписьИнформация);
	НастройкиФормы.Вставить("ТолькоПросмотр"   , Истина);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкиФормы",			НастройкиФормы);
	ПараметрыФормы.Вставить("ИмяСтруктуры",				СтруктураПараметров.ИмяСтруктуры);
	ПараметрыФормы.Вставить("ТаблицаМаппингаАдресВоВХ",	СтруктураАдресаВоВХ.ТаблицаМаппингаАдресВоВХ); 
	ПараметрыФормы.Вставить("СообщениеАдресВоВХ",		СтруктураАдресаВоВХ.СообщениеАдресВоВХ);
	ПараметрыФормы.Вставить("ЗначениеАдресВоВХ",		СтруктураАдресаВоВХ.ЗначениеАдресВоВХ);
	
	Состояние("Открываю форму просмотра");
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("УниверсальнаяФормаРедактированияУправляемая2", , ПараметрыФормы, ЭтаФорма);

КонецПроцедуры

&НаСервере
Функция ПоместитьКонтекстДляУниверсальнойФормыВоВХ(СтруктураПараметров)
	
	ПрочитатьСообщениеИзКэша();
	
	СтруктураАдресаВоВХ = Неопределено;
	Значение = Неопределено;
	
	Если ЗначениеЗаполнено(СтруктураПараметров.ИмяТабличнойЧасти) Тогда
		
		ТабЧасть = Сообщение[СтруктураПараметров.ИмяТабличнойЧасти];
		Значение = ТабЧасть[СтруктураПараметров.ИндексСтроки][СтруктураПараметров.ПутьВСообщении];
		
	Иначе
		
		МодульОбъекта().ПроверитьСвойствоПоляВСтруктуре(Сообщение, СтруктураПараметров.ПутьВСообщении, Значение);
		
	КонецЕсли;
	
	Если Значение = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТаблицаМаппинга 		= МодульОбъекта().ПолучитьОписаниеРеквизитовДопСправочника_КонтурEDI(СтруктураПараметров.ИмяСтруктуры);
	КопияСообщения 			= ЗначениеИзСтрокиВнутр(ЗначениеВСтрокуВнутр(Сообщение));
	КопияЗначения 			= ЗначениеИзСтрокиВнутр(ЗначениеВСтрокуВнутр(Значение));
	КопияТаблицыМаппинга 	= ЗначениеИзСтрокиВнутр(ЗначениеВСтрокуВнутр(ТаблицаМаппинга));
	
	СтруктураАдресаВоВХ = Новый Структура;
	СтруктураАдресаВоВХ.Вставить("ЗначениеАдресВоВХ", 			ПоместитьВоВременноеХранилище(КопияЗначения, ЭтаФорма.УникальныйИдентификатор));
	СтруктураАдресаВоВХ.Вставить("СообщениеАдресВоВХ", 			ПоместитьВоВременноеХранилище(КопияСообщения, ЭтаФорма.УникальныйИдентификатор));
	СтруктураАдресаВоВХ.Вставить("ТаблицаМаппингаАдресВоВХ", 	ПоместитьВоВременноеХранилище(КопияТаблицыМаппинга, ЭтаФорма.УникальныйИдентификатор));
	
	Возврат СтруктураАдресаВоВХ;
	
КонецФункции

&НаКлиенте
Процедура ДополнительныеРеквизитыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуРедактирования = Ложь;
	ИмяРеквизита = Элемент.ТекущиеДанные.ИмяРеквизита;
	
	Если ИмяРеквизита = "Информация о транспорте" Тогда
		
		ОткрытьФормуРедактирования = Истина;
		СтруктураПараметров = ПолучитьСтруктуруПараметровОткрытияФормыРедактирования();
		СтруктураПараметров.ПутьВСообщении		= "ИнформацияОТранспорте";
		СтруктураПараметров.ИмяСтруктуры 		= "ИнформацияОТранспорте";
		СтруктураПараметров.НадписьИнформация 	= "Информация о транспорте";
		
	ИначеЕсли ИмяРеквизита = "Информация о тарифе на перевозку" Тогда
		
		ОткрытьФормуРедактирования = Истина;
		СтруктураПараметров = ПолучитьСтруктуруПараметровОткрытияФормыРедактирования();
		СтруктураПараметров.ПутьВСообщении 		= "ТарифНаПеревозку";
		СтруктураПараметров.ИмяСтруктуры 		= "ТарифНаПеревозку";
		СтруктураПараметров.НадписьИнформация 	= "Информация о тарифе на перевозку";
		
	КонецЕсли;
	
	Если ОткрытьФормуРедактирования Тогда
		ОткрытьУниверсальнуюФормуРедактирования(СтруктураПараметров)
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьСтруктуруПараметровОткрытияФормыРедактирования()
	
	Структура = Новый Структура;
	Структура.Вставить("ПутьВСообщении");
	Структура.Вставить("ИмяСтруктуры");
	Структура.Вставить("НадписьИнформация");
	Структура.Вставить("ИндексСтроки");
	Структура.Вставить("ИмяТабличнойЧасти");
	
	Возврат Структура;
	
КонецФункции

&НаКлиенте
Процедура ГрузополучателиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элемент.ТекущийЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуРедактирования = Ложь;
	ИмяКолонки = Элемент.ТекущийЭлемент.Имя;
	
	Если ИмяКолонки = "ГрузополучателиГрузополучательEDI" Тогда
		
		ОткрытьФормуРедактирования = Истина;
		СтруктураПараметров = ПолучитьСтруктуруПараметровОткрытияФормыРедактирования();
		СтруктураПараметров.ПутьВСообщении 		= "ГрузополучательEDI";
		СтруктураПараметров.ИмяСтруктуры 		= "ЮрФизЛицо";
		СтруктураПараметров.ИндексСтроки		= ВыбраннаяСтрока;
		СтруктураПараметров.ИмяТабличнойЧасти	= "Грузополучатели";
		СтруктураПараметров.НадписьИнформация 	= "Информация о грузополучателе";
		
	ИначеЕсли ИмяКолонки = "ГрузополучателиТарифНаПеревозку" Тогда
		
		ОткрытьФормуРедактирования = Истина;
		СтруктураПараметров = ПолучитьСтруктуруПараметровОткрытияФормыРедактирования();
		СтруктураПараметров.ПутьВСообщении 		= "ТарифНаПеревозку";
		СтруктураПараметров.ИмяСтруктуры 		= "ТарифНаПеревозку";
		СтруктураПараметров.ИндексСтроки		= ВыбраннаяСтрока;
		СтруктураПараметров.ИмяТабличнойЧасти	= "Грузополучатели";
		СтруктураПараметров.НадписьИнформация 	= "Информация о тарифе на перевозку";
		
	КонецЕсли;
	
	Если ОткрытьФормуРедактирования Тогда
		ОткрытьУниверсальнуюФормуРедактирования(СтруктураПараметров)
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьXMLфайл(Команда)
	
	ТекстФайла = ПоказатьXMLфайлСервер();
	
	Если ТекстФайла = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекДок = Новый ТекстовыйДокумент;
	ТекДок.УстановитьТекст(ТекстФайла);
	ИмяВремФайла = ПолучитьИмяВременногоФайла("xml");
	ТекДок.Записать(ИмяВремФайла, КодировкаТекста.UTF8);
	
	ЗапуститьПриложение(ИмяВремФайла);

КонецПроцедуры

&НаСервере
Функция ПоказатьXMLфайлСервер()
	
	ПрочитатьСообщениеИзКэша();
	ВремСообщение = ЗначениеИзСтрокиВнутр(ЗначениеВстрокуВнутр(Сообщение));
	
	РезультатКонвертации = МодульОбъекта().КонвертироватьИсходящееСообщениеПоНовому(ВремСообщение);
	
	Если НЕ РезультатКонвертации.Успешно Тогда 
		Для каждого СтрокаОшибки из РезультатКонвертации.ТаблицаОшибок Цикл
			МодульОбъекта().Сообщить_КонтурEDI(СтрокаОшибки.ТекстОшибки);
		КонецЦикла;
		Возврат Неопределено;
	КонецЕсли;
	
	ТекДок = Новый ТекстовыйДокумент;
	ТекДок.Прочитать(РезультатКонвертации.ПутьКФайлу, КодировкаТекста.UTF8);
	
	Возврат ТекДок.ПолучитьТекст();
	
КонецФункции

&НаКлиенте
Процедура УдалитьСообщение(Команда)
	
	ВерныйПароль = Ложь;
	ТекПароль = "";
	ТекстПросьбы = "Введите пароль от учетной записи";
	ДопПараметрДляПередачиВОбработчик=Неопределено;
	
	Если Объект.ПараметрыКлиентСервер.МодальностьЗапрещена Тогда 
		Выполнить("ПоказатьВводСтроки(Новый ОписаниеОповещения(""ОбработчикВводаПароля"", ЭтаФорма, ДопПараметрДляПередачиВОбработчик), ТекПароль, ТекстПросьбы, , ложь)"); 
	Иначе
		Если ВвестиСтроку(ТекПароль, ТекстПросьбы) Тогда
			ОбработчикВводаПароля(ТекПароль);	
		иначе
			//nop
		КонецЕсли;
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикВводаПароля(ТекПароль, ДопПараметрОбработчика = Неопределено) Экспорт
	
	ТекущийПарольПользователя = ПолучитьПарольВызовСервера();
	
	ВерныйПароль=Ложь;
	Если СокрЛП(ТекПароль) = СокрЛП(ТекущийПарольПользователя) 
		ИЛИ СокрЛП(ТекПароль) = "******" Тогда
		ВерныйПароль = Истина;
	КонецЕсли;
	Если НЕ ВерныйПароль 
		И ТекПароль = Неопределено Тогда
		Возврат;  //отказ от ввода
	ИначеЕсли НЕ ВерныйПароль Тогда
		Сообщить("Неверный пароль");
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = "Текущее сообщение будет навсегда удалено из 1С. Продолжить?";
	КнопкиВопроса = Новый СписокЗначений;
	КнопкиВопроса.Добавить("Да, удалить сообщение");
	КнопкиВопроса.Добавить("Нет, не удалять");
	ДопПараметрДляПередачиВОбработчик = Неопределено;
	РезультатВопроса = Неопределено;
	
	Если Объект.ПараметрыКлиентСервер.МодальностьЗапрещена Тогда
		Выполнить("ПоказатьВопрос(Новый ОписаниеОповещения(""ОбработчикСогласияУдаленияСообщения"", ЭтаФорма, ДопПараметрДляПередачиВОбработчик), ТекстВопроса, КнопкиВопроса,,,""Контур.EDI"")");
	Иначе
		РезультатВопроса = Вопрос(ТекстВопроса, КнопкиВопроса, , , "Контур.EDI");
		ОбработчикСогласияУдаленияСообщения(РезультатВопроса, ДопПараметрДляПередачиВОбработчик);
	КонецЕсли;

КонецПроцедуры // ОбработчикВводаПароля()

&НаСервере
Функция ПолучитьПарольВызовСервера()

	Возврат МодульОбъекта().ПараметрыПользователяEDI.Пароль;	

КонецФункции // ПолучитьПарольВызовСервера()

&НаКлиенте
Процедура ОбработчикСогласияУдаленияСообщения(РезультатВопроса,ДопПараметр=Неопределено) Экспорт

	Если РезультатВопроса = "Да, удалить сообщение" Тогда 
		УдалитьСообщениеВызовСервера();
		ЭтаФорма.Закрыть();
	КонецЕсли;

КонецПроцедуры // ОбработчикСогласияУдаленияСообщения()

&НаСервере
Процедура УдалитьСообщениеВызовСервера()
	
	ПрочитатьСообщениеИзКэша();
	МодульОбъекта().УдалитьСообщение(Сообщение);

КонецПроцедуры // УдалитьСообщениеВызовСервера()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОсновнаяФорма().ПриОткрытииФормы(ЭтаФорма, "ФормаСообщенияПеревозкиУправляемая");

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	ОсновнаяФорма().ПриОткрытииФормы(ЭтаФорма, "ФормаСообщенияПеревозкиУправляемая");
КонецПроцедуры
	


