&НаСервере
Перем МодульОбъекта;

&НаКлиенте
Перем МодульОбъектаКлиент;

// СЛУЖЕБНЫЕ {

// Инициализация модуля и его экспортных функций
&НаСервере
Функция МодульОбъекта()
	
	Если МодульОбъекта = Неопределено Тогда
		
		Если Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект Тогда
			МодульОбъекта = МодульОбъектаИзКэша();
		КонецЕсли;
		
		Если МодульОбъекта = Неопределено Тогда
			МодульОбъекта = РеквизитФормыВЗначение("Объект");
			МодульОбъекта.ИнициализироватьПодключаемыеМодули(); // TODO Проверить что из этого используется на формах и перенести в ПараметрыКлиентСервер
			МодульОбъекта.ОбновитьКэшМодуляОбъекта();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат МодульОбъекта;
	
КонецФункции

&НаСервере
Функция МодульОбъектаИзКэша()
	
	Результат = Неопределено;
	
	Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда
		АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект;
 		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Структура = ПолучитьИзВременногоХранилища(АдресВХ);
			Если ТипЗнч(Структура) = Тип("Структура") и Структура.Свойство("ОбработкаОбъект") Тогда
			 	Результат = Структура.ОбработкаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульОбъектаКлиент()
	
	Если ТипЗнч(МодульОбъектаКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбъектаКлиент;
	КонецЕсли;
	
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		МодульОбъектаКлиент = ОсновнаяФорма.МодульОбъектаКлиент();
		Возврат МодульОбъектаКлиент;
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
	
	Если ЭтаФорма.ВладелецФормы = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ЭтаФорма.ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;
    	
КонецФункции

&НаКлиенте
Функция НоваяМетрика()
	
	Возврат ОсновнаяФорма().НоваяМетрика();
	
КонецФункции

// } СЛУЖЕБНЫЕ

&НаКлиенте
Процедура КоманднаяПанельТСОткрыть(Кнопка)
		
	Если Параметры.ОткрытаКакВыбор Тогда
		ТекСтрока = Элементы.СписокПартнеров.ТекущиеДанные;
		Если ЗначениеЗаполнено(ТекСтрока.Ссылка) Тогда
			ЭтаФорма.Закрыть(ТекСтрока.Ссылка);
		КонецЕсли;
	иначе
		ОткрытьФормуТорговойСети();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуТорговойСети()
	
	ТекСтрока = Элементы.СписокПартнеров.ТекущиеДанные;
	
	Если НЕ ТекСтрока=Неопределено Тогда
		Если ЗначениеЗаполнено(ТекСтрока.Ссылка) Тогда
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("СсылкаНаПартнера", ТекСтрока.Ссылка);
			
			МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Партнеры_ЭлементУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбработчикПослередактированияПартнера", ЭтаФорма);
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
	Параметры.Свойство("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	
	Параметры.МодальностьЗапрещена=МодульОбъекта().МодальностьЗапрещена();
	ПутьКФормам = МодульОбъекта().Метаданные().ПолноеИмя() + ".Форма.";
	
	МыПоставщик		= МодульОбъекта().ПолучитьКонстантуEDI("МыПоставщик") = Истина;
	МыТорговаяСеть	= МодульОбъекта().ПолучитьКонстантуEDI("МыТорговаяСеть") = Истина;
	
	Если НЕ МыПоставщик Тогда
		Элементы.ФормаДобавитьТорговуюСеть.Видимость = Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Заголовок) Тогда
		 ЭтаФорма.Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
	Если Параметры.ОткрытаКакВыбор Тогда
		Элементы.ФормаОткрыть.Заголовок = "Выбрать";
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикПослередактированияПартнера(ПараметрыЗакрытияИнтерактивнойФормы=неопределено, ДополнительныеПараметры=неопределено) Экспорт
	Элементы.СписокПартнеров.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьТорговуюСеть(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОткрытаКакДобавлениеПартнера",Истина);
	
	Состояние("Запрашиваю данные по сетям...",30,,);
	
	Если МыПоставщик Тогда
		
		МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Сервис_СтартовыйПомощникУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбработчикПослередактированияПартнера", ЭтаФорма);
		
	Иначе
		
		МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Партнеры_ЭлементУправляемая", , ПараметрыФормы, ЭтаФорма, "ОбработчикПослередактированияПартнера", ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьТСВручную(Команда)
	
	МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("Партнеры_ЭлементУправляемая", , , ЭтаФорма, "ОбработчикПослередактированияПартнера", ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура СписокПартнеровВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка=Ложь;
	
	Если Параметры.ОткрытаКакВыбор Тогда
		ТекСтрока = Элементы.СписокПартнеров.ТекущиеДанные;
		Если ЗначениеЗаполнено(ТекСтрока.Ссылка) Тогда
			ЭтаФорма.Закрыть(ТекСтрока.Ссылка);
		КонецЕсли;
	иначе
		ОткрытьФормуТорговойСети();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПартнеровПередУдалением(Элемент, Отказ)
	отказ = истина;
КонецПроцедуры

&НаКлиенте
Процедура СписокПартнеровПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	отказ = истина;
КонецПроцедуры

&НаКлиенте
Процедура СписокПартнеровПередНачаломИзменения(Элемент, Отказ)
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура УдалитьТорговуюСеть(Команда)
	
	ТекСтрока = Элементы.СписокПартнеров.ТекущиеДанные;
	
	Если НЕ ТекСтрока = Неопределено Тогда
		ТекстВопроса ="Партнер """+ТекСтрока.Наименование+""" будет удален. Продолжить?";
		КнопкиВопроса=новый СписокЗначений;
		КнопкиВопроса.Добавить("Да, удалить партнера");
		КнопкиВопроса.Добавить("Отмена");
		ДопПараметрДляПередачиВОбработчик=ТекСтрока;
		РезультатВопроса = Неопределено;
		
		Если Параметры.МодальностьЗапрещена Тогда
			Выполнить("ПоказатьВопрос(Новый ОписаниеОповещения(""ОбработчикПодтвержденияУдаленияПартнера"", ЭтаФорма, ДопПараметрДляПередачиВОбработчик), ТекстВопроса, КнопкиВопроса,,,""Контур.EDI"")");
		Иначе
			РезультатВопроса = Вопрос(ТекстВопроса, КнопкиВопроса,,,"Контур.EDI");
			ОбработчикПодтвержденияУдаленияПартнера(РезультатВопроса,ДопПараметрДляПередачиВОбработчик);
		КонецЕсли;	
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикПодтвержденияУдаленияПартнера(РезультатВопроса,ДопПараметрПереданныйВОбработчик=Неопределено) Экспорт

	Если РезультатВопроса="Да, удалить партнера" Тогда
		УдалитьТорговуюСетьСервер(ДопПараметрПереданныйВОбработчик.Ссылка);
		Элементы.СписокПартнеров.Обновить();

	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УдалитьТорговуюСетьСервер(СсылкаНаПартнера)

	МодульОбъекта().УдалитьЭлементСправочника("Партнеры",СсылкаНаПартнера);
	
КонецПроцедуры // УдалитьТорговуюСетьСервер()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОсновнаяФорма().ПриОткрытииФормы(ЭтаФорма, "Партнеры_СписокУправляемая");

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	ОсновнаяФорма().ПриЗакрытииФормы(ЭтаФорма, "Партнеры_СписокУправляемая");	

КонецПроцедуры


