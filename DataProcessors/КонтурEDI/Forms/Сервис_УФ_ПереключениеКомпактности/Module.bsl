&НаСервере
Перем МодульОбъекта;

&НаКлиенте
Перем МодульОбъектаКлиент;

// СЛУЖЕБНЫЕ {

// Инициализация модуля и его экспортных функций
&НаСервере
Функция МодульОбъекта()
	
	Если МодульОбъекта = Неопределено Тогда
		
		Если Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект Тогда
			МодульОбъекта = МодульОбъектаИзКэша();
		КонецЕсли;
		
		Если МодульОбъекта = Неопределено Тогда
			МодульОбъекта = РеквизитФормыВЗначение("Объект");
			МодульОбъекта.ИнициализироватьПодключаемыеМодули(); // TODO Проверить что из этого используется на формах и перенести в ПараметрыКлиентСервер
			МодульОбъекта.ОбновитьКэшМодуляОбъекта();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат МодульОбъекта;
	
КонецФункции

&НаСервере
Функция МодульОбъектаИзКэша()
	
	Результат = Неопределено;
	
	Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда
		АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект;
 		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Структура = ПолучитьИзВременногоХранилища(АдресВХ);
			Если ТипЗнч(Структура) = Тип("Структура") и Структура.Свойство("ОбработкаОбъект") Тогда
			 	Результат = Структура.ОбработкаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульОбъектаКлиент()
	
	Если ТипЗнч(МодульОбъектаКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбъектаКлиент;
	КонецЕсли;
	
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		МодульОбъектаКлиент = ОсновнаяФорма.МодульОбъектаКлиент();
		Возврат МодульОбъектаКлиент;
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
	
	Если ЭтаФорма.ВладелецФормы = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ЭтаФорма.ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;
    	
КонецФункции

&НаКлиенте
Функция НоваяМетрика()
	
	Возврат ОсновнаяФорма().НоваяМетрика();
	
КонецФункции

// } СЛУЖЕБНЫЕ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
	Параметры.Свойство("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	
	Пользователь = ПользователиИнформационнойБазы.ТекущийПользователь();
	Настройка = ХранилищеСистемныхНастроек.Загрузить("Общее/НастройкиКлиентскогоПриложения", "",, Пользователь.Имя);
	Если Не ТипЗнч(Настройка) = Тип("НастройкиКлиентскогоПриложения") Тогда
		Настройка = Новый НастройкиКлиентскогоПриложения;
	КонецЕсли;
	ТекущееСостояниеИнтерфейса = Настройка.ВариантМасштабаФормКлиентскогоПриложения;
		
	Если Настройка.ВариантИнтерфейсаКлиентскогоПриложения = ВариантИнтерфейсаКлиентскогоПриложения.Такси Тогда
		ЭтаФорма.Элементы.ДекорацияТекущееСостояние.Заголовок = "Текущая настройка интерфейса - "+ТекущееСостояниеИнтерфейса;
		Если ТекущееСостояниеИнтерфейса = ВариантМасштабаФормКлиентскогоПриложения.Обычный Тогда
			ЭтаФорма.Элементы.КомандаИнтерфейсОбычный.Видимость = Ложь;
		ИначеЕсли ТекущееСостояниеИнтерфейса = ВариантМасштабаФормКлиентскогоПриложения.Компактный Тогда
			ЭтаФорма.Элементы.КомандаИнтерфейсКомпактный.Видимость = Ложь;
		Иначе 
			ЭтаФорма.Элементы.КомандаИнтерфейсАвто.Видимость = Ложь;
		КонецЕсли;
	Иначе   //Формы - не такси, смысла не имеет  
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаИнтерфейсАвто(Команда)
	ИзменитьНаСервере(ВариантМасштабаФормКлиентскогоПриложения.Авто);
	//ЗавершитьРаботуСистемы(Истина, Истина, "");	
	Сообщить("Режим изменен, перезапустите 1С, убедившись что все данные записаны.");
КонецПроцедуры

&НаКлиенте
Процедура КомандаИнтерфейсКомпактный(Команда)
	ИзменитьНаСервере(ВариантМасштабаФормКлиентскогоПриложения.Компактный);
	//ЗавершитьРаботуСистемы(Истина, Истина, "");	
	Сообщить("Режим изменен, перезапустите 1С, убедившись что все данные записаны.");
КонецПроцедуры

&НаКлиенте
Процедура КомандаИнтерфейсОбычный(Команда)
	ИзменитьНаСервере(ВариантМасштабаФормКлиентскогоПриложения.Обычный);	
	//ЗавершитьРаботуСистемы(Истина, Истина, "");	
	Сообщить("Режим изменен, перезапустите 1С, убедившись что все данные записаны.");
КонецПроцедуры

&НаСервере
Процедура ИзменитьНаСервере(НовоеСостояниеИнтерфейса)
	УстановитьПривилегированныйРежим(Истина);
	Пользователь = ПользователиИнформационнойБазы.ТекущийПользователь();
	Настройка = ХранилищеСистемныхНастроек.Загрузить("Общее/НастройкиКлиентскогоПриложения", "",, Пользователь.Имя);
	Если Не ТипЗнч(Настройка) = Тип("НастройкиКлиентскогоПриложения") Тогда
		Настройка = Новый НастройкиКлиентскогоПриложения;
	КонецЕсли;
	Настройка.ВариантМасштабаФормКлиентскогоПриложения = НовоеСостояниеИнтерфейса;
	ХранилищеСистемныхНастроек.Сохранить("Общее/НастройкиКлиентскогоПриложения", "", Настройка,, Пользователь.Имя);
	УстановитьПривилегированныйРежим(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОсновнаяФорма().ПриОткрытииФормы(ЭтаФорма, "Сервис_УФ_ПереключениеКомпактности");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	ОсновнаяФорма().ПриЗакрытииФормы(ЭтаФорма, "Сервис_УФ_ПереключениеКомпактности");		
	
КонецПроцедуры
