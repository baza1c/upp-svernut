&НаСервере
Перем МассивДобавляемыхРеквизитов;

&НаСервере
Перем МодульОбъекта;

&НаКлиенте
Перем МодульОбъектаКлиент;

// СЛУЖЕБНЫЕ {

// Инициализация модуля и его экспортных функций
&НаСервере
Функция МодульОбъекта()
	
	Если МодульОбъекта = Неопределено Тогда
		
		Если Объект.ПараметрыКлиентСервер.КэшироватьОбработкуОбъект Тогда
			МодульОбъекта = МодульОбъектаИзКэша();
		КонецЕсли;
		
		Если МодульОбъекта = Неопределено Тогда
			МодульОбъекта = РеквизитФормыВЗначение("Объект");
			МодульОбъекта.ИнициализироватьПодключаемыеМодули(); // TODO Проверить что из этого используется на формах и перенести в ПараметрыКлиентСервер
			МодульОбъекта.ОбновитьКэшМодуляОбъекта();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат МодульОбъекта;
	
КонецФункции

&НаСервере
Функция МодульОбъектаИзКэша()
	
	Результат = Неопределено;
	
	Если ТипЗнч(Объект.ПараметрыКлиентСервер) = Тип("Структура") Тогда
		АдресВХ = Объект.ПараметрыКлиентСервер.АдресаВХ.ОбработкаОбъект;
 		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Структура = ПолучитьИзВременногоХранилища(АдресВХ);
			Если ТипЗнч(Структура) = Тип("Структура") и Структура.Свойство("ОбработкаОбъект") Тогда
			 	Результат = Структура.ОбработкаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульОбъектаКлиент()
	
	Если ТипЗнч(МодульОбъектаКлиент) = Тип("УправляемаяФорма") Тогда
		Возврат МодульОбъектаКлиент;
	КонецЕсли;
	
	ОсновнаяФорма = ОсновнаяФорма();
	Если ТипЗнч(ОсновнаяФорма) = Тип("УправляемаяФорма") Тогда
		МодульОбъектаКлиент = ОсновнаяФорма.МодульОбъектаКлиент();
		Возврат МодульОбъектаКлиент;
	Иначе
		Сообщить("Ошибка! Не удалось получить контекст основной формы.");
	КонецЕсли;
	          	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
	
	Если ЭтаФорма.ВладелецФормы = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ЭтаФорма.ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;
    	
КонецФункции

&НаКлиенте
Функция НоваяМетрика()
	
	Возврат ОсновнаяФорма().НоваяМетрика();
	
КонецФункции

// } СЛУЖЕБНЫЕ


&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("ИмяРеквизита",				ИмяРеквизита);
	СтруктураРезультата.Вставить("АдресСохраненногоЗначения",	ПоместитьРеузультатВХранилище_КонтурEDI());

	Закрыть(СтруктураРезультата);
	
КонецПроцедуры

&НаСервере
Функция ПоместитьРеузультатВХранилище_КонтурEDI()
	   
	Возврат ПоместитьВоВременноеХранилище(РеквизитФормыВЗначение("Дерево"));		

КонецФункции // ПоместитьРеузльтатВХранилище_КонтурEDI()

&НаСервере
Процедура ИнициализироватьПеременныеУниверсальнойФормы()
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", 			Объект.ПараметрыКлиентСервер);
	Параметры.Свойство("ОбъектПараметрыКлиентСерверМетрика", 	Объект.ПараметрыКлиентСерверМетрика);
	
	ТекстыОбработчиковСобытий						= Новый Структура;
	ТекстыПроцедурыПослеМодальныхВызововПоСобытиям	= Новый Структура;
	ТекстыПроцедурыСобытий_Сервер					= Новый Структура;
	КлючевыеРеквизиты								= Новый Структура;
	
	МассивДобавляемыхРеквизитов = ПолучитьМассивДобавляемыхРеквизитов(); 
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнициализироватьПеременныеУниверсальнойФормы(); 	
		
	СтруктураРеквизитов	= ПолучитьИзВременногоХранилища(Параметры.АдресВХ_СтруктураРеквизитов);	
	ИмяРеквизита		= СтруктураРеквизитов.ИмяРеквизита;
		
	Если СтруктураРеквизитов.Свойство("ДеревоЗначения") И ЗначениеЗаполнено(СтруктураРеквизитов.ДеревоЗначения) Тогда
		РеквизитДерево = ДобавитьРеквизитДеревоЗначения_КонтурEDI(СтруктураРеквизитов.ДеревоЗначения, "Дерево", "Дерево");   		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(МассивДобавляемыхРеквизитов) Тогда
		ИзменитьРеквизиты(МассивДобавляемыхРеквизитов);
	КонецЕсли;
	
	Если СтруктураРеквизитов.Свойство("ДеревоЗначения") И ЗначениеЗаполнено(СтруктураРеквизитов.ДеревоЗначения) Тогда
		
		ЗначениеВРеквизитФормы(СтруктураРеквизитов.ДеревоЗначения,РеквизитДерево.Имя);
		
		СтруктураОбработчиков = Неопределено;
		Если СтруктураРеквизитов.НастройкаФормы.Свойство("ВключитьОбработчикиСобытий") И ТипЗнч(СтруктураРеквизитов.НастройкаФормы.ВключитьОбработчикиСобытий) = Тип("Соответствие") Тогда
			СтруктураОбработчиков = СтруктураРеквизитов.НастройкаФормы.ВключитьОбработчикиСобытий;
		КонецЕсли;
		
		СтруктураОбработчиковСервер = Неопределено;
		Если СтруктураРеквизитов.НастройкаФормы.Свойство("ВключитьОбработчикиСобытийСеврвер") И ТипЗнч(СтруктураРеквизитов.НастройкаФормы.ВключитьОбработчикиСобытийСеврвер) = Тип("Соответствие") Тогда
			СтруктураОбработчиковСервер = СтруктураРеквизитов.НастройкаФормы.ВключитьОбработчикиСобытийСеврвер;
		КонецЕсли;	
		
		СтруктураОбработчиковПосле = Неопределено;
		Если СтруктураРеквизитов.НастройкаФормы.Свойство("ВключитьОбработчикиСобытийПосле") И ТипЗнч(СтруктураРеквизитов.НастройкаФормы.ВключитьОбработчикиСобытийПосле) = Тип("Соответствие") Тогда
			СтруктураОбработчиковПосле = СтруктураРеквизитов.НастройкаФормы.ВключитьОбработчикиСобытийПосле;
		КонецЕсли;	
		
		ДобавитьЭлементФормыДеревоЗначения_КонтурEDI(РеквизитДерево, СтруктураРеквизитов.ДеревоЗначения.Колонки, РеквизитДерево.Имя, РеквизитДерево.Заголовок, Элементы.ГруппаРеквизитов, СтруктураОбработчиков, СтруктураОбработчиковСервер, СтруктураОбработчиковПосле);
		
		Если СтруктураРеквизитов.НастройкаРеквизитов.Количество() > 0 Тогда
			СтрокаНастройки = СтруктураРеквизитов.НастройкаРеквизитов[0];
			
			Если СтруктураРеквизитов.НастройкаРеквизитов.Колонки.Найти("КлючевыеРеквизиты") <> Неопределено И ЗначениеЗаполнено(СтрокаНастройки.КлючевыеРеквизиты)  Тогда
				КлючевыеРеквизиты = МодульОбъекта().ПолучитьСтруктуру_ИзТекстаНастройкиРеквизитов(СтрокаНастройки, "КлючевыеРеквизиты");
			КонецЕсли;
		КонецЕсли;

	КонецЕсли;
	
	ВыполнитьНастройкуФормы(СтруктураРеквизитов.НастройкаФормы);
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьНастройкуФормы(НастройкиФормы)

	Если НЕ ТипЗнч(НастройкиФормы) = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если НастройкиФормы.Свойство("ИзменятьСоставСтрок") Тогда
		Элементы["Дерево"].ИзменятьСоставСтрок = НастройкиФормы.ИзменятьСоставСтрок = Истина;	
	КонецЕсли;
	Если НастройкиФормы.Свойство("НадписьИнформация") Тогда
		ЭтаФорма.Заголовок = НастройкиФормы.НадписьИнформация;	
	КонецЕсли; 	
	
	УстановитьДоступностьКолонок(НастройкиФормы);
		
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьКолонок(НастройкиФормы)

	Если НастройкиФормы.Свойство("НедоступныеПоляИКолонки") И ЗначениеЗаполнено(НастройкиФормы.НедоступныеПоляИКолонки) Тогда
		Для каждого ИмяКолонки Из НастройкиФормы.НедоступныеПоляИКолонки Цикл
			Колонка = Элементы["Дерево"].ПодчиненныеЭлементы.Найти(ИмяКолонки);
			Если Колонка <> неопределено Тогда
				 Колонка.Видимость = Ложь;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;  
	
КонецПроцедуры // УстановитьДоступностьКолонок() 

&НаСервере
Процедура ВывестиГруппуОбычнуюГруппуНаФорму_КонтурEDI(ИмяГруппы, РодительГруппы = Неопределено, ОтображениеГруппы = Неопределено, ЗаголовокГруппы = "", ГруппировкаПодчиненныхЭлементов = Неопределено)
    				
	ГруппаФормы = Элементы.Добавить("Группа_"+ИмяГруппы, Тип("ГруппаФормы"), РодительГруппы);
	
    ГруппаФормы.Вид							= ВидГруппыФормы.ОбычнаяГруппа;
    ГруппаФормы.Отображение					= ?(ОтображениеГруппы = Неопределено, ОтображениеОбычнойГруппы.Нет, ОтображениеГруппы);
    ГруппаФормы.ОтображатьЗаголовок			= ЗначениеЗаполнено(ЗаголовокГруппы);
	ГруппаФормы.Заголовок					= ЗаголовокГруппы;
    ГруппаФормы.Группировка					= ?(ГруппировкаПодчиненныхЭлементов = Неопределено, ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная, ГруппировкаПодчиненныхЭлементов);  
	
КонецПроцедуры

&НаСервере
Функция ДобавитьРеквизитФормы_КонтурEDI(ИмяРеквизита, ТипРеквизита, ПутьРеквизита, ЗаголовокРеквизита, СохраняемыеДанные = Ложь)
	
	Возврат Новый РеквизитФормы(ИмяРеквизита, ТипРеквизита, ПутьРеквизита, ЗаголовокРеквизита, СохраняемыеДанные); 	

КонецФункции

&НаСервере
Функция ВывестиЭлементФормы_КонтурEDI(ИмяЭлемента, ТипЭлемента, РодительЭлемента = Неопределено, ПутьКДаннымЭлемента, ВидЭлемента = Неопределено, ТолькоПросмотрЭлемента = Ложь, ОтбражениеТаблицы = Неопределено)
	
	НовыйЭлемент = Элементы.Добавить(ИмяЭлемента, ТипЭлемента, РодительЭлемента);
	
    НовыйЭлемент.ПутьКДанным		= ПутьКДаннымЭлемента;     
    НовыйЭлемент.ТолькоПросмотр		= ТолькоПросмотрЭлемента;
	
	Если ЗначениеЗаполнено(ВидЭлемента) Тогда
		НовыйЭлемент.Вид			= ВидЭлемента;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтбражениеТаблицы) Тогда
		 НовыйЭлемент.Отображение	= ОтбражениеТаблицы;
	 КонецЕсли;
	 
	 Если МодульОбъекта().ПеременнаяСодержитСвойство(НовыйЭлемент, "ИсторияВыбораПриВводе") Тогда 
		 
		 Попытка
			 
			 НовыйЭлемент.ИсторияВыбораПриВводе = Вычислить("ИсторияВыбораПриВводе.НеИспользовать");
			 
		 Исключение
			 
			 Комментарий = "ИсторияВыбораПриВводе.НеИспользовать - Доступен, начиная с версии 8.3.4";
			 
		 КонецПопытки;
		 
	КонецЕсли;
		
	Возврат НовыйЭлемент;

КонецФункции

&НаСервере
Функция ПолучитьМассивДобавляемыхРеквизитов()

	Если ТипЗнч(МассивДобавляемыхРеквизитов) <> Тип("Массив") Тогда
		МассивДобавляемыхРеквизитов = Новый Массив;
	КонецЕсли;
	
	Возврат МассивДобавляемыхРеквизитов;

КонецФункции // ПолучитьМассивДобавляемыхРеквизитов()()
  
&НаСервере
Функция ДобавитьРеквизитДеревоЗначения_КонтурEDI(Дерево, ИмяРеквизита = Неопределено, ЗаголовокРеквизита)

	МассивТипов       = Новый Массив;
	МассивТипов.Добавить(ТипЗнч(Дерево));
			
	РеквизитДерево = ДобавитьРеквизитФормы_КонтурEDI(?(ЗначениеЗаполнено(ИмяРеквизита), ИмяРеквизита ,"ДеревоРедактирования"), Новый ОписаниеТипов(МассивТипов), "", ЗаголовокРеквизита, Ложь);
	
	МассивДобавляемыхРеквизитов.Добавить(РеквизитДерево);	
	
	Для каждого Колонка Из Дерево.Колонки Цикл
			
		РеквизитКолонка = ДобавитьРеквизитФормы_КонтурEDI(Колонка.Имя, Колонка.ТипЗначения, РеквизитДерево.Имя, Колонка.Заголовок, Ложь);
		МассивДобавляемыхРеквизитов.Добавить(РеквизитКолонка); 	
		
	КонецЦикла;
		
	Возврат РеквизитДерево;

КонецФункции // ДобавитьРеквизитСТипомДеревоЗначения()

&НаСервере
Процедура ДобавитьЭлементФормыДеревоЗначения_КонтурEDI(РеквизитДерево, КолонкиДерева, ИмяРеквизита = Неопределено, ЗаголовокРеквизита, РодительЭлемента, СтруктураОбработчиков, СтруктураОбработчиковСервер, СтруктураОбработчиковПосле)
	
	ЭлементДерево = ВывестиЭлементФормы_КонтурEDI(ИмяРеквизита, Тип("ТаблицаФормы"), РодительЭлемента, ИмяРеквизита, Неопределено, , ОтображениеТаблицы.Дерево);
	
	ПолеФормыТип = Тип("ПолеФормы");
	ПоляФормыВид = ВидПоляФормы.ПолеВвода;
	
	Для каждого Колонка Из КолонкиДерева Цикл
		
		ПутьКолонки = "" + РеквизитДерево.Имя + "." + Колонка.Имя;
		
		ЭлементКолонки = ВывестиЭлементФормы_КонтурEDI(Колонка.Имя, ПолеФормыТип, ЭлементДерево, ПутьКолонки, ПоляФормыВид);   		
		
		Если ЗначениеЗаполнено(СтруктураОбработчиков) Тогда
			
			УстановитьОбработчикиСобытий_УниверсальнаяФормаКонтурEDI(СтруктураОбработчиков, Колонка.Имя, ТекстыОбработчиковСобытий, "УниверсальныйОбработчикСобытия", ЭлементКолонки);
											
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтруктураОбработчиковСервер) Тогда
			
			УстановитьОбработчикиСобытий_УниверсальнаяФормаКонтурEDI(СтруктураОбработчиковСервер, Колонка.Имя, ТекстыПроцедурыСобытий_Сервер);
								
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтруктураОбработчиковПосле) Тогда
			
			УстановитьОбработчикиСобытий_УниверсальнаяФормаКонтурEDI(СтруктураОбработчиковПосле, Колонка.Имя, ТекстыПроцедурыПослеМодальныхВызововПоСобытиям);
								
		КонецЕсли;
	КонецЦикла;
			
КонецПроцедуры

&НаСервере
Процедура УстановитьОбработчикиСобытий_УниверсальнаяФормаКонтурEDI(СтруктураОбработчиков, ИмяЭлемента, КудаВставить, ИмяОбработчика = "", Элемент = Неопределено)

	МассивСобытий = СтруктураОбработчиков[ИмяЭлемента];
	
	Если МассивСобытий <> Неопределено Тогда
		
		Для каждого Событие Из МассивСобытий Цикл
			КудаВставить.Вставить(ИмяЭлемента + Событие.Имя, Событие.Текст); 
			
			Если ЗначениеЗаполнено(ИмяОбработчика) Тогда
				Элемент.УстановитьДействие("" + Событие.Имя, ИмяОбработчика + Событие.Имя);  //"ДеревоРеквизитовУниверсальнаяПроцедура" + Событие
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли; 
	
КонецПроцедуры // УстановитьОбработчикиСобытий()
   
&НаКлиенте
Процедура ПоказатьУровеньДерева_КонтурEDI(Уровень, Дерево, ЭлементДерево)
	
	СвернутьДерево_КонтурEDI(Дерево, ЭлементДерево); 
	
	РазвернутьДерево_КонтурEDI(Дерево, Уровень,  ЭлементДерево);
	
КонецПроцедуры

&НаКлиенте
Процедура СвернутьДерево_КонтурEDI(Дерево, ЭлементДерево)
	
	Для Каждого Строка Из Дерево.ПолучитьЭлементы() Цикл
		
		ИдентификаторСтроки = Строка.ПолучитьИдентификатор();
		
		СвернутьДерево_КонтурEDI(Строка, ЭлементДерево); 
		
		ЭлементДерево.Свернуть(ИдентификаторСтроки);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьДерево_КонтурEDI(Дерево, знач Уровень, ЭлементДерево)
	
	Уровень=Уровень-1;
	
	Для Каждого Строка Из Дерево.ПолучитьЭлементы() Цикл		
		Если Уровень >= 0 Тогда
			
			ИдентификаторСтроки = Строка.ПолучитьИдентификатор();
			
			ЭлементДерево.Развернуть(ИдентификаторСтроки);
			
			РазвернутьДерево_КонтурEDI(Строка, Уровень, ЭлементДерево); 
			
		КонецЕсли;
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПоказатьУровеньДерева_КонтурEDI(2, ЭтаФорма["Дерево"], Элементы["Дерево"]);

КонецПроцедуры

///ОбработчикиСобытийКолонокТаблицы  
&НаКлиенте
Процедура УниверсальныйОбработчикПоОповещению(Параметр, ДопПараметры) Экспорт
	
	Если ДопПараметры.Свойство("ИмяЭлемента") И ДопПараметры.Свойство("ИмяСобытия") Тогда
		
		ПоискТекста = "" + ДопПараметры.ИмяЭлемента + ДопПараметры.ИмяСобытия;
		
		Если ТекстыПроцедурыПослеМодальныхВызововПоСобытиям.Свойство(ПоискТекста) Тогда
			Выполнить(ТекстыПроцедурыПослеМодальныхВызововПоСобытиям[ПоискТекста]);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры  

&НаСервере
Процедура УниверсальныйОбработчикПоОповещению_Сервер(Параметр, ДопПараметры) Экспорт
	
	Если ДопПараметры.Свойство("ИмяЭлемента") И ДопПараметры.Свойство("ИмяСобытия" + "_Сервер") Тогда
		
		ПоискТекста = "" + ДопПараметры.ИмяЭлемента + ДопПараметры.ИмяСобытия + "_Сервер";
		
		Если ТекстыПроцедурыПослеМодальныхВызововПоСобытиям.Свойство(ПоискТекста) Тогда
			Выполнить(ТекстыПроцедурыПослеМодальныхВызововПоСобытиям[ПоискТекста]);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры  

&НаКлиенте
Процедура УниверсальныйОбработчикСобытияПриИзменении(Элемент)
	
	Если ТекстыОбработчиковСобытий.Свойство(Элемент.Имя + "ПриИзменении") Тогда
		Выполнить(ТекстыОбработчиковСобытий[Элемент.Имя + "ПриИзменении"]);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УниверсальныйОбработчикСобытияПриИзменении_Сервер(ИмяЭлемента, ЕщеПараметры)
	
	Если ТекстыПроцедурыСобытий_Сервер.Свойство(ИмяЭлемента + "ПриИзменении_Сервер") Тогда
		Выполнить(ТекстыПроцедурыСобытий_Сервер[ИмяЭлемента + "ПриИзменении_Сервер"]);
	КонецЕсли;

КонецПроцедуры 

&НаКлиенте
Процедура УниверсальныйОбработчикСобытияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ТекстыОбработчиковСобытий.Свойство(Элемент.Имя + "НачалоВыбора") Тогда
		Выполнить(ТекстыОбработчиковСобытий[Элемент.Имя + "НачалоВыбора"]);
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура УниверсальныйОбработчикСобытияНачалоВыбора_Сервер(ИмяЭлемента, ЕщеПараметры)
	
	Если ТекстыПроцедурыСобытий_Сервер.Свойство(ИмяЭлемента + "НачалоВыбора_Сервер") Тогда
		Выполнить(ТекстыПроцедурыСобытий_Сервер[ИмяЭлемента + "НачалоВыбора_Сервер"]);
	КонецЕсли;
		
КонецПроцедуры 

&НаКлиенте
Процедура УниверсальныйОбработчикСобытияНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	Если ТекстыОбработчиковСобытий.Свойство(Элемент.Имя + "НачалоВыбораИзСписка") Тогда
		Выполнить(ТекстыОбработчиковСобытий[Элемент.Имя + "НачалоВыбораИзСписка"]);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УниверсальныйОбработчикСобытияНачалоВыбораИзСписка_Сервер(ИмяЭлемента, ЕщеПараметры)
	
	Если ТекстыПроцедурыСобытий_Сервер.Свойство(ИмяЭлемента + "НачалоВыбораИзСписка_Сервер") Тогда
		Выполнить(ТекстыПроцедурыСобытий_Сервер[ИмяЭлемента + "НачалоВыбораИзСписка_Сервер"]);
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура УниверсальныйОбработчикСобытияОчистка(Элемент, СтандартнаяОбработка)
	
	Если ТекстыОбработчиковСобытий.Свойство(Элемент.Имя + "Очистка") Тогда
		Выполнить(ТекстыОбработчиковСобытий[Элемент.Имя + "Очистка"]);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УниверсальныйОбработчикСобытияОчистка_Сервер(ИмяЭлемента, ЕщеПараметры)
	
	Если ТекстыПроцедурыСобытий_Сервер.Свойство(ИмяЭлемента + "Очистка_Сервер") Тогда
		Выполнить(ТекстыПроцедурыСобытий_Сервер[ИмяЭлемента + "Очистка_Сервер"]);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УниверсальныйОбработчикСобытияРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	Если ТекстыОбработчиковСобытий.Свойство(Элемент.Имя + "Регулирование") Тогда
		Выполнить(ТекстыОбработчиковСобытий[Элемент.Имя + "Регулирование"]);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УниверсальныйОбработчикСобытияРегулирование_Сервер(ИмяЭлемента, ЕщеПараметры)
	
	Если ТекстыПроцедурыСобытий_Сервер.Свойство(ИмяЭлемента + "Регулирование_Сервер") Тогда
		Выполнить(ТекстыПроцедурыСобытий_Сервер[ИмяЭлемента + "Регулирование_Сервер"]);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УниверсальныйОбработчикСобытияОткрытие(Элемент, СтандартнаяОбработка)
	
	Если ТекстыОбработчиковСобытий.Свойство(Элемент.Имя + "Открытие") Тогда
		Выполнить(ТекстыОбработчиковСобытий[Элемент.Имя + "Открытие"]);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УниверсальныйОбработчикСобытияОткрытие_Сервер(ИмяЭлемента, ЕщеПараметры)
	
	Если ТекстыПроцедурыСобытий_Сервер.Свойство(ИмяЭлемента + "Открытие_Сервер") Тогда
		Выполнить(ТекстыПроцедурыСобытий_Сервер[ИмяЭлемента + "Открытие_Сервер"]);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УниверсальныйОбработчикСобытияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТекстыОбработчиковСобытий.Свойство(Элемент.Имя + "ОбработкаВыбора") Тогда
		Выполнить(ТекстыОбработчиковСобытий[Элемент.Имя + "ОбработкаВыбора"]);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УниверсальныйОбработчикСобытияОбработкаВыбора_Сервер(ИмяЭлемента, ЕщеПараметры)
	
	Если ТекстыПроцедурыСобытий_Сервер.Свойство(ИмяЭлемента + "ОбработкаВыбора_Сервер") Тогда
		Выполнить(ТекстыПроцедурыСобытий_Сервер[ИмяЭлемента + "ОбработкаВыбора_Сервер"]);
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура УниверсальныйОбработчикСобытияАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	Если ТекстыОбработчиковСобытий.Свойство(Элемент.Имя + "АвтоПодбор") Тогда
		Выполнить(ТекстыОбработчиковСобытий[Элемент.Имя + "АвтоПодбор"]);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УниверсальныйОбработчикСобытияАвтоПодбор_Сервер(ИмяЭлемента, ЕщеПараметры)
	
	Если ТекстыПроцедурыСобытий_Сервер.Свойство(ИмяЭлемента + "АвтоПодбор_Сервер") Тогда
		Выполнить(ТекстыПроцедурыСобытий_Сервер[ИмяЭлемента + "АвтоПодбор_Сервер"]);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УниверсальныйОбработчикСобытияОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ТекстыОбработчиковСобытий.Свойство(Элемент.Имя + "ОкончаниеВводаТекста") Тогда
		Выполнить(ТекстыОбработчиковСобытий[Элемент.Имя + "ОкончаниеВводаТекста"]);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УниверсальныйОбработчикСобытияОкончаниеВводаТекста_Сервер(ИмяЭлемента, ЕщеПараметры)
	
	Если ТекстыПроцедурыСобытий_Сервер.Свойство(ИмяЭлемента + "ОкончаниеВводаТекста_Сервер") Тогда
		Выполнить(ТекстыПроцедурыСобытий_Сервер[ИмяЭлемента + "ОкончаниеВводаТекста_Сервер"]);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьЭлементДопСправочникаИЗаполнитьПодчиненныеРеквизиты(Элемент, Дерево_ИмяЭлемента, ИмяСправочника, ПоискПоСправочнику, КакойРеквизитЗаполнили, СтандартнаяОбработка)

	ТекСтрока = Элементы[Дерево_ИмяЭлемента].ТекущиеДанные;
	
	Если ТекСтрока.ИмяРеквизита = КакойРеквизитЗаполнили Тогда 	
		СтандартнаяОбработка = Ложь;
		ЕщеПараметры = Новый Структура("АдресВХранилище", Неопределено);
		
		ПолучитьСписокЭлементовСправочникаДляВыбора_Сервер(ИмяСправочника, ПоискПоСправочнику, ЕщеПараметры);                                      
		АдресВХранилище = ЕщеПараметры.АдресВХранилище; 
		Если НЕ ЗначениеЗаполнено(АдресВХранилище) Тогда
			Сообщить("В справочнике нет записей для выбора.");
		Иначе
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("АдресТаблицыВХранилище",	АдресВХранилище);
			ПараметрыФормы.Вставить("ИмяЭлемента",				Элемент.Имя);
			ПараметрыФормы.Вставить("ИмяСобытия",				"НачалоВыбора");
			ПараметрыФормы.Вставить("Дерево_ИмяЭлемента",		Дерево_ИмяЭлемента);
			
			МодульОбъектаКлиент().ОткрытьФорму_КонтурEDI("УниверсальнаяФормаВыбораУправляемая", , ПараметрыФормы, ЭтаФорма, "УниверсальныйОбработчикПоОповещению", ЭтаФорма, ПараметрыФормы);
		КонецЕсли;
	КонецЕсли;   	
	
КонецПроцедуры 

&НаСервере  
Процедура ПолучитьСписокЭлементовСправочникаДляВыбора_Сервер(ИмяСправочника, ПоискПоСправочнику, ЕщеПараметры)
            	   	                                      
	АдресВХранилище = МодульОбъекта().ПоместитьЗаписиДопСравочникаВо_ВременноеХранилище(ИмяСправочника, ПоискПоСправочнику); 
	ЕщеПараметры.Вставить("АдресВХранилище", АдресВХранилище); 
	
КонецПроцедуры 

&НаСервере  
Процедура ОбработатьВыбранныйЭлементДопСправочника_КонтурEDI(АдресВХ, ДопПараметры)  
	
	Если Не  ЭтоАдресВременногоХранилища(АдресВХ) Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаЗначенийВыбора = ПолучитьИзВременногоХранилища(АдресВХ);
	Если ТаблицаЗначенийВыбора.Количество() > 0 Тогда
		
		ТекСтрока = Элементы[ДопПараметры.Дерево_ИмяЭлемента].ТекущаяСтрока;
		ТекСтрока = ЭтаФорма[ДопПараметры.Дерево_ИмяЭлемента].НайтиПоИдентификатору(ТекСтрока);
		
		ВыбранныйЭлемент = ТаблицаЗначенийВыбора.Найти(Истина, "Выбор");
		
		Если ВыбранныйЭлемент <> Неопределено Тогда
			ТекСтрока.Значение = ВыбранныйЭлемент.Ссылка;
			
			ЕщеПараметры = Новый Структура("Дерево_ИмяЭлемента", ДопПараметры.Дерево_ИмяЭлемента);
			
			ПриИзмененииВыбранногоЭлементаСправочникаВДереве_Сервер(ЕщеПараметры);
		КонецЕсли;
		
	КонецЕсли; 
		
КонецПроцедуры

&НаСервере 
Процедура ПриИзмененииВыбранногоЭлементаСправочникаВДереве_Сервер(ЕщеПараметры)

	ТекСтрока = Элементы[ЕщеПараметры.Дерево_ИмяЭлемента].ТекущаяСтрока;
	ТекСтрока = ЭтаФорма[ЕщеПараметры.Дерево_ИмяЭлемента].НайтиПоИдентификатору(ТекСтрока);

	СтрокиРеквизитов = ТекСтрока.ПолучитьЭлементы();
	
	Если ЗначениеЗаполнено(ТекСтрока.Значение) И ТипЗнч(ТекСтрока.Значение) = Тип("СправочникСсылка.КонтурEDI_ДополнительныеСправочники") Тогда
		ЭлементСправочника	= МодульОбъекта().ПрочитатьЭлементДопСправочника_КонтурEDI(ТекСтрока.Значение); 		
		
		Для каждого СтрДерева Из СтрокиРеквизитов Цикл
			Если ЭлементСправочника.Свойство(СтрДерева.ИмяРеквизита) Тогда
				
				СтрДерева.Значение = ЭлементСправочника[СтрДерева.ИмяРеквизита];
				
			КонецЕсли;
		КонецЦикла; 
	Иначе 
		Если КлючевыеРеквизиты.Свойство(ТекСтрока.ИмяРеквизита) Тогда
			
			КореньДерева		= ЭтаФорма[ЕщеПараметры.Дерево_ИмяЭлемента].НайтиПоИдентификатору(0);
			Если ЗначениеЗаполнено(КореньДерева.Значение) Тогда
				ЭлементСправочника	= МодульОбъекта().ПрочитатьЭлементДопСправочника_КонтурEDI(КореньДерева.Значение); 
				
				Если НЕ ЭлементСправочника.Свойство(ТекСтрока.ИмяРеквизита) ИЛИ ЭлементСправочника[ТекСтрока.ИмяРеквизита] <> ТекСтрока.Значение Тогда 
					КореньДерева.Значение = Справочники.КонтурEDI_ДополнительныеСправочники.ПустаяСсылка();
				КонецЕсли;
			КонецЕсли;
		КонецЕсли; 
		
		Для каждого СтрДерева Из СтрокиРеквизитов Цикл
			
			СтрДерева.Значение = МодульОбъекта().ПривестиЗначениеПоТипуРеквизита("", МодульОбъекта().ПолучитьПолноеИмяТипаЗначения(СтрДерева.Значение));
			
		КонецЦикла; 		
	КонецЕсли;
	
КонецПроцедуры // ()
///}