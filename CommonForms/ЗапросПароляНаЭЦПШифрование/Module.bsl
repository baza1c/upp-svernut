
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервере
Процедура ДобавитьПарольВСертификат(ПарольПользователя)
	
	Если Не ТипЗнч(ВыбранныйСертификат) = Тип("Строка") Тогда
		НачатьТранзакцию();
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Справочник.СертификатыЭЦП");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", ВыбранныйСертификат);
		Блокировка.Заблокировать();
		
		Сертификат = ВыбранныйСертификат.ПолучитьОбъект();
		Сертификат.ЗапомнитьПарольКСертификату = Истина;
		Сертификат.ПарольПользователя = ПарольПользователя;
		Сертификат.Записать();
		
		ЗафиксироватьТранзакцию();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьВидимость()
	
	Если ЗапросПароляДляСертификата Тогда
		Элементы.ГруппаЗаполнитьПароль1.Доступность = Ложь;
		Элементы.ВыбранныйСертификат1.Доступность = Ложь;
	КонецЕсли;
	
	Если ТаблицаЭД.Количество() > 1 Тогда
		Элементы.ОбъектыДляОбработки.Заголовок = НСтр("ru = 'Список'");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыбранныйСертификат) Тогда
		Элементы.ВыбранныйСертификат.ТолькоПросмотр = Истина;
		Элементы.ВыбранныйСертификат1.ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПараметрыВозвратаПароля()
	
	РезультатВыбора = Новый Структура;
	
	РезультатВыбора.Вставить("ВыбранныйСертификат",         ВыбранныйСертификат);
	РезультатВыбора.Вставить("ЗапомнитьПарольКСертификату", ЗапомнитьПароль);
	РезультатВыбора.Вставить("ПарольПользователя",          ПарольПользователя);
	РезультатВыбора.Вставить("Комментарий",                 "");
	// В случае запроса пароля пользователя из помощника создания соглашения ЭД
	// вместо сертификата приходит его представление.
	Если Не ТипЗнч(ВыбранныйСертификат) = Тип("Строка") Тогда
		РезультатВыбора.Вставить("Отпечаток",               ВыбранныйСертификат.Отпечаток);
	КонецЕсли;
	
	Возврат РезультатВыбора;
	
КонецФункции

&НаСервере
Процедура ОбработкаВыбораСертификата()
	
	ПарольПользователя = "";
	Если ВыбранныйСертификат.ЗапомнитьПарольКСертификату Тогда
		Элементы.ГруппаЗаполнитьПароль.Доступность = Ложь;
		ЗапомнитьПароль = Истина;
		ПарольПользователя = ВыбранныйСертификат.ПарольПользователя;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Выбрать(Команда)
	
	// Блок проверки на заполненность сертификата ЭП.
	Если Не ЗначениеЗаполнено(ВыбранныйСертификат) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Сертификат подписи"));
		Возврат;
	КонецЕсли;
	
	Если ЗапросПароляДляСертификата ИЛИ ЗапомнитьПароль Тогда
		Если ПарольПользователя = "" Тогда
			Ответ = Вопрос(НСтр("ru = 'Задан пустой пароль. Продолжить?'"), РежимДиалогаВопрос.ДаНет, 30,
				КодВозвратаДиалога.Да);
			Если Ответ <> КодВозвратаДиалога.Да Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗапомнитьПароль И Не ЗапросПароляДляСертификата Тогда
		ДобавитьПарольВСертификат(ПарольПользователя);
		Оповестить("ЗапросПароляДляСертификата", ВыбранныйСертификат);
	КонецЕсли;
	
	ЭтаФорма.Закрыть(ПараметрыВозвратаПароля());
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЕЙ ФОРМЫ

&НаКлиенте
Процедура ВыбранныйСертификатОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", ВыбранныйСертификат);
	ПараметрыФормы.Вставить("ТолькоПросмотр", Истина);
	ОткрытьФорму("Справочник.СертификатыЭЦП.Форма.ФормаЭлемента", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныйСертификатПриИзменении(Элемент)
	
	ОбработкаВыбораСертификата();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыДляОбработкиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТаблицаЭД.Количество() > 1 Тогда
		МассивСтруктур = Новый Массив;
		Для Каждого СтрокаДанных Из ТаблицаЭД Цикл
			СтруктураПараметров = Новый Структура;
			СтруктураПараметров.Вставить("ЭлектронныйДокумент", СтрокаДанных.ЭлектронныйДокумент);
			СтруктураПараметров.Вставить("ВладелецЭД",          СтрокаДанных.ВладелецЭД);
			СтруктураПараметров.Вставить("НаправлениеЭД",       СтрокаДанных.НаправлениеЭД);
			МассивСтруктур.Добавить(СтруктураПараметров);
		КонецЦикла;
		ФормаПросмотраЭД = ОткрытьФорму("Обработка.ЭлектронныеДокументы.Форма.ФормаСпискаВыгружаемыхДокументов",
			Новый Структура("СтруктураЭД", МассивСтруктур), ЭтаФорма);
		
	ИначеЕсли ТипЗнч(ТаблицаЭД[0].ВладелецЭД) = Тип("ДокументСсылка.ПакетЭД") Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ключ",          ТаблицаЭД[0].ВладелецЭД);
		ПараметрыФормы.Вставить("ТолькоПросмотр", Истина);
		ОткрытьФорму("Документ.ПакетЭД.Форма.ФормаДокумента", ПараметрыФормы);
	ИначеЕсли ТипЗнч(ТаблицаЭД[0].ВладелецЭД) = Тип("ДокументСсылка.ПроизвольныйЭД") Тогда
		
		// Откроем вложение по стандартному механизму
		ДанныеФайла = ПрисоединенныеФайлыСлужебныйВызовСервера.ПолучитьДанныеФайла(ТаблицаЭД[0].ЭлектронныйДокумент,
			УникальныйИдентификатор);
		ПрисоединенныеФайлыКлиент.ОткрытьФайл(ДанныеФайла, Ложь);
	Иначе
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЭлектронныйДокумент", ТаблицаЭД[0].ЭлектронныйДокумент);
		ПараметрыФормы.Вставить("ВладелецЭД",          ТаблицаЭД[0].ВладелецЭД);
		ОткрытьФорму("Обработка.ЭлектронныеДокументы.Форма.ФормаЗагрузкиПросмотраЭД", ПараметрыФормы, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ВидОперации = Параметры.ВидОперации;
	
	Если Параметры.Свойство("ЗапросПароляДляСертификата") Тогда
		ЗапросПароляДляСертификата = Параметры.ЗапросПароляДляСертификата;
		ЗапомнитьПароль = ЗапросПароляДляСертификата;
	КонецЕсли;
	
	СертификатЭП = "";
	Если Параметры.Свойство("СертификатЭП", СертификатЭП) Тогда
		ВыбранныйСертификат = Параметры.СертификатЭП;
	КонецЕсли;
	
	СертификатыЭП = "";
	Если Параметры.Свойство("СертификатыЭП", СертификатыЭП) Тогда
		Если СертификатыЭП.Количество() > 1 Тогда
			Для каждого Сертификат Из СертификатыЭП Цикл
				Элементы.ВыбранныйСертификат.РежимВыбораИзСписка = Истина;
				Элементы.ВыбранныйСертификат.СписокВыбора.Добавить(
					Справочники.СертификатыЭЦП.НайтиПоРеквизиту("Отпечаток", Сертификат.Отпечаток));
			КонецЦикла;
		Иначе
			ВыбранныйСертификат = Справочники.СертификатыЭЦП.НайтиПоРеквизиту("Отпечаток", СертификатыЭП[0].Отпечаток);
			ОбработкаВыбораСертификата();
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ГруппаСтраницыЗапросаПароля.ТекущаяСтраница = Элементы.ГруппаАвторизацииСохраненияПароля;
	
	ОбъектыДляОбработки = Неопределено;
	Если Параметры.Свойство("ОбъектыДляОбработки", ОбъектыДляОбработки) И ОбъектыДляОбработки <> Неопределено Тогда
		
		Элементы.ГруппаСтраницыЗапросаПароля.ТекущаяСтраница = Элементы.ГруппаОбъектыДляОбработки;
		Блокировка = Новый БлокировкаДанных;
		
		Если ТипЗнч(ОбъектыДляОбработки) = Тип("Массив") Тогда
			
			ТаблицаДанныхДляБлокировки = Новый ТаблицаЗначений;
			ТаблицаДанныхДляБлокировки.Колонки.Добавить("ЭлектронныйДокумент");
			ТаблицаДанныхДляБлокировки.Колонки.Добавить("НаправлениеЭД");
			ТаблицаДанныхДляБлокировки.Колонки.Добавить("ВладелецЭД");
			
			Для каждого СтрокаЭД Из ОбъектыДляОбработки Цикл
				НоваяСтрока = ТаблицаДанныхДляБлокировки.Добавить();
				НоваяСтрока.ЭлектронныйДокумент = СтрокаЭД;
				НоваяСтрока.НаправлениеЭД       = Перечисления.НаправленияЭД.Исходящий;
				НоваяСтрока.ВладелецЭД          = СтрокаЭД.ВладелецФайла;
			КонецЦикла;
			
			ЭлементБлокировки = Блокировка.Добавить("Справочник.ЭДПрисоединенныеФайлы");
			ЭлементБлокировки.ИсточникДанных = ТаблицаДанныхДляБлокировки;
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "ЭлектронныйДокумент");
			
			ТаблицаЭД.Загрузить(ТаблицаДанныхДляБлокировки);
			
			Если ОбъектыДляОбработки.Количество() = 1 Тогда
				ЭлектронныйДокумент = ОбъектыДляОбработки[0];
				
				ШаблонГиперссылки = НСтр("ru = '%1 № %2 от %3'");
				Если ТипЗнч(ЭлектронныйДокумент.ВладелецФайла) = Тип("СправочникСсылка.СоглашенияОбИспользованииЭД") Тогда
					СтруктураРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЭлектронныйДокумент,
						"ВидЭД, НомерДокументаОтправителя, ДатаДокументаОтправителя, Организация");
					Если СтруктураРеквизитов.ВидЭД = Перечисления.ВидыЭД.КаталогТоваров Тогда
						ШаблонГиперссылки = НСтр("ru = '%1 %2 от %3'");
						ТекстГиперссылки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонГиперссылки,
							СтруктураРеквизитов.ВидЭД, СтруктураРеквизитов.Организация,
							Формат(СтруктураРеквизитов.ДатаДокументаОтправителя, "ДЛФ=Д"));
					ИначеЕсли СтруктураРеквизитов.ВидЭД = Перечисления.ВидыЭД.ЗапросВыписки Тогда
						ТекстГиперссылки = Строка(ОбъектыДляОбработки[0]);
					Иначе
						ТекстГиперссылки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонГиперссылки,
							СтруктураРеквизитов.ВидЭД, СтруктураРеквизитов.НомерДокументаОтправителя,
							Формат(СтруктураРеквизитов.ДатаДокументаОтправителя, "ДЛФ=Д"));
					КонецЕсли;
				Иначе
					СтруктураРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЭлектронныйДокумент.ВладелецФайла,
						"Номер, Дата");
					ТекстГиперссылки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонГиперссылки,
						Строка(ТипЗнч(ЭлектронныйДокумент.ВладелецФайла)),
						СтруктураРеквизитов.Номер, Формат(СтруктураРеквизитов.Дата, "ДЛФ=Д"));
				КонецЕсли;
				
				ТекстГиперссылки = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ТекстГиперссылки);
			ИначеЕсли ОбъектыДляОбработки.Количество() > 1 Тогда
				
				ШаблонГиперссылки = НСтр("ru = 'Электронные документы (%1)'");
				ТекстГиперссылки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонГиперссылки,
					ОбъектыДляОбработки.Количество());
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(ОбъектыДляОбработки) = Тип("СправочникСсылка.ЭДПрисоединенныеФайлы") Тогда
			ЭлектронныйДокумент = ОбъектыДляОбработки;

			ЭлементБлокировки = Блокировка.Добавить("Справочник.ЭДПрисоединенныеФайлы");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", ЭлектронныйДокумент);
			
			НоваяСтрока = ТаблицаЭД.Добавить();
			НоваяСтрока.ЭлектронныйДокумент = ЭлектронныйДокумент;
			НоваяСтрока.НаправлениеЭД       = Перечисления.НаправленияЭД.Исходящий;
			НоваяСтрока.ВладелецЭД          = ЭлектронныйДокумент.ВладелецФайла;
			
			ШаблонГиперссылки = НСтр("ru = '%1 № %2 от %3'");
			Если ТипЗнч(ЭлектронныйДокумент.ВладелецФайла) = Тип("СправочникСсылка.СоглашенияОбИспользованииЭД") Тогда
				СтруктураРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЭлектронныйДокумент,
					"ВидЭД, НомерДокументаОтправителя, ДатаДокументаОтправителя, Организация");
				Если СтруктураРеквизитов.ВидЭД = Перечисления.ВидыЭД.КаталогТоваров Тогда
					ШаблонГиперссылки = НСтр("ru = '%1 %2 от %3'");
					ТекстГиперссылки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонГиперссылки,
						СтруктураРеквизитов.ВидЭД, СтруктураРеквизитов.Организация,
						Формат(СтруктураРеквизитов.ДатаДокументаОтправителя, "ДЛФ=Д"));
				Иначе
					ТекстГиперссылки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонГиперссылки,
						СтруктураРеквизитов.ВидЭД, СтруктураРеквизитов.НомерДокументаОтправителя,
						Формат(СтруктураРеквизитов.ДатаДокументаОтправителя, "ДЛФ=Д"));
				КонецЕсли;
			Иначе
				СтруктураРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЭлектронныйДокумент.ВладелецФайла,
					"Номер, Дата");
				ТекстГиперссылки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонГиперссылки,
					Строка(ТипЗнч(ЭлектронныйДокумент.ВладелецФайла)),
					СтруктураРеквизитов.Номер, Формат(СтруктураРеквизитов.Дата, "ДЛФ=Д"));
			КонецЕсли;
			
			ТекстГиперссылки = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ТекстГиперссылки);
			
		ИначеЕсли ТипЗнч(ОбъектыДляОбработки) = Тип("ДокументСсылка.ПакетЭД") Тогда
			ПакетЭД = ОбъектыДляОбработки;
			
			ЭлементБлокировки = Блокировка.Добавить("Документ.ПакетЭД");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", ПакетЭД);
			Блокировка.Заблокировать();
			
			НоваяСтрока = ТаблицаЭД.Добавить();
			НоваяСтрока.ЭлектронныйДокумент = ПакетЭД;
			НоваяСтрока.НаправлениеЭД       = Перечисления.НаправленияЭД.Исходящий;
			НоваяСтрока.ВладелецЭД          = ПакетЭД;
			
			ШаблонГиперссылки = НСтр("ru = '%1 № %2 от %3'");
			СтруктураРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПакетЭД, "Номер, Дата");
			ТекстГиперссылки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонГиперссылки,
				Строка(ТипЗнч(ПакетЭД)),
				СтруктураРеквизитов.Номер, Формат(СтруктураРеквизитов.Дата, "ДЛФ=Д"));
				
			ТекстГиперссылки = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ТекстГиперссылки);
			
		КонецЕсли;
		
		НадписьОбъектыДляОбработки = ТекстГиперссылки;
		Блокировка.Заблокировать();
		
	КонецЕсли;
	
	УстановитьДоступностьВидимость();
	
КонецПроцедуры


