
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтруктураПараметров = ПолучитьИзВременногоХранилища(Параметры.АдресВХранилище);
	
	ПримененныеСкидки = СтруктураПараметров.ПримененныеСкидки;
	МассивСкидок = СтруктураПараметров.МассивСкидок;
	Контрагент = СтруктураПараметров.Контрагент;
	ДатаДок = СтруктураПараметров.ДатаДок;
	ЗаказСсылка = СтруктураПараметров.ЗаказСсылка;
	
	Если ЗначениеЗаполнено(ЗаказСсылка) Тогда
		ГраницаПроверкиУсловия = Новый Граница(ЗаказСсылка.МоментВремени(), ВидГраницы.Исключая);
	Иначе
		ГраницаПроверкиУсловия = Новый Граница(КонецДня(ДатаДок), ВидГраницы.Включая);
	КонецЕсли;
	
	ТаблицаСкидок = ИнформацияОРасчетеСкидокПоДокументуВЦелом.Выгрузить();
	
	Для каждого Скидка из МассивСкидок Цикл
		
		НоваяСтрока = ТаблицаСкидок.Добавить();
		
		НоваяСтрока.Значение = абс_Дистрибуция.ПолучитьИмяСкидки(Скидка);
		
		СтрокаПримененнаяСкидка = ПримененныеСкидки.Найти(Скидка, "Скидка");
		Если Не СтрокаПримененнаяСкидка = Неопределено Тогда
			НоваяСтрока.Комментарий = СтрокаПримененнаяСкидка.Комментарий;
		КонецЕсли;
		
		НоваяСтрока.Управляемая = Скидка.абс_НазначаетсяВручную;
		
		Если Скидка.Условие = Перечисления.УсловияСкидкиНаценки.абс_ПроцентомСОграничением Тогда
			 НоваяСтрока.УсловияВыполнены = абс_Дистрибуция.ПроверитьВыполнениеУсловияСкидки(Скидка, ЗаказСсылка);
		Иначе
			НоваяСтрока.УсловияВыполнены = Истина;
		КонецЕсли;
		
		//+Лео Сафонов ЕА Лимит по скидкам
		//Если НЕ ПроверитьОграничениеПоСкидке(Скидка,ЗаказСсылка) Тогда
		//	НоваяСтрока.УсловияВыполнены = Ложь;	
		//КонецЕсли;	
        //-Лео Сафонов ЕА Лимит по скидкам
		
		Если Скидка.абс_НазначаетсяВручную Тогда
			НоваяСтрока.НазначенаПользователем = Не СтрокаПримененнаяСкидка = Неопределено;
		Иначе
			НоваяСтрока.НазначенаПользователем = НоваяСтрока.УсловияВыполнены;
		КонецЕсли;
		
		НоваяСтрока.ДокументСкидки = Скидка;
		
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ТаблицаСкидок, "ИнформацияОРасчетеСкидокПоДокументуВЦелом");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Рассчитать(Команда)
	
	Закрыть(ПолучитьАдресВХранилище());
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Прочее

&НаСервере
Функция ПолучитьАдресВХранилище()
	
	УправляемыеСкидки = Новый ТаблицаЗначений;
	УправляемыеСкидки.Колонки.Добавить("Скидка");
	УправляемыеСкидки.Колонки.Добавить("Комментарий");	
	                     
	ТаблицаСкидок = РеквизитФормыВЗначение("ИнформацияОРасчетеСкидокПоДокументуВЦелом");
	НайденныеСтроки = ТаблицаСкидок.НайтиСтроки(Новый Структура("НазначенаПользователем", Истина));
	
	Для каждого НайденнаяСтрока Из НайденныеСтроки Цикл
		НоваяСтрока = УправляемыеСкидки.Добавить();
		НоваяСтрока.Скидка = НайденнаяСтрока.ДокументСкидки;
		НоваяСтрока.Комментарий = НайденнаяСтрока.Комментарий;
	КонецЦикла;
	
	АдресВХранилище = ПоместитьВоВременноеХранилище(УправляемыеСкидки, Новый УникальныйИдентификатор);
	
	Возврат АдресВХранилище;
	
КонецФункции

//+Лео Сафонов ЕА  Лимит по скидкам
&НаСервере
Функция ПроверитьОграничениеПоСкидке(СсылкаСкидка,ЗаказСсылка)
    ЛимитыНеИсчерпаны = Истина;
	УсловияВыполнены = Истина;

	ТекстСообщения = "";
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УстановкаСкидокНоменклатурыТовары.Ссылка,
		|	УстановкаСкидокНоменклатурыТовары.Номенклатура,
		|	УстановкаСкидокНоменклатурыТовары.Лео_ЛимитШтук КАК ЛимитШтук,
		|	УстановкаСкидокНоменклатурыТовары.Лео_ЛимитСумма КАК ЛимитСумма,
		|	ВЫБОР
		|		КОГДА УстановкаСкидокНоменклатурыТовары.Лео_ЛимитШтук > 0
		|			ТОГДА ВЫБОР
		|					КОГДА УстановкаСкидокНоменклатурыТовары.Лео_ЛимитШтук >= ЕСТЬNULL(Лео_СкидкиОбороты.КоличествоОборот, 0)
		|						ТОГДА ИСТИНА
		|					ИНАЧЕ ЛОЖЬ
		|				КОНЕЦ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ЛимитПоКоличествуНеПривышен,
		|	ВЫБОР
		|		КОГДА УстановкаСкидокНоменклатурыТовары.Лео_ЛимитСумма > 0
		|			ТОГДА ВЫБОР
		|					КОГДА УстановкаСкидокНоменклатурыТовары.Лео_ЛимитСумма >= ЕСТЬNULL(Лео_СкидкиОбороты.СуммаСкидкиОборот, 0)
		|						ТОГДА ИСТИНА
		|					ИНАЧЕ ЛОЖЬ
		|				КОНЕЦ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ЛимитПоСуммеНеПривышен,
		|	 ЕСТЬNULL(Лео_СкидкиОбороты.СуммаСкидкиОборот,0) КАК СуммаОборот,
		|	 ЕСТЬNULL(Лео_СкидкиОбороты.КоличествоОборот,0) КАК КоличествоОборот
		|ИЗ
		|	Документ.УстановкаСкидокНоменклатуры.Товары КАК УстановкаСкидокНоменклатурыТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Лео_Скидки.Обороты(, , , ДокументСкидки <> &ДокументСкидки) КАК Лео_СкидкиОбороты
		|		ПО УстановкаСкидокНоменклатурыТовары.Номенклатура = Лео_СкидкиОбороты.Номенклатура
		|			И УстановкаСкидокНоменклатурыТовары.Ссылка = Лео_СкидкиОбороты.Скидка
		|ГДЕ
		|	УстановкаСкидокНоменклатурыТовары.Ссылка = &Скидка";

	Запрос.УстановитьПараметр("Скидка", СсылкаСкидка);
   	Запрос.УстановитьПараметр("ДокументСкидки",ЗаказСсылка);
	
	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если ВыборкаДетальныеЗаписи.ЛимитПоКоличествуНеПривышен И ВыборкаДетальныеЗаписи.ЛимитПоСуммеНеПривышен Тогда
			//ЛимитыНеИсчерпаны  = Истина;
			
		Иначе
			ЛимитыНеИсчерпаны  = Ложь;
			ТекстСообщения = ?(ВыборкаДетальныеЗаписи.ЛимитПоКоличествуНеПривышен,"", " превышение лимита на " + Строка(ВыборкаДетальныеЗаписи.ЛимитШтук-ВыборкаДетальныеЗаписи.КоличествоОборот) + " " + ВыборкаДетальныеЗаписи.Номенклатура.ЕдиницаХраненияОстатков.Наименование + Символы.ПС);
			ТекстСообщения = ТекстСообщения + ?(ВыборкаДетальныеЗаписи.ЛимитПоСуммеНеПривышен,"", ?(ЗначениеЗаполнено(ТекстСообщения), ",","")+ " превышение лимита на " + Строка(ВыборкаДетальныеЗаписи.ЛимитСумма-ВыборкаДетальныеЗаписи.СуммаОборот) + " руб.");
			Сообщить("Артикул " + ВыборкаДетальныеЗаписи.Номенклатура.Артикул + ":" + ТекстСообщения);
		КонецЕсли;
	КонецЦикла;

	Возврат ЛимитыНеИсчерпаны;
КонецФункции
//-Лео Сафонов ЕА   Лимит по скидкам


