//Процедура выполнения на сервере произвольного кода
//
Процедура ВыполнитьКодНаСервере(ТекстМодуля, СтруктураПараметров = Неопределено, СтруктураЗапросов = Неопределено, ДопПараметры = Неопределено) Экспорт
	
	Выполнить(ТекстМодуля);
	
КонецПроцедуры

//АБС ВСТАВКА 39056  18.02.2014 13:03:40  Шамов
Функция ПолучитьДатуСервера() Экспорт
	
	Возврат ТекущаяДата();
	
КонецФункции
//АБС ВСТАВКА 39056 КОНЕЦ

////////////////////////////////////////////////////////////////////////////////
// РЕГЛАМЕНТНОЕ ВЫПОЛНЕНИЕ ВНЕШНИХ ОБРАБОТОК

Процедура ВыполнениеВнешнихОбработок(ВнешняяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ВнешняяОбработка) Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьВнешнююОбработку(ВнешняяОбработка);
	
КонецПроцедуры

// Функция проверяет, что внешнюю обработку можно запускать
//
Функция ВнешняяОбработкаПравильная(ВнешняяОбработка)
	
	СтрокаСообщенияОбОшибке = "[" + Строка(ТекущаяДата()) + "]";
	
	Отказ = ВнешняяОбработка.ВидОбработки <> Перечисления.ВидыДополнительныхВнешнихОбработок.Обработка;
	
	Если Отказ Тогда
		
		// добавляем запись об ошибках в ЖР
		КлючСообщенияПротокола = "Внешняя обработка." + ВнешняяОбработка.Наименование;
		
		ЗаписьЖурналаРегистрации(
			КлючСообщенияПротокола, УровеньЖурналаРегистрации.Ошибка,,
			ВнешняяОбработка.Ссылка,
			СтрокаСообщенияОбОшибке);
		
	КонецЕсли;
	
	Возврат Не Отказ;
	
КонецФункции

// Выполняет внешнюю обработку
//
Процедура ВыполнитьВнешнююОбработку(ВнешняяОбработка)
	
	Если ТипЗнч(ВнешняяОбработка) = Тип("СправочникСсылка.ВнешниеОбработки") Тогда
		
		ВнешняяОбработкаОбъект = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ВнешняяОбработка, "Ссылка, Наименование, ВидОбработки, ХранилищеВнешнейОбработки");
		
		// неправильную настройку игнорируем
		Если Не ВнешняяОбработкаПравильная(ВнешняяОбработкаОбъект) Тогда
			Возврат;
		КонецЕсли;
		
		ИмяФайла = ПолучитьИмяВременногоФайла();
		ДвоичныеДанные = ВнешняяОбработка.ХранилищеВнешнейОбработки.Получить();
		ДвоичныеДанные.Записать(ИмяФайла);
		
		Обработка = ВнешниеОбработки.Создать(ИмяФайла);
		
		КлючСообщенияПротокола = "Внешняя обработка." + ВнешняяОбработкаОбъект.Наименование;
		
		Попытка
			Обработка.ВыполнитьОбработку();
			
		Исключение
			СтрокаСообщенияОбОшибке =
			"При вызове процедуры ВыполнитьОбработку() произошла ошибка: " + ОписаниеОшибки();
			
			ЗаписьЖурналаРегистрации(
				КлючСообщенияПротокола, УровеньЖурналаРегистрации.Ошибка,,
				ВнешняяОбработка,
				СтрокаСообщенияОбОшибке);
			
		КонецПопытки;
		
		УдалитьФайлы(ИмяФайла);
		
		ЗаписьЖурналаРегистрации(
			КлючСообщенияПротокола, УровеньЖурналаРегистрации.Информация,,
			ВнешняяОбработка,
			"Закончено выполнение внешней обработки");
			
	ИначеЕсли ТипЗнч(ВнешняяОбработка) = Тип("СправочникСсылка.абс_МодулиДляВыполнения") Тогда
		
		МодульОбъект = ВнешняяОбработка.ПолучитьОбъект();
		
		КлючСообщенияПротокола = "Модуль для выполнения." + МодульОбъект.Наименование + " (Код: " + МодульОбъект.Код + ")";
		
		Попытка
			
			МодульОбъект.ВыполнитьМодуль();
			
		Исключение
			СтрокаСообщенияОбОшибке =
			"При вызове процедуры ВыполнитьМодуль() произошла ошибка: " + ОписаниеОшибки();
			
			ЗаписьЖурналаРегистрации(
				КлючСообщенияПротокола, УровеньЖурналаРегистрации.Ошибка,,
				ВнешняяОбработка,
				СтрокаСообщенияОбОшибке);
			
		КонецПопытки;
		
		ЗаписьЖурналаРегистрации(
			КлючСообщенияПротокола, УровеньЖурналаРегистрации.Информация,,
			ВнешняяОбработка,
			"Закончено выполнение внешней обработки");

			
	КонецЕсли;
			
КонецПроцедуры


// Функция получает расписание регл. задания. Если регл. задание не задано, то возвращает "Неопределено"
//
Функция ПолучитьРасписаниеВыполненияОбменаДанными(ВнешняяОбработка) Экспорт
	
	// возвращаемое значение функции
	РасписаниеРегламентногоЗадания = Неопределено;
	
	РегламентноеЗаданиеОбъект = абс_ВнешниеОбработкиСервер.НайтиРегламентноеЗаданиеПоПараметру(ВнешняяОбработка.РегламентноеЗаданиеGUID);
	
	Если РегламентноеЗаданиеОбъект <> Неопределено Тогда
		
		РасписаниеРегламентногоЗадания = РегламентноеЗаданиеОбъект.Расписание;
		
	КонецЕсли;
	
	Возврат ЗначениеВСтрокуВнутр(РасписаниеРегламентногоЗадания);
	
КонецФункции


// Функция возвращает существующее или создает новое регламентное задание для переданного объекта
//
Функция СоздатьРегламентноеЗаданиеПриНеобходимости(Отказ, РегламентноеЗаданиеGUID)
	
	РегламентноеЗаданиеОбъект = абс_ВнешниеОбработкиСервер.НайтиРегламентноеЗаданиеПоПараметру(РегламентноеЗаданиеGUID);
	
	// при необходимости создаем регл. задание
	Если РегламентноеЗаданиеОбъект = Неопределено Тогда
		
		РегламентноеЗаданиеОбъект = РегламентныеЗадания.СоздатьРегламентноеЗадание("абс_ВыполнениеВнешнихОбработок");
		
	КонецЕсли;
	
	Возврат РегламентноеЗаданиеОбъект;
	
КонецФункции

// Процедура устанавливает параметры регл. задания для переданного объекта
//
Процедура УстановитьПараметрыРегламентногоЗадания(РегламентноеЗаданиеОбъект, РасписаниеРегламентногоЗадания, Ссылка, Наименование, ИспользоватьРегламентноеЗадание)
	
	ПараметрыРегламентногоЗадания = Новый Массив;
	ПараметрыРегламентногоЗадания.Добавить(Ссылка);
	
	НаименованиеРегламентногоЗадания =
	"Выполнение: " + ТипЗнч(Ссылка) + ", Наименование: " + СокрЛП(Наименование) + ", Код: " + СокрЛП(Ссылка.Код);
	
	РегламентноеЗаданиеОбъект.Наименование	= Лев(НаименованиеРегламентногоЗадания, 120);
	РегламентноеЗаданиеОбъект.Использование	= ИспользоватьРегламентноеЗадание;
	РегламентноеЗаданиеОбъект.Параметры		= ПараметрыРегламентногоЗадания;
	
	// обновляем расписание, если оно было изменено
	Если РасписаниеРегламентногоЗадания <> Неопределено Тогда
		РегламентноеЗаданиеОбъект.Расписание = РасписаниеРегламентногоЗадания;
	КонецЕсли;
	
	Попытка 
		РегламентноеЗаданиеОбъект.Записать();
	Исключение
		
	КонецПопытки;
	
КонецПроцедуры

// Функция сохраняет регламентное задание и возвращает его уникальный идентифакатор
//
Функция ОбновитьДанныеРегламентногоЗадания(Отказ, РасписаниеРегламентногоЗадания, Ссылка, Наименование, ИспользоватьРегламентноеЗадание, РегламентноеЗаданиеGUID) Экспорт
	
	// получаем регламентное задание по идентификатору, если объект не находим, то создаем новый
	РегламентноеЗаданиеОбъект = СоздатьРегламентноеЗаданиеПриНеобходимости(Отказ, РегламентноеЗаданиеGUID);
	
	Если Отказ Тогда
		Возврат РегламентноеЗаданиеGUID;
	КонецЕсли;
	
	// обновляем свойства РЗ
	УстановитьПараметрыРегламентногоЗадания(РегламентноеЗаданиеОбъект, ЗначениеИзСтрокиВнутр(РасписаниеРегламентногоЗадания), Ссылка, Наименование, ИспользоватьРегламентноеЗадание);
	
	// записываем измененное задание
	абс_ВнешниеОбработкиСервер.ЗаписатьРегламентноеЗадание(Отказ, РегламентноеЗаданиеОбъект);
	
	// Возвращаем GUID регл. задания в реквизит объекта (РегламентноеЗаданиеGUID)
	Возврат Строка(РегламентноеЗаданиеОбъект.УникальныйИдентификатор);
	
КонецФункции

