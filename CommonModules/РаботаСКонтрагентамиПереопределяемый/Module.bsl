                                ///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2018, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьЭлементКонтактнойИнформации(НаборЗаписей, СтруктураДанных, ВидКонтактнойИнформации) Экспорт
	
	ТЗ = НаборЗаписей.Выгрузить();
	СтрокаТЗ = ТЗ.Найти(ВидКонтактнойИнформации, "Вид");
	
	Если СтруктураДанных <> Неопределено Тогда
		Если СтрокаТЗ <> Неопределено Тогда
			Индекс = ТЗ.Индекс(СтрокаТЗ);
			Запись = НаборЗаписей.Получить(Индекс);
		Иначе
			Запись = НаборЗаписей.Добавить();
		КонецЕсли;
		Запись.Тип = ВидКонтактнойИнформации.Тип;
		Запись.Вид = ВидКонтактнойИнформации;
		Запись.Представление = СтруктураДанных.Представление;   
		
		Запись.Значение = СтруктураДанных.КонтактнаяИнформация;
		СтруктураАдреса = УправлениеКонтактнойИнформацией.ПолучитьСтруктуруАдресаИзСтроки(СтруктураДанных.КонтактнаяИнформация);                
		Запись.Поле1 = СтруктураАдреса.Индекс;                      	//индекс
		Запись.Поле2 = СтруктураАдреса.Регион;                      	//регион
		Запись.Поле3 = СтруктураАдреса.Район;                       	//район
		Запись.Поле4 = СтруктураАдреса.Город;                       	//город
		Запись.Поле5 = СтруктураАдреса.НаселенныйПункт;             	//населенный  пункт
		Запись.Поле6 = СтруктураАдреса.Улица;                       	//улица
		Запись.Поле7 = СтруктураАдреса.Дом;                        		//номер дома
		Запись.Поле8 = СтруктураАдреса.Корпус;                        	//номер строения
		Запись.Поле9 = СтруктураАдреса.Квартира;                        //номер кв
		Запись.Поле10 = ""; 
		
		Запись.ТипДома		=	Перечисления.ТипыДомов.Дом;
		Запись.ТипКвартиры 	=	Перечисления.ТипыКвартир.Квартира;
        Запись.ТипКорпуса	=	Перечисления.ТипыКорпусов.Корпус;
		
	Иначе
		Если СтрокаТЗ <> Неопределено Тогда
			Индекс = ТЗ.Индекс(СтрокаТЗ);
			Запись = НаборЗаписей.Получить(Индекс);
			Для Сч = 1 По 10 Цикл
				Запись["Поле"+Строка(Сч)] = "";
			КонецЦикла;
			Запись.Представление = "";
			Запись.Комментарий = "";
			Запись.ЗначениеПоУмолчанию = Ложь;
			Запись.ТипДома     = Перечисления.ТипыДомов.ПустаяСсылка();
			Запись.ТипКорпуса  = Перечисления.ТипыКорпусов.ПустаяСсылка();
			Запись.ТипКвартиры = Перечисления.ТипыКвартир.ПустаяСсылка();
		КонецЕсли
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
