
// Вызывает событие "ВыполнитьРегламентныеДействия" подключаемого модуля
// и передает в него параметр ПараметрыДействия.
// 
// Параметры:
//   Обработка - СправочникСсылка.ДополнительныеОтчетыИОбработки - ссылка на внешнюю обработку для конфигураций на базе БСП;
//             - СправочникСсылка.ВнешниеОбработки - ссылка на внешнюю обработку для конфигураций старых поколений;
//             - Строка - имя встроенной обработки;
//             - ДвоичныеДанные - бинарные данные файла внешней обработки;
//   ПараметрыДействия - Произвольный - (Необязательный) дополнительные параметры события.
//
Процедура ВыполнитьРегламентныеДействия(Обработка, ПараметрыДействия = Неопределено) Экспорт
	
	ОбработкаОбъект = СоздатьОбработкуКонтурЭДО(Обработка);
	ОбработкаОбъект.ВыполнитьКоманду("ЭДО_ВыполнитьРегламентныеДействия", ПараметрыДействия);
	
КонецПроцедуры

// Выполняет команду обработки КонтурЭДО.
//
Процедура ВыполнитьКоманду(Обработка, ПараметрыПроцедуры, АдресРезультата) Экспорт
	
	ОбработкаОбъект = СоздатьОбработкуКонтурЭДО(Обработка);
	
	Если НЕ ПараметрыПроцедуры.Свойство("РезультатВыполнения") Тогда
		ПараметрыПроцедуры.Вставить("РезультатВыполнения", Новый Структура);
	КонецЕсли;
	
	ОбработкаОбъект.ВыполнитьКоманду(ПараметрыПроцедуры.ИдентификаторКоманды, ПараметрыПроцедуры);
	
	Если ТипЗнч(АдресРезультата) = Тип("Строка") И ЭтоАдресВременногоХранилища(АдресРезультата) Тогда
		АдресРезультата = ПоместитьВоВременноеХранилище(ПараметрыПроцедуры.РезультатВыполнения, АдресРезультата);
	КонецЕсли;
	
КонецПроцедуры

// Создает обработку КонтурЭДО по переданным параметрам.
//
Функция СоздатьОбработкуКонтурЭДО(Обработка)
	
	Результат = Неопределено;
	
	ОписаниеОшибки = "";
	
	Если ТипЗнч(Обработка) = Тип("Строка") Тогда // Встроенная обработка
		
		Результат = Обработки[Обработка].Создать();
		
	ИначеЕсли ТипЗнч(Обработка) = Тип("ДвоичныеДанные") Тогда // Обработка КонтурЭДО_МодульОбъекта
		
		Результат = ВнешниеОбработки_ПодключитьБезПредупреждений(Обработка);
		
	Иначе // Внешняя обработка
		
		ИмяПоляХранилища = "";
		
		МетаданныеСправочника = Обработка.Метаданные();
		
		Если МетаданныеСправочника.Реквизиты.Найти("ХранилищеОбработки") <> Неопределено Тогда
			
			ИмяПоляХранилища = "ХранилищеОбработки";
			
		ИначеЕсли МетаданныеСправочника.Реквизиты.Найти("ХранилищеВнешнейОбработки") <> Неопределено Тогда
			
			ИмяПоляХранилища = "ХранилищеВнешнейОбработки";
			
		Иначе
			
			ОписаниеОшибки = "Не найден реквизит хранения файла обработки КонтурЭДО в справочнике """ + МетаданныеСправочника.Синоним + """ для запуска в фоновом задании!";
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ИмяПоляХранилища) Тогда
			
			Результат = ВнешниеОбработки_ПодключитьБезПредупреждений(Обработка[ИмяПоляХранилища].Получить());
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		ВызватьИсключение ОписаниеОшибки;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Создает внешнюю обработку без предупреждения об опасных действиях.
//
Функция ВнешниеОбработки_ПодключитьБезПредупреждений(ДвоичныеДанные)
	
	ОписаниеЗащитыБезПредупреждений = ОписаниеЗащитыБезПредупреждений();
	
	Если ОписаниеЗащитыБезПредупреждений = Неопределено Тогда 
		
		// Если недоступно ОписаниеЗащитыОтОпасныхДействий,
		// подключаем обработку через файл для совместимости со старыми платформами.
		
		Если ТипЗнч(ДвоичныеДанные) = Тип("ДвоичныеДанные") Тогда
			
			ИмяВремФайла = ПолучитьИмяВременногоФайла("epf");
			
			ДвоичныеДанные.Записать(ИмяВремФайла);
			
			Результат = ВнешниеОбработки_Создать_Обертка(ИмяВремФайла, Ложь, ОписаниеЗащитыБезПредупреждений);
			
			УдалитьФайлы(ИмяВремФайла);
			
			Возврат Результат;
			
		КонецЕсли;	
		
	Иначе
		
		АдресОбработки = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
		
		ИмяОбработки = ВнешниеОбработки.Подключить(АдресОбработки,, Ложь, ОписаниеЗащитыБезПредупреждений);
		
		УдалитьИзВременногоХранилища(АдресОбработки);
		
		Результат = ВнешниеОбработки_Создать_Обертка(ИмяОбработки, Ложь, ОписаниеЗащитыБезПредупреждений);
		
		Возврат Результат;
		
	КонецЕсли;
	
КонецФункции

// Обертка для создания внешних обработок разными версиями платформы.
//
Функция ВнешниеОбработки_Создать_Обертка(ИмяФайлаИлиОбработки, БезопасныйРежим, ОписаниеЗащитыБезПредупреждений)
	
	Результат = Неопределено;
	
	СИ = Новый СистемнаяИнформация;
	
	Если ЭДО_Служебные_СравнитьВерсии(СИ.ВерсияПриложения, "8.2") = "ПерваяМеньше" Тогда
		
		Результат = ВнешниеОбработки.Создать(ИмяФайлаИлиОбработки);
		
	ИначеЕсли ЭДО_Служебные_СравнитьВерсии(СИ.ВерсияПриложения, "8.3.11") = "ПерваяМеньше" Тогда
		
		Результат = ВнешниеОбработки.Создать(ИмяФайлаИлиОбработки, БезопасныйРежим);
		
	Иначе
		
		Результат = ВнешниеОбработки.Создать(ИмяФайлаИлиОбработки, БезопасныйРежим, ОписаниеЗащитыБезПредупреждений);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает строку: "Равны" / "ПерваяБольше" / "ПерваяМеньше".
// Версии типа "1.0.1b" сравнивать нельзя.
//
Функция ЭДО_Служебные_СравнитьВерсии(Знач ПерваяВерсия, Знач ВтораяВерсия, Разделитель = ".")
	
	Результат = "Равны";  // ПерваяБольше / ПерваяМеньше
	
	Пока НЕ ПустаяСтрока(ПерваяВерсия) ИЛИ НЕ ПустаяСтрока(ВтораяВерсия) Цикл
		
		Поз = Найти(ПерваяВерсия, Разделитель);
		Если Поз > 0 Тогда
			НомерПервойВерсии = Число(Лев(ПерваяВерсия, Поз - 1));
			ПерваяВерсия = Сред(ПерваяВерсия, Поз + 1);
		Иначе
			НомерПервойВерсии = ?(ПустаяСтрока(ПерваяВерсия), 0, Число(ПерваяВерсия));
			ПерваяВерсия = "";
		КонецЕсли;
		
		Поз = Найти(ВтораяВерсия, Разделитель);
		Если Поз > 0 Тогда
			НомерВторойВерсии = Число(Лев(ВтораяВерсия, Поз - 1));
			ВтораяВерсия = Сред(ВтораяВерсия, Поз + 1);
		Иначе
			НомерВторойВерсии = ?(ПустаяСтрока(ВтораяВерсия), 0, Число(ВтораяВерсия));
			ВтораяВерсия = "";
		КонецЕсли;
		
		Если НомерПервойВерсии > НомерВторойВерсии Тогда
			
			Результат = "ПерваяБольше";
			Прервать;
			
		ИначеЕсли НомерПервойВерсии < НомерВторойВерсии Тогда
			
			Результат = "ПерваяМеньше";
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращает если возможно ОписаниеЗащитыОтОпасныхДействий.
//
Функция ОписаниеЗащитыБезПредупреждений()
	
	Результат = Неопределено;
	
	Попытка
		Результат = Новый("ОписаниеЗащитыОтОпасныхДействий");
		Результат.ПредупреждатьОбОпасныхДействиях = Ложь;
	Исключение КонецПопытки;
	
	Возврат Результат;
	
КонецФункции