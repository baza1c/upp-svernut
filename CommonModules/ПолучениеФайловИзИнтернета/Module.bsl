
// Получить файл из Интернета по протоколу http(s), либо ftp и сохранить его во временный файл.
//
// Параметры:
//   URL                  - Строка - url файла в формате
//                                   [Протокол://]<Сервер>/<Путь к файлу на сервере>
//   ПараметрыПолучения   - Структура со свойствами
//     ПутьДляСохранения    - Строка - путь на сервере (включая имя файла), для сохранения скачанного файла
//     Пользователь         - Строка - пользователь от имени которого установлено соединение
//     Пароль               - Строка - пароль пользователя от которого установлено соединение
//     Порт                 - Число  - порт сервера с которым установлено соединение
//     ЗащищенноеСоединение - Булево - для случая http загрузки флаг указывает,
//                                     что соединение должно производиться через https
//     ПассивноеСоединение  - Булево - для случая ftp загрузки флаг указывает,
//                                     что соединение должно пассивным (или активным)
//
// Возвращаемое значение:
//   Структура, со свойствами
//     Статус - Булево - ключ присутствует в структуре всегда, значения
//                       Истина - вызов функции успешно завершен
//                       Ложь   - вызов функции завершен неудачно
//     Путь   - Строка - путь к файлу на сервере, ключ используется только
//                       если статус Истина
//     СообщениеОбОшибке - Строка - сообщение об ошибке, если статус Ложь
//
Функция СкачатьФайлНаСервере(знач URL, ПараметрыПолучения = Неопределено) Экспорт
	
	// Объявление переменных перед первым использованием в качестве
	// параметра метода Свойство, при анализе параметров получения файлов
	// из ПараметрыПолучения. Содержат значения переданных параметров получения файла
	Перем ПутьДляСохранения, Пользователь, Пароль, Порт,
	      ЗащищенноеСоединение, ПассивноеСоединение;
	
	// Инициализируем параметры скачивания файла
	Если ПараметрыПолучения = Неопределено Тогда
		ПараметрыПолучения = Новый Структура;
	КонецЕсли;
	
	Если Не ПараметрыПолучения.Свойство("ПутьДляСохранения", ПутьДляСохранения) Тогда
		ПутьДляСохранения = Неопределено;
	КонецЕсли;
	
	Если Не ПараметрыПолучения.Свойство("Пользователь", Пользователь) Тогда
		Пользователь = Неопределено;
	КонецЕсли;
	
	Если Не ПараметрыПолучения.Свойство("Пароль", Пароль) Тогда
		Пароль = Неопределено;
	КонецЕсли;
	
	Если Не ПараметрыПолучения.Свойство("Порт", Порт) Тогда
		Порт = Неопределено;
	КонецЕсли;
	
	Если Не ПараметрыПолучения.Свойство("ЗащищенноеСоединение", ЗащищенноеСоединение) Тогда
		ЗащищенноеСоединение = Неопределено;
	КонецЕсли;
	
	Если Не ПараметрыПолучения.Свойство("ПассивноеСоединение", ПассивноеСоединение) Тогда
		ПассивноеСоединение = Неопределено;
	КонецЕсли;
	
	НастройкаСохранения = Новый Соответствие;
	НастройкаСохранения.Вставить("МестоХранения", "Сервер");
	НастройкаСохранения.Вставить("Путь", ПутьДляСохранения);
	
	Результат = ПолучениеФайловИзИнтернетаКлиентСервер.ПодготовитьПолучениеФайла(
		URL,
		Пользователь,
		Пароль,
		Порт,
		ЗащищенноеСоединение,
		ПассивноеСоединение,
		НастройкаСохранения);
	
	Возврат Результат;
	
КонецФункции

// Получить файл из Интернета по протоколу http(s), либо ftp и сохранить его во временное хранилище.
//
// Параметры:
//   URL                  - Строка - url файла в формате:
//                                   [Протокол://]<Сервер>/<Путь к файлу на сервере>
//   ПараметрыПолучения   - Структура со свойствами
//     Пользователь         - Строка - пользователь от имени которого установлено соединение
//     Пароль               - Строка - пароль пользователя от которого установлено соединение
//     Порт                 - Число  - порт сервера с которым установлено соединение
//     ЗащищенноеСоединение - Булево - для случая http загрузки флаг указывает,
//                                     что соединение должно производиться через https
//     ПассивноеСоединение  - Булево - для случая ftp загрузки флаг указывает,
//                                     что соединение должно пассивным (или активным)
//
// Возвращаемое значение:
//   Структура со свойствами
//     Статус  - Булево - ключ присутствует в структуре всегда, значения
//                        Истина - вызов функции успешно завершен
//                        Ложь   - вызов функции завершен неудачно
//     Путь    - Строка - адрес временного хранилища с двоичными данными файла,
//                        ключ используется только если статус Истина
//     СообщениеОбОшибке - Строка - сообщение об ошибке, если статус Ложь
//
Функция СкачатьФайлВоВременноеХранилище(знач URL, ПараметрыПолучения = Неопределено) Экспорт
	
	// Объявление переменных перед первым использованием в качестве
	// параметра метода Свойство, при анализе параметров получения файлов
	// из ПараметрыПолучения. Содержат значения переданных параметров получения файла
	Перем ПутьДляСохранения, Пользователь, Пароль, Порт,
	      ЗащищенноеСоединение, ПассивноеСоединение;
		  
	// Получаем параметры получения файла
	Если ПараметрыПолучения = Неопределено Тогда
		ПараметрыПолучения = Новый Структура;
	КонецЕсли;
	
	Если Не ПараметрыПолучения.Свойство("ПутьДляСохранения", ПутьДляСохранения) Тогда
		ПутьДляСохранения = Неопределено;
	КонецЕсли;
	
	Если Не ПараметрыПолучения.Свойство("Пользователь", Пользователь) Тогда
		Пользователь = Неопределено;
	КонецЕсли;
	
	Если Не ПараметрыПолучения.Свойство("Пароль", Пароль) Тогда
		Пароль = Неопределено;
	КонецЕсли;
	
	Если Не ПараметрыПолучения.Свойство("Порт", Порт) Тогда
		Порт = Неопределено;
	КонецЕсли;
	
	Если Не ПараметрыПолучения.Свойство("ЗащищенноеСоединение", ЗащищенноеСоединение) Тогда
		ЗащищенноеСоединение = Неопределено;
	КонецЕсли;
	
	Если Не ПараметрыПолучения.Свойство("ПассивноеСоединение", ПассивноеСоединение) Тогда
		ПассивноеСоединение = Неопределено;
	КонецЕсли;
	
	НастройкаСохранения = Новый Соответствие;
	НастройкаСохранения.Вставить("МестоХранения", "ВременноеХранилище");
	
	Результат = ПолучениеФайловИзИнтернетаКлиентСервер.ПодготовитьПолучениеФайла(
		URL,
		Пользователь,
		Пароль,
		Порт,
		ЗащищенноеСоединение,
		ПассивноеСоединение,
		НастройкаСохранения);
	
	Возврат Результат;
	
КонецФункции

// Записывает двоичные данные в файл, хранящийся во временном хранилище
//
// Параметры:
//   АдресВоВременномХранилище - Строка - адрес двоичных данных файла 
//                                        во временном хранилище
//   ИмяФайла                  - Строка - путь по которому файл необходимо сохранить
//                                        на сервере
//
Функция СохранитьФайлИзВременногоХранилищаНаСервере(АдресВоВременномХранилище, ИмяФайла) Экспорт
	
	ДанныеФайла = ПолучитьИзВременногоХранилища(АдресВоВременномХранилище);
	ДанныеФайла.Записать(ИмяФайла);
	
	Возврат Истина;
	
КонецФункции

// Получает имя временного файла вызовом одноименной системной функции на сервере
//
Функция ПолучитьИмяВременногоФайлаНаСервере() Экспорт

	Возврат ПолучитьИмяВременногоФайла();

КонецФункции

// Возвращает параметры настройки прокси сервера на стороне сервера 1С:Предприятие
//
Функция ПолучитьНастройкиПроксиНаСервере1СПредприятие() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат Константы.НастройкаПроксиСервера.Получить().Получить();
	
КонецФункции

// Возвращает параметры настройки прокси сервера на стороне сервера 1С:Предприятие
//
Процедура СохранитьНастройкиПроксиНаСервере1СПредприятие(знач Настройки) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Константы.НастройкаПроксиСервера.Установить(Новый ХранилищеЗначения(Настройки));
	
КонецПроцедуры

// Возвращает настройку прокси сервера для доступа к интернет со стороны
// клиента для текущего пользователя.
//
Функция ПолучитьНастройкуПроксиСервера() Экспорт
	
	Возврат ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("НастройкаПроксиСервера");
	
КонецФункции

// Записывает событие-ошибку в журнал регистрации. Имя события
// "Получение файлов из Интернета".
// Параметры
//   СообщениеОбОшибке - строка сообщение об ошибке
// 
Процедура ЗаписатьОшибкуВЖурналРегистрации(знач СообщениеОбОшибке) Экспорт
	
	ЗаписьЖурналаРегистрации(НСтр("ru = 'Получение файлов из Интернета'"), УровеньЖурналаРегистрации.Ошибка,
		, , СообщениеОбОшибке);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обновление информационной базы

// Добавляет в список Обработчики процедуры-обработчики обновления,
// необходимые данной подсистеме.
//
// Параметры:
//   Обработчики - ТаблицаЗначений - см. описание функции НоваяТаблицаОбработчиковОбновления
//                                   общего модуля ОбновлениеИнформационнойБазы.
// 
Процедура ЗарегистрироватьОбработчикиОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "1.2.1.4";
	Обработчик.Процедура = "ПолучениеФайловИзИнтернета.ОбновитьХранимыеНастройкиПрокси";
	
КонецПроцедуры	

// Инициализирует новые настройки прокси-сервера "ИспользоватьПрокси"
// и "ИспользоватьСистемныеНастройки".
//
Процедура ОбновитьХранимыеНастройкиПрокси() Экспорт
	
	МассивПользователейИБ = ПользователиИнформационнойБазы.ПолучитьПользователей();
	
	Для Каждого ПользовательИБ Из МассивПользователейИБ Цикл
		
		НастройкаПроксиСервера = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"НастройкаПроксиСервера", ,	, ,	ПользовательИБ.Имя);
		
		Если ТипЗнч(НастройкаПроксиСервера) = Тип("Соответствие") Тогда
			
			СохранитьНастройкиПользователя = Ложь;
			Если НастройкаПроксиСервера.Получить("ИспользоватьПрокси") = Неопределено Тогда
				НастройкаПроксиСервера.Вставить("ИспользоватьПрокси", Ложь);
				СохранитьНастройкиПользователя = Истина;
			КонецЕсли;
			Если НастройкаПроксиСервера.Получить("ИспользоватьСистемныеНастройки") = Неопределено Тогда
				НастройкаПроксиСервера.Вставить("ИспользоватьСистемныеНастройки", Ложь);
				СохранитьНастройкиПользователя = Истина;
			КонецЕсли;
			Если СохранитьНастройкиПользователя Тогда
				ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
					"НастройкаПроксиСервера", , НастройкаПроксиСервера, , ПользовательИБ.Имя);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	НастройкаПроксиСервера = ПолучитьНастройкиПроксиНаСервере1СПредприятие();
	
	Если ТипЗнч(НастройкаПроксиСервера) = Тип("Соответствие") Тогда
		
		СохранитьНастройкиСервера = Ложь;
		Если НастройкаПроксиСервера.Получить("ИспользоватьПрокси") = Неопределено Тогда
			НастройкаПроксиСервера.Вставить("ИспользоватьПрокси", Ложь);
			СохранитьНастройкиСервера = Истина;
		КонецЕсли;
		Если НастройкаПроксиСервера.Получить("ИспользоватьСистемныеНастройки") = Неопределено Тогда
			НастройкаПроксиСервера.Вставить("ИспользоватьСистемныеНастройки", Ложь);
			СохранитьНастройкиСервера = Истина;
		КонецЕсли;
		Если СохранитьНастройкиСервера Тогда
			СохранитьНастройкиПроксиНаСервере1СПредприятие(НастройкаПроксиСервера);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры


// Запускает диагностику сетевого ресурса.
// В модели сервиса возвращается только описание ошибки.
//
// Параметры:
//  URL - Строка - адрес URL ресурса, диагностику которого надо выполнить.
//  ЗаписыватьОшибку - Булево - признак необходимости записи ошибок в журнал регистрации.
//  ПроверятьДоставкуПакетов - Булево - включать в диагностику команду PING к требуемому ресурсу URL.
//  ТекстОшибки - Строка - исходный текст исключения.
//
// Возвращаемое значение:
//  Структура:
//    *  ОписаниеОшибки    - Строка - краткое описание ошибки.
//    *  ЖурналДиагностики - Строка - подробный журнал диагностики с техническими подробностями.
//
// Пример:
//	// Диагностика веб-сервиса адресного классификатора.
//	Результат = ПолучениеФайловИзИнтернета.ДиагностикаСоединения("https://api.orgaddress.1c.com/orgaddress/v1?wsdl");
//	
//	ОписаниеОшибки    = Результат.ОписаниеОшибки;
//	ЖурналДиагностики = Результат.ЖурналДиагностики;
//
Функция ДиагностикаСоединения(URL, ЗаписыватьОшибку = Истина, ПроверятьДоставкуПакетов = Истина, ТекстОшибки = "") Экспорт
	
	Описание = Новый Массив;
	Описание.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'При обращении по URL: %1'"), 
		URL));
	Описание.Добавить(ПолучениеФайловИзИнтернетаСлужебный.ПредставлениеМестаДиагностики());
	
	СтруктураСсылки = ОбщегоНазначенияКлиентСервер.СтруктураURI(URL);
	
	Если Не ПустаяСтрока(ТекстОшибки)
		И СтрНайти(ВРег(ТекстОшибки), ВРег("Удаленный узел не прошел проверку")) > 0 Тогда // АПК:1297 Нелокализуемый фрагмент информации об ошибке в исключении.
		
		Описание.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Необходимо установить корневой и промежуточные сертификаты к %1 на компьютере <%2>.'"),
			СтруктураСсылки.ИмяСервера,
			ИмяКомпьютера()));
		
		ПолучениеФайловИзИнтернетаЛокализация.ПриФормированииСообщенияОбИзвестнойПроблеме(Описание, ТекстОшибки);
		
		ОписаниеОшибки = СтрСоединить(Описание, Символы.ПС);
		
		Результат = Новый Структура;
		Результат.Вставить("ОписаниеОшибки", ОписаниеОшибки);
		Результат.Вставить("ЖурналДиагностики", "");
		
		Возврат Результат;
		
	КонецЕсли;
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Описание.Добавить(НСтр("ru = 'Обратитесь к администратору.'"));
		
		ПолучениеФайловИзИнтернетаЛокализация.ПриФормированииСообщенияОбИзвестнойПроблеме(Описание, ТекстОшибки);
		
		ОписаниеОшибки = СтрСоединить(Описание, Символы.ПС);
		
		Результат = Новый Структура;
		Результат.Вставить("ОписаниеОшибки", ОписаниеОшибки);
		Результат.Вставить("ЖурналДиагностики", "");
		
		Возврат Результат;
	КонецЕсли;
	
	Журнал = Новый Массив;
	Если ПроверятьДоставкуПакетов Тогда
		Журнал.Добавить(
			НСтр("ru = 'Журнал диагностики:
			           |Выполняется проверка доступности сервера.
			           |Описание диагностируемой ошибки см. в следующем сообщении журнала.'"));
	Иначе
		Журнал.Добавить(
			НСтр("ru = 'Журнал диагностики:
			           |Выполняется проверка доступности контрольного сервера.
			           |Описание диагностируемой ошибки см. в следующем сообщении журнала.'"));
	КонецЕсли;
	Журнал.Добавить();
	
	СостояниеНастроекПрокси = ПолучениеФайловИзИнтернетаСлужебный.СостояниеНастроекПрокси(СтруктураСсылки.Схема);
	СоединениеЧерезПрокси = СостояниеНастроекПрокси.СоединениеЧерезПрокси;
	Журнал.Добавить(СостояниеНастроекПрокси.Представление);
	
	Если СоединениеЧерезПрокси И Не СостояниеНастроекПрокси.ИспользуютсяСистемныеНастройкиПрокси Тогда 
		
		Описание.Добавить(
			НСтр("ru = 'Диагностика соединения не выполнена, т.к. настроен прокси-сервер.
			           |Обратитесь к администратору.'"));
		
	Иначе 
		
		АдресСервераРесурса = СтруктураСсылки.Хост;
		АдресСервераКонтрольнойПроверки = "google.com";
		
		Если Метаданные.ОбщиеМодули.Найти("ПолучениеФайловИзИнтернетаСлужебныйЛокализация") <> Неопределено Тогда
			МодульПолучениеФайловИзИнтернетаСлужебныйЛокализация = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернетаСлужебныйЛокализация");
			АдресСервераКонтрольнойПроверки = МодульПолучениеФайловИзИнтернетаСлужебныйЛокализация.АдресСервераКонтрольнойПроверки();
		КонецЕсли;
		
		Если ПроверятьДоставкуПакетов Тогда
			РезультатДоступностиРесурса = ПолучениеФайловИзИнтернетаСлужебный.ПроверитьДоступностьСервера(АдресСервераРесурса);
			
			Журнал.Добавить();
			Журнал.Добавить("1) " + РезультатДоступностиРесурса.ЖурналДиагностики);
		
			Если РезультатДоступностиРесурса.Доступен Тогда 
				
				Описание.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Выполнено обращение к несуществующему ресурсу на сервере %1
					           |или возникли неполадки на удаленном сервере.'"),
					АдресСервераРесурса));
				
			Иначе 
				
				РезультатКонтрольнойПроверки = ПолучениеФайловИзИнтернетаСлужебный.ПроверитьДоступностьСервера(АдресСервераКонтрольнойПроверки);
				Журнал.Добавить("2) " + РезультатКонтрольнойПроверки.ЖурналДиагностики);
				
				Если Не РезультатКонтрольнойПроверки.Доступен Тогда
					
					Описание.Добавить(
						НСтр("ru = 'Отсутствует доступ в сеть интернет по причине:
						           |- компьютер не подключен к интернету;
						           |- неполадки у интернет-провайдера;
						           |- подключение к интернету блокирует межсетевой экран, 
						           |  антивирус или другое программное обеспечение.'"));
					
				Иначе 
					
					Описание.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Сервер %1 недоступен по причине:
						           |- неполадки у интернет-провайдера;
						           |- подключение к серверу блокирует межсетевой экран, 
						           |  антивирус или другое программное обеспечение;
						           |- сервер отключен или на техническом обслуживании.'"),
						АдресСервераРесурса));
					
					ЖурналТрассировки = ПолучениеФайловИзИнтернетаСлужебный.ЖурналТрассировкиМаршрутаСервера(АдресСервераРесурса);
					Журнал.Добавить("3) " + ЖурналТрассировки);
					
				КонецЕсли;
				
			КонецЕсли;
		Иначе
			РезультатКонтрольнойПроверки = ПолучениеФайловИзИнтернетаСлужебный.ПроверитьДоступностьСервера(АдресСервераКонтрольнойПроверки);
				Журнал.Добавить("1) " + РезультатКонтрольнойПроверки.ЖурналДиагностики);
				
				Если Не РезультатКонтрольнойПроверки.Доступен Тогда
					
					Описание.Добавить(
						НСтр("ru = 'Отсутствует доступ в сеть интернет по причине:
						           |- компьютер не подключен к интернету;
						           |- неполадки у интернет-провайдера;
						           |- подключение к интернету блокирует межсетевой экран, 
						           |  антивирус или другое программное обеспечение.'"));
					
				Иначе 
					
					Описание.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Сервер %1 недоступен по причине:
						           |- неполадки у интернет-провайдера;
						           |- подключение к серверу блокирует межсетевой экран, 
						           |  антивирус или другое программное обеспечение;
						           |- сервер отключен или на техническом обслуживании.'"),
						АдресСервераРесурса));
					
					ЖурналТрассировки = ПолучениеФайловИзИнтернетаСлужебный.ЖурналТрассировкиМаршрутаСервера(АдресСервераРесурса);
					Журнал.Добавить("2) " + ЖурналТрассировки);
					
				КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	ПолучениеФайловИзИнтернетаЛокализация.ПриФормированииСообщенияОбИзвестнойПроблеме(Описание, ТекстОшибки);
	
	ОписаниеОшибки = СтрСоединить(Описание, Символы.ПС);
	
	Журнал.Вставить(0);
	Журнал.Вставить(0, ОписаниеОшибки);
	
	ЖурналДиагностики = СтрСоединить(Журнал, Символы.ПС);
	
	Если ЗаписыватьОшибку Тогда
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Диагностика соединения'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,, ЖурналДиагностики);
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ОписаниеОшибки", ОписаниеОшибки);
	Результат.Вставить("ЖурналДиагностики", ЖурналДиагностики);
	
	Возврат Результат;
	
КонецФункции

// Определяет значение таймаута в секундах для загрузки файла в зависимости от размера этого файла.
// Если известен размер файла, то размер в мегабайтах * 128, иначе
// предельное время загрузки, но не более 43200.
// Минимальный таймаут - 30, это время необходимое для установки соединения.
//
// Параметры:
//  Размер - Число - размер файла в байтах.
//
// Возвращаемое значение:
//  Число
//
Функция ТаймаутЗагрузкиФайла(Размер) Экспорт
	
	БайтВМегабайте = 1048576;
	
	Таймаут = Окр(Размер / БайтВМегабайте * 128);
	Если Таймаут > 43200 Тогда
		Таймаут = 43200;
	ИначеЕсли Таймаут < 30 Тогда
			Таймаут = 30;
	КонецЕсли;
	
	Возврат Таймаут;
	
КонецФункции
