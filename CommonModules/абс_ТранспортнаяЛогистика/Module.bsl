
Процедура абс_ПриЗаписиРеализацииТоваровУслугПриЗаписи(Источник, Отказ) Экспорт
	Если Источник.абс_Статус=Перечисления.абс_СтатусыОтгрузки.Отгружен Тогда
		СформироватьЗаявкаНаТранспорт(Источник.Ссылка);
	КонецЕсли;
КонецПроцедуры

Процедура СформироватьЗаявкаНаТранспорт(ДокументОтгрузки)
	Запрос=Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	абс_ЗаявкаНаТранспорт.Ссылка
	|ИЗ
	|	Документ.абс_ЗаявкаНаТранспорт КАК абс_ЗаявкаНаТранспорт
	|ГДЕ
	|	абс_ЗаявкаНаТранспорт.ДокументОтгрузки = &ДокументОтгрузки";
	Запрос.УстановитьПараметр("ДокументОтгрузки",ДокументОтгрузки);
	Результат=Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	ДокументЗаявка=Документы.абс_ЗаявкаНаТранспорт.СоздатьДокумент();
	ДокументЗаявка.Заполнить(ДокументОтгрузки);
	ДокументЗаявка.Записать();
КонецПроцедуры

Функция ПолучитьТранспортныйМаршрутПоРеализации(ДокументОтгрузки) Экспорт
	
	Если Не ЗначениеЗаполнено(ДокументОтгрузки) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	абс_ТранспортныйМаршрутЗадания.Ссылка
	|ИЗ
	|	Документ.абс_ТранспортныйМаршрут.Задания КАК абс_ТранспортныйМаршрутЗадания
	|ГДЕ
	|	абс_ТранспортныйМаршрутЗадания.ЗаявкаНаТранспорт.ДокументОтгрузки = &ДокументОтгрузки
	|
	|УПОРЯДОЧИТЬ ПО
	|	абс_ТранспортныйМаршрутЗадания.Ссылка.МоментВремени УБЫВ";
	Запрос.УстановитьПараметр("ДокументОтгрузки", ДокументОтгрузки);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Процедура абс_ПриУстановкеНовогоНомераТранспортногоМаршрута(Источник, СтандартнаяОбработка, Префикс) Экспорт
	Префикс = Сред(Источник.Дата,4,2) + "/" + Лев(Источник.Дата,2) + "-"
КонецПроцедуры

Процедура ЗаполнитьДанныеДляПечатиТТНВПеремещении(ПеремещениеТоваров, ТранспортныйМаршрут, ЗаявкаНаТранспорт) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	абс_ТранспортныйМаршрутЗадания.Ссылка.ТКПеревозчик КАК абс_Перевозчик,
	|	абс_ТранспортныйМаршрутЗадания.Ссылка.ВодительЭкспедитор КАК абс_Водитель,
	|	абс_ТранспортныйМаршрутЗадания.ЗаявкаНаТранспорт.Контрагент КАК абс_Заказчик,
	|	абс_ТранспортныйМаршрутЗадания.Ссылка.НомерПломбы КАК абс_НомерПломбы,
	|	абс_ТранспортныйМаршрутЗадания.Ссылка.ТранспортноеСредство КАК абс_ТранспортноеСредство,
	|	абс_ТранспортныйМаршрутЗадания.ДатаДоставки КАК абс_СрокДоставки,
	|	абс_ТранспортныйМаршрутЗадания.ЗаявкаНаТранспорт.ВсегоКоличествоПалет КАК абс_КоличествоПалет,
	|	абс_ТранспортныйМаршрутЗадания.ЗаявкаНаТранспорт.АдресПромежуточный КАК абс_ПунктРазгрузки,
	|	абс_ТранспортныйМаршрутЗадания.ЗаявкаНаТранспорт.Склад.абс_Контрагент КАК КонтрагентСклада
	|ИЗ
	|	Документ.абс_ТранспортныйМаршрут.Задания КАК абс_ТранспортныйМаршрутЗадания
	|ГДЕ
	|	абс_ТранспортныйМаршрутЗадания.Ссылка = &ТранспортныйМаршрут
	|	И абс_ТранспортныйМаршрутЗадания.ЗаявкаНаТранспорт = &ЗаявкаНаТранспорт";
	
	Запрос.УстановитьПараметр("ТранспортныйМаршрут", ТранспортныйМаршрут);
	Запрос.УстановитьПараметр("ЗаявкаНаТранспорт", ЗаявкаНаТранспорт);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		ДокументОбъект = ПеремещениеТоваров.ПолучитьОбъект();
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, Выборка);
		
		Если ЗначениеЗаполнено(Выборка.КонтрагентСклада) Тогда
			ДокументОбъект.абс_ПунктПогрузки = УправлениеКонтактнойИнформацией.ПолучитьАдресИзКонтактнойИнформации(Выборка.КонтрагентСклада, "Фактический");
		КонецЕсли;
		
		Попытка
			ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
		Исключение
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьСпособыДоставкиКонтрагентаПоАдресуДоставки(Контрагент, АдресДоставки) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	абс_СпособыДоставкиКонтрагентов.Ссылка
	|ИЗ
	|	Справочник.абс_СпособыДоставкиКонтрагентов КАК абс_СпособыДоставкиКонтрагентов
	|ГДЕ
	|	абс_СпособыДоставкиКонтрагентов.Владелец = &Контрагент
	|	И абс_СпособыДоставкиКонтрагентов.АдресДоставки = &АдресДоставки";
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("АдресДоставки", АдресДоставки);
	
	МассивСпособовДоставки = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат МассивСпособовДоставки;
	
КонецФункции

Функция ПолучитьОсновнойАдресКонтрагента(Контрагент) Экспорт
	
	Если Не ЗначениеЗаполнено(Контрагент) Тогда
		Возврат "";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	КонтактнаяИнформация.Представление
	|ИЗ
	|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.ЗначениеПоУмолчанию
	|	И КонтактнаяИнформация.Объект = &Контрагент
	|	И КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес)
	|
	|УПОРЯДОЧИТЬ ПО
	|	КонтактнаяИнформация.абс_ДатаАктуальностиКИ УБЫВ";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат СокрЛП(Выборка.Представление);
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции


   ///Leo   01.04.16 Никитин добавлена функция
Функция ПолучитьФактическийАдресКонтрагента(Контрагент) Экспорт
	
	Если Не ЗначениеЗаполнено(Контрагент) Тогда
		Возврат "";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	КонтактнаяИнформация.Представление
	|ИЗ
	|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Объект = &Контрагент
	|	И КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес)
	|	И КонтактнаяИнформация.Вид = &Вид
	|
	|УПОРЯДОЧИТЬ ПО
	|	КонтактнаяИнформация.абс_ДатаАктуальностиКИ УБЫВ";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("Вид", справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента);

	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат СокрЛП(Выборка.Представление);
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

   ///Leo   08.02.22 Никитин добавлена функция
Функция ПолучитьДоставкиАдресКонтрагента(Контрагент) Экспорт
	
	Если Не ЗначениеЗаполнено(Контрагент) Тогда
		Возврат "";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	КонтактнаяИнформация.Представление
	|ИЗ
	|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Объект = &Контрагент
	|	И КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес)
	|	И КонтактнаяИнформация.Вид = &Вид
	|
	|УПОРЯДОЧИТЬ ПО
	|	КонтактнаяИнформация.абс_ДатаАктуальностиКИ УБЫВ";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("Вид", справочники.ВидыКонтактнойИнформации.АдресДоставкиКонтрагента);

	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат СокрЛП(Выборка.Представление);
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции


