
Процедура ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,ДатаСведений,ВидТарифа,ПФР = 0,ПФРНакопительная = 0,ФСС = 0,ФФОМС = 0,ТФОМС =0) Экспорт 

    НоваяСтрока = ТаблицаТарифов.Добавить();
	НоваяСтрока.Период = ДатаСведений;
	НоваяСтрока.ВидТарифа = ВидТарифа;
	НоваяСтрока.ПФР = ПФР;
	НоваяСтрока.ПФРНакопительная = ПФРНакопительная;
	НоваяСтрока.ФСС = ФСС;
	НоваяСтрока.ТФОМС = ТФОМС;
	НоваяСтрока.ФФОМС = ФФОМС;

КонецПроцедуры

Функция ТаблицаТарифовСтраховыхВзносов() Экспорт 

	ТаблицаТарифов = Новый ТаблицаЗначений;
	ТаблицаТарифов.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	ТаблицаТарифов.Колонки.Добавить("ВидТарифа", Новый ОписаниеТипов("ПеречислениеСсылка.ТарифыСтраховыхВзносов"));
	ТаблицаТарифов.Колонки.Добавить("ПФР", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаТарифов.Колонки.Добавить("ПФРНакопительная", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаТарифов.Колонки.Добавить("ФСС", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаТарифов.Колонки.Добавить("ТФОМС", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаТарифов.Колонки.Добавить("ФФОМС", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаТарифов.Индексы.Добавить("ВидТарифа");
					
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20100101',Перечисления.ТарифыСтраховыхВзносов.ОбщийНалоговыйРежим,
			20.0,	6.0,	2.9,	1.1,	2.0);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС, ТФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20110101',Перечисления.ТарифыСтраховыхВзносов.ОбщийНалоговыйРежим,
			26.0,	6.0,	2.9,	3.1,	2.0);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС, ТФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20120101',Перечисления.ТарифыСтраховыхВзносов.ОбщийНалоговыйРежим,
			22.0,	6.0,	2.9,	5.1);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20160101',Перечисления.ТарифыСтраховыхВзносов.ОбщийНалоговыйРежим,
			26.0,	6.0,	2.9,	5.1);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС
	
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20100101',Перечисления.ТарифыСтраховыхВзносов.УпрощенныйНалоговыйРежим,
			14.0,	6.0,	0.0,	0.0,	0.0);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС, ТФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20110101',Перечисления.ТарифыСтраховыхВзносов.УпрощенныйНалоговыйРежим,
			26.0,	6.0,	2.9,	3.1,	2.0);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС, ТФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20120101',Перечисления.ТарифыСтраховыхВзносов.УпрощенныйНалоговыйРежим,
			22.0,	6.0,	2.9,	5.1);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20160101',Перечисления.ТарифыСтраховыхВзносов.УпрощенныйНалоговыйРежим,
			26.0,	6.0,	2.9,	5.1);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС
	
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20100101',Перечисления.ТарифыСтраховыхВзносов.ЕНВД,
			14.0,	6.0,	0.0,	0.0,	0.0);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС, ТФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20110101',Перечисления.ТарифыСтраховыхВзносов.ЕНВД,
			26.0,	6.0,	2.9,	3.1,	2.0);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС, ТФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20120101',Перечисления.ТарифыСтраховыхВзносов.ЕНВД,
			22.0,	6.0,	2.9,	5.1);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20160101',Перечисления.ТарифыСтраховыхВзносов.ЕНВД,
			26.0,	6.0,	2.9,	5.1);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС
	
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20100101',Перечисления.ТарифыСтраховыхВзносов.СельХозПроизводители,
			15.8,	6.0,	1.9,	1.1,	1.2);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС, ТФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20110101',Перечисления.ТарифыСтраховыхВзносов.СельХозПроизводители,
			16.0,	6.0,	1.9,	1.1,	1.2);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС, ТФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20120101',Перечисления.ТарифыСтраховыхВзносов.СельХозПроизводители,
			16.0,	6.0,	1.9,	2.3);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20130101',Перечисления.ТарифыСтраховыхВзносов.СельХозПроизводители,
			21.0,	6.0,	2.4,	3.7);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20150101',Перечисления.ТарифыСтраховыхВзносов.СельХозПроизводители,
			26.0,	6.0,	2.9,	5.1);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС

	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20100101',Перечисления.ТарифыСтраховыхВзносов.ЕСХН,
			10.3,	6.0,	0.0,	0.0,	0.0);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС, ТФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20110101',Перечисления.ТарифыСтраховыхВзносов.ЕСХН,
			16.0,	6.0,	1.9,	1.1,	1.2);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС, ТФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20120101',Перечисления.ТарифыСтраховыхВзносов.ЕСХН,
			16.0,	6.0,	1.9,	2.3);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20130101',Перечисления.ТарифыСтраховыхВзносов.ЕСХН,
			21.0,	6.0,	2.4,	3.7);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20150101',Перечисления.ТарифыСтраховыхВзносов.ЕСХН,
			26.0,	6.0,	2.9,	5.1);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС

	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20110101',Перечисления.ТарифыСтраховыхВзносов.НародныеХудожественныеПромыслы,
			16.0,	6.0,	1.9,	1.1,	1.2);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС, ТФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20120101',Перечисления.ТарифыСтраховыхВзносов.НародныеХудожественныеПромыслы,
			16.0,	6.0,	1.9,	2.3);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20130101',Перечисления.ТарифыСтраховыхВзносов.НародныеХудожественныеПромыслы,
			21.0,	6.0,	2.4,	3.7);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20150101',Перечисления.ТарифыСтраховыхВзносов.НародныеХудожественныеПромыслы,
			26.0,	6.0,	2.9,	5.1);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС
			
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20100101',Перечисления.ТарифыСтраховыхВзносов.ОрганизацияИнвалидов,
			14.0,	6.0,	0.0,	0.0,	0.0);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС, ТФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20110101',Перечисления.ТарифыСтраховыхВзносов.ОрганизацияИнвалидов,
			16.0,	6.0,	1.9,	1.1,	1.2);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС, ТФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20120101',Перечисления.ТарифыСтраховыхВзносов.ОрганизацияИнвалидов,
			16.0,	6.0,	1.9,	2.3);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20130101',Перечисления.ТарифыСтраховыхВзносов.ОрганизацияИнвалидов,
			21.0,	6.0,	2.4,	3.7);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20150101',Перечисления.ТарифыСтраховыхВзносов.ОрганизацияИнвалидов,
			26.0,	6.0,	2.9,	5.1);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС

	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20100101',Перечисления.ТарифыСтраховыхВзносов.РезидентТехникоВнедренческойЗоны,
			14.0,	6.0,	0.0,	0.0,	0.0);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС, ТФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20110101',Перечисления.ТарифыСтраховыхВзносов.РезидентТехникоВнедренческойЗоны,
			 8.0,	6.0,	2.0,	2.0,	2.0);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС, ТФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20120101',Перечисления.ТарифыСтраховыхВзносов.РезидентТехникоВнедренческойЗоны,
			 8.0,	6.0,	2.0,	4.0);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20180101',Перечисления.ТарифыСтраховыхВзносов.РезидентТехникоВнедренческойЗоны,
			13.0,	6.0,	2.9,	5.1);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20190101',Перечисления.ТарифыСтраховыхВзносов.РезидентТехникоВнедренческойЗоны,
			20.0,	6.0,	2.9,	5.1);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20200101',Перечисления.ТарифыСтраховыхВзносов.РезидентТехникоВнедренческойЗоны,
			26.0,	6.0,	2.9,	5.1);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС

	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20100101',Перечисления.ТарифыСтраховыхВзносов.ITОрганизации,
			14.0,	6.0,	0.0,	0.0,	0.0);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС, ТФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20110101',Перечисления.ТарифыСтраховыхВзносов.ITОрганизации,
			 8.0,	6.0,	2.0,	2.0,	2.0);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС, ТФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20120101',Перечисления.ТарифыСтраховыхВзносов.ITОрганизации,
			 8.0,	6.0,	2.0,	4.0);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20180101',Перечисления.ТарифыСтраховыхВзносов.ITОрганизации,
			13.0,	6.0,	2.9,	5.1);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20190101',Перечисления.ТарифыСтраховыхВзносов.ITОрганизации,
			20.0,	6.0,	2.9,	5.1);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20200101',Перечисления.ТарифыСтраховыхВзносов.ITОрганизации,
			26.0,	6.0,	2.9,	5.1);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС

	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20100101',Перечисления.ТарифыСтраховыхВзносов.ХозОбществаБюджетныхВУЗов,
			 0.0,	0.0,	0.0,	0.0,	0.0);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС, ТФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20110101',Перечисления.ТарифыСтраховыхВзносов.ХозОбществаБюджетныхВУЗов,
			 8.0,	6.0,	2.0,	2.0,	2.0);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС, ТФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20120101',Перечисления.ТарифыСтраховыхВзносов.ХозОбществаБюджетныхВУЗов,
			 8.0,	6.0,	2.0,	4.0);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20180101',Перечисления.ТарифыСтраховыхВзносов.ХозОбществаБюджетныхВУЗов,
			13.0,	6.0,	2.9,	5.1);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20190101',Перечисления.ТарифыСтраховыхВзносов.ХозОбществаБюджетныхВУЗов,
			20.0,	6.0,	2.9,	5.1);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20200101',Перечисления.ТарифыСтраховыхВзносов.ХозОбществаБюджетныхВУЗов,
			26.0,	6.0,	2.9,	5.1);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС

	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20100101',Перечисления.ТарифыСтраховыхВзносов.СМИ,
			 0.0,	0.0,	0.0,	0.0,	0.0);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС, ТФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20110101',Перечисления.ТарифыСтраховыхВзносов.СМИ,
			20.0,	6.0,	2.9,	1.1,	2.0);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС, ТФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20120101',Перечисления.ТарифыСтраховыхВзносов.СМИ,
			20.8,	6.0,	2.9,	3.3);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20130101',Перечисления.ТарифыСтраховыхВзносов.СМИ,
			21.6,	6.0,	2.9,	3.5);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20140101',Перечисления.ТарифыСтраховыхВзносов.СМИ,
			23.2,	6.0,	2.9,	3.9);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20150101',Перечисления.ТарифыСтраховыхВзносов.СМИ,
			26.0,	6.0,	2.9,	5.1);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС

	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20100101',Перечисления.ТарифыСтраховыхВзносов.УпрощенныйНалоговыйРежимПроизводство,
			14.0,	6.0,	0.0,	0.0,	0.0);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС, ТФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20110101',Перечисления.ТарифыСтраховыхВзносов.УпрощенныйНалоговыйРежимПроизводство,
			18.0,	6.0,	2.9,	3.1,	2.0);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС, ТФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20120101',Перечисления.ТарифыСтраховыхВзносов.УпрощенныйНалоговыйРежимПроизводство,
			20.0,	6.0);	// ПФР (всего), ПФРНакопительная
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20140101',Перечисления.ТарифыСтраховыхВзносов.УпрощенныйНалоговыйРежимПроизводство,
			26.0,	6.0,	2.9,	5.1);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС

	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20120101',Перечисления.ТарифыСтраховыхВзносов.ДляЧленовЭкипажейМорскихСудовПодФлагомРФ,
			 );	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС, ТФОМС - не взимаются
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20280101',Перечисления.ТарифыСтраховыхВзносов.ДляЧленовЭкипажейМорскихСудовПодФлагомРФ,
			26.0,	6.0,	2.9,	5.1);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС

	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20120101',Перечисления.ТарифыСтраховыхВзносов.ДляФармацевтовАптек,
			20.0,	6.0);	// ПФР (всего), ПФРНакопительная
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20140101',Перечисления.ТарифыСтраховыхВзносов.ДляФармацевтовАптек,
			26.0,	6.0,	2.9,	5.1);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС

	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20120101',Перечисления.ТарифыСтраховыхВзносов.НекоммерческиеОрганизации,
			20.0,	6.0);	// ПФР (всего), ПФРНакопительная
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20140101',Перечисления.ТарифыСтраховыхВзносов.НекоммерческиеОрганизации,
			26.0,	6.0,	2.9,	5.1);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС

	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20120101',Перечисления.ТарифыСтраховыхВзносов.БлаготворительныеОрганизации,
			20.0,	6.0);	// ПФР (всего), ПФРНакопительная
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20140101',Перечисления.ТарифыСтраховыхВзносов.БлаготворительныеОрганизации,
			26.0,	6.0,	2.9,	5.1);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС

	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20120101',Перечисления.ТарифыСтраховыхВзносов.ОрганизацииОказывающиеИнжиниринговыеУслуги,
			22.0,	6.0,	2.9,	5.1);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20140101',Перечисления.ТарифыСтраховыхВзносов.ОрганизацииОказывающиеИнжиниринговыеУслуги,
			26.0,	6.0,	2.9,	5.1);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС

	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20130101',Перечисления.ТарифыСтраховыхВзносов.ИндивидуальныйПредпринимательПрименяющийПатент,
			20.0,	6.0);	// ПФР (всего), ПФРНакопительная
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20140101',Перечисления.ТарифыСтраховыхВзносов.ИндивидуальныйПредпринимательПрименяющийПатент,
			26.0,	6.0,	2.9,	5.1);	// ПФР (всего), ПФРНакопительная, ФСС, ФФОМС

	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20100101',Перечисления.ТарифыСтраховыхВзносов.УчастникПроектаИнновационныйЦентрСколково,
			14.0,	6.0);	// ПФР (всего), ПФРНакопительная

	Возврат ТаблицаТарифов	

КонецФункции

Функция ТекущиеТарифыСтраховыхВзносовПоВидамТарифа(ДатаАктуальности) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТарифСтраховыхВзносов.Период,
	|	ТарифСтраховыхВзносов.ВидТарифа,
	|	ТарифСтраховыхВзносов.ПФР,
	|	ТарифСтраховыхВзносов.ПФРНакопительная,
	|	ТарифСтраховыхВзносов.ФСС,
	|	ТарифСтраховыхВзносов.ФФОМС
	|ПОМЕСТИТЬ ВТТарифы
	|ИЗ
	|	&ТаблицаТарифов КАК ТарифСтраховыхВзносов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДатыСреза.ВидТарифа,
	|	ТарифСтраховыхВзносов.ПФР,
	|	ТарифСтраховыхВзносов.ПФРНакопительная,
	|	ТарифСтраховыхВзносов.ФСС,
	|	ТарифСтраховыхВзносов.ФФОМС
	|ИЗ
	|	(ВЫБРАТЬ
	|		МАКСИМУМ(ТарифСтраховыхВзносов.Период) КАК Период,
	|		ТарифСтраховыхВзносов.ВидТарифа КАК ВидТарифа
	|	ИЗ
	|		ВТТарифы КАК ТарифСтраховыхВзносов
	|	ГДЕ
	|		ТарифСтраховыхВзносов.Период <= &ДатаАктуальности
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТарифСтраховыхВзносов.ВидТарифа) КАК ДатыСреза
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТарифы КАК ТарифСтраховыхВзносов
	|		ПО ДатыСреза.ВидТарифа = ТарифСтраховыхВзносов.ВидТарифа
	|			И ДатыСреза.Период = ТарифСтраховыхВзносов.Период";
	Запрос.УстановитьПараметр("ТаблицаТарифов", ТаблицаТарифовСтраховыхВзносов());
	Запрос.УстановитьПараметр("ДатаАктуальности", ДатаАктуальности);
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции


// Формирует таблицу с отработанным временем в различных условиях вредности
//	с полями:
//		Сотрудник
//		ВзимаютсяВзносыЗаЗанятыхНаРаботахСДосрочнойПенсией
//		ДоляВремени
//
// Параметры
//  МенеджерВременныхТаблиц - содержит вр.т. ВТСотрудники с полем Сотрудник
//
// Возвращаемое значение:
//   таблица значений
//
Функция ДолиДляРаспределенияВредности(МенеджерВременныхТаблиц, МесяцРасчета) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(МесяцРасчета));
	Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(МесяцРасчета));
	Запрос.УстановитьПараметр("ДатаСледующегоМесяца", КонецМесяца(МесяцРасчета) + 1);
	
	ДатаМесяца = НачалоМесяца(МесяцРасчета);
	ДатыТекст = "ВЫБРАТЬ ДАТАВРЕМЯ(" + Формат(ДатаМесяца,"ДФ=гггг,М,д,Ч,м,с") + ") КАК Период";
	ДатыТекст = ДатыТекст + "
	|ПОМЕСТИТЬ ВТДаты";
	Пока ДатаМесяца < НачалоДня(КонецМесяца(МесяцРасчета)) Цикл
		ДатаМесяца = ДатаМесяца + 86400;
		ДатыТекст = ДатыТекст +"
		|ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ДАТАВРЕМЯ(" + Формат(ДатаМесяца,"ДФ=гггг,М,д,Ч,м,с") + ")";
	КонецЦикла;
	Запрос.Текст = ДатыТекст;
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СотрудникиОрганизаций.Ссылка КАК Сотрудник,
	|	ДатыМесяца.Период КАК Период,
	|	ВЫБОР
	|		КОГДА СотрудникиОрганизаций.ДатаУвольнения = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ЛОЖЬ
	|		КОГДА СотрудникиОрганизаций.ДатаУвольнения < &НачалоМесяца
	|			ТОГДА ИСТИНА
	|		КОГДА ДатыМесяца.Период ЕСТЬ NULL 
	|			ТОГДА ИСТИНА
	|		КОГДА СотрудникиОрганизаций.ДатаУвольнения < ДатыМесяца.Период
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК УжеУволен
	|ПОМЕСТИТЬ ВТДатыСотрудников
	|ИЗ
	|	Справочник.СотрудникиОрганизаций КАК СотрудникиОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДаты КАК ДатыМесяца
	|		ПО (ДатыМесяца.Период <= ВЫБОР
	|				КОГДА СотрудникиОрганизаций.ДатаУвольнения = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|					ТОГДА &КонецМесяца
	|				ИНАЧЕ СотрудникиОрганизаций.ДатаУвольнения
	|			КОНЕЦ)
	|			И (ДатыМесяца.Период >= СотрудникиОрганизаций.ДатаПриемаНаРаботу)
	|ГДЕ
	|	СотрудникиОрганизаций.Ссылка В
	|			(ВЫБРАТЬ
	|				Сотрудники.Сотрудник КАК Сотрудник
	|			ИЗ
	|				ВТСотрудники КАК Сотрудники)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаписиРегистра.Сотрудник КАК Сотрудник,
	|	ЗаписиРегистра.Период,
	|	ЗаписиРегистра.УжеУволен,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА ЗаписиРегистра.УжеУволен
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыРаботСДосрочнойПенсией.ПустаяСсылка)
	|			КОГДА РаботникиОрганизаций.ПериодЗавершения <= ЗаписиРегистра.Период
	|					И РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				ТОГДА РаботникиОрганизаций.ДолжностьЗавершения.ВзимаютсяВзносыЗаЗанятыхНаРаботахСДосрочнойПенсией
	|			ИНАЧЕ РаботникиОрганизаций.Должность.ВзимаютсяВзносыЗаЗанятыхНаРаботахСДосрочнойПенсией
	|		КОНЕЦ, ЗНАЧЕНИЕ(Перечисление.ВидыРаботСДосрочнойПенсией.ПустаяСсылка)) КАК ВзимаютсяВзносыЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА ЗаписиРегистра.УжеУволен
	|				ТОГДА ЗНАЧЕНИЕ(Справочник.ГрафикиРаботы.ПустаяСсылка)
	|			КОГДА РаботникиОрганизаций.ПериодЗавершения <= ЗаписиРегистра.Период
	|					И РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				ТОГДА РаботникиОрганизаций.ГрафикРаботыЗавершения
	|			ИНАЧЕ РаботникиОрганизаций.ГрафикРаботы
	|		КОНЕЦ, ЗНАЧЕНИЕ(Справочник.ГрафикиРаботы.ПустаяСсылка)) КАК ГрафикРаботы
	|ПОМЕСТИТЬ ВТВредностиСотрудников
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДатыСотрудников.Сотрудник КАК Сотрудник,
	|		ДатыСотрудников.Период КАК Период,
	|		МАКСИМУМ(РаботникиОрганизаций.Период) КАК ПериодРегистра,
	|		ДатыСотрудников.УжеУволен КАК УжеУволен
	|	ИЗ
	|		ВТДатыСотрудников КАК ДатыСотрудников
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
	|			ПО ДатыСотрудников.Сотрудник = РаботникиОрганизаций.Сотрудник
	|				И ДатыСотрудников.Период >= РаботникиОрганизаций.Период
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ДатыСотрудников.Сотрудник,
	|		ДатыСотрудников.Период,
	|		ДатыСотрудников.УжеУволен) КАК ЗаписиРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
	|		ПО ЗаписиРегистра.Сотрудник = РаботникиОрганизаций.Сотрудник
	|			И ЗаписиРегистра.ПериодРегистра = РаботникиОрганизаций.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаписиРегистра.Сотрудник,
	|	ЗаписиРегистра.Период,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ЗаписиРегистра.УжеУволен
	|				ТОГДА ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.ПустаяСсылка)
	|			КОГДА ПлановыеНачисленияРаботниковОрганизаций.ПериодЗавершения <= ЗаписиРегистра.Период
	|					И ПлановыеНачисленияРаботниковОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				ТОГДА ПлановыеНачисленияРаботниковОрганизаций.ВидРасчетаЗавершения
	|			ИНАЧЕ ПлановыеНачисленияРаботниковОрганизаций.ВидРасчета
	|		КОНЕЦ КАК ПланВидовРасчета.ОсновныеНачисленияОрганизаций) КАК ВидРасчета
	|ПОМЕСТИТЬ ВТНачисленияСотрудников
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДатыСотрудников.Сотрудник КАК Сотрудник,
	|		ДатыСотрудников.УжеУволен КАК УжеУволен,
	|		ДатыСотрудников.Период КАК Период,
	|		МАКСИМУМ(ПлановыеНачисленияРаботниковОрганизаций.Период) КАК ПериодРегистра
	|	ИЗ
	|		ВТВредностиСотрудников КАК ДатыСотрудников
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций КАК ПлановыеНачисленияРаботниковОрганизаций
	|			ПО ДатыСотрудников.Сотрудник = ПлановыеНачисленияРаботниковОрганизаций.Сотрудник
	|				И ДатыСотрудников.Период >= ПлановыеНачисленияРаботниковОрганизаций.Период
	|				И (ПлановыеНачисленияРаботниковОрганизаций.ВидРасчетаИзмерение.Код ЕСТЬ NULL )
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ДатыСотрудников.Сотрудник,
	|		ДатыСотрудников.УжеУволен,
	|		ДатыСотрудников.Период) КАК ЗаписиРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций КАК ПлановыеНачисленияРаботниковОрганизаций
	|		ПО ЗаписиРегистра.Сотрудник = ПлановыеНачисленияРаботниковОрганизаций.Сотрудник
	|			И ЗаписиРегистра.ПериодРегистра = ПлановыеНачисленияРаботниковОрганизаций.Период
	|			И (ПлановыеНачисленияРаботниковОрганизаций.ВидРасчетаИзмерение.Код ЕСТЬ NULL )
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ПлановыеНачисленияРаботниковОрганизаций.ПериодЗавершения <= ЗаписиРегистра.Период
	|					И ПлановыеНачисленияРаботниковОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				ТОГДА ПлановыеНачисленияРаботниковОрганизаций.ДействиеЗавершения
	|			ИНАЧЕ ПлановыеНачисленияРаботниковОрганизаций.Действие
	|		КОНЕЦ <> ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Прекратить)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаписиРегистра.Сотрудник,
	|	ЗаписиРегистра.Период,
	|	ВЫБОР
	|		КОГДА ЗаписиРегистра.УжеУволен
	|			ТОГДА ЛОЖЬ
	|		КОГДА ПлановыеНачисленияРаботниковОрганизаций.ВидРасчетаИзмерение ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ВОтпускеПоУходу
	|ПОМЕСТИТЬ ВТОтпускаПоУходуСотрудников
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДатыСотрудников.Сотрудник КАК Сотрудник,
	|		ДатыСотрудников.УжеУволен КАК УжеУволен,
	|		ДатыСотрудников.Период КАК Период,
	|		МАКСИМУМ(ПлановыеНачисленияРаботниковОрганизаций.Период) КАК ПериодРегистра
	|	ИЗ
	|		ВТВредностиСотрудников КАК ДатыСотрудников
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций КАК ПлановыеНачисленияРаботниковОрганизаций
	|			ПО ДатыСотрудников.Сотрудник = ПлановыеНачисленияРаботниковОрганизаций.Сотрудник
	|				И ДатыСотрудников.Период >= ПлановыеНачисленияРаботниковОрганизаций.Период
	|				И (ПлановыеНачисленияРаботниковОрганизаций.ВидРасчетаИзмерение = ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.ОтпускПоУходуЗаРебенкомБезОплаты))
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ДатыСотрудников.Сотрудник,
	|		ДатыСотрудников.УжеУволен,
	|		ДатыСотрудников.Период) КАК ЗаписиРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций КАК ПлановыеНачисленияРаботниковОрганизаций
	|		ПО ЗаписиРегистра.Сотрудник = ПлановыеНачисленияРаботниковОрганизаций.Сотрудник
	|			И ЗаписиРегистра.ПериодРегистра = ПлановыеНачисленияРаботниковОрганизаций.Период
	|			И (ПлановыеНачисленияРаботниковОрганизаций.ВидРасчетаИзмерение = ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.ОтпускПоУходуЗаРебенкомБезОплаты))
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ПлановыеНачисленияРаботниковОрганизаций.ПериодЗавершения <= ЗаписиРегистра.Период
	|					И ПлановыеНачисленияРаботниковОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				ТОГДА ПлановыеНачисленияРаботниковОрганизаций.ДействиеЗавершения
	|			ИНАЧЕ ПлановыеНачисленияРаботниковОрганизаций.Действие
	|		КОНЕЦ <> ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Прекратить)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокРаботников.Сотрудник,
	|	СписокРаботников.Период,
	|	ВЫБОР
	|		КОГДА ГрафикиРаботыПоВидамВремени.План
	|			ТОГДА ГрафикиРаботыПоВидамВремени.ДополнительноеЗначение
	|		ИНАЧЕ ГрафикиРаботыПоВидамВремени.ДополнительноеЗначениеПолное
	|	КОНЕЦ КАК Часы,
	|	ВЫБОР
	|		КОГДА ГрафикиРаботыПоВидамВремени.План
	|			ТОГДА ГрафикиРаботыПоВидамВремени.ОсновноеЗначение
	|		ИНАЧЕ ГрафикиРаботыПоВидамВремени.ОсновноеЗначениеПолное
	|	КОНЕЦ КАК Дни,
	|	ГрафикиРаботыПоВидамВремени.План
	|ПОМЕСТИТЬ ВТДанныеИндивидуальныхГрафиковИТабелей
	|ИЗ
	|	ВТВредностиСотрудников КАК СписокРаботников
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикиРаботыПоВидамВремени КАК ГрафикиРаботыПоВидамВремени
	|		ПО СписокРаботников.Сотрудник = ГрафикиРаботыПоВидамВремени.ГрафикРаботы
	|			И СписокРаботников.Период = ГрафикиРаботыПоВидамВремени.Дата
	|			И (ГрафикиРаботыПоВидамВремени.Месяц = &НачалоМесяца)
	|			И (ГрафикиРаботыПоВидамВремени.ВидУчетаВремени = ЗНАЧЕНИЕ(Перечисление.ВидыУчетаВремени.ПоДням))
	|ГДЕ
	|	ГрафикиРаботыПоВидамВремени.ГрафикРаботы ЕСТЬ НЕ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокРаботников.Сотрудник КАК Сотрудник,
	|	СписокРаботников.Период,
	|	ГрафикиРаботыПоВидамВремени.ОсновноеЗначение КАК Дни,
	|	ГрафикиРаботыПоВидамВремени.ДополнительноеЗначение КАК Часы
	|ПОМЕСТИТЬ ВТОбщиеГрафикиРаботы
	|ИЗ
	|	ВТВредностиСотрудников КАК СписокРаботников
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикиРаботыПоВидамВремени КАК ГрафикиРаботыПоВидамВремени
	|		ПО СписокРаботников.ГрафикРаботы = ГрафикиРаботыПоВидамВремени.ГрафикРаботы
	|			И СписокРаботников.Период = ГрафикиРаботыПоВидамВремени.Дата
	|			И (ГрафикиРаботыПоВидамВремени.Месяц = &НачалоМесяца)
	|			И (ГрафикиРаботыПоВидамВремени.ВидУчетаВремени = ЗНАЧЕНИЕ(Перечисление.ВидыУчетаВремени.ПоДням))
	|ГДЕ
	|	ГрафикиРаботыПоВидамВремени.ГрафикРаботы ЕСТЬ НЕ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиДляУточненияВредностей.Сотрудник,
	|	СотрудникиДляУточненияВредностей.Период,
	|	ВЫБОР
	|		КОГДА СотрудникиДляУточненияВредностей.УжеУволен
	|			ТОГДА 0
	|		КОГДА ФактическиОтработано.Сотрудник ЕСТЬ НЕ NULL 
	|			ТОГДА ФактическиОтработано.Часы
	|		КОГДА ИндивидуальныеГрафикиРаботы.Сотрудник ЕСТЬ НЕ NULL 
	|			ТОГДА ИндивидуальныеГрафикиРаботы.Часы
	|		ИНАЧЕ ЕСТЬNULL(ОбщиеГрафикиРаботы.Часы, 0)
	|	КОНЕЦ КАК ОтработаноЧасов,
	|	ВЫБОР
	|		КОГДА СотрудникиДляУточненияВредностей.УжеУволен
	|			ТОГДА 0
	|		КОГДА ФактическиОтработано.Сотрудник ЕСТЬ НЕ NULL 
	|			ТОГДА ФактическиОтработано.Дни
	|		КОГДА ИндивидуальныеГрафикиРаботы.Сотрудник ЕСТЬ НЕ NULL 
	|			ТОГДА ИндивидуальныеГрафикиРаботы.Дни
	|		ИНАЧЕ ЕСТЬNULL(ОбщиеГрафикиРаботы.Дни, 0)
	|	КОНЕЦ КАК ОтработаноДней,
	|	ВЫБОР
	|		КОГДА СотрудникиДляУточненияВредностей.УжеУволен
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыРаботСДосрочнойПенсией.ПустаяСсылка)
	|		КОГДА ЕСТЬNULL(ОтпускаПоУходуСотрудников.ВОтпускеПоУходу, ЛОЖЬ)
	|				И ЕСТЬNULL(НачисленияСотрудников.ВидРасчета, ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.ПустаяСсылка)) = ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыРаботСДосрочнойПенсией.ПустаяСсылка)
	|		ИНАЧЕ СотрудникиДляУточненияВредностей.ВзимаютсяВзносыЗаЗанятыхНаРаботахСДосрочнойПенсией
	|	КОНЕЦ КАК ВзимаютсяВзносыЗаЗанятыхНаРаботахСДосрочнойПенсией
	|ПОМЕСТИТЬ ВТСотрудникСВредностьюИОтработаннымВременем
	|ИЗ
	|	ВТВредностиСотрудников КАК СотрудникиДляУточненияВредностей
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеИндивидуальныхГрафиковИТабелей КАК ФактическиОтработано
	|		ПО СотрудникиДляУточненияВредностей.Сотрудник = ФактическиОтработано.Сотрудник
	|			И СотрудникиДляУточненияВредностей.Период = ФактическиОтработано.Период
	|			И (НЕ ФактическиОтработано.План)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеИндивидуальныхГрафиковИТабелей КАК ИндивидуальныеГрафикиРаботы
	|		ПО СотрудникиДляУточненияВредностей.Сотрудник = ИндивидуальныеГрафикиРаботы.Сотрудник
	|			И СотрудникиДляУточненияВредностей.Период = ИндивидуальныеГрафикиРаботы.Период
	|			И (ИндивидуальныеГрафикиРаботы.План)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОбщиеГрафикиРаботы КАК ОбщиеГрафикиРаботы
	|		ПО СотрудникиДляУточненияВредностей.Сотрудник = ОбщиеГрафикиРаботы.Сотрудник
	|			И СотрудникиДляУточненияВредностей.Период = ОбщиеГрафикиРаботы.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияСотрудников КАК НачисленияСотрудников
	|		ПО СотрудникиДляУточненияВредностей.Сотрудник = НачисленияСотрудников.Сотрудник
	|			И СотрудникиДляУточненияВредностей.Период = НачисленияСотрудников.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОтпускаПоУходуСотрудников КАК ОтпускаПоУходуСотрудников
	|		ПО СотрудникиДляУточненияВредностей.Сотрудник = ОтпускаПоУходуСотрудников.Сотрудник
	|			И СотрудникиДляУточненияВредностей.Период = ОтпускаПоУходуСотрудников.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТДанныеИндивидуальныхГрафиковИТабелей
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТОбщиеГрафикиРаботы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТОтпускаПоУходуСотрудников
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТДаты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТДатыСотрудников
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВредностиСотрудников.Сотрудник КАК Сотрудник,
	|	ВредностиСотрудников.Период КАК Период,
	|	ЕСТЬNULL(ВредностиСотрудников.ГрафикРаботы.СуммированныйУчетРабочегоВремени, ЛОЖЬ) КАК СуммированныйУчетРабочегоВремени,
	|	НачисленияСотрудников.ВидРасчета.СпособРасчета КАК СпособРасчета,
	|	НачисленияСотрудников.ВидРасчета.ОбозначениеВТабелеУчетаРабочегоВремени КАК ОбозначениеВТабелеУчетаРабочегоВремени,
	|	НачисленияСотрудников.ВидРасчета.ВидВремени КАК ВидВремени,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА НачисленияСотрудников.ВидРасчета.СпособРасчета <> ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПроизвольнаяФормула)
	|				ТОГДА ЛОЖЬ
	|			КОГДА ОсновныеНачисленияОрганизацийПоказатели.Ссылка В (ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.ТарифнаяСтавкаЧасовая), ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.ОтработаноВремениВЧасах), ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.НормаВремениВЧасах), ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.ВремяВЧасах))
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьЧасовойПоказатель
	|ИЗ
	|	ВТВредностиСотрудников КАК ВредностиСотрудников
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияСотрудников КАК НачисленияСотрудников
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.ОсновныеНачисленияОрганизаций.Показатели КАК ОсновныеНачисленияОрганизацийПоказатели
	|			ПО НачисленияСотрудников.ВидРасчета = ОсновныеНачисленияОрганизацийПоказатели.Ссылка
	|		ПО ВредностиСотрудников.Сотрудник = НачисленияСотрудников.Сотрудник
	|			И ВредностиСотрудников.Период = НачисленияСотрудников.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ВредностиСотрудников.Сотрудник,
	|	ВредностиСотрудников.Период,
	|	ВредностиСотрудников.ГрафикРаботы.СуммированныйУчетРабочегоВремени,
	|	НачисленияСотрудников.ВидРасчета.СпособРасчета,
	|	ЕСТЬNULL(ВредностиСотрудников.ГрафикРаботы.СуммированныйУчетРабочегоВремени, ЛОЖЬ),
	|	НачисленияСотрудников.ВидРасчета.ВидВремени,
	|	НачисленияСотрудников.ВидРасчета.ОбозначениеВТабелеУчетаРабочегоВремени
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудник,
	|	Период";
	
	ТаблицаКОбработке = Запрос.Выполнить().Выгрузить();
	ТаблицаКОбработке.Колонки.Добавить("ВидУчетаВремени", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыУчетаВремени"));
	Для каждого СтрокаТЗ Из ТаблицаКОбработке Цикл
	    Если СтрокаТЗ.ЕстьЧасовойПоказатель Тогда
			СтрокаТЗ.ВидУчетаВремени = Перечисления.ВидыУчетаВремени.ПоЧасам
		Иначе
			СтрокаТЗ.ВидУчетаВремени = ПроведениеРасчетов.ПолучитьВидУчетаВремени(СтрокаТЗ.СпособРасчета, СтрокаТЗ.ВидВремени, СтрокаТЗ.СуммированныйУчетРабочегоВремени, СтрокаТЗ.ОбозначениеВТабелеУчетаРабочегоВремени);	
		КонецЕсли;
	КонецЦикла;
	
	Запрос.УстановитьПараметр("УчетВремениСотрудников", ТаблицаКОбработке);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УчетВремениСотрудников.Сотрудник,
	|	УчетВремениСотрудников.Период,
	|	УчетВремениСотрудников.ВидУчетаВремени
	|ПОМЕСТИТЬ ВТУчетВремениСотрудниковПоДням
	|ИЗ
	|	&УчетВремениСотрудников КАК УчетВремениСотрудников
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УчетВремениСотрудниковПоДням.Сотрудник,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА УчетВремениСотрудниковПоДням.ВидУчетаВремени = ЗНАЧЕНИЕ(Перечисление.ВидыУчетаВремени.ПоЧасам)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК РассчитыватьДолюПоЧасам
	|ПОМЕСТИТЬ ВТУчетВремениСотрудников
	|ИЗ
	|	ВТУчетВремениСотрудниковПоДням КАК УчетВремениСотрудниковПоДням
	|
	|СГРУППИРОВАТЬ ПО
	|	УчетВремениСотрудниковПоДням.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникСВредностьюИОтработаннымВременем.Сотрудник КАК Сотрудник,
	|	СотрудникСВредностьюИОтработаннымВременем.ВзимаютсяВзносыЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	СУММА(ВЫБОР
	|			КОГДА УчетВремениСотрудников.РассчитыватьДолюПоЧасам
	|				ТОГДА СотрудникСВредностьюИОтработаннымВременем.ОтработаноЧасов
	|			ИНАЧЕ СотрудникСВредностьюИОтработаннымВременем.ОтработаноДней
	|		КОНЕЦ) КАК ДоляВремени
	|ИЗ
	|	ВТСотрудникСВредностьюИОтработаннымВременем КАК СотрудникСВредностьюИОтработаннымВременем
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУчетВремениСотрудников КАК УчетВремениСотрудников
	|		ПО СотрудникСВредностьюИОтработаннымВременем.Сотрудник = УчетВремениСотрудников.Сотрудник
	|
	|СГРУППИРОВАТЬ ПО
	|	СотрудникСВредностьюИОтработаннымВременем.Сотрудник,
	|	СотрудникСВредностьюИОтработаннымВременем.ВзимаютсяВзносыЗаЗанятыхНаРаботахСДосрочнойПенсией
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТВредностиСотрудников
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТНачисленияСотрудников
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТУчетВремениСотрудниковПоДням
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТСотрудникСВредностьюИОтработаннымВременем
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТУчетВремениСотрудников";
    Результаты = Запрос.ВыполнитьПакет();
	
	ТаблицаДолейДляРаспределенияВредности = Результаты[2].Выгрузить();
	ТаблицаДолейДляРаспределенияВредности.Индексы.Добавить("Сотрудник");
	
	Возврат ТаблицаДолейДляРаспределенияВредности
	
КонецФункции

// в переданной таблице распределяет, если потребуется, суммы дохода по разным условиям вредности
Процедура УточнитьДанныеОРаботахСДосрочнойПенсией(ТаблицаДанных, ТаблицаДолейДляРаспределенияВредности, ПроверятьПериодДействия, МесяцРасчета = Неопределено, ОписаниеНачислений = Неопределено, ИмяПоляРезультата = "Результат") Экспорт
	
	СтруктураОтбора = Новый Структура("Сотрудник");
	Если ТипЗнч(ТаблицаДанных) = Тип("ТаблицаЗначений") Тогда
		НоваяТаблица = ТаблицаДанных.СкопироватьКолонки();
	Иначе
		НоваяТаблица = ТаблицаДанных.ВыгрузитьКолонки();
	КонецЕсли;
	
	Для каждого СтрокаТЗ Из ТаблицаДанных Цикл
		Сумма = СтрокаТЗ[ИмяПоляРезультата];
		Если Сумма = 0 Тогда
			Продолжить;
		КонецЕсли;
		Если ПроверятьПериодДействия Тогда
			ОписаниеНачисления = ОписаниеНачислений[СтрокаТЗ.ВидРасчета];
			Если Не ОписаниеНачисления.РаспределяетсяВоВсехСлучаях Тогда // учет некоторых начислений уточняем всегда - они начислялись без учета кадровых изменений
				Если НачалоМесяца(СтрокаТЗ.ПериодДействияНачало) = МесяцРасчета Тогда // учет начислений текущего месяца не меняем
					Продолжить;	
				ИначеЕсли ОписаниеНачисления.ВидСтажаСЗВ4 = Перечисления.ВидыСтажаСЗВ4.ВключаетсяВСтажДляДосрочногоНазначенияПенсии Тогда // из начислений за другие месяцы не трогаем соответствующие периодам вредного стажа
					Продолжить;	
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		СтруктураОтбора.Сотрудник = СтрокаТЗ.Сотрудник;
		СтрокиРаспределения = ТаблицаДолейДляРаспределенияВредности.НайтиСтроки(СтруктураОтбора);
		ВсегоСтрок = СтрокиРаспределения.Количество();
		Если ВсегоСтрок = 1 Тогда
			СтрокаТЗ.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией = СтрокиРаспределения[0].ВзимаютсяВзносыЗаЗанятыхНаРаботахСДосрочнойПенсией
		ИначеЕсли ВсегоСтрок > 1 Тогда
			
			Коэффициенты = Новый Массив;
			Для каждого СтрокаРаспределения Из СтрокиРаспределения Цикл
				Коэффициенты.Добавить(СтрокаРаспределения.ДоляВремени);
			КонецЦикла; 
			РаспределениеРесурса = ОбщегоНазначенияЗК.РаспределитьПропорционально(Сумма,Коэффициенты);
			Если РаспределениеРесурса <> Неопределено Тогда
				Индекс = 0;
				Для каждого СтрокаРаспределения Из СтрокиРаспределения Цикл
					Если Индекс = 0 Тогда
						СтрокаКОбработке = СтрокаТЗ;
					Иначе 
						СтрокаКОбработке = НоваяТаблица.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаКОбработке, СтрокаТЗ);
					КонецЕсли;
					СтрокаКОбработке[ИмяПоляРезультата] = РаспределениеРесурса[Индекс];
					СтрокаКОбработке.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией = СтрокаРаспределения.ВзимаютсяВзносыЗаЗанятыхНаРаботахСДосрочнойПенсией;
					Индекс = Индекс + 1;
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	ОбщегоНазначенияЗК.ЗагрузитьВТаблицуЗначений(НоваяТаблица, ТаблицаДанных);
	
КонецПроцедуры

