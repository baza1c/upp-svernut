
Процедура абс_ПроверитьКонтрагентаНаСтопЛистОбработкаПроведения(Источник, Отказ, РежимПроведения) Экспорт
	
	НеОстанавливатьсяПриПроведении = Ложь;
	
	Если Не Источник.Метаданные().Реквизиты.Найти("абс_Статус") = Неопределено Тогда
		
		НеОстанавливатьсяПриПроведении = (Источник.абс_Статус = Перечисления.абс_СтатусыОтгрузки.Создан ИЛИ Источник.абс_Статус = Перечисления.абс_СтатусыОтгрузки.НеСогласован);
		
	КонецЕсли;
	
	Если Не Источник.Метаданные().Реквизиты.Найти("Контрагент") = Неопределено Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	абс_СтопЛистПоКонтрагентамСрезПоследних.СтатусЗадолженности
		|ИЗ
		|	РегистрСведений.абс_СтопЛистПоКонтрагентам.СрезПоследних(, Контрагент = &Контрагент) КАК абс_СтопЛистПоКонтрагентамСрезПоследних";
		Запрос.УстановитьПараметр("Контрагент", Источник.Контрагент);
		
		ВыборкаДетальныеЗаписи = Запрос.Выполнить().Выбрать(); 
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.СтатусЗадолженности) И НЕ ВыборкаДетальныеЗаписи.СтатусЗадолженности = Перечисления.абс_СтатусыЗадолженности.Разблокирован Тогда
				
				Сообщить("Отгрузка запрещена, т.к. контрагент """ + Источник.Контрагент + """ находится в стоп-листе!");
				
				ВыполнитьРасслкуПоБлокировкеЗаказаПокупателя(Источник);
				
				Если НЕ НеОстанавливатьсяПриПроведении Тогда
					Отказ = Истина;
				КонецЕсли;
				
			КонецЕСли;	
		КонецЦикла;
	КонецЕСли;	
КонецПроцедуры

Процедура ВыполнитьРасслкуПоБлокировкеЗаказаПокупателя(Документ)
	
	
	
КонецПроцедуры

Функция КонтрагентНаходитсяВСтопЛисте(Контрагент) Экспорт
	
	СтатусВозврата = Ложь;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	абс_СтопЛистПоКонтрагентамСрезПоследних.СтатусЗадолженности
	|ИЗ
	|	РегистрСведений.абс_СтопЛистПоКонтрагентам.СрезПоследних(, Контрагент = &Контрагент) КАК абс_СтопЛистПоКонтрагентамСрезПоследних";
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	ВыборкаДетальныеЗаписи = Запрос.Выполнить().Выбрать(); 
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.СтатусЗадолженности) И НЕ ВыборкаДетальныеЗаписи.СтатусЗадолженности = Перечисления.абс_СтатусыЗадолженности.Разблокирован Тогда
			СтатусВозврата = Истина;
		КонецЕсли;
	КонецЕсли;
	Возврат СтатусВозврата;
	
КонецФункции

Функция ПолучитьТаблицуСтопЛиста() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.Контрагент,
	|	СУММА(ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.СуммаВзаиморасчетовОстаток) КАК СуммаОбщейЗадолженности,
	|	СУММА(ВЫБОР
	|			КОГДА ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДокументРасчетовСКонтрагентом ССЫЛКА Документ.РеализацияТоваровУслуг
	|				ТОГДА ВЫБОР
	|						КОГДА РАЗНОСТЬДАТ(ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДокументРасчетовСКонтрагентом.абс_ДатаОплаты, &ДатаОтчета, ДЕНЬ) > 0
	|							ТОГДА ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.СуммаВзаиморасчетовОстаток
	|						ИНАЧЕ 0
	|					КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаПросроченнойЗадолженности,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДокументРасчетовСКонтрагентом ССЫЛКА Документ.РеализацияТоваровУслуг
	|				ТОГДА ВЫБОР
	|						КОГДА РАЗНОСТЬДАТ(ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДокументРасчетовСКонтрагентом.абс_ДатаОплаты, &ДатаОтчета, ДЕНЬ) > 0
	|							ТОГДА 
	// {Лео Катанович  // не учитываем количество дней при кредиторской задолженности
	|                                ВЫБОР
	|									КОГДА ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.СуммаВзаиморасчетовОстаток > 0
	|										ТОГДА РАЗНОСТЬДАТ(ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДокументРасчетовСКонтрагентом.абс_ДатаОплаты, &ДатаОтчета, ДЕНЬ)
	|									ИНАЧЕ 0
	|								КОНЕЦ
	// Лео Катанович}
	|						ИНАЧЕ 0
	|					КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоДнейПросроченнойЗадолженности,
	|	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДоговорКонтрагента,
	|	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДокументРасчетовСКонтрагентом
	|ПОМЕСТИТЬ втДанныеСДоговорами
	|ИЗ
	|	РегистрНакопления.ВзаиморасчетыСКонтрагентамиПоДокументамРасчетов.Остатки(
	|			КОНЕЦПЕРИОДА(&ДатаОтчета, ДЕНЬ),
	|			НЕ Контрагент.Регион = ЗНАЧЕНИЕ(Справочник.Регионы.ПустаяСсылка)
	|				И ДоговорКонтрагента.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СПокупателем)) КАК ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки
	|ГДЕ
	|	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.СуммаВзаиморасчетовОстаток <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.Контрагент,
	|	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДоговорКонтрагента,
	|	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДокументРасчетовСКонтрагентом
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Контрагенты.Ссылка КАК Контрагент,
	|	ЗначенияСвойствОбъектов.Значение КАК МораторнаяЗадолженность
	|ПОМЕСТИТЬ втМораторнаяЗадолженность
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|		ПО Контрагенты.Ссылка = ЗначенияСвойствОбъектов.Объект
	|			И (ЗначенияСвойствОбъектов.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СвойстваОбъектов.абс_МораторнаяЗадолженность))
	|			И (НЕ ЗначенияСвойствОбъектов.Значение = ЗНАЧЕНИЕ(Справочник.ЗначенияСвойствОбъектов.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(втДанныеСДоговорами.Контрагент, втМораторнаяЗадолженность.Контрагент) КАК Контрагент,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(втДанныеСДоговорами.ДоговорКонтрагента.КонтролироватьСуммуЗадолженности, ЛОЖЬ)
	|				ТОГДА втДанныеСДоговорами.СуммаОбщейЗадолженности - втДанныеСДоговорами.ДоговорКонтрагента.ДопустимаяСуммаЗадолженности
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаКонтролируемойЗадолженности,
	|	СУММА(ВЫБОР
	|			КОГДА втДанныеСДоговорами.ДоговорКонтрагента.КонтролироватьЧислоДнейЗадолженности
	|				ТОГДА ВЫБОР
	|						КОГДА ЕСТЬNULL(втДанныеСДоговорами.ДоговорКонтрагента.абс_ОграничениеКонтроляПросроченнойЗадолженности, ЛОЖЬ)
	|							ТОГДА втДанныеСДоговорами.СуммаПросроченнойЗадолженности - втДанныеСДоговорами.ДоговорКонтрагента.абс_ДопустимаяСуммаПросроченнойЗадолженности
	|						ИНАЧЕ ЕСТЬNULL(втДанныеСДоговорами.СуммаПросроченнойЗадолженности, 0)
	|					КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаКонтролируемойПросроченнойЗадолженности,
	|	МАКСИМУМ(ЕСТЬNULL(втДанныеСДоговорами.КоличествоДнейПросроченнойЗадолженности, 0)) КАК КоличествоДнейПросроченнойЗадолженности,
	|	МАКСИМУМ(ЕСТЬNULL(втДанныеСДоговорами.ДоговорКонтрагента.ДопустимаяСуммаЗадолженности, 0)) КАК ДопустимаяСуммаЗадолженности,
	|	МАКСИМУМ(ЕСТЬNULL(втДанныеСДоговорами.ДоговорКонтрагента.ДопустимоеЧислоДнейЗадолженности, 0)) КАК ДопустимоеЧислоДнейЗадолженности,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЕСТЬNULL(втДанныеСДоговорами.ДоговорКонтрагента.абс_ОграничениеКонтроляПросроченнойЗадолженности, ЛОЖЬ)
	|				ТОГДА втДанныеСДоговорами.ДоговорКонтрагента.абс_ДопустимаяСуммаПросроченнойЗадолженности
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ДопустимаяСуммаПросроченнойЗадолженности,
	|	СУММА(ЕСТЬNULL(втДанныеСДоговорами.СуммаОбщейЗадолженности, 0)) КАК СуммаОбщейЗадолженности,
	|	СУММА(ЕСТЬNULL(втДанныеСДоговорами.СуммаПросроченнойЗадолженности, 0)) КАК СуммаПросроченнойЗадолженности,
	|	втМораторнаяЗадолженность.МораторнаяЗадолженность
	|ПОМЕСТИТЬ втПросроченнаяЗадолженность
	|ИЗ
	|	втДанныеСДоговорами КАК втДанныеСДоговорами
	|		ПОЛНОЕ СОЕДИНЕНИЕ втМораторнаяЗадолженность КАК втМораторнаяЗадолженность
	|		ПО втДанныеСДоговорами.Контрагент = втМораторнаяЗадолженность.Контрагент
	|
	|СГРУППИРОВАТЬ ПО
	|	втМораторнаяЗадолженность.МораторнаяЗадолженность,
	|	ЕСТЬNULL(втДанныеСДоговорами.Контрагент, втМораторнаяЗадолженность.Контрагент)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫБОР
	|		КОГДА втПросроченнаяЗадолженность.Контрагент ЕСТЬ NULL 
	|			ТОГДА абс_СтопЛистПоКонтрагентамСрезПоследних.Контрагент
	|		ИНАЧЕ втПросроченнаяЗадолженность.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	втПросроченнаяЗадолженность.СуммаОбщейЗадолженности,
	|	втПросроченнаяЗадолженность.СуммаПросроченнойЗадолженности,
	|	втПросроченнаяЗадолженность.КоличествоДнейПросроченнойЗадолженности,
	|	ЕСТЬNULL(абс_СтопЛистПоКонтрагентамСрезПоследних.СтатусЗадолженности, ЗНАЧЕНИЕ(Перечисление.абс_СтатусыЗадолженности.ПустаяСсылка)) КАК ТекущийРазделСтопЛиста,
	|	втПросроченнаяЗадолженность.ДопустимаяСуммаЗадолженности КАК ДопустимаяСуммаЗадолженности,
	|	втПросроченнаяЗадолженность.ДопустимоеЧислоДнейЗадолженности КАК ДопустимоеЧислоДнейЗадолженности,
	|	ВЫБОР
	|		КОГДА НЕ втПросроченнаяЗадолженность.МораторнаяЗадолженность ЕСТЬ NULL 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.абс_СтатусыЗадолженности.МораторнаяЗадолженность)
	|		КОГДА втПросроченнаяЗадолженность.СуммаОбщейЗадолженности ЕСТЬ NULL 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.абс_СтатусыЗадолженности.Разблокирован)
	|		КОГДА втПросроченнаяЗадолженность.СуммаКонтролируемойЗадолженности > 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.абс_СтатусыЗадолженности.ПревышениеКредитногоЛимита)
	|		КОГДА втПросроченнаяЗадолженность.ДопустимаяСуммаЗадолженности <> 0
	|				И втПросроченнаяЗадолженность.СуммаОбщейЗадолженности > втПросроченнаяЗадолженность.ДопустимаяСуммаЗадолженности
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.абс_СтатусыЗадолженности.ПревышениеКредитногоЛимита)
	|		КОГДА втПросроченнаяЗадолженность.СуммаКонтролируемойПросроченнойЗадолженности > 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.абс_СтатусыЗадолженности.ПревышениеЧислаДнейЗадолженности)
	|		КОГДА втПросроченнаяЗадолженность.ДопустимаяСуммаПросроченнойЗадолженности > 0
	|				И втПросроченнаяЗадолженность.СуммаПросроченнойЗадолженности - втПросроченнаяЗадолженность.ДопустимаяСуммаПросроченнойЗадолженности > 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.абс_СтатусыЗадолженности.ПревышениеЛимитаПДЗПоСумме)
	|		ИНАЧЕ ВЫБОР
	|				КОГДА абс_СтопЛистПоКонтрагентамСрезПоследних.СтатусЗадолженности ЕСТЬ NULL 
	|					ТОГДА ЗНАЧЕНИЕ(Перечисление.абс_СтатусыЗадолженности.ПустаяСсылка)
	|				ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.абс_СтатусыЗадолженности.Разблокирован)
	|			КОНЕЦ
	|	КОНЕЦ КАК РазделСтопЛиста,
	|	втПросроченнаяЗадолженность.ДопустимаяСуммаПросроченнойЗадолженности,
	|	абс_СтопЛистПоКонтрагентамСрезПоследних.Период КАК ДатаАктуальности
	|ПОМЕСТИТЬ втДанныеСтопЛиста
	|ИЗ
	|	втПросроченнаяЗадолженность КАК втПросроченнаяЗадолженность
	|		ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.абс_СтопЛистПоКонтрагентам.СрезПоследних КАК абс_СтопЛистПоКонтрагентамСрезПоследних
	|		ПО втПросроченнаяЗадолженность.Контрагент = абс_СтопЛистПоКонтрагентамСрезПоследних.Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанныеСтопЛиста.Контрагент,
	|	втДанныеСтопЛиста.СуммаОбщейЗадолженности,
	|	втДанныеСтопЛиста.СуммаПросроченнойЗадолженности,
	|	втДанныеСтопЛиста.КоличествоДнейПросроченнойЗадолженности,
	|	втДанныеСтопЛиста.ТекущийРазделСтопЛиста,
	|	втДанныеСтопЛиста.ДопустимаяСуммаЗадолженности,
	|	втДанныеСтопЛиста.ДопустимоеЧислоДнейЗадолженности,
	|	втДанныеСтопЛиста.РазделСтопЛиста,
	|	ИСТИНА КАК Пометка,
	|	ВЫБОР
	|		КОГДА втДанныеСтопЛиста.СуммаОбщейЗадолженности = 0
	|			ТОГДА 0
	|		ИНАЧЕ втДанныеСтопЛиста.СуммаПросроченнойЗадолженности / втДанныеСтопЛиста.СуммаОбщейЗадолженности * 100
	|	КОНЕЦ КАК ПроцентПДЗ,
	|	втДанныеСтопЛиста.ДопустимаяСуммаПросроченнойЗадолженности,
	|	втДанныеСтопЛиста.ДатаАктуальности
	|ИЗ
	|	втДанныеСтопЛиста КАК втДанныеСтопЛиста";
	Запрос.УстановитьПараметр("ДатаОтчета", КонецДня(ТекущаяДата()));	
	Результат = Запрос.Выполнить(); 
	
	Возврат Результат.Выгрузить();
	
КонецФункции

Процедура УстановитьСтатусыСтопЛиста(ТаблицаСтопЛиста) Экспорт
	
	ТЗ_НужныйСтроки = ТаблицаСтопЛиста.НайтиСтроки(Новый Структура("Пометка", Истина));
	ДЛя Каждого Строка Из ТЗ_НужныйСтроки Цикл
		
		ЗаписьРС = РегистрыСведений.абс_СтопЛистПоКонтрагентам.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(ЗаписьРС, Строка);
		ЗаписьРС.Период 				= ТекущаяДата();
		ЗаписьРС.Контрагент 			= Строка.Контрагент;
		ЗаписьРС.СтатусЗадолженности 	= Строка.РазделСтопЛиста;
		
		ЗаписьРС.Записать();
		
		МассивЗаказов = ПолучитьЗаказыКонтрагентаДоСтатусаКОтгрузке(Строка.Контрагент);
		
		Для Каждого ЗаказПокупателя из МассивЗаказов Цикл
			
			//выполним рассылку по разблокировке заказов
			СтруктураПараметров = Новый Структура;
			СтруктураПараметров.Вставить("Контрагент", Строка.Контрагент);
			СтруктураПараметров.Вставить("СуммаОбщейЗадолженности", Строка.СуммаОбщейЗадолженности);
			СтруктураПараметров.Вставить("СуммаПросроченнойЗадолженности", Строка.СуммаПросроченнойЗадолженности);
			СтруктураПараметров.Вставить("КоличествоДнейПросроченнойЗадолженности", Строка.КоличествоДнейПросроченнойЗадолженности);
			СтруктураПараметров.Вставить("ТекущийРазделСтопЛиста", Строка.ТекущийРазделСтопЛиста);
			СтруктураПараметров.Вставить("РазделСтопЛиста", Строка.РазделСтопЛиста);
			СтруктураПараметров.Вставить("ПроцентПДЗ", Строка.ПроцентПДЗ);
			СтруктураПараметров.Вставить("ДопустимаяСуммаЗадолженности", Строка.ДопустимаяСуммаЗадолженности);
			СтруктураПараметров.Вставить("ЗаказПокупателя", ЗаказПокупателя);
			
			абс_ОбменДаннымиСервер.ВыполнитьРассылкуБлокировкиЗаказа(СтруктураПараметров);
		КонецЦикла;
		
		Строка.ТекущийРазделСтопЛиста = Строка.РазделСтопЛиста;
		Строка.Пометка = Ложь;
		
	КонецЦикла;	
	
КонецПроцедуры

Процедура ВыполнитьРассылкуСтопЛиста(СтруктураПараметров)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	абс_НастройкиОповещения.ТипОповещения,
	|	абс_НастройкиОповещения.Действует,
	|	абс_НастройкиОповещения.АдресЭлектроннойПочты
	|ИЗ
	|	РегистрСведений.абс_НастройкиОповещения КАК абс_НастройкиОповещения
	|ГДЕ
	|	абс_НастройкиОповещения.Действует
	|	И абс_НастройкиОповещения.ТипОповещения = ЗНАЧЕНИЕ(Перечисление.абс_ТипыОповещений.РассылкаПоСтоплисту)
	|	И НЕ (ВЫРАЗИТЬ(абс_НастройкиОповещения.АдресЭлектроннойПочты КАК СТРОКА(100))) = """"";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	МассивАдресов = РазобратьСтрокуВМассивПоРазделителю(Выборка.АдресЭлектроннойПочты, ",");
	
	//получим эл.адрес менеджера региона контрагента
	МенеджерРегиона = СтруктураПараметров.Контрагент.Регион.абс_Менеджер;
	Если ЗначениеЗаполнено(МенеджерРегиона) Тогда
		АдресМенеджераРегиона = ПолучитьКонтактнуюИнформацию(МенеджерРегиона, Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты, Справочники.ВидыКонтактнойИнформации.СлужебныйАдресЭлектроннойПочтыПользователя);
		Если Не АдресМенеджераРегиона = "" Тогда
			Если МассивАдресов.Найти(АдресМенеджераРегиона) = Неопределено Тогда
				МассивАдресов.Добавить(АдресМенеджераРегиона);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	//получим эл.адрес ответственного менеджера контрагента
	МенеджерКонтрагента = СтруктураПараметров.Контрагент.ОсновнойМенеджерПокупателя;
	Если ЗначениеЗаполнено(МенеджерКонтрагента) Тогда
		АдресМенеджераКонтрагента = ПолучитьКонтактнуюИнформацию(МенеджерКонтрагента, Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты, Справочники.ВидыКонтактнойИнформации.СлужебныйАдресЭлектроннойПочтыПользователя);
		Если Не АдресМенеджераКонтрагента = "" Тогда
			Если МассивАдресов.Найти(АдресМенеджераКонтрагента) = Неопределено Тогда
				МассивАдресов.Добавить(АдресМенеджераКонтрагента);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ТекстПисьма = СформироватьТекстПисьмаПоСтопЛисту(СтруктураПараметров);
	
	ТемаПисьма = СформироватьТемуПисьмаПоСтопЛисту(СтруктураПараметров);
	
	//отправка
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("УчетнаяЗапись", Справочники.УчетныеЗаписиЭлектроннойПочты.УведомленияДистрибуции);
	СтруктураПараметров.Вставить("Тема", ТемаПисьма);
	СтруктураПараметров.Вставить("Тело", ТекстПисьма);
	СписокКому = Новый СписокЗначений;
	СписокКому.ЗагрузитьЗначения(МассивАдресов);
	СтруктураПараметров.Вставить("Кому",СписокКому);
	
	СтруктураПисьма = УправлениеЭлектроннойПочтой.НаписатьПисьмо(глЗначениеПеременной("глТекущийПользователь"),СтруктураПараметров,,,,,, Истина, Ложь);
	
	ЭлектронныеПисьма = Новый Соответствие;
	
	ЭлектронныеПисьма.Вставить(СтруктураПисьма.ПисьмоСсылка);
	
	ИзмененныйСоставПисем = Новый Соответствие;	
	ТекущийПользователь= глЗначениеПеременной("глТекущийПользователь");	
	ДоступныеУчетныеЗаписи = УправлениеЭлектроннойПочтой.ПолучитьДоступныеУчетныеЗаписи(ТекущийПользователь);
	Для каждого Письмо Из ЭлектронныеПисьма Цикл
		Если ТипЗнч(Письмо.Значение) <> Тип("ДокументОбъект.ЭлектронноеПисьмо") Тогда
			ОбъектПисьмо = Письмо.Ключ.ПолучитьОбъект();
			
			ОбъектПисьмо.СтатусПисьма = Перечисления.СтатусыПисем.Исходящее;
			Если НЕ ЗначениеЗаполнено(ОбъектПисьмо.Ответственный) Тогда
				ОбъектПисьмо.Ответственный = ТекущийПользователь;
			КонецЕсли; 
			
			Попытка
				ОбъектПисьмо.Записать();
			Исключение
				Продолжить;;
			КонецПопытки;
		Иначе
			Письмо.Значение.СтатусПисьма = Перечисления.СтатусыПисем.Исходящее;
			Если НЕ ЗначениеЗаполнено(Письмо.Значение.Ответственный) Тогда
				Письмо.Значение.Ответственный = ТекущийПользователь;
			КонецЕсли; 
		КонецЕсли; 
		ИзмененныйСоставПисем.Вставить(Письмо.Ключ, Письмо.Значение);  		
	КонецЦикла;
	
	УправлениеЭлектроннойПочтой.ПолучениеОтправкаПисем(глЗначениеПеременной("глСоответствиеТекстовЭлектронныхПисем"), ПараметрыСеанса.ТекущийПользователь,,
	ИзмененныйСоставПисем, Истина , , Ложь, );
	
	
КонецПроцедуры

Функция ПолучитьКонтактнуюИнформацию(ОбъектКИ, ТипКИ, ВидКИ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Объект", ОбъектКИ);
	Запрос.УстановитьПараметр("Тип"   , ТипКИ);
	Запрос.УстановитьПараметр("Вид"   , ВидКИ);
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	КонтактнаяИнформация.Представление
	|ИЗ
	|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Объект = &Объект
	|	И
	|	КонтактнаяИнформация.Тип = &Тип
	|	И
	|	КонтактнаяИнформация.Вид = &Вид
	|";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат "";
	Иначе
		Возврат РезультатЗапроса.Выгрузить()[0].Представление;
	КонецЕсли;
	
КонецФункции

Функция СформироватьТекстПисьмаПоСтопЛисту(СтруктураПараметров) Экспорт
	
	ТекстПисьма = "Добрый день! <BR> <BR>";
	
	Если СтруктураПараметров.РазделСтопЛиста = Перечисления.абс_СтатусыЗадолженности.ПревышениеЧислаДнейЗадолженности 
		ИЛИ СтруктураПараметров.РазделСтопЛиста = Перечисления.абс_СтатусыЗадолженности.ПревышениеЛимитаПДЗПоСумме  Тогда
		
		ТекстПисьма = ТекстПисьма + "Информируем, что у Контрагента " + СтруктураПараметров.Контрагент 
		+ " имеется просроченная дебиторская задолженность в размере " + Формат(СтруктураПараметров.СуммаПросроченнойЗадолженности, "ЧДЦ=2") 
		+ " рублей (" + СтруктураПараметров.ПроцентПДЗ+ "%)  со сроком " + СтруктураПараметров.КоличествоДнейПросроченнойЗадолженности + " календарных дней. <BR>" 
		+ "Общая дебиторская задолженность " + Формат(СтруктураПараметров.СуммаОбщейЗадолженности, "ЧДЦ=2")  + " рублей. <BR>";
		
		МассивЗаказов = ПолучитьЗаказыКонтрагентаДоСтатусаКОтгрузке(СтруктураПараметров.Контрагент);	
		Если МассивЗаказов.Количество()>0 Тогда
			ТекстПисьма = ТекстПисьма + "В связи с наличием просроченной дебиторской задолженности заблокирована Реализация по следующим Заказам клиента: <BR>";
			Для Каждого ЗаказПокупателя из МассивЗаказов Цикл
				ТекстПисьма = ТекстПисьма + ЗаказПокупателя + " на сумму " + Формат(ЗаказПокупателя.СуммаДокумента, "ЧДЦ=2")  + " рублей. <BR>";
			КонецЦикла;
		КонецЕсли;
		
		ТекстПисьма = ТекстПисьма + "Просим погасить просроченную дебиторскую задолженность.";
		
	ИначеЕсли  СтруктураПараметров.РазделСтопЛиста = Перечисления.абс_СтатусыЗадолженности.ПревышениеКредитногоЛимита  Тогда
		
		ТекстПисьма = ТекстПисьма + "Информируем, что по Контрагенту " + СтруктураПараметров.Контрагент + " превышен лимит общей дебиторской задолженности на "
		+ Формат(СтруктураПараметров.СуммаОбщейЗадолженности-СтруктураПараметров.ДопустимаяСуммаЗадолженности, "ЧДЦ=2")	+ " рублей. <BR>" 
		+ "Общая дебиторская задолженность " + Формат(СтруктураПараметров.СуммаОбщейЗадолженности, "ЧДЦ=2")  + " рублей. <BR>"
		+ "Лимит дебиторской задолженности " + Формат(СтруктураПараметров.ДопустимаяСуммаЗадолженности, "ЧДЦ=2")  + " рублей. <BR>";
		
		МассивЗаказов = ПолучитьЗаказыКонтрагентаДоСтатусаКОтгрузке(СтруктураПараметров.Контрагент);	
		Если МассивЗаказов.Количество()>0 Тогда
			ТекстПисьма = ТекстПисьма + "В связи с превышением лимита дебиторской задолженности заблокирована Реализация по следующим Заказам клиента: <BR>";
			Для Каждого ЗаказПокупателя из МассивЗаказов Цикл
				ТекстПисьма = ТекстПисьма + ЗаказПокупателя + " на сумму " + Формат(ЗаказПокупателя.СуммаДокумента, "ЧДЦ=2")  + " рублей. <BR>";
			КонецЦикла;
		КонецЕсли;
		
		ТекстПисьма = ТекстПисьма + "Просим погасить дебиторскую задолженность.";
		
	ИначеЕсли  СтруктураПараметров.РазделСтопЛиста = Перечисления.абс_СтатусыЗадолженности.МораторнаяЗадолженность  Тогда
		
		ТекстПисьма = ТекстПисьма + "Информируем, что по Контрагенту " + СтруктураПараметров.Контрагент + " образовалась мораторная задолженность "  
		+ "Общая дебиторская задолженность " + Формат(СтруктураПараметров.СуммаОбщейЗадолженности, "ЧДЦ=2")  + " рублей. <BR>";
		
		МассивЗаказов = ПолучитьЗаказыКонтрагентаДоСтатусаКОтгрузке(СтруктураПараметров.Контрагент);	
		Если МассивЗаказов.Количество()>0 Тогда
			ТекстПисьма = ТекстПисьма + "В связи с наличием мораторной задолженности заблокирована Реализация по следующим Заказам клиента: <BR>";
			Для Каждого ЗаказПокупателя из МассивЗаказов Цикл
				ТекстПисьма = ТекстПисьма + ЗаказПокупателя + " на сумму " + Формат(ЗаказПокупателя.СуммаДокумента, "ЧДЦ=2")  + " рублей. <BR>";
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли  СтруктураПараметров.РазделСтопЛиста = Перечисления.абс_СтатусыЗадолженности.Разблокирован
		ИЛИ Не ЗначениеЗаполнено(СтруктураПараметров.РазделСтопЛиста) Тогда
		
		МассивЗаказов = ПолучитьЗаказыКонтрагентаДоСтатусаКОтгрузке(СтруктураПараметров.Контрагент);	
		Если МассивЗаказов.Количество()>0 Тогда
			ТекстПисьма = ТекстПисьма + "Заказы клиента " + СтруктураПараметров.Контрагент + " разблокированы для оформления Реализации: <BR>";
			Для Каждого ЗаказПокупателя из МассивЗаказов Цикл
				ТекстПисьма = ТекстПисьма + ЗаказПокупателя + "  <BR>";
			КонецЦикла;
		Иначе
			ТекстПисьма = ТекстПисьма + "Заказы клиента " + СтруктураПараметров.Контрагент + " разблокированы для оформления Реализации ";
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТекстПисьма
	
КонецФункции

Функция СформироватьТемуПисьмаПоСтопЛисту(СтруктураПараметров) Экспорт
	
	Если  СтруктураПараметров.РазделСтопЛиста = Перечисления.абс_СтатусыЗадолженности.Разблокирован
		ИЛИ Не ЗначениеЗаполнено(СтруктураПараметров.РазделСтопЛиста) Тогда
		
		ТемаПисьма = "Разблокирована Реализация по Заказам клиента " + СтруктураПараметров.Контрагент;
		
	Иначе
		ТемаПисьма = "Заблокирована Реализация по Заказам клиента " + СтруктураПараметров.Контрагент;
		
	КонецЕсли;
	
	Возврат ТемаПисьма
	
КонецФункции

Функция ПолучитьЗаказыКонтрагентаДоСтатусаКОтгрузке(Контрагент) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Заказы.Ссылка КАК ЗаказПокупателя,
	|	абс_КонтрольДЗПоЗаказамСрезПоследних.СтатусДЗ
	|ИЗ
	|	Документ.ЗаказПокупателя КАК Заказы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.абс_КонтрольДЗПоЗаказам.СрезПоследних КАК абс_КонтрольДЗПоЗаказамСрезПоследних
	|		ПО абс_КонтрольДЗПоЗаказамСрезПоследних.ЗаказПокупателя = Заказы.Ссылка
	|ГДЕ
	|	Заказы.Контрагент = &Контрагент
	|	И Заказы.абс_Статус В(&МассивСтатусов)
	|	И абс_КонтрольДЗПоЗаказамСрезПоследних.СтатусДЗ <> ЗНАЧЕНИЕ(Перечисление.абс_СтатусыДЗПоЗаказам.Разблокирован)
	|	И абс_КонтрольДЗПоЗаказамСрезПоследних.ПринудительнаяБлокировка = Ложь";         // Отсекаем принудительную блокировку

	
	МассивСтатусов = Новый Массив;
	МассивСтатусов.Добавить(Перечисления.абс_СтатусыОтгрузки.Создан);
	МассивСтатусов.Добавить(Перечисления.абс_СтатусыОтгрузки.НеСогласован);
	МассивСтатусов.Добавить(Перечисления.абс_СтатусыОтгрузки.Согласован);
	МассивСтатусов.Добавить(Перечисления.абс_СтатусыОтгрузки.КОтгрузке);
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("МассивСтатусов", МассивСтатусов);
	
	МассивЗаказов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ЗаказПокупателя");
	
	Возврат МассивЗаказов;
	
КонецФункции



Функция РазобратьСтрокуВМассивПоРазделителю(Знач Стр, СтрРазделитель = ".") Экспорт
	
	Результат = Новый Массив;
	
	ВхождениеРазделителя = Найти(Стр, СтрРазделитель);
	Пока ВхождениеРазделителя <> 0 Цикл
		ЧастьДоРазделителя = СокрЛП(Лев(Стр, ВхождениеРазделителя - 1));
		Результат.Добавить(ЧастьДоРазделителя);
		Стр = СокрЛП(Сред(Стр, ВхождениеРазделителя + 1));
		ВхождениеРазделителя = Найти(Стр, СтрРазделитель);
	КонецЦикла;
	
	Если НЕ ПустаяСтрока(Стр) Тогда
		Результат.Добавить(СокрЛП(Стр));
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура СформироватьОтчетСтопЛист(МассивФайлов, МассивКонтрагентов) Экспорт
	
	СтруктураПараметров = СформироватьОтчетСтопЛистМораторнаяЗадолженность();
	
	ТабДок = СтруктураПараметров.ТабДок;
	
	МассивКонтрагентов = СтруктураПараметров.МассивКонтрагентов;
	
	Если Не ТабДок = Неопределено Тогда
		
		ПутьКФайлу = КаталогВременныхФайлов() + "STOP-лист. Мораторная задолженность на " + Формат(ТекущаяДата(), "ДФ=дд.ММ.ггг") + ".xls";
		
		//завершение процессов эксель
		WMI = ПолучитьCOMОбъект("winmgmts:{impersonationLevel=impersonate}!\\.\root\CIMV2");
		
		Результат = WMI.ExecQuery("Select * from Win32_Process Where Name = 'EXCEL.EXE'");
		
		Для Каждого СтрокаРез Из Результат Цикл
			
			Сообщить(СтрокаРез.name + " ID = " + СтрокаРез.ProcessID);
			
			Рез = СтрокаРез.Terminate();
			
			Если Рез = 0 Тогда
				Сообщить("Процесс завершен.");
			КонецЕсли;
			
		КонецЦикла;
		
		Попытка
			ТабДок.Записать(ПутьКФайлу ,ТипФайлаТабличногоДокумента.XLS);
			МассивФайлов.Добавить(ПутьКФайлу);
		Исключение
			Сообщить("Ошибка при создании файла Excel " + ОписаниеОшибки());
		КонецПопытки;
		//ЛЕО Зароднюк 17.04.2024  не работало в 8.3
		//Попытка
		//	Excel = Новый COMОбъект("Excel.Application");
		//	Книга = Excel.WorkBooks.Open(ПутьКФайлу);
		//	ExcelЛист = Книга.Sheets(1);
		//	ExcelPS =  ExcelЛист.PageSetup;
		//	
		//	ScriptCtrl = Новый ComОбъект("MSScriptControl.ScriptControl");
		//	ScriptCtrl.Language = "vbscript";
		//	ScriptCtrl.AddCode("Function SetAutoZoom()
		//	|	ExcelPS.Zoom = False
		//	|	ExcelPS.FitToPagesWide = 1
		//	|	ExcelPS.FitToPagesTall = 10
		//	|End Function");
		//	ScriptCtrl.AddObject("ExcelPS", ExcelPS);
		//	ScriptCtrl.Run("SetAutoZoom");
		//	Книга.Save();
		//	Excel.Quit();
		//Исключение
		//	Сообщить("Ошибка при форматировании файла Excel " + ОписаниеОшибки());
		//	Excel.Quit();
		//КонецПопытки;
		
	КонецЕсли;
	
	СтруктураПараметров = СформироватьОтчетСтопЛистПросроченнаяЗадолженность();
	
	ТабДок = СтруктураПараметров.ТабДок;
	
	МассивКонтрагентовП = СтруктураПараметров.МассивКонтрагентов;
	
	Для Каждого Контрагент Из МассивКонтрагентовП Цикл
		Если МассивКонтрагентов.Найти(Контрагент)=Неопределено Тогда
			МассивКонтрагентов.Добавить(Контрагент);
		КонецЕсли;
	КонецЦикла;
	
	Если Не ТабДок = Неопределено Тогда
		
		ПутьКФайлу = КаталогВременныхФайлов() + "STOP-лист на " + Формат(ТекущаяДата(), "ДФ=дд.ММ.ггг") + ".xls";
		
		//завершение процессов эксель
		WMI = ПолучитьCOMОбъект("winmgmts:{impersonationLevel=impersonate}!\\.\root\CIMV2");
		
		Результат = WMI.ExecQuery("Select * from Win32_Process Where Name = 'EXCEL.EXE'");
		
		Для Каждого СтрокаРез Из Результат Цикл
			
			Сообщить(СтрокаРез.name + " ID = " + СтрокаРез.ProcessID);
			
			Рез = СтрокаРез.Terminate();
			
			Если Рез = 0 Тогда
				Сообщить("Процесс завершен.");
			КонецЕсли;
			
		КонецЦикла;
		
		Попытка
			ТабДок.Записать(ПутьКФайлу ,ТипФайлаТабличногоДокумента.XLS);
			МассивФайлов.Добавить(ПутьКФайлу);
		Исключение
			Сообщить("Ошибка при создании файла Excel " + ОписаниеОшибки());
		КонецПопытки;
		//ЛЕО Зароднюк 17.04.2024  не работало в 8.3
		//Попытка
		//	Excel = Новый COMОбъект("Excel.Application");
		//	Книга = Excel.WorkBooks.Open(ПутьКФайлу);
		//	ExcelЛист = Книга.Sheets(1);
		//	ExcelPS =  ExcelЛист.PageSetup;
		//	
		//	ScriptCtrl = Новый ComОбъект("MSScriptControl.ScriptControl");
		//	ScriptCtrl.Language = "vbscript";
		//	ScriptCtrl.AddCode("Function SetAutoZoom()
		//	|	ExcelPS.Zoom = False
		//	|	ExcelPS.FitToPagesWide = 1
		//	|	ExcelPS.FitToPagesTall = 10
		//	|End Function");
		//	ScriptCtrl.AddObject("ExcelPS", ExcelPS);
		//	ScriptCtrl.Run("SetAutoZoom");
		//	Книга.Save();
		//	Excel.Quit();
		//Исключение
		//	Сообщить("Ошибка при форматировании файла Excel " + ОписаниеОшибки());
		//	Excel.Quit();
		//КонецПопытки;
		
	КонецЕсли;	
	
КонецПроцедуры

Процедура ВыполнитьРассылкуОтчетаСтопЛист(МассивФайлов, МассивКонтрагентов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	абс_НастройкиОповещения.ТипОповещения,
	|	абс_НастройкиОповещения.Действует,
	|	абс_НастройкиОповещения.АдресЭлектроннойПочты
	|ИЗ
	|	РегистрСведений.абс_НастройкиОповещения КАК абс_НастройкиОповещения
	|ГДЕ
	|	абс_НастройкиОповещения.Действует
	|	И абс_НастройкиОповещения.ТипОповещения = ЗНАЧЕНИЕ(Перечисление.абс_ТипыОповещений.РассылкаПоСтоплисту)
	|	И НЕ (ВЫРАЗИТЬ(абс_НастройкиОповещения.АдресЭлектроннойПочты КАК СТРОКА(100))) = """"";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
		
	ТекстЛога = ЛогированиеФайлЛога();
	ТекстСтроки = " ----------------------------------------------------------------------------------------------------------------------------------------------------------------- " + Символы.ПС ;
	ТекстЛога.Записать(ТекстСтроки);

	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	МассивАдресов = РазобратьСтрокуВМассивПоРазделителю(Выборка.АдресЭлектроннойПочты, ",");
	
	Для Каждого Контрагент Из МассивКонтрагентов Цикл
		Если ЗначениеЗаполнено(Контрагент.абс_Отдел) Тогда
			Менеджер = Контрагент.абс_Отдел.абс_Менеджер;
			Если ЗначениеЗаполнено(Менеджер) Тогда
				АдресМенеджераСтр = абс_УправлениеДебиторскойЗадолженностью.ПолучитьКонтактнуюИнформацию(Менеджер, Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты, Справочники.ВидыКонтактнойИнформации.СлужебныйАдресЭлектроннойПочтыПользователя);
				МассивАдресовВрем = абс_УправлениеДебиторскойЗадолженностью.РазобратьСтрокуВМассивПоРазделителю(АдресМенеджераСтр, ",");
				Для каждого АдресМенеджера из МассивАдресовВрем Цикл
					Если МассивАдресов.Найти(АдресМенеджера) = Неопределено Тогда
						МассивАдресов.Добавить(АдресМенеджера);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		Если ЗначениеЗаполнено(Контрагент.абс_БизнесРегион) Тогда
			Менеджер = Контрагент.абс_БизнесРегион.абс_Менеджер;
			Если ЗначениеЗаполнено(Менеджер) Тогда
				АдресМенеджераСтр = абс_УправлениеДебиторскойЗадолженностью.ПолучитьКонтактнуюИнформацию(Менеджер, Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты, Справочники.ВидыКонтактнойИнформации.СлужебныйАдресЭлектроннойПочтыПользователя);
				МассивАдресовВрем = абс_УправлениеДебиторскойЗадолженностью.РазобратьСтрокуВМассивПоРазделителю(АдресМенеджераСтр, ",");
				Для каждого АдресМенеджера из МассивАдресовВрем Цикл
					Если МассивАдресов.Найти(АдресМенеджера) = Неопределено Тогда
						МассивАдресов.Добавить(АдресМенеджера);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		Если ЗначениеЗаполнено(Контрагент.абс_Территория) Тогда
			Менеджер = Контрагент.абс_Территория.абс_Менеджер;
			Если ЗначениеЗаполнено(Менеджер) Тогда
				АдресМенеджераСтр = абс_УправлениеДебиторскойЗадолженностью.ПолучитьКонтактнуюИнформацию(Менеджер, Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты, Справочники.ВидыКонтактнойИнформации.СлужебныйАдресЭлектроннойПочтыПользователя);
				МассивАдресовВрем = абс_УправлениеДебиторскойЗадолженностью.РазобратьСтрокуВМассивПоРазделителю(АдресМенеджераСтр, ",");
				Для каждого АдресМенеджера из МассивАдресовВрем Цикл
					Если МассивАдресов.Найти(АдресМенеджера) = Неопределено Тогда
						МассивАдресов.Добавить(АдресМенеджера);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ТекстСтроки = "массив адресов для рассылки сформирован сформирован " + Формат(ТекущаяДата());
	ТекстЛога.Записать(ТекстСтроки);	
	
	ТекстПисьма = "Список контрагентов с просроченной дебиторской задолженностью";
	
	ТемаПисьма = "STOP-лист. Просроченная дебиторская задолженность на " + Формат(ТекущаяДата(), "ДФ=дд.ММ.ггг");
	
	//отправка
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("УчетнаяЗапись", Справочники.УчетныеЗаписиЭлектроннойПочты.УведомленияДистрибуции);
	СтруктураПараметров.Вставить("Тема", ТемаПисьма);
	СтруктураПараметров.Вставить("Тело", ТекстПисьма);
	СписокКому = Новый СписокЗначений;
	СписокКому.ЗагрузитьЗначения(МассивАдресов);
	СтруктураПараметров.Вставить("Кому",СписокКому);
	
	Если МассивФайлов.Количество()>0 Тогда
		
		СписокФайловВложений = Новый СписокЗначений;
		
		Для каждого Файл из МассивФайлов Цикл
			ИмяФайла = РаботаСФайлами.ПолучитьИмяФайлаИзПолногоПути(Файл);
			
			ФайлВложения = СписокФайловВложений.Добавить();
			
			СтруктураВложения  = Новый Структура();
			
			ДвоичныеДанныеФайла = Новый ДвоичныеДанные(Файл);
			СтруктураВложения.Вставить("Хранилище", ДвоичныеДанныеФайла);
			
			СтруктураВложения.Вставить("ИмяФайла", ИмяФайла);
			
			ФайлВложения.Значение = СтруктураВложения;
		КонецЦикла;
		
		СтруктураПараметров.Вставить("СписокФайловВложений", СписокФайловВложений);
	КонецЕсли;
	
	СтруктураПисьма = УправлениеЭлектроннойПочтой.НаписатьПисьмо(глЗначениеПеременной("глТекущийПользователь"),СтруктураПараметров,,,,,, Истина, Ложь);
	
	ЭлектронныеПисьма = Новый Соответствие;
	
	ЭлектронныеПисьма.Вставить(СтруктураПисьма.ПисьмоСсылка);
	
	ИзмененныйСоставПисем = Новый Соответствие;	
	ТекущийПользователь= глЗначениеПеременной("глТекущийПользователь");	
	ДоступныеУчетныеЗаписи = УправлениеЭлектроннойПочтой.ПолучитьДоступныеУчетныеЗаписи(ТекущийПользователь);
	
	ТекстСтроки = "Формирование письма " + Формат(ТекущаяДата())+ Символы.ПС;
	ТекстЛога.Записать(ТекстСтроки);	
		
	Для каждого Письмо Из ЭлектронныеПисьма Цикл
		Если ТипЗнч(Письмо.Значение) <> Тип("ДокументОбъект.ЭлектронноеПисьмо") Тогда
			ОбъектПисьмо = Письмо.Ключ.ПолучитьОбъект();
			
			ОбъектПисьмо.СтатусПисьма = Перечисления.СтатусыПисем.Исходящее;
			Если НЕ ЗначениеЗаполнено(ОбъектПисьмо.Ответственный) Тогда
				ОбъектПисьмо.Ответственный = ТекущийПользователь;
			КонецЕсли; 
			
			Попытка
				ОбъектПисьмо.Записать();
				ТекстСтроки = "письмо записано " + Формат(ТекущаяДата())+ Символы.ПС;
				ТекстЛога.Записать(ТекстСтроки);
			Исключение
				ТекстСтроки = "письмо не записано " + Формат(ТекущаяДата())+ Символы.ПС;
				ТекстЛога.Записать(ТекстСтроки);
				Продолжить;
			КонецПопытки;
		Иначе
			Письмо.Значение.СтатусПисьма = Перечисления.СтатусыПисем.Исходящее;
			Если НЕ ЗначениеЗаполнено(Письмо.Значение.Ответственный) Тогда
				Письмо.Значение.Ответственный = ТекущийПользователь;
			КонецЕсли; 
		КонецЕсли; 
		ИзмененныйСоставПисем.Вставить(Письмо.Ключ, Письмо.Значение);  		
	КонецЦикла;
	
	ТекстСтроки = "попытка отправки письма " + Формат(ТекущаяДата())+ Символы.ПС;
	ТекстЛога.Записать(ТекстСтроки);	
		
	ТекстОшибок = "";	
		
	УправлениеЭлектроннойПочтой.ПолучениеОтправкаПисем(глЗначениеПеременной("глСоответствиеТекстовЭлектронныхПисем"), ПараметрыСеанса.ТекущийПользователь,,
	ИзмененныйСоставПисем, Истина , , Ложь,ТекстОшибок );
	
	ТекстСтроки = ТекстОшибок + Формат(ТекущаяДата())+ Символы.ПС;
	ТекстЛога.Записать(ТекстСтроки);
	
	ТекстСтроки = "письмо должно было уйти, а там хз... " + Формат(ТекущаяДата())+ Символы.ПС;
	ТекстЛога.Записать(ТекстСтроки);
	
КонецПроцедуры

Функция СформироватьОтчетСтопЛистМораторнаяЗадолженность() Экспорт
	
	МассивКонтрагентов = Новый Массив;
	СтруктураПараметров = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	абс_СтопЛистПоКонтрагентамСрезПоследних.Контрагент,
	|	абс_СтопЛистПоКонтрагентамСрезПоследних.СуммаОбщейЗадолженности КАК ОбщаяЗадолженность,
	|	абс_СтопЛистПоКонтрагентамСрезПоследних.СуммаПросроченнойЗадолженности КАК ПросроченнаяЗадолженность,
	|	абс_СтопЛистПоКонтрагентамСрезПоследних.КоличествоДнейПросроченнойЗадолженности КАК КоличествоДнейПДЗ,
	|	ЗначенияСвойствОбъектов.Значение КАК ВидЗадолженности,
	|	абс_СтопЛистПоКонтрагентамСрезПоследних.Контрагент.ОсновнойДоговорКонтрагента.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ЗначенияСвойствОбъектов.Значение ССЫЛКА Справочник.ЗначенияСвойствОбъектов
	|			ТОГДА ЗначенияСвойствОбъектов.Значение.абс_Порядок
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Порядок
	|ИЗ
	|	РегистрСведений.абс_СтопЛистПоКонтрагентам.СрезПоследних КАК абс_СтопЛистПоКонтрагентамСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|		ПО абс_СтопЛистПоКонтрагентамСрезПоследних.Контрагент = ЗначенияСвойствОбъектов.Объект
	|			И (ЗначенияСвойствОбъектов.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СвойстваОбъектов.абс_МораторнаяЗадолженность))
	|ГДЕ
	|	абс_СтопЛистПоКонтрагентамСрезПоследних.СтатусЗадолженности = ЗНАЧЕНИЕ(Перечисление.абс_СтатусыЗадолженности.МораторнаяЗадолженность)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	абс_СтопЛистПоКонтрагентамСрезПоследних.Контрагент.Наименование
	|ИТОГИ
	|	СУММА(ОбщаяЗадолженность),
	|	СУММА(ПросроченнаяЗадолженность)
	|ПО
	|	ОБЩИЕ,
	|	ВидЗадолженности";
	
	Результат = Запрос.Выполнить();
	
	ТабДок = Неопределено;
	
	ТекстЛога = ЛогированиеФайлЛога ();
	ТекстСтроки = " ----------------------------------------------------------------------------------------------------------------------------------------------------------------- " + Символы.ПС ;
	ТекстЛога.Записать(ТекстСтроки);

	Если Не Результат.Пустой() Тогда
		Макет = ПолучитьОбщийМакет("абс_СтопЛистМораторная");
		ТабДок = Новый ТабличныйДокумент;
		
		ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
		ОбластьЗаголовок.Параметры.Заголовок = "Список контрагентов с мораторной задолженностью на " + Формат(ТекущаяДата(), "ДФ=дд.ММ.ггг");
		ТабДок.Вывести(ОбластьЗаголовок);
		
		ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
		ТабДок.Вывести(ОбластьШапка);
		
		ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
		ОбластьВид = Макет.ПолучитьОбласть("ВидЗадолженности");
		ОбластьИтоги = Макет.ПолучитьОбласть("Итоги");
		
		ТабДок.НачатьАвтогруппировкуСтрок();
		
		ВыборкаИтоги = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Если ВыборкаИтоги.Следующий() Тогда
			
			ЗаполнитьЗначенияСвойств(ОбластьИтоги.Параметры, ВыборкаИтоги);
			ТабДок.Вывести(ОбластьИтоги);
			
			ВыборкаВид = ВыборкаИтоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаВид.Следующий() Цикл
				
				ЗаполнитьЗначенияСвойств(ОбластьВид.Параметры,ВыборкаВид);
				ТабДок.Вывести(ОбластьВид,1);
				
				Выборка = ВыборкаВид.Выбрать();
				Пока Выборка.Следующий() Цикл
					ЗаполнитьЗначенияСвойств(ОбластьСтрока.Параметры,Выборка);
					ОбластьСтрока.Параметры.ПроцентПДЗ = "" +  Формат(?(Выборка.ОбщаяЗадолженность = 0, 0 , Выборка.ПросроченнаяЗадолженность/Выборка.ОбщаяЗадолженность*100),"ЧДЦ=1") + "%";
					ТабДок.Вывести(ОбластьСтрока,2);
					МассивКонтрагентов.Добавить(Выборка.Контрагент);
				КонецЦикла;
			КонецЦикла;
			
			ТабДок.ЗакончитьАвтогруппировкуСтрок();
			
		КонецЕсли;
		
		ТабДок.АвтоМасштаб = Истина;
		
		ТекстСтроки = "Стоп лист по мораторной задолженности сформирован " + Формат(ТекущаяДата())+ Символы.ПС;
		ТекстЛога.Записать(ТекстСтроки);	

	КонецЕсли;
	
	СтруктураПараметров.Вставить("ТабДок", ТабДок);
	СтруктураПараметров.Вставить("МассивКонтрагентов", МассивКонтрагентов);
	
	Возврат СтруктураПараметров;
	
КонецФункции

Функция СформироватьОтчетСтопЛистПросроченнаяЗадолженность() Экспорт
	
	ТабДок = Неопределено;
	
	МассивКонтрагентов = Новый Массив;
	
	СтруктураПараметров = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	абс_СтопЛистПоКонтрагентамСрезПоследних.Контрагент,
	|	абс_СтопЛистПоКонтрагентамСрезПоследних.СтатусЗадолженности,
	|	абс_СтопЛистПоКонтрагентамСрезПоследних.СуммаОбщейЗадолженности КАК ОбщаяЗадолженность,
	|	абс_СтопЛистПоКонтрагентамСрезПоследних.СуммаПросроченнойЗадолженности КАК ПросроченнаяЗадолженность,
	|	абс_СтопЛистПоКонтрагентамСрезПоследних.КоличествоДнейПросроченнойЗадолженности КАК КоличествоДнейПДЗ,
	|	абс_СтопЛистПоКонтрагентамСрезПоследних.Контрагент.абс_Отдел КАК Подразделение,
	|	абс_СтопЛистПоКонтрагентамСрезПоследних.Контрагент.абс_БизнесРегион КАК БизнесРегион,
	|	абс_СтопЛистПоКонтрагентамСрезПоследних.Контрагент.абс_БизнесРегион.абс_Менеджер КАК МенеджерБизнесРегиона,
	|	абс_СтопЛистПоКонтрагентамСрезПоследних.Контрагент.абс_Отдел.абс_Менеджер КАК МенеджерОтдела,
	|	ДоговорКонтрагента.ДопустимоеЧислоДнейЗадолженности КАК ЧислоДнейЗадолженностиНеБолее,
	|	ДоговорКонтрагента.абс_ДопустимаяСуммаПросроченнойЗадолженности КАК ЛимитПДЗпоСумме,
	|	СтатусДебитора.Значение КАК СтатусДебитора,
	|	ДоговорКонтрагента.ДопустимаяСуммаЗадолженности КАК КредитныйЛимит
	|ИЗ
	|	РегистрСведений.абс_СтопЛистПоКонтрагентам.СрезПоследних КАК абс_СтопЛистПоКонтрагентамСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ДоговорыКонтрагентов.Владелец КАК Владелец,
	|			МАКСИМУМ(ДоговорыКонтрагентов.ДопустимоеЧислоДнейЗадолженности) КАК ДопустимоеЧислоДнейЗадолженности,
	|			МАКСИМУМ(ДоговорыКонтрагентов.абс_ДопустимаяСуммаПросроченнойЗадолженности) КАК абс_ДопустимаяСуммаПросроченнойЗадолженности,
	|			МАКСИМУМ(ДоговорыКонтрагентов.ДопустимаяСуммаЗадолженности) КАК ДопустимаяСуммаЗадолженности
	|		ИЗ
	|			Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ДоговорыКонтрагентов.Владелец) КАК ДоговорКонтрагента
	|		ПО абс_СтопЛистПоКонтрагентамСрезПоследних.Контрагент = ДоговорКонтрагента.Владелец
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ЗначенияСвойствОбъектов.Значение КАК Значение,
	|			ЗначенияСвойствОбъектов.Объект КАК Объект
	|		ИЗ
	|			РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|		ГДЕ
	|			ЗначенияСвойствОбъектов.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СвойстваОбъектов.лео_СтатусДебитора)) КАК СтатусДебитора
	|		ПО абс_СтопЛистПоКонтрагентамСрезПоследних.Контрагент = СтатусДебитора.Объект
	|ГДЕ
	|	НЕ абс_СтопЛистПоКонтрагентамСрезПоследних.СтатусЗадолженности = ЗНАЧЕНИЕ(Перечисление.абс_СтатусыЗадолженности.Разблокирован)
	|	И НЕ абс_СтопЛистПоКонтрагентамСрезПоследних.СтатусЗадолженности = ЗНАЧЕНИЕ(Перечисление.абс_СтатусыЗадолженности.ПустаяСсылка)
	|	И НЕ абс_СтопЛистПоКонтрагентамСрезПоследних.СтатусЗадолженности = ЗНАЧЕНИЕ(Перечисление.абс_СтатусыЗадолженности.МораторнаяЗадолженность)
	|
	|СГРУППИРОВАТЬ ПО
	|	абс_СтопЛистПоКонтрагентамСрезПоследних.Контрагент,
	|	абс_СтопЛистПоКонтрагентамСрезПоследних.СтатусЗадолженности,
	|	абс_СтопЛистПоКонтрагентамСрезПоследних.СуммаОбщейЗадолженности,
	|	абс_СтопЛистПоКонтрагентамСрезПоследних.СуммаПросроченнойЗадолженности,
	|	абс_СтопЛистПоКонтрагентамСрезПоследних.КоличествоДнейПросроченнойЗадолженности,
	|	абс_СтопЛистПоКонтрагентамСрезПоследних.Контрагент.абс_Отдел,
	|	абс_СтопЛистПоКонтрагентамСрезПоследних.Контрагент.абс_БизнесРегион,
	|	абс_СтопЛистПоКонтрагентамСрезПоследних.Контрагент.абс_БизнесРегион.абс_Менеджер,
	|	абс_СтопЛистПоКонтрагентамСрезПоследних.Контрагент.абс_Отдел.абс_Менеджер,
	|	ДоговорКонтрагента.ДопустимоеЧислоДнейЗадолженности,
	|	ДоговорКонтрагента.абс_ДопустимаяСуммаПросроченнойЗадолженности,
	|	СтатусДебитора.Значение,
	|	ДоговорКонтрагента.ДопустимаяСуммаЗадолженности
	|
	|УПОРЯДОЧИТЬ ПО
	|	абс_СтопЛистПоКонтрагентамСрезПоследних.Контрагент.абс_БизнесРегион.Родитель.Порядок,
	|	абс_СтопЛистПоКонтрагентамСрезПоследних.Контрагент.абс_БизнесРегион.Порядок,
	|	абс_СтопЛистПоКонтрагентамСрезПоследних.Контрагент.Наименование
	|ИТОГИ
	|	СУММА(ОбщаяЗадолженность),
	|	СУММА(ПросроченнаяЗадолженность)
	|ПО
	|	ОБЩИЕ,
	|	Подразделение,
	|	БизнесРегион";
		
	Результат = Запрос.Выполнить();
	
	ТекстЛога = ЛогированиеФайлЛога ();
	ТекстСтроки = " ----------------------------------------------------------------------------------------------------------------------------------------------------------------- " + Символы.ПС ;
	ТекстЛога.Записать(ТекстСтроки);
	
	Если Не Результат.Пустой() Тогда
		
		Макет = ПолучитьОбщийМакет("абс_СтопЛистПДЗ");
		ТабДок = Новый ТабличныйДокумент;
		
		ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
		ОбластьЗаголовок.Параметры.Заголовок = "Стоп-лист. Список контрагентов с просроченной дебиторской задолженностью на " + Формат(ТекущаяДата(), "ДФ=дд.ММ.ггг");
		ТабДок.Вывести(ОбластьЗаголовок);
		
		ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
		ТабДок.Вывести(ОбластьШапка);
		
		ОбластьПодразделение = Макет.ПолучитьОбласть("Подразделение");
		ОбластьРегион = Макет.ПолучитьОбласть("БизнесРегион");
		ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
		ОбластьИтоги = Макет.ПолучитьОбласть("Итоги");
		
		ТабДок.НачатьАвтогруппировкуСтрок();
		
		ВыборкаИтоги = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Если ВыборкаИтоги.Следующий() Тогда
			
			ЗаполнитьЗначенияСвойств(ОбластьИтоги.Параметры,ВыборкаИтоги);
			ТабДок.Вывести(ОбластьИтоги);
			
			ВыборкаПодразделение = ВыборкаИтоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаПодразделение.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(ОбластьПодразделение.Параметры,ВыборкаПодразделение);
				ТабДок.Вывести(ОбластьПодразделение,1);
				
				ВыборкаРегион = ВыборкаПодразделение.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаРегион.Следующий() Цикл
					
					ЗаполнитьЗначенияСвойств(ОбластьРегион.Параметры,ВыборкаРегион);
					ТабДок.Вывести(ОбластьРегион,2);
					
					Выборка = ВыборкаРегион.Выбрать();
					Пока Выборка.Следующий() Цикл
						ЗаполнитьЗначенияСвойств(ОбластьСтрока.Параметры,Выборка);
						ОбластьСтрока.Параметры.ПроцентПДЗ = "" +  Формат(?(Выборка.ОбщаяЗадолженность = 0, 0 , Выборка.ПросроченнаяЗадолженность/Выборка.ОбщаяЗадолженность*100),"ЧДЦ=1") + "%";
						
						Если ОбластьСтрока.Параметры.СтатусЗадолженности = Перечисления.абс_СтатусыЗадолженности.ПревышениеЛимитаПДЗПоСумме ИЛИ 
							ОбластьСтрока.Параметры.СтатусЗадолженности = Перечисления.абс_СтатусыЗадолженности.ПревышениеКредитногоЛимита 
						Тогда					
							ОбластьСтрока.Область(1, 11, 1, 11).ЦветТекста = WebЦвета.Красный; 
						КонецЕсли;
										
						Если ОбластьСтрока.Параметры.ЧислоДнейЗадолженностиНеБолее = 0 Тогда					
							ОбластьСтрока.Область(1, 7, 1, 7).ЦветТекста = WebЦвета.Красный; 
							ОбластьСтрока.Область(1, 2, 1, 2).ЦветТекста = WebЦвета.Красный; 
						КонецЕсли;
						
						ТабДок.Вывести(ОбластьСтрока,3);
						ОбластьСтрока.Область(1, 11, 1, 11).ЦветТекста = WebЦвета.Черный;
						ОбластьСтрока.Область(1, 7, 1, 7).ЦветТекста = WebЦвета.Черный;
						ОбластьСтрока.Область(1, 2, 1, 2).ЦветТекста = WebЦвета.Черный; 
						МассивКонтрагентов.Добавить(Выборка.Контрагент);
						
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
		
		ТабДок.ЗакончитьАвтогруппировкуСтрок();
		ТабДок.АвтоМасштаб = Истина;
		
		ТекстСтроки = "Стоп лист по просроченной задолженности сформирован " + Формат(ТекущаяДата())+ Символы.ПС;
		ТекстЛога.Записать(ТекстСтроки);	
		
	КонецЕсли;
	
	СтруктураПараметров.Вставить("ТабДок", ТабДок);
	СтруктураПараметров.Вставить("МассивКонтрагентов", МассивКонтрагентов);
	
	Возврат СтруктураПараметров;
	
КонецФункции

Функция ЛогированиеФайлЛога ();
	
	ИмяФайла = "ЛогРассылкиОтчетаПДЗ_" + Формат(ТекущаяДата(),"ДФ=гг_ММ_д") +".txt";
	//КаталогЛога = ЭтотОбъект.КаталогЗагрузки;
	КаталогЛога = Справочники.Лео_РабочиеКаталоги.КаталогЛогированияРассылкиОтчетаПДЗ.Каталог;
	ТекстЛога = Новый ЗаписьТекста(КаталогЛога+ИмяФайла, КодировкаТекста.UTF16,,Истина,);
	Возврат  ТекстЛога
	
КонецФункции


