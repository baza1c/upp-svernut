
#Область СлужебныйПрограммныйИнтерфейс

Функция ЗагрузитьОтчетОПродажах(ПараметрыОтчета, УникальныйИдентификатор) Экспорт
	ПараметрыВыполнения = ДЛительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Загрузка отчета из маркетплейс'");
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения, "ОбменСМаркетплейс.ЗагрузитьОтчетОПродажах", ПараметрыОтчета);
КонецФункции

Функция ДоступенОбменСМаркетплейс() Экспорт
	
	Если РольДоступна("ПолныеПрава") Или РольДоступна("ДобавлениеИзменениеДанныхБухгалтерии") Тогда
		
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция НачатьПолучениеСпискаТоваров(НастройкаИнтеграции, УникальныйИдентификатор) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Получения списка товаров маркетплейса'");
	
	Результат = ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "ОбменСМаркетплейс.СписокТоваровМаркетплейс", 
		НастройкаИнтеграции);
		
	Возврат Результат;
	
КонецФункции 

Процедура ОбработатьРезультатыСопоставления(РезультатыСопоставления, НастройкаИнтеграции) Экспорт
	ОбменСМаркетплейс.ОбработатьРезультатыСопоставления(РезультатыСопоставления, НастройкаИнтеграции);
КонецПроцедуры 

Функция ОбработатьРезультатПолученияСпискаТоваров(АдресСписокТоваровМаркетплейс) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Результат",         Истина);
	Результат.Вставить("СообщениеОбОшибке",	"");
	Результат.Вставить("СписокТоваров",   	Новый Массив);
	Результат.Вставить("КодОтветаСервера",  0);
	
	РезультатПолученияСпискаТоваров = ПолучитьИзВременногоХранилища(АдресСписокТоваровМаркетплейс);
	
	Если НЕ ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатПолученияСпискаТоваров, "Результат", Ложь) Тогда
		Результат.Результат         = Ложь;
		Результат.СообщениеОбОшибке = РезультатПолученияСпискаТоваров.СообщениеОбОшибке;
		Если ТипЗнч(РезультатПолученияСпискаТоваров) = Тип("Структура") И РезультатПолученияСпискаТоваров.Свойство("СписокТоваров") Тогда 
			СписокТоваров = РезультатПолученияСпискаТоваров.СписокТоваров; 
			Если ТипЗнч(СписокТоваров) = Тип("Структура") И СписокТоваров.Свойство("КодОтветаСервера") Тогда
				Результат["КодОтветаСервера"] = СписокТоваров.КодОтветаСервера;
			КонецЕсли;
		КонецЕсли;
		Возврат Результат;                  
	КонецЕсли;
	
	СписокТоваровМаркетплейс = РезультатПолученияСпискаТоваров.СписокТоваров;
	
	СопоставлениеНоменклатуры = Новый Соответствие;
	
	// Найдем номенклатуру для которой сопоставления уже настроены
	ПараметрыПоиска = Новый ФиксированнаяСтруктура("НоменклатураКонтрагента", СписокТоваровМаркетплейс);
	МассивНайденнойНоменклатуры   = СопоставлениеНоменклатурыКонтрагентов.НайтиСоответствиеНоменклатуры(ПараметрыПоиска, Ложь);
	Для Каждого СтрокаНоменклатуры Из МассивНайденнойНоменклатуры Цикл
		Идентификатор = СтрокаНоменклатуры.НоменклатураКонтрагента.Идентификатор;
		Номенклатура  = СтрокаНоменклатуры.НоменклатураИБ.Номенклатура;
		Если ЗначениеЗаполнено(Номенклатура) Тогда
			СопоставлениеНоменклатуры.Вставить(Идентификатор, Номенклатура);
		КонецЕсли;
	КонецЦикла;
	
	НастройкаИнтеграцииОбъект = РезультатПолученияСпискаТоваров.НастройкаИнтеграции.ПолучитьОбъект();
	Если НЕ НастройкаИнтеграцииОбъект.КаталогЗагружен Тогда
		ОбменСМаркетплейс.ОбработатьРезультатыСопоставления(МассивНайденнойНоменклатуры, НастройкаИнтеграцииОбъект.Ссылка);
		
		НастройкаИнтеграцииОбъект.КаталогЗагружен = Истина;
		НастройкаИнтеграцииОбъект.Записать();
	КонецЕсли;
	
	// Уберем из загруженной номенклатуры ту, для которой сопоставления уже настроены
	Для Каждого СтрокаНоменклатуры Из СписокТоваровМаркетплейс Цикл
		Если СопоставлениеНоменклатуры.Получить(СтрокаНоменклатуры.Идентификатор) = Неопределено Тогда
			Результат.СписокТоваров.Добавить(СтрокаНоменклатуры);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции 

Функция ИнтеграцияНастроена(НастройкаИнтеграции) Экспорт
	Возврат ОбменСМаркетплейс.ИнтеграцияНастроена(НастройкаИнтеграции);
КонецФункции

#Область НоваяАвторизацияЯндекс

//Получает владельца элемента справочника НастройкиИнтеграцииМаркетплейс
//Параметры: Настройка Тип("СправочникСсылка.НастройкиИнтеграцииМаркетплейс")
//
//Возвращаемое значение: Владлец Тип("СправочникСсылка.Орагнизации")
//
Функция ОрганизацияНастройки(Настройка) Экспорт
	
	Возврат Настройка.Владелец;
	
КонецФункции

//Получает настройку интеграции по уникальному идентификатору
//Параметры: УникальныйИдентификаторНастройки Тип("УникальныйИдентификатор")
//
//Возвращаемое значение: Настройка Тип("СпраовчникСсылка.НастройкиИнтеграцииМаркетплейс")
//
Функция ПолучитьНастройкуПоУникальномуИдентификатору(УникальныйИдентификаторНастройки) Экспорт
	
	Возврат Справочники.НастройкиИнтеграцииМаркетплейс.ПолучитьСсылку(УникальныйИдентификаторНастройки);
	
КонецФункции

//Получает настройку интеграции Яндекс.Маркет для организации
//Параметры: Организация тип("СправочникСсылка.Организации")
//
//Возвращаемое значение: Настройка Тип("СправочникСсылка.НастройкиИнтеграцииМаркетплейс")
//
Функция ПолучитьНастройкуИнтеграции(Организация) Экспорт
	
	Настройка = Справочники.НастройкиИнтеграцииМаркетплейс.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	НастройкиИнтеграцииМаркетплейс.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.НастройкиИнтеграцииМаркетплейс КАК НастройкиИнтеграцииМаркетплейс
	|ГДЕ
	|	НастройкиИнтеграцииМаркетплейс.Владелец = &Организация
	|	И НастройкиИнтеграцииМаркетплейс.ВидМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыМаркетплейсов.ЯндексМаркет)";
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Настройка = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Настройка;
	
КонецФункции

//Получает видмаркетплейса из настройки интеграции
Функция ВидМаркетплейсаНастройки(Настройка) Экспорт
	
	Возврат Настройка.ВидМаркетплейса;
	
КонецФункции

#КонецОбласти

#КонецОбласти
