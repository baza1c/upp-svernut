Перем мУдалятьДвижения;
Перем мВалютаРегламентированногоУчета Экспорт;

#Если Клиент Тогда

// Функция формирует табличный документ с печатной формой
//
// Возвращаемое значение:
//  Табличный документ - печатная форма
//
Функция ПечатьПланаПродаж()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущийДокумент", ЭтотОбъект.Ссылка);
	Запрос.УстановитьПараметр("ВедениеУчетаПоПроектам", УправлениеПроектами.ВедениеУчетаПоПроектам());

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПланПродаж.Номер КАК Номер,
	|	ПланПродаж.Дата КАК Дата,
	|	NULL КАК Организация,
	|	ПланПродаж.Подразделение КАК Подразделение,
	|	ПРЕДСТАВЛЕНИЕ(ПланПродаж.Подразделение) КАК ПодразделениеПредставление,
	|	ПланПродаж.Сценарий КАК Сценарий,
	|	ПРЕДСТАВЛЕНИЕ(ПланПродаж.Сценарий) КАК СценарийПредставление,
	|	ВЫБОР
	|		КОГДА &ВедениеУчетаПоПроектам = ИСТИНА
	|			ТОГДА ПланПродаж.Проект
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Проект,
	|	ВЫБОР
	|		КОГДА &ВедениеУчетаПоПроектам = ИСТИНА
	|			ТОГДА ПРЕДСТАВЛЕНИЕ(ПланПродаж.Проект)
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК ПроектПредставление,
	|	ПланПродаж.Сценарий.Периодичность КАК Периодичность,
	|	ПРЕДСТАВЛЕНИЕ(ПланПродаж.Сценарий.Периодичность) КАК ПериодичностьПредставление,
	|	ПланПродаж.ДатаПланирования КАК ДатаПланирования,
	|	ПланПродаж.УчитыватьНДС КАК УчитыватьНДС,
	|	ПланПродаж.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ПланПродаж.Ответственный КАК Ответственный,
	|	ПРЕДСТАВЛЕНИЕ(ПланПродаж.Ответственный) КАК ОтветственныйПредставление,
	|	ПланПродаж.СоставПлана.(
	|		НомерСтроки КАК НомерСтроки,
	|		Контрагент КАК Контрагент,
	|		Договор КАК Договор,
	|		Номенклатура КАК Номенклатура,
	|		ВЫБОР
	|			КОГДА ПланПродаж.СоставПлана.Номенклатура.НаименованиеПолное ЕСТЬ NULL 
	|				ТОГДА ПланПродаж.СоставПлана.Номенклатура.Наименование
	|			ИНАЧЕ ВЫБОР
	|					КОГДА (ВЫРАЗИТЬ(ПланПродаж.СоставПлана.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))) = """"
	|						ТОГДА ПланПродаж.СоставПлана.Номенклатура.Наименование
	|					ИНАЧЕ ПланПродаж.СоставПлана.Номенклатура.НаименованиеПолное
	|				КОНЕЦ
	|		КОНЕЦ КАК НоменклатураПредставление,
	|		ХарактеристикаНоменклатуры КАК Характеристика,
	|		NULL КАК Серия,
	|		Заказ КАК Заказ,
	|		Заказ.Номер КАК ЗаказНомер,
	|		Заказ.Дата КАК ЗаказДата,
	|		Количество,
	|		ПРЕДСТАВЛЕНИЕ(ПланПродаж.СоставПлана.ЕдиницаИзмерения) КАК ЕдиницаИзмеренияПредставление,
	|		Цена,
	|		Сумма,
	|		СуммаНДС
	|	)
	|ИЗ
	|	Документ.ПланПродаж КАК ПланПродаж
	|ГДЕ
	|	ПланПродаж.Ссылка = &ТекущийДокумент
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();
	ВыборкаСоставПлана = Шапка.СоставПлана.Выбрать();
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПланПродаж_ПланПродаж";
	Макет = ПолучитьМакет("ПланПродаж");

	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.ТекстЗаголовка = ОбщегоНазначения.СформироватьЗаголовокДокумента(Шапка, "План продаж");
	ТабДокумент.Вывести(ОбластьМакета);

	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(Шапка);
	
	ДатаНачала = Шапка.ДатаПланирования;
	ДатаОкончания = Шапка.ДатаПланирования;
	УправлениеПланированием.ВыровнятьПериод(ДатаНачала, ДатаОкончания, Шапка.Периодичность);

	ОбластьМакета.Параметры.ПериодПланирования = ПредставлениеПериода(ДатаНачала, ДатаОкончания, "ФП=Истина");
	ТабДокумент.Вывести(ОбластьМакета);

	ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьМакета.Параметры.Заполнить(Шапка);
	ТабДокумент.Вывести(ОбластьМакета);

	ОбластьСтроки = Макет.ПолучитьОбласть("Строка");
	ВыборкаСоставПлана = Шапка.СоставПлана.Выбрать();
	Пока ВыборкаСоставПлана.Следующий() Цикл
		
		ОбластьСтроки.Параметры.Заполнить(ВыборкаСоставПлана);
		ОбластьСтроки.Параметры.НоменклатураПредставление = ВыборкаСоставПлана.НоменклатураПредставление + ФормированиеПечатныхФорм.ПредставлениеСерий(ВыборкаСоставПлана);
		Если НЕ ЗначениеЗаполнено(ВыборкаСоставПлана.Заказ) Тогда
			ОбластьСтроки.Параметры.ЗаказПредставление = "";
		Иначе
			ОбластьСтроки.Параметры.ЗаказПредставление = ВыборкаСоставПлана.ЗаказНомер + " от " + Формат(ВыборкаСоставПлана.ЗаказДата, "ДФ=dd.MM.yyyy");
		КонецЕсли;
		ОбластьСтроки.Параметры.Всего = ВыборкаСоставПлана.Сумма + ?(Шапка.СуммаВключаетНДС = Истина, 0, ВыборкаСоставПлана.СуммаНДС);

		ТабДокумент.Вывести(ОбластьСтроки);
		
	КонецЦикла;

	ОбластьМакета = Макет.ПолучитьОбласть("Итоги");
	ОбластьМакета.Параметры.Заполнить(Шапка);
	ОбластьМакета.Параметры.ОтветственныйПредставление = "/" + Шапка.ОтветственныйПредставление + "/";
	ОбластьМакета.Параметры.ИтогоВсего = СоставПлана.Итог("Сумма") + ?(Шапка.СуммаВключаетНДС = Истина, 0, СоставПлана.Итог("СуммаНДС"));
	ОбластьМакета.Параметры.ИтогоСтоимость = СоставПлана.Итог("Сумма");
	ТабДокумент.Вывести(ОбластьМакета);

	Возврат ТабДокумент;

КонецФункции // ПечатьПланаПродаж()

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт

	Если ЭтоНовый() Тогда
		Предупреждение("Документ можно распечатать только после его записи");
		Возврат;
	ИначеЕсли Не УправлениеДопПравамиПользователей.РазрешитьПечатьНепроведенныхДокументов(Проведен) Тогда
		Предупреждение("Недостаточно полномочий для печати непроведенного документа!");
		Возврат;
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(Сценарий) Тогда
		Сообщить("Не указан сценарий планирования!");
		Возврат;
	КонецЕсли;
	
	Если ИмяМакета = "ПланПродаж" Тогда
		// Получить экземпляр документа на печать
		ТабДокумент = ПечатьПланаПродаж();
		
	ИначеЕсли ТипЗнч(ИмяМакета) = Тип("ДвоичныеДанные") Тогда

		ТабДокумент = УниверсальныеМеханизмы.НапечататьВнешнююФорму(Ссылка, ИмяМакета);
		
		Если ТабДокумент = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект, ЭтотОбъект.Метаданные().Представление()), Ссылка);

КонецПроцедуры // Печать

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	Возврат Новый Структура("ПланПродаж", "План продаж");

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Выгружает результат запроса в табличную часть, добавляет ей необходимые колонки для проведения.
//
// Параметры: 
//  РезультатЗапросаПоСоставПлана - результат запроса по табличной части "СоставПлана",
//  ВыборкаПоШапкеДокумента   - выборка по результату запроса по шапке документа.
//
// Возвращаемое значение:
//  Сформированная таблица значений.
//
Функция ПодготовитьТаблицуСоставПлана(РезультатЗапросаПоСоставПлана, ВыборкаПоШапкеДокумента)

	ТаблицаСоставПлана = РезультатЗапросаПоСоставПлана.Выгрузить();

	// Переименуем колонку "Сумма" в "Стоимость" (как в регистрах).
	ТаблицаСоставПлана.Колонки.Сумма.Имя = "Стоимость";

	// Надо рассчитать стоимость без НДС.
	Для каждого СтрокаТаблицы Из ТаблицаСоставПлана Цикл

		СтрокаТаблицы.Стоимость = СтрокаТаблицы.Стоимость - 
		                          ?(УчитыватьНДС И СуммаВключаетНДС, СтрокаТаблицы.НДС, 0);

		// Суммы пересчитаем в валюту упр. учета
		СтрокаТаблицы.Стоимость = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СтрокаТаблицы.Стоимость, ВалютаДокумента,
		                                 ВыборкаПоШапкеДокумента.ВалютаУправленческогоУчета, 
		                                 КурсДокумента,
		                                 ВыборкаПоШапкеДокумента.КурсВалютыУправленческогоУчета, 
		                                 КратностьДокумента,
		                                 ВыборкаПоШапкеДокумента.КратностьВалютыУправленческогоУчета);

		СтрокаТаблицы.НДС = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СтрокаТаблицы.НДС, ВалютаДокумента,
		                                 ВыборкаПоШапкеДокумента.ВалютаУправленческогоУчета, 
		                                 КурсДокумента,
		                                 ВыборкаПоШапкеДокумента.КурсВалютыУправленческогоУчета, 
		                                 КратностьДокумента,
		                                 ВыборкаПоШапкеДокумента.КратностьВалютыУправленческогоУчета);
										 
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДокументПланирования) Тогда
			
			СтрокаТаблицы.ДокументПланирования = Ссылка;
			
		КонецЕсли;

	КонецЦикла;

	Возврат ТаблицаСоставПлана;

КонецФункции // ПодготовитьТаблицуСоставПлана()

// Выгружает результат запроса в табличную часть, добавляет ей необходимые колонки для проведения.
//
// Параметры: 
//  РезультатЗапросаПоСоставПлана - результат запроса по табличной части "СоставПлана",
//  ВыборкаПоШапкеДокумента   - выборка по результату запроса по шапке документа.
//
// Возвращаемое значение:
//  Сформированная таблица значений.
//
Функция абс_ПодготовитьТаблицуСоставПлана(РезультатЗапросаПоСоставПлана, ВыборкаПоШапкеДокумента)
	
	ТаблицаСоставПлана = РезультатЗапросаПоСоставПлана.Выгрузить();

	// Переименуем колонку "Сумма" в "Стоимость" (как в регистрах).
	ТаблицаСоставПлана.Колонки.Сумма.Имя = "Стоимость";

	// Надо рассчитать стоимость без НДС.
	Для каждого СтрокаТаблицы Из ТаблицаСоставПлана Цикл

		СтрокаТаблицы.Стоимость = СтрокаТаблицы.Стоимость - 
		                          ?(УчитыватьНДС И СуммаВключаетНДС, СтрокаТаблицы.НДС, 0);

		// Суммы пересчитаем в валюту упр. учета
		СтрокаТаблицы.Стоимость = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СтрокаТаблицы.Стоимость, ВалютаДокумента,
		                                 ВыборкаПоШапкеДокумента.ВалютаУправленческогоУчета, 
		                                 КурсДокумента,
		                                 ВыборкаПоШапкеДокумента.КурсВалютыУправленческогоУчета, 
		                                 КратностьДокумента,
		                                 ВыборкаПоШапкеДокумента.КратностьВалютыУправленческогоУчета);

		СтрокаТаблицы.НДС = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СтрокаТаблицы.НДС, ВалютаДокумента,
		                                 ВыборкаПоШапкеДокумента.ВалютаУправленческогоУчета, 
		                                 КурсДокумента,
		                                 ВыборкаПоШапкеДокумента.КурсВалютыУправленческогоУчета, 
		                                 КратностьДокумента,
		                                 ВыборкаПоШапкеДокумента.КратностьВалютыУправленческогоУчета);
										 
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДокументПланирования) Тогда
			
			СтрокаТаблицы.ДокументПланирования = Ссылка;
			
		КонецЕсли;

	КонецЦикла;
	
	ТаблицаСоставПланаКопия = ТаблицаСоставПлана.СкопироватьКолонки();
	
	Если Сценарий.абс_ТипСценарияПланирования=Перечисления.абс_ТипСценарияПланирования.Прогноз18 Тогда
		КоличествоМесяцев = 18;
	Иначе
		КоличествоМесяцев = 12;
	КонецЕсли;	
	
	Для каждого СтрокаТаблицы Из ТаблицаСоставПлана Цикл
			
		Для Н = 1 По КоличествоМесяцев Цикл
			
			СтрокаПлан = ТаблицаСоставПланаКопия.Добавить();
			
			ЗаполнитьЗначенияСвойств(СтрокаПлан, СтрокаТаблицы, ,"НДС,Количество,Стоимость");
			
			СтрокаПлан.Период 		= ДобавитьМесяц(ДатаПланирования, н - 1);
			
			СтрокаПлан.Стоимость 	= СтрокаТаблицы["Сумма" 		+ Строка(Н)];
			СтрокаПлан.Количество 	= СтрокаТаблицы["Количество" 	+ Строка(Н)];
			
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.Номенклатура) Тогда
				 Сообщить("Не заполнена строка " + Строка(СтрокаТаблицы.НомерСтроки));
				 //Продолжить;
			КонецЕсли;	 
				
			ТекСтавкаНДС = СтрокаТаблицы.Номенклатура.СтавкаНДС;
			
			Если СтрокаПлан.Контрагент.НеЯвляетсяРезидентом Тогда
				ТекСтавкаНДС = Перечисления.СтавкиНДС.НДС0;
			КонецЕсли;
			
			СтрокаПлан.НДС = УчетНДС.РассчитатьСуммуНДС(СтрокаПлан.Стоимость,
	                                                   УчитыватьНДС, СуммаВключаетНДС,
	                                                   УчетНДС.ПолучитьСтавкуНДС(ТекСтавкаНДС));
																   
			СтрокаТаблицы.Стоимость = СтрокаТаблицы.Стоимость - 
		                          ?(УчитыватьНДС И СуммаВключаетНДС, СтрокаТаблицы.НДС, 0);										   
			
		КонецЦикла;
		
	КонецЦикла;

	Возврат ТаблицаСоставПланаКопия;

КонецФункции // ПодготовитьТаблицуСоставПлана()

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизитов шапки, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
// все проверяемые реквизиты должны быть включены в выборку по шапке.
//
// Параметры: 
//  ВыборкаПоШапкеДокумента - выборка из результата запроса по шапке документа,
//  Отказ                   - флаг отказа в проведении.
//
Процедура ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок)

	// Укажем, что надо проверить:
	СтруктураОбязательныхПолей = Новый Структура("ВалютаДокумента, КурсДокумента, КратностьДокумента,
	                        					 |ДатаПланирования, Сценарий");

	// Теперь вызовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураОбязательныхПолей, Отказ, Заголовок);
	
КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Проверяет правильность заполнения строк табличной части "СоставПлана".
//
// Параметры:
// Параметры: 
//  ТаблицаПоСоставПлана    - таблица значений, содержащая данные для проведения и проверки ТЧ СоставПлана
//  ВыборкаПоШапкеДокумента - выборка из результата запроса по шапке документа,
//  Отказ                   - флаг отказа в проведении.
//  Заголовок               - строка, заголовок сообщения об ошибке проведения.
//
Процедура ПроверитьЗаполнениеТабличнойЧастиСоставПлана(ТаблицаПоСоставПлана, ВыборкаПоШапкеДокумента, Отказ, Заголовок)

	// Укажем, что надо проверить:
	//СтруктураОбязательныхПолей = Новый Структура("Номенклатура");
	СтруктураОбязательныхПолей = Новый Структура();
		
	//АБС ИЗМЕНЕНИЕ Отключаем контроль
	//Если Сценарий.УчетПоКоличеству Тогда
	//			
	//	СтруктураОбязательныхПолей.Вставить("Количество");
	//	
	//КонецЕсли;
	//
	//Если Сценарий.УчетПоСуммам И НЕ Сценарий.УчетПоКоличеству Тогда
	//	
	//	СтруктураОбязательныхПолей.Вставить("Сумма");
	//	СтруктураОбязательныхПолей.Вставить("СтавкаНДС");
	//	
	//КонецЕсли;
	//АБС ИЗМЕНЕНИЕ Отключаем контроль КОНЕЦ
	
	// Теперь вызовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "СоставПлана", СтруктураОбязательныхПолей, Отказ, Заголовок);

	// Здесь наборов быть не должно.
	УправлениеЗапасами.ПроверитьЧтоНетНаборов(ЭтотОбъект, "СоставПлана", ТаблицаПоСоставПлана, Отказ, Заголовок);
	
	// Здесь наборов-комплектов быть не должно.
	УправлениеЗапасами.ПроверитьЧтоНетКомплектов(ЭтотОбъект, "СоставПлана", ТаблицаПоСоставПлана, Отказ, Заголовок);
	
    //АБС ВСТАВКА   04.04.2014 13:14:45  Балашенко
	абс_Планирование.ПроверитьЗаполнениеТЧСоставПлана(ЭтотОбъект, Отказ, Заголовок);
	//АБС ВСТАВКА  КОНЕЦ
КонецПроцедуры // ПроверитьЗаполнениеТабличнойЧастиСоставПлана()

// По результату запроса по шапке документа формируем движения по регистрам.
//
// Параметры: 
//  РежимПроведения           - режим проведения документа (оперативный или неоперативный),
//  ВыборкаПоШапкеДокумента   - выборка из результата запроса по шапке документа,
//  ТаблицаПоСоставПлана      - таблица значений, содержащая данные для проведения и проверки ТЧ СоставПлана
//  Отказ                     - флаг отказа в проведении,
//  Заголовок                 - строка, заголовок сообщения об ошибке проведения.
//
Процедура ДвиженияПоРегистрам(РежимПроведения, ТаблицаПоСоставПлана, Отказ, Заголовок)

	// ПО регистру "ПланыПродаж".
	
	НаборДвижений = Движения.ПланыПродаж;
	
	// Получим таблицу значений, совпадающую со структурой набора записей регистра.
	ТаблицаДвижений = НаборДвижений.Выгрузить();
	ТаблицаДвижений.Очистить();
	
	//АБС ВСТАВКА   08.04.2014 11:45:19  Балашенко
	//заполним подразделение и организацию из табличной части документа
	ТаблицаПоСоставПлана.Колонки.Добавить("Подразделение");
	ТаблицаПоСоставПлана.Колонки.Добавить("абс_Организация");
	ТаблицаПоСоставПлана.Колонки.Добавить("абс_БизнесРегион");
	ТаблицаПоСоставПлана.Колонки.Добавить("абс_Территория");
	ТаблицаПоСоставПлана.Колонки.Добавить("абс_Город");
	Для каждого СтрокаТЗ из ТаблицаПоСоставПлана Цикл
		СтрокаТЧ = СоставПлана.Найти(СтрокаТЗ.НомерСтроки,"НомерСтроки");
		Если Не СтрокаТЧ = Неопределено Тогда
			СтрокаТЗ.Подразделение = СтрокаТЧ.абс_Подразделение;
			СтрокаТЗ.абс_Организация = СтрокаТЧ.абс_Организация;
			СтрокаТЗ.абс_БизнесРегион = СтрокаТЧ.абс_БизнесРегион;
			СтрокаТЗ.абс_Территория = СтрокаТЧ.абс_Территория;
			СтрокаТЗ.абс_Город = СтрокаТЧ.абс_Город;
		КонецЕсли;
	КонецЦикла;
	//АБС ВСТАВКА  КОНЕЦ
	
	// Заполним таблицу движений.
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаПоСоставПлана, ТаблицаДвижений);
	
	// Недостающие поля.
	ТаблицаДвижений.ЗаполнитьЗначения(Сценарий, "Сценарий");
	
	//АБС ИЗМЕНЕНИЕ   08.04.2014 11:53:10  Балашенко
	//ТаблицаДвижений.ЗаполнитьЗначения(Подразделение, "Подразделение");
	//
	////абс вставка
	//ТаблицаДвижений.ЗаполнитьЗначения(Организация, "абс_Организация");
	//абс вставка конец
	//АБС ИЗМЕНЕНИЕ  КОНЕЦ
	
	Если УправлениеПроектами.ВедениеУчетаПоПроектам() Тогда
		ТаблицаДвижений.ЗаполнитьЗначения(Проект, "Проект");
	КонецЕсли;	
	
	НаборДвижений.мТаблицаДвижений   = ТаблицаДвижений;
	
	Если Не Отказ Тогда
		
		Движения.ПланыПродаж.ДобавитьДвижение();
		
	КонецЕсли;
	
	//абс вставка 38760
	//Если используется механизм согласования документов ПланПродаж, и документ еще не прошел по маршуту утверждения 
	//	- добавим запись в регистр СостоянияСогласования
	НаборЗаписейСостояниеСогласования = РегистрыСведений.абс_СостоянияСогласованияДокументов.СоздатьНаборЗаписей();
	НаборЗаписейСостояниеСогласования.Отбор.ДокументДляСогласования.Установить(Ссылка);
	НаборЗаписейСостояниеСогласования.Прочитать();
	Если абс_СогласованиеДокументов.ИспользуетсяСогласованиеДокументов(Организация,Перечисления.абс_ТипыДокументовДляСогласования.ПланПродаж,Дата) Тогда
		Если Состояние=Перечисления.СостоянияОбъектов.Подготовлен Тогда
			Запрос=Новый Запрос;
			Запрос.Текст=
			"ВЫБРАТЬ
			|	абс_СостоянияСогласованияДокументовСрезПоследних.Состояние
			|ИЗ
			|	РегистрСведений.абс_СостоянияСогласованияДокументов.СрезПоследних(, ДокументДляСогласования = &Ссылка) КАК абс_СостоянияСогласованияДокументовСрезПоследних";
			Запрос.УстановитьПараметр("Ссылка",Ссылка);
			Выборка=Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				Если Выборка.Состояние=Перечисления.СостоянияОбъектов.Отклонен Тогда
					МаршрутСогласования = абс_СогласованиеДокументов.ОпределитьМаршрутСогласования(Неопределено,Перечисления.абс_ТипыДокументовДляСогласования.ПланПродаж);
					Если НЕ ЗначениеЗаполнено(МаршрутСогласования) Тогда
						Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);
						ОбщегоНазначения.СообщитьОбОшибке("Не удалось определить маршрут согласования документа", Отказ, Заголовок);
						Возврат;
					КонецЕсли;
					
					НоваяЗапись = НаборЗаписейСостояниеСогласования.Добавить();
					
					НоваяЗапись.Период = ТекущаяДата();
					НоваяЗапись.Активность = Истина;
					НоваяЗапись.ДокументДляСогласования = Ссылка;
					НоваяЗапись.Пользователь = Ответственный;
					НоваяЗапись.Этап = МаршрутСогласования;
					НоваяЗапись.Уровень = МаршрутСогласования.Уровень() + 1;
					НоваяЗапись.Состояние = Перечисления.СостоянияОбъектов.Подготовлен;
					
					НаборЗаписейСостояниеСогласования.Записать();
					Возврат;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Если НаборЗаписейСостояниеСогласования.Количество() > 1 Тогда
			//Документ уже пошел по маршруту согласования
			Возврат;
		КонецЕсли;
		МаршрутСогласования = абс_СогласованиеДокументов.ОпределитьМаршрутСогласования(Неопределено,Перечисления.абс_ТипыДокументовДляСогласования.ПланПродаж);
		Если НЕ ЗначениеЗаполнено(МаршрутСогласования) Тогда
			Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);
			ОбщегоНазначения.СообщитьОбОшибке("Не удалось определить маршрут согласования документа", Отказ, Заголовок);
			Возврат;
		КонецЕсли;
		//Уже есть запись с правильным маршрутом согласования
		Если НаборЗаписейСостояниеСогласования.Количество() = 1 И
			НаборЗаписейСостояниеСогласования[0].Этап = МаршрутСогласования Тогда
			Возврат;
		КонецЕсли;
		НаборЗаписейСостояниеСогласования.Очистить();
		НоваяЗапись = НаборЗаписейСостояниеСогласования.Добавить();
		
		НоваяЗапись.Период = ТекущаяДата();
		НоваяЗапись.Активность = Истина;
		НоваяЗапись.ДокументДляСогласования = Ссылка;
		НоваяЗапись.Пользователь = Ответственный;
		НоваяЗапись.Этап = МаршрутСогласования;
		НоваяЗапись.Уровень = МаршрутСогласования.Уровень() + 1;
		НоваяЗапись.Состояние = Перечисления.СостоянияОбъектов.Подготовлен;
		
		НаборЗаписейСостояниеСогласования.Записать();
	ИначеЕсли НаборЗаписейСостояниеСогласования.Количество() > 0 Тогда
		//Если есть записи по этому документу, а согласование не используется - очистим
		НаборЗаписейСостояниеСогласования.Очистить();
		НаборЗаписейСостояниеСогласования.Записать();
	КонецЕсли;
	//абс вставка 38760 конец
	
КонецПроцедуры // ДвиженияПоРегистрам()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура вызывается перед записью документа 
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка  Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ОбработкаТабличныхЧастей.ПриЗаписиПроверитьСтавкуНДС(ЭтотОбъект, СоставПлана);
	
	Для каждого Строка из СоставПлана Цикл
		
		Если НЕ Сценарий.УчетПоКоличеству И Строка.Количество <> 0 Тогда
			
			Строка.Количество =  0;
			
		КонецЕсли;
		
		Если НЕ Сценарий.УчетПоСуммам  
			И (Строка.Цена <> 0 ИЛИ Строка.Сумма <> 0 ИЛИ Строка.СуммаНДС <> 0) 
			Тогда
			
			Строка.Цена = 0;
			Строка.Сумма = 0;
			Строка.СуммаНДС = 0;
			
		КонецЕсли;
		
	КонецЦикла;

	// Посчитать сумму документа и записать ее в соответствующий реквизит шапки для показа в журналах
	СуммаДокумента = УчетНДС.ПолучитьСуммуДокументаСНДС(ЭтотОбъект, "СоставПлана");

	мУдалятьДвижения = НЕ ЭтоНовый();
	
КонецПроцедуры // ПередЗаписью

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если мУдалятьДвижения Тогда
		
		ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ);
		
	КонецЕсли;

	ГраницаФиксации = УправлениеПланированием.ПолучитьГраницуФиксацииПериодов(Перечисления.ВидыПланирования.Продажи, Подразделение, Проект, Сценарий);
	
	Если ДатаПланирования <= ГраницаФиксации Тогда
		
		ОбщегоНазначения.СообщитьОбОшибке("Документ не может быть проведен, поскольку дата планирования находится в закрытом периоде (до " + Формат(ГраницаФиксации, "ДФ=dd.MM.yyyy; ДЛФ=D") + ").", Отказ);
		Возврат;
		
	КонецЕсли;
	
	СтруктураШапкиДокумента = ОбщегоНазначения.СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);

	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);

	// Заполним по шапке документа дерево параметров, нужных при проведении.
	ДеревоПолейЗапросаПоШапке = УправлениеЗапасами.СформироватьДеревоПолейЗапросаПоШапке();
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "Константы", "ВалютаУправленческогоУчета", "ВалютаУправленческогоУчета");
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "Константы", "КурсВалютыУправленческогоУчета", "КурсВалютыУправленческогоУчета");

	// Сформируем запрос на дополнительные параметры, нужные при проведении, по данным шапки документа
	СтруктураШапкиДокумента = УправлениеЗапасами.СформироватьЗапросПоДеревуПолей(ЭтотОбъект, ДеревоПолейЗапросаПоШапке, СтруктураШапкиДокумента, мВалютаРегламентированногоУчета);

	// Проверим правильность заполнения шапки документа
	ПроверитьЗаполнениеШапки(СтруктураШапкиДокумента, Отказ, Заголовок);
	
	// Получим необходимые данные для проведения и проверки заполнения данные по табличной части "СоставПлана".
	СтруктураПолей = УправлениеЗапасами.СформироватьСтруктуруПолейТовары();
	
	СтруктураПолей.Вставить("Набор", "Номенклатура.Набор");
 	СтруктураПолей.Вставить("Комплект", "Номенклатура.Комплект");
	СтруктураПолей.Вставить("Заказ", "Заказ");
	СтруктураПолей.Вставить("Контрагент", "Контрагент");
	СтруктураПолей.Вставить("Договор", "Договор");
	СтруктураПолей.Вставить("Источник", "Источник");
	СтруктураПолей.Вставить("ДокументПланирования", "Источник");
	СтруктураПолей.Вставить("ВариантРаспределения", "ВариантРаспределения");
	СтруктураПолей.Удалить("СтавкаНДС");
	СтруктураПолей.Удалить("СерияНоменклатуры");

	//АБС ВСТАВКА   16.06.2014 9:39:19  Фролов
	Для Н = 1 по 18 Цикл
		СтруктураПолей.Вставить("Сумма" + Строка(н)			, "Сумма" + Строка(н));
		СтруктураПолей.Вставить("Количество" + Строка(н)	, "Количество" + Строка(н));		
	КонецЦикла;
	//АБС ВСТАВКА  КОНЕЦ 
	
	СтруктураСложныхПолей = Новый Структура;
	СтруктураСложныхПолей.Вставить("Период",
	"ВЫБОР
	|	КОГДА РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(Док.Период, " + ?(ПериодичностьДетализации = Перечисления.Периодичность.ПустаяСсылка(), ?(Сценарий.Периодичность = Перечисления.Периодичность.ПустаяСсылка(), "ДЕНЬ", Строка(Сценарий.Периодичность)), Строка(ПериодичностьДетализации)) + "), Док.Ссылка.ДатаПланирования, ДЕНЬ) > 0
	|		ТОГДА Док.Ссылка.ДатаПланирования
	|	ИНАЧЕ НАЧАЛОПЕРИОДА(Док.Период, " + ?(ПериодичностьДетализации = Перечисления.Периодичность.ПустаяСсылка(), ?(Сценарий.Периодичность = Перечисления.Периодичность.ПустаяСсылка(), "ДЕНЬ", Строка(Сценарий.Периодичность)), Строка(ПериодичностьДетализации)) + ")
	|КОНЕЦ");
	РезультатЗапросаПоСоставПлана = УправлениеЗапасами.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "СоставПлана", СтруктураПолей, СтруктураСложныхПолей);
	
	// Подготовим таблицу СоставПлана для проведения.
	//ТаблицаПоСоставПлана = ПодготовитьТаблицуСоставПлана(РезультатЗапросаПоСоставПлана, СтруктураШапкиДокумента);
	ТаблицаПоСоставПлана = абс_ПодготовитьТаблицуСоставПлана(РезультатЗапросаПоСоставПлана, СтруктураШапкиДокумента);
	
	// Проверить заполнение ТЧ "СоставПлана".
	ПроверитьЗаполнениеТабличнойЧастиСоставПлана(ТаблицаПоСоставПлана, СтруктураШапкиДокумента, Отказ, Заголовок);
	
	// Движения по документу
	Если Не Отказ Тогда

		ДвиженияПоРегистрам(РежимПроведения, ТаблицаПоСоставПлана, Отказ, Заголовок);

	КонецЕсли; 

КонецПроцедуры	// ОбработкаПроведения

//абс вставка 38760
Процедура ОбработкаЗаполнения(ДанныеЗаполнения,СтандартнаяОбработка)
	Состояние=Перечисления.СостоянияОбъектов.Подготовлен;
	ДатаПланирования = ТекущаяДата();
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Состояние=Перечисления.СостоянияОбъектов.Подготовлен;
	абс_ЭтапСогласования=Справочники.абс_МаршрутыСогласованияДокументов.ПустаяСсылка();
	абс_УровеньСогласования=0;
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	// Удаление движений по регистру абс_СостоянияСогласованияДокументов, если документ был подготовлен
	НаборЗаписейСостояниеСогласования = РегистрыСведений.абс_СостоянияСогласованияДокументов.СоздатьНаборЗаписей();
	НаборЗаписейСостояниеСогласования.Отбор.ДокументДляСогласования.Установить(Ссылка);
	НаборЗаписейСостояниеСогласования.Прочитать();
	Если НаборЗаписейСостояниеСогласования.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	Если абс_СогласованиеДокументов.ИспользуетсяСогласованиеДокументов(Организация,Перечисления.абс_ТипыДокументовДляСогласования.ПланПродаж,Дата) Тогда
		Если НаборЗаписейСостояниеСогласования.Количество() > 1 Тогда
			//Документ уже пошел по маршруту согласования, не следует ничего менять
			Возврат;
		КонецЕсли;
		//Если в наборе записей только одна запись - значит документ только подготовлен.
		//Очистим набор записей
		НаборЗаписейСостояниеСогласования.Очистить();
		НаборЗаписейСостояниеСогласования.Записать();
	ИначеЕсли НаборЗаписейСостояниеСогласования.Количество() > 0 Тогда
		//Если есть записи по этому документу, а согласование не используется - очистим
		НаборЗаписейСостояниеСогласования.Очистить();
		НаборЗаписейСостояниеСогласования.Записать();
	КонецЕсли;
КонецПроцедуры

//абс вставка 38760 конец 

мВалютаРегламентированногоУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");