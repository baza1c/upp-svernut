&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Загрузка = ПолучитьТаблицуПоДокументу();
			
КонецПроцедуры

&НаКлиенте
Процедура Загрузить(Команда)
	
	Оповестить("абс_ПланСкладскогоОстатка_ЗагрузкаИзEXCEL", Загрузка);
	Закрыть();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТаблицуПоДокументу() Экспорт
	
	обОбъект = РеквизитФормыВЗначение("Объект");
	дНачалоПериодаПланирования = НачалоДня(обОбъект.НачалоПериода);
	дКонецПериодаПланирования = НачалоДня(обОбъект.КонецПериода);
	
	Макет = Документы.абс_ПланСкладскогоОстатка.ПолучитьМакет("ЗагрузкаEXCEL");
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.Очистить();
	
	ОбластьШапкаКлиентНоменклатура 	= Макет.ПолучитьОбласть("Шапка|КлиентНоменклатура");
	ОбластьШапкаПериод 				= Макет.ПолучитьОбласть("Шапка|Период");
	ОбластьСтрокаКлиентНоменклатура = Макет.ПолучитьОбласть("Строка|КлиентНоменклатура");
	ОбластьСтрокаПериод			 	= Макет.ПолучитьОбласть("Строка|Период");
	
	//нарисуем шапку
	ТабДок.Вывести(ОбластьШапкаКлиентНоменклатура);
	
	ВремДень = дНачалоПериодаПланирования;
	
	Пока ВремДень <= дКонецПериодаПланирования Цикл
		ОбластьШапкаПериод.Параметры.Период = ВремДень;		
		ТабДок.Присоединить(ОбластьШапкаПериод);	
		ВремДень = ВремДень + 86400
	КонецЦикла;
	//
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(ПланПроизводстваСоставПлана.Период, ДЕНЬ) КАК Период,
		|	ПланПроизводстваСоставПлана.Номенклатура КАК Номенклатура,
		|	ПланПроизводстваСоставПлана.Номенклатура.Артикул КАК Артикул,
		|	ПланПроизводстваСоставПлана.ПрогнозСкладскогоОстатка КАК Количество
		|ИЗ
		|	Документ.абс_ПланСкладскогоОстатка.МодельСкладскогоОстатка КАК ПланПроизводстваСоставПлана
		|ГДЕ
		|	ПланПроизводстваСоставПлана.Ссылка = &Ссылка
		|	И ПланПроизводстваСоставПлана.Номенклатура ССЫЛКА Справочник.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период
		|ИТОГИ
		|	СУММА(Количество)
		|ПО
		|	Номенклатура,
		|	Период ПЕРИОДАМИ(ДЕНЬ, &НачПериода, &КонПериода)";

	Запрос.УстановитьПараметр("Ссылка", обОбъект.Ссылка);
	Запрос.УстановитьПараметр("НачПериода", дНачалоПериодаПланирования);
	Запрос.УстановитьПараметр("КонПериода", дКонецПериодаПланирования);

	Результат = Запрос.Выполнить();

	ВыборкаКлиентНоменклатура = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Пока ВыборкаКлиентНоменклатура.Следующий() Цикл
		
		ОбластьСтрокаКлиентНоменклатура.Параметры.Заполнить(ВыборкаКлиентНоменклатура);
	  	ТабДок.Вывести(ОбластьСтрокаКлиентНоменклатура);

		ВыборкаПериод = ВыборкаКлиентНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Период", "ВСЕ");

		Пока ВыборкаПериод.Следующий() Цикл
			ОбластьСтрокаПериод.Параметры.Заполнить(ВыборкаПериод);
			ТабДок.Присоединить(ОбластьСтрокаПериод);
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции

