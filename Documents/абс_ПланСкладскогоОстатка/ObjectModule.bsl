//заполняет табличные части документа
Процедура ЗаполнитьДанныеДляРасчета(Объект) Экспорт
	Запрос=Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	ПланыПродажОбороты.Номенклатура КАК Номенклатура,
	|	ПланыПродажОбороты.КоличествоОборот КАК Количество,
	|	ПланыПродажОбороты.Период,
	|	ПланыПродажОбороты.Номенклатура.ЕдиницаХраненияОстатков КАК ЕдиницаИзмерения,
	|	ЕСТЬNULL(ПланыПродажОбороты.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент, 1) КАК Коэффициент
	|ПОМЕСТИТЬ ПланПродажМесяц
	|ИЗ
	|	РегистрНакопления.ПланыПродаж.Обороты(НАЧАЛОПЕРИОДА(&НачалоПериода, МЕСЯЦ), &КонецПериода, Месяц, Сценарий = &Сценарий) КАК ПланыПродажОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПланПродажМесяц.Номенклатура КАК Номенклатура,
	|	ПланПродажМесяц.Номенклатура.ЕдиницаХраненияОстатков КАК ЕдиницаИзмерения,
	|	ПланПродажМесяц.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент КАК Коэффициент
	|ПОМЕСТИТЬ ТаблицаНоменклатуры
	|ИЗ
	|	ПланПродажМесяц КАК ПланПродажМесяц
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНоменклатуры.Номенклатура КАК Номенклатура,
	|	ЕСТЬNULL(абс_СтраховойЗапасСрезПоследних.Дней, Константы.абс_КоличествоДнейНеснижаемогоОстатка) КАК Дней
	|ИЗ
	|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.абс_СтраховойЗапас.СрезПоследних(
	|				&НачалоПериода,
	|				Номенклатура В
	|					(ВЫБРАТЬ
	|						ТаблицаНоменклатуры.Номенклатура
	|					ИЗ
	|						ТаблицаНоменклатуры КАК ТаблицаНоменклатуры)) КАК абс_СтраховойЗапасСрезПоследних
	|		ПО ТаблицаНоменклатуры.Номенклатура = абс_СтраховойЗапасСрезПоследних.Номенклатура,
	|	Константы КАК Константы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ЗаказыПокупателейОстатки.КоличествоОстаток, 0) * ЕСТЬNULL(ЗаказыПокупателейОстатки.ЕдиницаИзмерения.Коэффициент, 1) / ЕСТЬNULL(ТаблицаНоменклатуры.Коэффициент, 1) КАК КоличествоЗаказ,
	|	ЗаказыПокупателейОстатки.Номенклатура,
	|	ЗаказыПокупателейОстатки.ЗаказПокупателя,
	|	ЗаказыПокупателейОстатки.ЗаказПокупателя.ДатаОтгрузки КАК ДатаОтгрузки
	|ИЗ
	|	РегистрНакопления.ЗаказыПокупателей.Остатки(
	|			&НачалоПериода,
	|			Номенклатура В
	|					(ВЫБРАТЬ
	|						ТаблицаНоменклатуры.Номенклатура
	|					ИЗ
	|						ТаблицаНоменклатуры КАК ТаблицаНоменклатуры)
	|				И (ЗаказПокупателя.ДатаОтгрузки МЕЖДУ &НачалоПериода И ДОБАВИТЬКДАТЕ(&КонецПериода, МЕСЯЦ, 1))) КАК ЗаказыПокупателейОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|		ПО ЗаказыПокупателейОстатки.Номенклатура = ТаблицаНоменклатуры.Номенклатура
	|ГДЕ
	|	ЗаказыПокупателейОстатки.КоличествоОстаток > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНоменклатуры.Номенклатура,
	|	ТоварыНаСкладахОстатки.КоличествоОстаток КАК Количество
	|ИЗ
	|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыНаСкладах.Остатки(
	|				&НачалоПериода,
	|				Склад В (&МассивСкладов)
	|					И Номенклатура В
	|						(ВЫБРАТЬ
	|							ТаблицаНоменклатуры.Номенклатура
	|						ИЗ
	|							ТаблицаНоменклатуры КАК ТаблицаНоменклатуры)) КАК ТоварыНаСкладахОстатки
	|		ПО ТаблицаНоменклатуры.Номенклатура = ТоварыНаСкладахОстатки.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПланыПродажОбороты.Период,
	|	ПланыПродажОбороты.Номенклатура,
	|	ПланыПродажОбороты.КоличествоОборот КАК КоличествоПлан
	|ИЗ
	|	РегистрНакопления.ПланыПродаж.Обороты(
	|			НАЧАЛОПЕРИОДА(&НачалоПериода, МЕСЯЦ),
	|			ДОБАВИТЬКДАТЕ(&КонецПериода, МЕСЯЦ, 1),
	|			Месяц,
	|			Номенклатура В
	|				(ВЫБРАТЬ
	|					ТаблицаНоменклатуры.Номенклатура
	|				ИЗ
	|					ТаблицаНоменклатуры КАК ТаблицаНоменклатуры)) КАК ПланыПродажОбороты";
	
	Запрос.УстановитьПараметр("НачалоПериода",НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",КонецПериода);
	Запрос.УстановитьПараметр("Сценарий",СценарийПрогноза18);
	МассивСкладов=Склады.Выгрузить(,"Склад");
	Запрос.УстановитьПараметр("МассивСкладов",МассивСкладов);
	
	Результат=Запрос.ВыполнитьПакет();
	
	//заполнение ТЧ План продаж и Заказы
	Если Результат[5].Пустой() Тогда
		Возврат;
	КонецЕсли;
	Выборка=Результат[5].Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаПлан=Объект.ПланПродаж.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПлан,Выборка);
	КонецЦикла;
	
	//заполнение ТЧ Страховой запас
	Выборка=Результат[2].Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока=Объект.СтраховойЗапас.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Выборка);
	КонецЦикла;
	
	//заполнение ТЧ Складские остатки
	Выборка=Результат[4].Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока=Объект.СкладскиеОстатки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Выборка);
	КонецЦикла;
	
	//заполнение ТЧ Заказы
	Выборка=Результат[3].Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока=Объект.Заказы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Выборка);
	КонецЦикла;
	
КонецПроцедуры

//процедура расчета ТЧ МодельСкладскогоОстатка
Процедура РассчитатьМодельСкладскогоОстатка(Объект) Экспорт
	
	Запрос=Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	абс_ПланСкладскогоОстаткаПланПродаж.Период,
	|	абс_ПланСкладскогоОстаткаПланПродаж.Номенклатура КАК Номенклатура,
	|	абс_ПланСкладскогоОстаткаПланПродаж.КоличествоПлан
	|ПОМЕСТИТЬ ПланПродажМесяц
	|ИЗ
	|	Документ.абс_ПланСкладскогоОстатка.ПланПродаж КАК абс_ПланСкладскогоОстаткаПланПродаж
	|ГДЕ
	|	абс_ПланСкладскогоОстаткаПланПродаж.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПланПродажМесяц.Период
	|ПОМЕСТИТЬ ТаблицаПериодов
	|ИЗ
	|	ПланПродажМесяц КАК ПланПродажМесяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПланПродажМесяц.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ ТаблицаНоменклатуры
	|ИЗ
	|	ПланПродажМесяц КАК ПланПродажМесяц
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	абс_ПланСкладскогоОстаткаСтраховойЗапас.Номенклатура,
	|	абс_ПланСкладскогоОстаткаСтраховойЗапас.Дней
	|ПОМЕСТИТЬ СтраховойЗапас
	|ИЗ
	|	Документ.абс_ПланСкладскогоОстатка.СтраховойЗапас КАК абс_ПланСкладскогоОстаткаСтраховойЗапас
	|ГДЕ
	|	абс_ПланСкладскогоОстаткаСтраховойЗапас.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегламентированныйПроизводственныйКалендарь.ДатаКалендаря,
	|	РегламентированныйПроизводственныйКалендарь.Пятидневка КАК Пятидневка
	|ПОМЕСТИТЬ ПроизводственныйКалендарь
	|ИЗ
	|	РегистрСведений.РегламентированныйПроизводственныйКалендарь КАК РегламентированныйПроизводственныйКалендарь
	|ГДЕ
	|	РегламентированныйПроизводственныйКалендарь.ДатаКалендаря МЕЖДУ &НачалоПериода И &КонецПериода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроизводственныйКалендарь.ДатаКалендаря,
	|	ПроизводственныйКалендарь.Пятидневка,
	|	ТаблицаНоменклатуры.Номенклатура,
	|	СтраховойЗапас.Дней
	|ПОМЕСТИТЬ НоменклатураПоДням
	|ИЗ
	|	ПроизводственныйКалендарь КАК ПроизводственныйКалендарь
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|			ЛЕВОЕ СОЕДИНЕНИЕ СтраховойЗапас КАК СтраховойЗапас
	|			ПО ТаблицаНоменклатуры.Номенклатура = СтраховойЗапас.Номенклатура
	|		ПО (ИСТИНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПериодов.Период,
	|	СУММА(РегламентированныйПроизводственныйКалендарь.Пятидневка) КАК РабочихДней
	|ПОМЕСТИТЬ ИтогРабочихДней
	|ИЗ
	|	ТаблицаПериодов КАК ТаблицаПериодов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегламентированныйПроизводственныйКалендарь КАК РегламентированныйПроизводственныйКалендарь
	|		ПО (ТаблицаПериодов.Период = НАЧАЛОПЕРИОДА(РегламентированныйПроизводственныйКалендарь.ДатаКалендаря, МЕСЯЦ))
	|ГДЕ
	|	РегламентированныйПроизводственныйКалендарь.Пятидневка = 1
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаПериодов.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	абс_ПланСкладскогоОстаткаСкладскиеОстатки.Номенклатура,
	|	абс_ПланСкладскогоОстаткаСкладскиеОстатки.Количество
	|ПОМЕСТИТЬ СкладскиеОстатки
	|ИЗ
	|	Документ.абс_ПланСкладскогоОстатка.СкладскиеОстатки КАК абс_ПланСкладскогоОстаткаСкладскиеОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КалендарьДатаНачала.ДатаКалендаря КАК ДатаНачала,
	|	КалендарьДатаОкончания.ДатаКалендаря КАК ДатаОкончания
	|ПОМЕСТИТЬ ПериодыКалендаря
	|ИЗ
	|	РегистрСведений.РегламентированныйПроизводственныйКалендарь КАК КалендарьДатаНачала
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегламентированныйПроизводственныйКалендарь КАК КалендарьДатаОкончания
	|		ПО КалендарьДатаНачала.ДатаКалендаря <= КалендарьДатаОкончания.ДатаКалендаря
	|ГДЕ
	|	КалендарьДатаНачала.ДатаКалендаря МЕЖДУ &НачалоПериода И ДОБАВИТЬКДАТЕ(&КонецПериода, МЕСЯЦ, 1)
	|	И КалендарьДатаОкончания.ДатаКалендаря МЕЖДУ &НачалоПериода И ДОБАВИТЬКДАТЕ(&КонецПериода, МЕСЯЦ, 1)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыКалендаря.ДатаНачала,
	|	ПериодыКалендаря.ДатаОкончания,
	|	СУММА(РегламентированныйПроизводственныйКалендарь.Пятидневка) КАК ПериодРабочихДней
	|ПОМЕСТИТЬ РабочиеДниПериодаКалендаря
	|ИЗ
	|	ПериодыКалендаря КАК ПериодыКалендаря
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегламентированныйПроизводственныйКалендарь КАК РегламентированныйПроизводственныйКалендарь
	|		ПО (РегламентированныйПроизводственныйКалендарь.ДатаКалендаря МЕЖДУ ПериодыКалендаря.ДатаНачала И ПериодыКалендаря.ДатаОкончания)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПериодыКалендаря.ДатаОкончания,
	|	ПериодыКалендаря.ДатаНачала
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	абс_ПланСкладскогоОстаткаЗаказы.Номенклатура,
	|	абс_ПланСкладскогоОстаткаЗаказы.ДатаОтгрузки,
	|	абс_ПланСкладскогоОстаткаЗаказы.КоличествоЗаказ
	|ПОМЕСТИТЬ Заказы
	|ИЗ
	|	Документ.абс_ПланСкладскогоОстатка.Заказы КАК абс_ПланСкладскогоОстаткаЗаказы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураПоДням.ДатаКалендаря КАК Период,
	|	НоменклатураПоДням.Номенклатура,
	|	СУММА(ВЫБОР
	|			КОГДА ВЫБОР
	|					КОГДА НоменклатураПоДням.Пятидневка = 1
	|						ТОГДА ПланПродажМесяц.КоличествоПлан / ИтогРабочихДней.РабочихДней
	|					ИНАЧЕ 0
	|				КОНЕЦ > ЕСТЬNULL(Заказы.КоличествоЗаказ, 0)
	|				ТОГДА ВЫБОР
	|						КОГДА НоменклатураПоДням.Пятидневка = 1
	|							ТОГДА ПланПродажМесяц.КоличествоПлан / ИтогРабочихДней.РабочихДней
	|						ИНАЧЕ 0
	|					КОНЕЦ
	|			ИНАЧЕ ЕСТЬNULL(Заказы.КоличествоЗаказ, 0)
	|		КОНЕЦ) КАК КоличествоПлан
	|ПОМЕСТИТЬ ПланПоДням
	|ИЗ
	|	НоменклатураПоДням КАК НоменклатураПоДням
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИтогРабочихДней КАК ИтогРабочихДней
	|		ПО (НАЧАЛОПЕРИОДА(НоменклатураПоДням.ДатаКалендаря, МЕСЯЦ) = ИтогРабочихДней.Период)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланПродажМесяц КАК ПланПродажМесяц
	|		ПО НоменклатураПоДням.Номенклатура = ПланПродажМесяц.Номенклатура
	|			И (НАЧАЛОПЕРИОДА(НоменклатураПоДням.ДатаКалендаря, МЕСЯЦ) = ПланПродажМесяц.Период)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Заказы КАК Заказы
	|		ПО НоменклатураПоДням.Номенклатура = Заказы.Номенклатура
	|			И НоменклатураПоДням.ДатаКалендаря = Заказы.ДатаОтгрузки
	|
	|СГРУППИРОВАТЬ ПО
	|	НоменклатураПоДням.ДатаКалендаря,
	|	НоменклатураПоДням.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураПоДням.ДатаКалендаря,
	|	НоменклатураПоДням.Номенклатура,
	|	МИНИМУМ(РабочиеДниПериодаКалендаря.ДатаОкончания) КАК ДатаОкончания,
	|	МИНИМУМ(НоменклатураПоДням.Дней) КАК Дней
	|ПОМЕСТИТЬ НоменклатураПоДнямСДатойОкончания
	|ИЗ
	|	НоменклатураПоДням КАК НоменклатураПоДням
	|		ЛЕВОЕ СОЕДИНЕНИЕ РабочиеДниПериодаКалендаря КАК РабочиеДниПериодаКалендаря
	|		ПО НоменклатураПоДням.ДатаКалендаря = РабочиеДниПериодаКалендаря.ДатаНачала
	|			И НоменклатураПоДням.Дней = РабочиеДниПериодаКалендаря.ПериодРабочихДней
	|
	|СГРУППИРОВАТЬ ПО
	|	НоменклатураПоДням.ДатаКалендаря,
	|	НоменклатураПоДням.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураПоДнямСДатойОкончания.ДатаКалендаря,
	|	НоменклатураПоДнямСДатойОкончания.Номенклатура,
	|	СУММА(ПланПоДням.КоличествоПлан) КАК УровеньОстатка,
	|	НоменклатураПоДнямСДатойОкончания.Дней,
	|	НоменклатураПоДнямСДатойОкончания.ДатаОкончания
	|ПОМЕСТИТЬ НеснижаемыйОстаток
	|ИЗ
	|	НоменклатураПоДнямСДатойОкончания КАК НоменклатураПоДнямСДатойОкончания
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланПоДням КАК ПланПоДням
	|		ПО (ПланПоДням.Период МЕЖДУ НоменклатураПоДнямСДатойОкончания.ДатаКалендаря И НоменклатураПоДнямСДатойОкончания.ДатаОкончания)
	|			И НоменклатураПоДнямСДатойОкончания.Номенклатура = ПланПоДням.Номенклатура
	|ГДЕ
	|	НоменклатураПоДнямСДатойОкончания.ДатаКалендаря МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|СГРУППИРОВАТЬ ПО
	|	НоменклатураПоДнямСДатойОкончания.ДатаКалендаря,
	|	НоменклатураПоДнямСДатойОкончания.Номенклатура,
	|	НоменклатураПоДнямСДатойОкончания.Дней,
	|	НоменклатураПоДнямСДатойОкончания.ДатаОкончания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НеснижаемыйОстаток.ДатаКалендаря,
	|	НеснижаемыйОстаток.Номенклатура,
	|	НеснижаемыйОстаток.УровеньОстатка,
	|	НеснижаемыйОстаток.Дней,
	|	НеснижаемыйОстаток.ДатаОкончания,
	|	МАКСИМУМ(СкладскиеОстатки.Количество) КАК КоличествоОстаток,
	|	СУММА(ЕСТЬNULL(ПланПоДням.КоличествоПлан, 0)) КАК КоличествоПлан
	|ПОМЕСТИТЬ НеснижааемыйОстатокСоСкладом
	|ИЗ
	|	НеснижаемыйОстаток КАК НеснижаемыйОстаток
	|		ЛЕВОЕ СОЕДИНЕНИЕ СкладскиеОстатки КАК СкладскиеОстатки
	|		ПО НеснижаемыйОстаток.Номенклатура = СкладскиеОстатки.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланПоДням КАК ПланПоДням
	|		ПО (ПланПоДням.Период МЕЖДУ &НачалоПериода И ДОБАВИТЬКДАТЕ(НеснижаемыйОстаток.ДатаКалендаря, ДЕНЬ, -1))
	|
	|СГРУППИРОВАТЬ ПО
	|	НеснижаемыйОстаток.ДатаОкончания,
	|	НеснижаемыйОстаток.ДатаКалендаря,
	|	НеснижаемыйОстаток.Номенклатура,
	|	НеснижаемыйОстаток.УровеньОстатка,
	|	НеснижаемыйОстаток.Дней
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НеснижаемыйОстаток.ДатаКалендаря КАК Период,
	|	НеснижаемыйОстаток.Номенклатура КАК Номенклатура,
	|	НеснижаемыйОстаток.УровеньОстатка КАК УровеньНеснижаемогоОстатка,
	|	МАКСИМУМ(СкладскиеОстатки.Количество) - СУММА(ЕСТЬNULL(ПланПоДнямОстаток.КоличествоПлан, 0)) КАК ПрогнозСкладскогоОстатка,
	|	ПланПоДнямПродажи.КоличествоПлан КАК ПрогнозПродаж
	|ИЗ
	|	НеснижаемыйОстаток КАК НеснижаемыйОстаток
	|		ЛЕВОЕ СОЕДИНЕНИЕ СкладскиеОстатки КАК СкладскиеОстатки
	|		ПО НеснижаемыйОстаток.Номенклатура = СкладскиеОстатки.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланПоДням КАК ПланПоДнямОстаток
	|		ПО (ПланПоДнямОстаток.Период МЕЖДУ &НачалоПериода И ДОБАВИТЬКДАТЕ(НеснижаемыйОстаток.ДатаКалендаря, ДЕНЬ, -1))
	|			И НеснижаемыйОстаток.Номенклатура = ПланПоДнямОстаток.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланПоДням КАК ПланПоДнямПродажи
	|		ПО НеснижаемыйОстаток.Номенклатура = ПланПоДнямПродажи.Номенклатура
	|			И НеснижаемыйОстаток.ДатаКалендаря = ПланПоДнямПродажи.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	НеснижаемыйОстаток.ДатаКалендаря,
	|	НеснижаемыйОстаток.Номенклатура,
	|	НеснижаемыйОстаток.УровеньОстатка,
	|	ПланПоДнямПродажи.КоличествоПлан
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура,
	|	Период";
	Запрос.УстановитьПараметр("НачалоПериода",НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",КонецПериода);
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Выборка=Запрос.Выполнить().Выбрать();
	//заполнение ТЧ Модель складского остатка
	Пока Выборка.Следующий() Цикл
		НоваяСтрока=Объект.МодельСкладскогоОстатка.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Выборка);
	КонецЦикла;;
		
КонецПроцедуры

Процедура ЗаполнитьТЧДокументаПоТаблице(Объект, ТабДок) Экспорт
	
	//рассчитаем количество строк
	СчСтрок = 2;
	СчКолонок = 1;
	
	ТекДанные = ТабДок.Область(СчСтрок, СчКолонок).Текст;
	     
	Пока ЗначениеЗаполнено(ТекДанные) Цикл
		
		СчСтрок = СчСтрок + 1;
		
		ТекДанные = ТабДок.Область(СчСтрок, СчКолонок).Текст;		
		
	КонецЦикла;
	
	КоличествоСтрок = СчСтрок-1;
	
	
	//рассчитаем количество строк
	СчСтрок = 1;
	СчКолонок = 3;
	
	ТекДанные = ТабДок.Область(СчСтрок, СчКолонок).Текст;
	     
	Пока ЗначениеЗаполнено(ТекДанные) Цикл
		
		СчКолонок = СчКолонок + 1;
		
		ТекДанные = ТабДок.Область(СчСтрок, СчКолонок).Текст;		
		
	КонецЦикла;
	
	КоличествоКолонок = СчКолонок-1;
		
	//считаем данные
	тзТЗ = Новый ТаблицаЗначений;
	тзТЗ.Колонки.Добавить("Артикул");
	тзТЗ.Колонки.Добавить("КодКлиента");
	тзТЗ.Колонки.Добавить("Номенклатура");
	тзТЗ.Колонки.Добавить("ЕдиницаИзмерения");
	тзТЗ.Колонки.Добавить("Коэффициент");
	тзТЗ.Колонки.Добавить("Количество");
	тзТЗ.Колонки.Добавить("Сумма");
	тзТЗ.Колонки.Добавить("СтавкаНДС");
	тзТЗ.Колонки.Добавить("Контрагент");
	тзТЗ.Колонки.Добавить("СуммаНДС");
	тзТЗ.Колонки.Добавить("Цена");
	тзТЗ.Колонки.Добавить("Период");
	
	Для СчСтрок = 2 По КоличествоСтрок Цикл
		
		сТекНоменклатура = СокрЛП(ТабДок.Область(СчСтрок, 1).Текст);
		
		Для СчКолонок = 3 По КоличествоКолонок Цикл
			
			сТекПериод = СокрЛП(ТабДок.Область(1, СчКолонок).Текст);
			дТекПериод = Дата(Сред(сТекПериод,7,4)+Сред(сТекПериод,4,2)+Лев(сТекПериод,2));
			
			СтрокаТЧ = тзТЗ.Добавить();
			
			
			СтрокаТЧ.Период = дТекПериод;
			
			СтрокаТЧ.Артикул = СтрЗаменить(СокрЛП(сТекНоменклатура), Символы.НПП, "");
			
			сЗначениеВЯчейке = СокрЛП(ТабДок.Область(СчСтрок, СчКолонок).Текст);
			Попытка
				чЗначениеВЯчейке = Число(сЗначениеВЯчейке);
			Исключение
				чЗначениеВЯчейке = 0;
			КонецПопытки;
			
			СтрокаТЧ.Количество = чЗначениеВЯчейке;
			
		КонецЦикла;
		
	КонецЦикла;
	
	тзТЗ.Свернуть("Период,Артикул,Номенклатура,ЕдиницаИзмерения,Коэффициент,СтавкаНДС,Цена", "Количество,Сумма,СуммаНДС");
	
	мНоменклатура = тзТЗ.ВыгрузитьКолонку("Артикул");
	//мКлиенты = тзТЗ.ВыгрузитьКолонку("КодКлиента");

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Номенклатура,
		|	Номенклатура.Артикул,
		|	Номенклатура.ЕдиницаХраненияОстатков КАК ЕдиницаИзмерения,
		|	Номенклатура.ЕдиницаХраненияОстатков.Коэффициент КАК Коэффициент,
		|	Номенклатура.СтавкаНДС
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.Артикул В(&мНоменклатура)";

	Запрос.УстановитьПараметр("мНоменклатура", мНоменклатура);

	Результаты = Запрос.ВыполнитьПакет();
	тзНоменклатура = Результаты[0].Выгрузить();
	тзНоменклатура.Индексы.Добавить("Артикул");
	
	Для Каждого СтрокаТЗ Из тзТЗ Цикл
		
		ТекСтрокаНоменклатуры = тзНоменклатура.Найти(СтрокаТЗ.Артикул, "Артикул");
		Если ТекСтрокаНоменклатуры = Неопределено Тогда
			Сообщить("Не найдена номенклатура""" + СтрокаТЗ.Артикул + """!");
			Продолжить;
		КонецЕсли;
			
		ЗаполнитьЗначенияСвойств(СтрокаТЗ, ТекСтрокаНоменклатуры);
				
	КонецЦикла;
	
	КоличествоСтрок = тзТЗ.Количество();
	Для Сч = 1 По КоличествоСтрок Цикл
		ТекСтрока = тзТЗ[КоличествоСтрок-Сч];
		Если ТекСтрока.Количество = 0
			Тогда
			тзТЗ.Удалить(ТекСтрока);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ТекСтрока из тзТЗ Цикл
		
		НоваяСтрока = Объект.МодельСкладскогоОстатка.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
		НоваяСтрока.ПрогнозСкладскогоОстатка = ТекСтрока.Количество;
		
	КонецЦикла;

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения,СтандартнаяОбработка)
	Ответственный=ПараметрыСеанса.ТекущийПользователь;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.абс_ПрогнозСкладскогоОстатка.Записывать = Истина;
	Движения.абс_ПрогнозСкладскогоОстатка.Очистить();

	Запрос=Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	абс_ПланСкладскогоОстаткаМодельСкладскогоОстатка.Период КАК ДатаОстатка,
	|	абс_ПланСкладскогоОстаткаМодельСкладскогоОстатка.Номенклатура,
	|	абс_ПланСкладскогоОстаткаМодельСкладскогоОстатка.ПрогнозСкладскогоОстатка,
	|	абс_ПланСкладскогоОстаткаМодельСкладскогоОстатка.ПрогнозПродаж,
	|	абс_ПланСкладскогоОстаткаМодельСкладскогоОстатка.УровеньНеснижаемогоОстатка,
	|	ЕСТЬNULL(абс_ПланСкладскогоОстаткаПланПроизводства.КоличествоПлан, 0) КАК ПланПроизводства,
	|	абс_ПланСкладскогоОстаткаСтраховойЗапас.Дней КАК СтраховойЗапас
	|ИЗ
	|	Документ.абс_ПланСкладскогоОстатка.МодельСкладскогоОстатка КАК абс_ПланСкладскогоОстаткаМодельСкладскогоОстатка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.абс_ПланСкладскогоОстатка.ПланПроизводства КАК абс_ПланСкладскогоОстаткаПланПроизводства
	|		ПО абс_ПланСкладскогоОстаткаМодельСкладскогоОстатка.Период = абс_ПланСкладскогоОстаткаПланПроизводства.Период
	|			И абс_ПланСкладскогоОстаткаМодельСкладскогоОстатка.Номенклатура = абс_ПланСкладскогоОстаткаПланПроизводства.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.абс_ПланСкладскогоОстатка.СтраховойЗапас КАК абс_ПланСкладскогоОстаткаСтраховойЗапас
	|		ПО абс_ПланСкладскогоОстаткаМодельСкладскогоОстатка.Номенклатура = абс_ПланСкладскогоОстаткаСтраховойЗапас.Номенклатура";
	
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	
	Выборка=Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.абс_ПрогнозСкладскогоОстатка.Добавить();
		Движение.Период = Дата;
		Движение.Сценарий = СценарийПрогнозаСкладскогоОстатка;
		ЗаполнитьЗначенияСвойств(Движение,Выборка);
	КонецЦикла;
	
КонецПроцедуры

