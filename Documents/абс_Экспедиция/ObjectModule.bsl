Процедура РаспределитьРасходы() Экспорт
	
	Отказ = Ложь;
	ПроверкаСуммыДолейТЧЗаказы(Отказ);
	
	Если Не Отказ Тогда
		
		СуммаВсего = СуммаСчетаТК;
		ДолейВсего = 100;
		
		Для Каждого Строка Из Заказы Цикл
			
			Строка.Расходы = СуммаВсего * Строка.Доля / ДолейВсего;
			СуммаВсего = СуммаВсего - Строка.Расходы;
			ДолейВсего = ДолейВсего - Строка.Доля;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверкаСуммыДолейТЧЗаказы(Отказ)
	
	Если Заказы.Количество() И Заказы.Итог("Доля") <> 100 Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Сумма долей расходов (колонка ""%"") ТЧ Заказы должна равняться 100";
		Сообщение.Сообщить();
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	//--------(1)Движения по регистру абс_ЗатратыПоЭкспедиции----------
	Движения.абс_ЗатратыПоЭкспедиции.Очистить();
	Движения.абс_ЗатратыПоЭкспедиции.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	абс_ЭкспедицияЗаказы.НомерЗадания КАК Заявка,
	               |	СУММА(абс_ЭкспедицияЗаказы.Расходы) КАК Расходы,
	               |	абс_ЭкспедицияЗаказы.ТранспортныйМаршрут,
	               |	абс_ЭкспедицияЗаказы.Ссылка.ТранспортнаяКомпания,
	               |	абс_ЭкспедицияЗаказы.Контрагент,
	               |	СУММА(абс_ЭкспедицияЗаказы.СуммаДокумента) КАК СуммаДокумента,
	               |	абс_ЭкспедицияЗаказы.Доля
	               |ПОМЕСТИТЬ ВТРасходыПоЗаявкам
	               |ИЗ
	               |	Документ.абс_Экспедиция.Заказы КАК абс_ЭкспедицияЗаказы
	               |ГДЕ
	               |	абс_ЭкспедицияЗаказы.Ссылка = &Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	абс_ЭкспедицияЗаказы.НомерЗадания,
	               |	абс_ЭкспедицияЗаказы.ТранспортныйМаршрут,
	               |	абс_ЭкспедицияЗаказы.Ссылка.ТранспортнаяКомпания,
	               |	абс_ЭкспедицияЗаказы.Контрагент,
	               |	абс_ЭкспедицияЗаказы.Доля
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТРасходыПоЗаявкам.Заявка,
	               |	МАКСИМУМ(ВТРасходыПоЗаявкам.Расходы) КАК Расходы,
	               //|	СУММА(ЕСТЬNULL(абс_ЗаявкаНаТранспортТовары.Объем, 0)) КАК Объем,
				   
				   |    МАКСИМУМ(абс_ЗаявкаНаТранспортТовары.ссылка.ВсегоКоличествоПалет) КАК Объем,
				   
	               |	СУММА(ЕСТЬNULL(абс_ЗаявкаНаТранспортТовары.ВесБрутто, 0)) КАК Вес,
	               |	абс_ЗаявкаНаТранспортТовары.Ссылка.Подразделение,
	               |	ВТРасходыПоЗаявкам.ТранспортныйМаршрут,
	               |	ВТРасходыПоЗаявкам.ТранспортнаяКомпания,
	               |	ВТРасходыПоЗаявкам.Контрагент,
	               |	МАКСИМУМ(ВТРасходыПоЗаявкам.СуммаДокумента) КАК СуммаДокумента,
	               |	ВТРасходыПоЗаявкам.Доля
	               |ИЗ
	               |	ВТРасходыПоЗаявкам КАК ВТРасходыПоЗаявкам
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.абс_ЗаявкаНаТранспорт.Товары КАК абс_ЗаявкаНаТранспортТовары
	               |		ПО (абс_ЗаявкаНаТранспортТовары.Ссылка = ВТРасходыПоЗаявкам.Заявка)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТРасходыПоЗаявкам.Заявка,
	               |	абс_ЗаявкаНаТранспортТовары.Ссылка.Подразделение,
	               |	ВТРасходыПоЗаявкам.ТранспортныйМаршрут,
	               |	ВТРасходыПоЗаявкам.ТранспортнаяКомпания,
	               |	ВТРасходыПоЗаявкам.Контрагент,
	               |	ВТРасходыПоЗаявкам.Доля";
	
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.абс_ЗатратыПоЭкспедиции.Добавить();
		Движение.Период           	  = Дата;
		Движение.Организация      	  = Организация;
		Движение.ЗаявкаНаТранспорт 	  = Выборка.Заявка;
		Движение.Сумма            	  = Выборка.Расходы;
		Движение.Вес              	  = Выборка.Вес;
		Движение.Объем            	  = Выборка.Объем;
		Движение.Подразделение	  	  = Выборка.Подразделение;
		Движение.ТранспортнаяКомпания = Выборка.ТранспортнаяКомпания;
		Движение.ТранспортныйМаршрут  = Выборка.ТранспортныйМаршрут;
		Движение.Контрагент			  = Выборка.Контрагент;
		Движение.Доля				  = Выборка.Доля;
		
	КонецЦикла;
	//\\------Конец (1)------------------------------------------------
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	//проверка суммы долей ТЧ Заказы
	ПроверкаСуммыДолейТЧЗаказы(Отказ);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.абс_ТранспортныйМаршрут") Тогда
		
		Организация = ДанныеЗаполнения.Организация;
		ТранспортнаяКомпания = ДанныеЗаполнения.ТКПеревозчик;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	абс_СводнаяЗаявкаНаТранспортМаршруты.ЗаявкаНаТранспорт КАК НомерЗадания,
		|	абс_СводнаяЗаявкаНаТранспортМаршруты.ЗаявкаНаТранспорт.Контрагент КАК Контрагент,
		|	абс_СводнаяЗаявкаНаТранспортМаршруты.ЗаявкаНаТранспорт.СуммаДокумента КАК СуммаДокумента,
		|	абс_СводнаяЗаявкаНаТранспортМаршруты.ТранспортныйМаршрут
		|ИЗ
		|	Документ.абс_СводнаяЗаявкаНаТранспорт.Маршруты КАК абс_СводнаяЗаявкаНаТранспортМаршруты
		|ГДЕ
		|	абс_СводнаяЗаявкаНаТранспортМаршруты.ТранспортныйМаршрут = &ТранспортныйМаршрут";  
		Запрос.УстановитьПараметр("ТранспортныйМаршрут", ДанныеЗаполнения); 
		
		Результат = Запрос.Выполнить();     
		ВыборкаДетальныеЗаписи = Результат.Выбрать();
		
		Заказы.Очистить();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			НоваяСтрока = Заказы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаДетальныеЗаписи);
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.абс_ЗаявкаНаТранспорт") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	абс_ЗаявкаНаТранспорт.Ссылка КАК НомерЗадания,
		|	абс_ЗаявкаНаТранспорт.Контрагент,
		|	абс_ЗаявкаНаТранспорт.СуммаДокумента,
		|	ЕСТЬNULL(абс_ТранспортныйМаршрутЗадания.Ссылка, ЗНАЧЕНИЕ(Документ.абс_ТранспортныйМаршрут.ПустаяСсылка)) КАК ТранспортныйМаршрут,
		|	абс_ЗаявкаНаТранспорт.Организация,
		|	ЕСТЬNULL(абс_ТранспортныйМаршрутЗадания.Ссылка.ТКПеревозчик, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) КАК ТранспортнаяКомпанияУдалитьДоИзменения,
		|	ЕСТЬNULL(абс_ЗаявкаНаТранспорт.СпособДоставкиКонтрагента.ТК2, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) КАК ТранспортнаяКомпания
		|ИЗ
		|	Документ.абс_ЗаявкаНаТранспорт КАК абс_ЗаявкаНаТранспорт
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.абс_ТранспортныйМаршрут.Задания КАК абс_ТранспортныйМаршрутЗадания
		|		ПО (абс_ТранспортныйМаршрутЗадания.ЗаявкаНаТранспорт = абс_ЗаявкаНаТранспорт.Ссылка)
		|ГДЕ
		|	абс_ЗаявкаНаТранспорт.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
		
		Выборка = Запрос.Выполнить().Выбрать();
				
		Если Выборка.Следующий() Тогда
			Организация = Выборка.Организация;
			ТранспортнаяКомпания = Выборка.ТранспортнаяКомпания;
			
			Заказы.Очистить();
			
            НоваяСтрока = Заказы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

