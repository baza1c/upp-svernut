////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

Процедура ЗаполнитьТЧДокументаПоТаблицеБухФакта(ТабДок) Экспорт
	
	// Очистим данные по тек периоду
	
	СоставБюджета.Очистить();
	
	//считаем данные
	//1. Статья оборотов по бюджетам/ справочник статьи затрат	
	//2. Аналитика бюджета/ содержание услуги, доп.сведения	
	//3. Контрагент	
	//4. Номер документа	
	//5. Дата документа	
	//6. Номенклатура	
	//7. Количество	
	//8. Сумма (без НДС)	
	//9. Сумма (с НДС)
	//10. Статья оборотов по бюджетам
		
	Для ТекСтрока = 2 По ТабДок.ВысотаТаблицы Цикл
		
		СчСтрок = ТекСтрока - 1;
			
		// Если указан код бюджетной статьи в колонке 10, то пытаемся искать по коду бюджетной статьи
		// Если код не указан, ищем по сатье затрат из колонки 1.
		
		КодБС = СокрЛП(ТабДок.Область(ТекСтрока, 10).Текст);
		Если ЗначениеЗаполнено(КодБС) Тогда
			//10. Статья оборотов по бюджетам
			СтатьяОборотовПоБюджетам = Справочники.СтатьиОборотовПоБюджетам.НайтиПоРеквизиту("абс_КодБюджетнойСтатьи", КодБС);
			
			Если СтатьяОборотовПоБюджетам.Пустая() Тогда
				Сообщить("В строке № " + СчСтрок + " не найдена статья оборотов по коду бюджетной статьи: """ + КодБС + """");
			КонецЕсли;
			
		Иначе
			//1. Статья оборотов по бюджетам/ справочник статьи затрат			
			Значение = СокрЛП(ТабДок.Область(ТекСтрока, 1).Текст);
			Если Значение="" Тогда
				Сообщить("В строке № " + СчСтрок + " не указана статья оборотов");
				СтатьяОборотовПоБюджетам = Справочники.СтатьиОборотовПоБюджетам.ПустаяСсылка();
			Иначе
				СтатьяЗатрат = Справочники.СтатьиЗатрат.НайтиПоНаименованию(Значение, Истина);
				Если СтатьяЗатрат.Пустая() Тогда
					Сообщить("В строке № " + СчСтрок + " не найдена статья затрат """ + Значение + """");
				Иначе
				    СтруктураСтатьиБС = РегистрыСведений.абс_СоответствиеСтатейЗатратСтатьямОборотовПоБюджетам.Получить(Новый Структура("СтатьяЗатратБУ", СтатьяЗатрат));
					СтатьяОборотовПоБюджетам = СтруктураСтатьиБС.СтатьяОборотовПоБюджетам;
					Если НЕ ЗначениеЗаполнено(СтруктураСтатьиБС.СтатьяОборотовПоБюджетам) Тогда
						Сообщить("В строке № " + СчСтрок + " не найдена статья оборотов по статье затрат """ + СтатьяЗатрат + """");
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;

		//2. Аналитика бюджета/ содержание услуги, доп.сведения	
		Значение = СокрЛП(ТабДок.Область(ТекСтрока, 2).Текст);
		Если Значение="" Тогда
			Сообщить("В строке № " + СчСтрок + " не указана аналитика статьи оборотов");
			АналитикаБюджета = Справочники.абс_АналитикиБюджета.ПустаяСсылка();
		ИначеЕсли НЕ ЗначениеЗаполнено(СтатьяОборотовПоБюджетам) Тогда
			Сообщить("В строке № " + СчСтрок + " не указана статья оборотов для поиска аналитики статьи оборотов """ + Значение + """");
			АналитикаБюджета = Справочники.абс_АналитикиБюджета.ПустаяСсылка();
		Иначе
			АналитикаБюджета = Справочники.абс_АналитикиБюджета.НайтиПоНаименованию(Значение, Истина, , СтатьяОборотовПоБюджетам);
			Сообщить("В строке № " + СчСтрок + " не найдена аналитика бюджета """ + Значение + """");
		КонецЕсли;
		
		//3. Контрагент	
		Значение = СокрЛП(ТабДок.Область(ТекСтрока, 3).Текст);
		Если Значение="" Тогда
			Сообщить("В строке № " + СчСтрок + " не указан контрагент");
			Контрагент = Справочники.Контрагенты.ПустаяСсылка();
		Иначе
			МассивПоиска = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Значение, "|");
			
			ЗапросКонтрагент = Новый Запрос(
			"ВЫБРАТЬ
			|	Контрагенты.Ссылка
			|ИЗ
			|	Справочник.Контрагенты КАК Контрагенты
			|ГДЕ
			|	Контрагенты.ИНН = &ИНН
			|	И Контрагенты.КПП = &КПП
			|	И Контрагенты.Наименование = &Наименование");
			
			ЗапросКонтрагент.УстановитьПараметр("Наименование"	, СокрЛП(МассивПоиска[0]));
			ЗапросКонтрагент.УстановитьПараметр("ИНН"			, СокрЛП(МассивПоиска[1]));
			ЗапросКонтрагент.УстановитьПараметр("КПП"			, СокрЛП(МассивПоиска[2]));
			
			ВыборкаКонтрагент = ЗапросКонтрагент.Выполнить().Выбрать();
			
			Если ВыборкаКонтрагент.Следующий() Тогда
				Контрагент = ВыборкаКонтрагент.Ссылка;
			Иначе
				Сообщить("В строке № " + СчСтрок + " не найден контрагент """ + Значение + """");
				Контрагент = Справочники.Контрагенты.ПустаяСсылка();
			КонецЕсли;			
		КонецЕсли;
		
		//4. Номер документа	
		НомерПервичногоДокумента = СокрЛП(ТабДок.Область(ТекСтрока, 4).Текст);

		//5. Дата документа	
		ДатаПервичногоДокумента = СокрЛП(ТабДок.Область(ТекСтрока, 5).Текст);
		
		//6. Номенклатура	
		Значение = СокрЛП(ТабДок.Область(ТекСтрока, 6).Текст);
		Если Значение="" Тогда
			Сообщить("В строке № " + СчСтрок + " не указана номенклатура");
			Номенклатура = Справочники.Номенклатура.ПустаяСсылка();
		Иначе
			
			Номенклатура = Справочники.Номенклатура.НайтиПоНаименованию(Значение, Истина);
			
			Если НЕ ЗначениеЗаполнено(Номенклатура) Тогда
				Сообщить("В строке № " + СчСтрок + " не найдена номенклатура """ + Значение + """");
			КонецЕсли;		
		КонецЕсли;
		
		//7. Количество	
		Значение = СокрЛП(ТабДок.Область(ТекСтрока, 7).Текст);
		
		Количество = 0;
		
		Попытка
			Количество = Число(Значение);
		Исключение
			Сообщить("В строке № " + СчСтрок + " ошибка преобразования количества в число """ + Значение + """");
		КонецПопытки;
		
		
		//8. Сумма (без НДС)	
		Значение = СокрЛП(ТабДок.Область(ТекСтрока, 8).Текст);
		
		СуммаБезНДС = 0;
		
		Попытка
			СуммаБезНДС = Число(Значение);
		Исключение
			Сообщить("В строке № " + СчСтрок + " ошибка преобразования суммы (без НДС) в число """ + Значение + """");
		КонецПопытки;		
		
		//9. Сумма (с НДС)
		Значение = СокрЛП(ТабДок.Область(ТекСтрока, 9).Текст);
		
		Сумма = 0;
		
		Попытка
			Сумма = Число(Значение);
		Исключение
			Сообщить("В строке № " + СчСтрок + " ошибка преобразования суммы (с НДС) в число """ + Значение + """");
		КонецПопытки;
		
		// Найдем строку и внесем в нее данные
		СтруктураАналитики = Новый Структура("СтатьяОборотовПоБюджетам,АналитикаБюджета,Контрагент,ДоговорКонтрагента,Регион,Номенклатура,НомерПервичногоДокумента,ДатаПервичногоДокумента",
			СтатьяОборотовПоБюджетам,
			АналитикаБюджета,
			Контрагент,
			Справочники.ДоговорыКонтрагентов.ПустаяСсылка(),
			Справочники.Регионы.ПустаяСсылка(),
			Номенклатура,
			НомерПервичногоДокумента,
			ДатаПервичногоДокумента);
			
		НоваяСтрока = СоставБюджета.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтруктураАналитики);
		
		НоваяСтрока.Количество 	= Количество;
		НоваяСтрока.СуммаБезНДС = СуммаБезНДС;
		НоваяСтрока.Сумма 		= Сумма;
		
	КонецЦикла;
	
	ЗаполнитьЦФОПоСтатьямБюджета();
	
КонецПроцедуры

Процедура ЗаполнитьЦФОПоСтатьямБюджета()
	
	ТаблицаСтатейАналитик = СоставБюджета.Выгрузить(,"СтатьяОборотовПоБюджетам,АналитикаБюджета");
	
	ТаблицаСтатейАналитик.Свернуть("СтатьяОборотовПоБюджетам,АналитикаБюджета");
	
	ЗапросСтатейАналитикЦФО = Новый Запрос(
	"ВЫБРАТЬ
	|	ТабСтатейАналитик.СтатьяОборотовПоБюджетам КАК СтатьяОборотовПоБюджетам,
	|	ТабСтатейАналитик.АналитикаБюджета КАК АналитикаБюджета
	|ПОМЕСТИТЬ ВТ_ТабСтатейАналитик
	|ИЗ
	|	&ТаблицаСтатейАналитик КАК ТабСтатейАналитик
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТабСтатейАналитик.СтатьяОборотовПоБюджетам КАК СтатьяОборотовПоБюджетам,
	|	ВТ_ТабСтатейАналитик.АналитикаБюджета КАК АналитикаБюджета,
	|	МАКСИМУМ(ЕСТЬNULL(абс_СтатьиБюджетаПоЦФО.ЦФО, ЗНАЧЕНИЕ(Справочник.Подразделения.Пустаяссылка))) КАК ЦФО
	|ПОМЕСТИТЬ ВТ_ТабЦФОПоСтатьеАналитике
	|ИЗ
	|	ВТ_ТабСтатейАналитик КАК ВТ_ТабСтатейАналитик
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.абс_СтатьиБюджетаПоЦФО КАК абс_СтатьиБюджетаПоЦФО
	|		ПО ВТ_ТабСтатейАналитик.СтатьяОборотовПоБюджетам = абс_СтатьиБюджетаПоЦФО.СтатьяОборотовПоБюджетам
	|			И ВТ_ТабСтатейАналитик.АналитикаБюджета = абс_СтатьиБюджетаПоЦФО.АналитикаБюджета
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТабСтатейАналитик.СтатьяОборотовПоБюджетам,
	|	ВТ_ТабСтатейАналитик.АналитикаБюджета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТабСтатейАналитик.СтатьяОборотовПоБюджетам КАК СтатьяОборотовПоБюджетам,
	|	ВТ_ТабСтатейАналитик.АналитикаБюджета КАК АналитикаБюджета,
	|	МАКСИМУМ(ЕСТЬNULL(абс_СтатьиБюджетаПоЦФО.ЦФО, ЗНАЧЕНИЕ(Справочник.Подразделения.Пустаяссылка))) КАК ЦФО
	|ПОМЕСТИТЬ ВТ_ТабЦФОПоСтатье
	|ИЗ
	|	ВТ_ТабСтатейАналитик КАК ВТ_ТабСтатейАналитик
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.абс_СтатьиБюджетаПоЦФО КАК абс_СтатьиБюджетаПоЦФО
	|		ПО ВТ_ТабСтатейАналитик.СтатьяОборотовПоБюджетам = абс_СтатьиБюджетаПоЦФО.СтатьяОборотовПоБюджетам
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТабСтатейАналитик.СтатьяОборотовПоБюджетам,
	|	ВТ_ТабСтатейАналитик.АналитикаБюджета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТабСтатейАналитик.СтатьяОборотовПоБюджетам,
	|	ВТ_ТабСтатейАналитик.АналитикаБюджета,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ВТ_ТабЦФОПоСтатьеАналитике.ЦФО = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|				ТОГДА ВТ_ТабЦФОПоСтатье.ЦФО
	|			ИНАЧЕ ВТ_ТабЦФОПоСтатьеАналитике.ЦФО
	|		КОНЕЦ) КАК ЦФО
	|ИЗ
	|	ВТ_ТабСтатейАналитик КАК ВТ_ТабСтатейАналитик
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТабЦФОПоСтатьеАналитике КАК ВТ_ТабЦФОПоСтатьеАналитике
	|		ПО ВТ_ТабСтатейАналитик.СтатьяОборотовПоБюджетам = ВТ_ТабЦФОПоСтатьеАналитике.СтатьяОборотовПоБюджетам
	|			И ВТ_ТабСтатейАналитик.АналитикаБюджета = ВТ_ТабЦФОПоСтатьеАналитике.АналитикаБюджета
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТабЦФОПоСтатье КАК ВТ_ТабЦФОПоСтатье
	|		ПО ВТ_ТабСтатейАналитик.СтатьяОборотовПоБюджетам = ВТ_ТабЦФОПоСтатье.СтатьяОборотовПоБюджетам
	|			И ВТ_ТабСтатейАналитик.АналитикаБюджета = ВТ_ТабЦФОПоСтатье.АналитикаБюджета
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТабСтатейАналитик.СтатьяОборотовПоБюджетам,
	|	ВТ_ТабСтатейАналитик.АналитикаБюджета");
	
	ЗапросСтатейАналитикЦФО.УстановитьПараметр("ТаблицаСтатейАналитик", ТаблицаСтатейАналитик);
	
	ТаблицаСтатейАналитикЦФО = ЗапросСтатейАналитикЦФО.Выполнить().Выгрузить();
	
	Для Каждого СтрБюджета Из СоставБюджета Цикл
		
		Если НЕ ЗначениеЗаполнено(СтрБюджета.СтатьяОборотовПоБюджетам) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокиЦФО = ТаблицаСтатейАналитикЦФО.НайтиСтроки(Новый Структура("СтатьяОборотовПоБюджетам,АналитикаБюджета", СтрБюджета.СтатьяОборотовПоБюджетам, СтрБюджета.АналитикаБюджета));
		
		Если СтрокиЦФО.Количество() > 0 Тогда
			СтрБюджета.ЦФО = СтрокиЦФО[0].ЦФО;
			
			Если НЕ ЗначениеЗаполнено(СтрБюджета.ЦФО) Тогда
				Сообщить("В строке № " + СтрБюджета.НомерСтроки + " не найдено ЦФО для статьи БС """ + СтрБюджета.СтатьяОборотовПоБюджетам + """ и аналитике """ + СтрБюджета.АналитикаБюджета + """" );
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СортироватьСоставБюджетаПоСтатье(Объект) Экспорт
	
	Объект.СоставБюджета.Сортировать("СтатьяОборотовПоБюджетам Возр, Контрагент Возр");
	
КонецПроцедуры

Процедура СортироватьСоставБюджетаПоКонтрагенту(Объект) Экспорт
	
	Объект.СоставБюджета.Сортировать("Контрагент Возр, СтатьяОборотовПоБюджетам Возр");
	
КонецПроцедуры

Процедура ЗаполнитьФактДДС(Объект) Экспорт
	
	Объект.СоставБюджетаДДС.Очистить();
	
	// Заполним расходные статьи ДДС по ЗРС	
	// Заполним расходную часть по ППВ с видом "Оплата от покупателя"	
	// Заполним расходную часть по прочим ППВ	
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КонтрагентыСвязанныеСОрганизациями.Контрагент
	|ПОМЕСТИТЬ ВТ_СобственныеКонтрагенты
	|ИЗ
	|	РегистрСведений.КонтрагентыСвязанныеСОрганизациями КАК КонтрагентыСвязанныеСОрганизациями
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаявкиНаРасходованиеСредствОбороты.ЗаявкаНаРасходование.СтатьяОборотов КАК СтатьяОборотовПоБюджетам,
	|	СУММА(ЗаявкиНаРасходованиеСредствОбороты.СуммаУпрРасход) КАК Сумма,
	|	1 КАК ВидЗаполнения,
	|	ЗаявкиНаРасходованиеСредствОбороты.Контрагент КАК Контрагент,
	|	ЗаявкиНаРасходованиеСредствОбороты.ЗаявкаНаРасходование.ЦФО КАК ЦФО
	|ПОМЕСТИТЬ ВТ_РасходДС_ОплатыПоЗРС_ПоСтатьям
	|ИЗ
	|	РегистрНакопления.ЗаявкиНаРасходованиеСредств.Обороты(
	|			НАЧАЛОПЕРИОДА(&Период, ГОД),
	|			КОНЕЦПЕРИОДА(&Период, ГОД),
	|			Регистратор,
	|			НЕ ЗаявкаНаРасходование = ЗНАЧЕНИЕ(Документ.ЗаявкаНаРасходованиеСредств.ПустаяСсылка)
	|				И ВЫБОР
	|					КОГДА &ОтборПоОрганизации
	|						ТОГДА Организация В (&Организация)
	|					ИНАЧЕ НЕ Контрагент В
	|								(ВЫБРАТЬ
	|									СобственныеКонтрагенты.Контрагент
	|								ИЗ
	|									ВТ_СобственныеКонтрагенты КАК СобственныеКонтрагенты)
	|				КОНЕЦ) КАК ЗаявкиНаРасходованиеСредствОбороты
	|ГДЕ
	|	НАЧАЛОПЕРИОДА(ЗаявкиНаРасходованиеСредствОбороты.Регистратор.Дата, МЕСЯЦ) = &ТекПериод
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаявкиНаРасходованиеСредствОбороты.ЗаявкаНаРасходование.СтатьяОборотов,
	|	ЗаявкиНаРасходованиеСредствОбороты.Контрагент,
	|	ЗаявкиНаРасходованиеСредствОбороты.ЗаявкаНаРасходование.ЦФО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ДвиженияДенежныхСредствОбороты.СуммаУпрОборот) КАК СуммаУпрПриход,
	|	ВЫРАЗИТЬ(ДвиженияДенежныхСредствОбороты.Контрагент КАК Справочник.Контрагенты) КАК Контрагент,
	|	НАЧАЛОПЕРИОДА(ДвиженияДенежныхСредствОбороты.Период, МЕСЯЦ) КАК Период,
	|	ДвиженияДенежныхСредствОбороты.СтатьяДвиженияДенежныхСредств
	|ПОМЕСТИТЬ ВТ_ПриходыДС_ОплатаОтПокупателей
	|ИЗ
	|	РегистрНакопления.ДвиженияДенежныхСредств.Обороты(
	|			НАЧАЛОПЕРИОДА(&Период, ГОД),
	|			КОНЕЦПЕРИОДА(&Период, ГОД),
	|			Месяц,
	|			ПриходРасход = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Приход)
	|				И ВЫБОР
	|					КОГДА &ОтборПоОрганизации
	|						ТОГДА Организация В (&Организация)
	|					ИНАЧЕ НЕ Контрагент В
	|								(ВЫБРАТЬ
	|									СобственныеКонтрагенты.Контрагент
	|								ИЗ
	|									ВТ_СобственныеКонтрагенты КАК СобственныеКонтрагенты)
	|				КОНЕЦ
	|				И (ВЫРАЗИТЬ(ДокументДвижения КАК Документ.ПлатежноеПоручениеВходящее).ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеБезналичныхДенежныхСредств.ОплатаПокупателя)
	|					ИЛИ ВЫРАЗИТЬ(ДокументДвижения КАК Документ.ПриходныйКассовыйОрдер).ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПКО.ОплатаПокупателя))) КАК ДвиженияДенежныхСредствОбороты
	|ГДЕ
	|	НАЧАЛОПЕРИОДА(ДвиженияДенежныхСредствОбороты.Период, МЕСЯЦ) = &ТекПериод
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫРАЗИТЬ(ДвиженияДенежныхСредствОбороты.Контрагент КАК Справочник.Контрагенты),
	|	НАЧАЛОПЕРИОДА(ДвиженияДенежныхСредствОбороты.Период, МЕСЯЦ),
	|	ДвиженияДенежныхСредствОбороты.СтатьяДвиженияДенежныхСредств
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВТ_ПриходыДС.СуммаУпрПриход) КАК Сумма,
	|	ЕСТЬNULL(абс_СтатьиБюджетаПоЦФО.СтатьяОборотовПоБюджетам, ЗНАЧЕНИЕ(Справочник.СтатьиОборотовПоБюджетам.ПустаяСсылка)) КАК СтатьяОборотовПоБюджетам,
	|	2 КАК ВидЗаполнения,
	|	ВТ_ПриходыДС.Контрагент КАК Контрагент,
	|	ВТ_ПриходыДС.Контрагент.Регион.абс_ЦФО КАК ЦФО
	|ПОМЕСТИТЬ ВТ_ПриходыДС_ОплатаОтПокупателей_ПоСтатьям
	|ИЗ
	|	ВТ_ПриходыДС_ОплатаОтПокупателей КАК ВТ_ПриходыДС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.абс_СтатьиБюджетаПоЦФО КАК абс_СтатьиБюджетаПоЦФО
	|		ПО (абс_СтатьиБюджетаПоЦФО.ЦФО = ВТ_ПриходыДС.Контрагент.Регион.абс_ЦФО)
	|			И (абс_СтатьиБюджетаПоЦФО.СтатьяОборотовПоБюджетам.абс_ВидЗаполнения = ЗНАЧЕНИЕ(Перечисление.абс_ВидыЗаполненияСтатейОборотовПоБюджетам.Выручка))
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСТЬNULL(абс_СтатьиБюджетаПоЦФО.СтатьяОборотовПоБюджетам, ЗНАЧЕНИЕ(Справочник.СтатьиОборотовПоБюджетам.ПустаяСсылка)),
	|	ВТ_ПриходыДС.Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ДвиженияДенежныхСредствОбороты.СуммаУпрОборот) КАК Сумма,
	|	ДвиженияДенежныхСредствОбороты.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	ДвиженияДенежныхСредствОбороты.Контрагент КАК Контрагент
	|ПОМЕСТИТЬ ВТ_ПриходыДС_Прочее
	|ИЗ
	|	РегистрНакопления.ДвиженияДенежныхСредств.Обороты(
	|			НАЧАЛОПЕРИОДА(&Период, ГОД),
	|			КОНЕЦПЕРИОДА(&Период, ГОД),
	|			Месяц,
	|			ПриходРасход = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Приход)
	|				И ВЫБОР
	|					КОГДА &ОтборПоОрганизации
	|						ТОГДА Организация В (&Организация)
	|					ИНАЧЕ НЕ Контрагент В
	|								(ВЫБРАТЬ
	|									СобственныеКонтрагенты.Контрагент
	|								ИЗ
	|									ВТ_СобственныеКонтрагенты КАК СобственныеКонтрагенты)
	|				КОНЕЦ
	|				И ВЫБОР
	|					КОГДА ДокументДвижения ССЫЛКА Документ.ПлатежноеПоручениеВходящее
	|						ТОГДА НЕ ВЫРАЗИТЬ(ДокументДвижения КАК Документ.ПлатежноеПоручениеВходящее).ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеБезналичныхДенежныхСредств.ОплатаПокупателя)
	|					КОГДА ДокументДвижения ССЫЛКА Документ.ПриходныйКассовыйОрдер
	|						ТОГДА НЕ ВЫРАЗИТЬ(ДокументДвижения КАК Документ.ПриходныйКассовыйОрдер).ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПКО.ОплатаПокупателя)
	|					КОГДА ДокументДвижения ССЫЛКА Документ.ПлатежныйОрдерПоступлениеДенежныхСредств
	|						ТОГДА ИСТИНА
	|					КОГДА ДокументДвижения ССЫЛКА Документ.ПлатежноеПоручениеИсходящее
	|						ТОГДА ИСТИНА
	|				КОНЕЦ) КАК ДвиженияДенежныхСредствОбороты
	|ГДЕ
	|	НАЧАЛОПЕРИОДА(ДвиженияДенежныхСредствОбороты.Период, МЕСЯЦ) = &ТекПериод
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвиженияДенежныхСредствОбороты.СтатьяДвиженияДенежныхСредств,
	|	ДвиженияДенежныхСредствОбороты.Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВТ_ПриходыДС_Прочее.Сумма) КАК Сумма,
	|	ЕСТЬNULL(абс_СоответствиеСтатейДвиженияДенежныхСредствСтатьямОборотовПоБюджетам.СтатьяОборотовПоБюджетам, ЗНАЧЕНИЕ(Справочник.статьиОборотовпоБюджетам.ПустаяСсылка)) КАК СтатьяОборотовПоБюджетам,
	|	3 КАК ВидЗаполнения,
	|	ВТ_ПриходыДС_Прочее.Контрагент КАК Контрагент,
	|	абс_СтатьиБюджетаПоЦФО.ЦФО КАК ЦФО
	|ПОМЕСТИТЬ ВТ_ПриходыДС_Прочее_ПоСтатьям
	|ИЗ
	|	ВТ_ПриходыДС_Прочее КАК ВТ_ПриходыДС_Прочее
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.абс_СоответствиеСтатейДвиженияДенежныхСредствСтатьямОборотовПоБюджетам КАК абс_СоответствиеСтатейДвиженияДенежныхСредствСтатьямОборотовПоБюджетам
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.абс_СтатьиБюджетаПоЦФО КАК абс_СтатьиБюджетаПоЦФО
	|			ПО абс_СоответствиеСтатейДвиженияДенежныхСредствСтатьямОборотовПоБюджетам.СтатьяОборотовПоБюджетам = абс_СтатьиБюджетаПоЦФО.СтатьяОборотовПоБюджетам
	|		ПО ВТ_ПриходыДС_Прочее.СтатьяДвиженияДенежныхСредств = абс_СоответствиеСтатейДвиженияДенежныхСредствСтатьямОборотовПоБюджетам.СтатьяДДСБУ
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСТЬNULL(абс_СоответствиеСтатейДвиженияДенежныхСредствСтатьямОборотовПоБюджетам.СтатьяОборотовПоБюджетам, ЗНАЧЕНИЕ(Справочник.статьиОборотовпоБюджетам.ПустаяСсылка)),
	|	ВТ_ПриходыДС_Прочее.Контрагент,
	|	абс_СтатьиБюджетаПоЦФО.ЦФО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_РасходДС_ОплатыПоЗРС_ПоСтатьям.ВидЗаполнения КАК ВидЗаполнения,
	|	ВТ_РасходДС_ОплатыПоЗРС_ПоСтатьям.СтатьяОборотовПоБюджетам КАК СтатьяОборотовПоБюджетам,
	|	ВТ_РасходДС_ОплатыПоЗРС_ПоСтатьям.Сумма КАК Сумма,
	|	ВТ_РасходДС_ОплатыПоЗРС_ПоСтатьям.Контрагент КАК Контрагент,
	|	ВТ_РасходДС_ОплатыПоЗРС_ПоСтатьям.ЦФО КАК ЦФО
	|ИЗ
	|	ВТ_РасходДС_ОплатыПоЗРС_ПоСтатьям КАК ВТ_РасходДС_ОплатыПоЗРС_ПоСтатьям
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ПриходыДС_ОплатаОтПокупателей_ПоСтатьям.ВидЗаполнения,
	|	ВТ_ПриходыДС_ОплатаОтПокупателей_ПоСтатьям.СтатьяОборотовПоБюджетам,
	|	ВТ_ПриходыДС_ОплатаОтПокупателей_ПоСтатьям.Сумма,
	|	ВТ_ПриходыДС_ОплатаОтПокупателей_ПоСтатьям.Контрагент,
	|	ВТ_ПриходыДС_ОплатаОтПокупателей_ПоСтатьям.ЦФО
	|ИЗ
	|	ВТ_ПриходыДС_ОплатаОтПокупателей_ПоСтатьям КАК ВТ_ПриходыДС_ОплатаОтПокупателей_ПоСтатьям
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ПриходыДС_Прочее_ПоСтатьям.ВидЗаполнения,
	|	ВТ_ПриходыДС_Прочее_ПоСтатьям.СтатьяОборотовПоБюджетам,
	|	ВТ_ПриходыДС_Прочее_ПоСтатьям.Сумма,
	|	ВТ_ПриходыДС_Прочее_ПоСтатьям.Контрагент,
	|	ВТ_ПриходыДС_Прочее_ПоСтатьям.ЦФО
	|ИЗ
	|	ВТ_ПриходыДС_Прочее_ПоСтатьям КАК ВТ_ПриходыДС_Прочее_ПоСтатьям");
	
	Запрос.УстановитьПараметр("Организация"			, Объект.Организация);
	Запрос.УстановитьПараметр("ОтборПоОрганизации"	, Истина);
	Запрос.УстановитьПараметр("ТекПериод"			, НачалоМесяца(Объект.ТекущийПериод));
	Запрос.УстановитьПараметр("Период"				, НачалоГода(Объект.ТекущийПериод));
	
	Объект.СоставБюджетаДДС.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаЗаполнения(ДанныеЗаполения)
	
	Ответственный 		= ПараметрыСеанса.ТекущийПользователь;
	
	ТекущийПериод		= НачалоМесяца(ТекущаяДата())
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеДокумента(Выборка, Отказ)
	
	Пока Выборка.Следующий() Цикл
		
		Если НЕ ЗначениеЗаполнено(Выборка.ЦФО) Тогда
			ОбщегоНазначения.СообщитьОбОшибке("В строке № " + Выборка.НомерСтроки + " не заполнено ЦФО.", Отказ);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Выборка.СтатьяОборотов) Тогда
			ОбщегоНазначения.СообщитьОбОшибке("В строке № " + Выборка.НомерСтроки + " не заполнено Статья оборотов по бюджетам.", Отказ);
		КонецЕсли;	
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.ОборотыБюджетов.Записывать = Истина;
	Движения.ОборотыБюджетов.Очистить();
	
	Запрос=Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	абс_ЗагрузкаБухФактаСоставБюджета.Ссылка КАК Ссылка,
	|	абс_ЗагрузкаБухФактаСоставБюджета.ЦФО КАК ЦФО,
	|	абс_ЗагрузкаБухФактаСоставБюджета.СтатьяОборотовПоБюджетам КАК СтатьяОборотов,
	|	абс_ЗагрузкаБухФактаСоставБюджета.АналитикаБюджета КАК абс_АналитикаБюджета,
	|	ЗНАЧЕНИЕ(Справочник.Регионы.ПустаяСсылка) КАК Регион,
	|	абс_ЗагрузкаБухФактаСоставБюджета.Контрагент КАК Контрагент,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) КАК ДоговорКонтрагента,
	|	ВЫБОР
	|		КОГДА абс_ЗагрузкаБухФактаСоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20)
	|				ИЛИ абс_ЗагрузкаБухФактаСоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20_120)
	|			ТОГДА 20
	|		КОГДА абс_ЗагрузкаБухФактаСоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10)
	|				ИЛИ абс_ЗагрузкаБухФактаСоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10_110)
	|			ТОГДА 10
	|		КОГДА абс_ЗагрузкаБухФактаСоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18)
	|				ИЛИ абс_ЗагрузкаБухФактаСоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118)
	|			ТОГДА 18 
	|		КОГДА абс_ЗагрузкаБухФактаСоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС5)
	|				ИЛИ абс_ЗагрузкаБухФактаСоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС5_105)
	|			ТОГДА 5
	|		КОГДА абс_ЗагрузкаБухФактаСоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС7)
	|				ИЛИ абс_ЗагрузкаБухФактаСоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС7_107)
	|			ТОГДА 7
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтавкаНДС,
	|	абс_ЗагрузкаБухФактаСоставБюджета.Ссылка.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА абс_ЗагрузкаБухФактаСоставБюджета.СтатьяОборотовПоБюджетам.абс_ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.абс_ТипыСтатейОборотовПоБюджетам.СтатьяРасходов)
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Коэффициент,
	|	абс_ЗагрузкаБухФактаСоставБюджета.СуммаБезНДС КАК СуммаБезНДС,
	|	абс_ЗагрузкаБухФактаСоставБюджета.Сумма КАК Сумма,
	|	абс_ЗагрузкаБухФактаСоставБюджета.Количество КАК Количество,
	|	абс_ЗагрузкаБухФактаСоставБюджета.НомерСтроки
	|ИЗ
	|	Документ.абс_ЗагрузкаБухФакта.СоставБюджета КАК абс_ЗагрузкаБухФактаСоставБюджета
	|ГДЕ
	|	абс_ЗагрузкаБухФактаСоставБюджета.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПроверитьЗаполнениеДокумента(Выборка, Отказ);
	
	Если Отказ Тогда
		Сообщить("Документ не проведен!");
		Возврат;
	КонецЕсли;
	
	Выборка.Сбросить();
	
	Пока Выборка.Следующий() Цикл	
		
		//движения по сценарию бух. Факт
		Сумма		= Выборка.Сумма;
		СуммаБезНДС	= Выборка.СуммаБезНДС;
		Количество	= Выборка.Количество;
		
		Если Сумма<>0 ИЛИ СуммаБезНДС<>0 ИЛИ Количество<>0 Тогда
			
			Движение = Движения.ОборотыБюджетов.Добавить();
			
			ЗаполнитьЗначенияСвойств(Движение, Выборка);
			
			Движение.ЦФО 			= ?(Движение.СтатьяОборотов.абс_ПрямаяЗатрата, Выборка.ЦФО, Движение.СтатьяОборотов.ОсновноеЦФО);
			Движение.абс_Бюджет		= Справочники.Бюджеты.СводныйБДР;				
			Движение.Сценарий		= Справочники.СценарииПланирования.БухгалтерскийФакт;				
			
			Движение.Период 		= НачалоМесяца(ТекущийПериод);
			
			Движение.СуммаУпр		= ?(Сумма = 0,СуммаБезНДС + СуммаБезНДС * Выборка.СтавкаНДС / 100, Сумма) * Выборка.Коэффициент;
			Движение.СуммаУпрБезНДС	= СуммаБезНДС	* Выборка.Коэффициент;
			Движение.Количество		= Количество	* Выборка.Коэффициент;
			
		КонецЕсли;
			
	КонецЦикла;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	абс_ЗагрузкаБухФактаСоставБюджетаДДС.Ссылка.Организация КАК Организация,
	|	абс_ЗагрузкаБухФактаСоставБюджетаДДС.СтатьяОборотовПоБюджетам КАК СтатьяОборотовПоБюджетам,
	|	СУММА(абс_ЗагрузкаБухФактаСоставБюджетаДДС.Сумма) КАК Сумма,
	|	ВЫБОР
	|		КОГДА абс_ЗагрузкаБухФактаСоставБюджетаДДС.СтатьяОборотовПоБюджетам.абс_ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.абс_ТипыСтатейОборотовПоБюджетам.СтатьяРасходов)
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Коэффициент,
	|	абс_ЗагрузкаБухФактаСоставБюджетаДДС.Контрагент КАК Контрагент,
	|	абс_ЗагрузкаБухФактаСоставБюджетаДДС.ЦФО
	|ИЗ
	|	Документ.абс_ЗагрузкаБухФакта.СоставБюджетаДДС КАК абс_ЗагрузкаБухФактаСоставБюджетаДДС
	|ГДЕ
	|	абс_ЗагрузкаБухФактаСоставБюджетаДДС.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	абс_ЗагрузкаБухФактаСоставБюджетаДДС.СтатьяОборотовПоБюджетам,
	|	ВЫБОР
	|		КОГДА абс_ЗагрузкаБухФактаСоставБюджетаДДС.СтатьяОборотовПоБюджетам.абс_ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.абс_ТипыСтатейОборотовПоБюджетам.СтатьяРасходов)
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ,
	|	абс_ЗагрузкаБухФактаСоставБюджетаДДС.Ссылка.Организация,
	|	абс_ЗагрузкаБухФактаСоставБюджетаДДС.Контрагент,
	|	абс_ЗагрузкаБухФактаСоставБюджетаДДС.ЦФО");
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Сумма = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Движение = Движения.ОборотыБюджетов.Добавить();
		
		Движение.СтатьяОборотов = Выборка.СтатьяОборотовПоБюджетам;
		
		Движение.абс_Бюджет		= Справочники.Бюджеты.БДДС;				
		Движение.Сценарий		= Справочники.СценарииПланирования.Факт;
		
		Движение.Контрагент		= Выборка.Контрагент;
		Движение.Организация	= Выборка.Организация;
		
		Движение.ЦФО			= Выборка.ЦФО;
		
		Движение.Период 		= НачалоМесяца(ТекущийПериод);
		
		Движение.СуммаУпр		= Выборка.Сумма * Выборка.Коэффициент;
		
	КонецЦикла;
	
КонецПроцедуры
