////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

Процедура ЗаполнитьДаннымиЗаГод(Объект) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	абс_СтатьиБюджетаПоЦФО.СтатьяОборотовПоБюджетам,
	|	абс_СтатьиБюджетаПоЦФО.ЦФО
	|ПОМЕСТИТЬ ВТ_СтатьиПоЦФО
	|ИЗ
	|	РегистрСведений.абс_СтатьиБюджетаПоЦФО КАК абс_СтатьиБюджетаПоЦФО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОборотыБюджетовОбороты.Период КАК Период,
	|	ЕСТЬNULL(ВТ_СтатьиПоЦФО.ЦФО, ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)) КАК ЦФО,
	|	ОборотыБюджетовОбороты.СтатьяОборотов КАК СтатьяОборотовПоБюджетам,
	|	ОборотыБюджетовОбороты.Контрагент КАК Контрагент,
	|	ВЫБОР
	|		КОГДА ОборотыБюджетовОбороты.СтатьяОборотов.абс_ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.абс_ТипыСтатейОборотовПоБюджетам.СтатьяРасходов)
	|			ТОГДА -ОборотыБюджетовОбороты.СуммаУпрОборот
	|		ИНАЧЕ ОборотыБюджетовОбороты.СуммаУпрОборот
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА ОборотыБюджетовОбороты.Сценарий = ЗНАЧЕНИЕ(Справочник.СценарииПланирования.План)
	|			ТОГДА ""План""
	|		КОГДА ОборотыБюджетовОбороты.Сценарий = ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Оперативныйфакт)
	|			ТОГДА ""ОперФакт""
	|		КОГДА ОборотыБюджетовОбороты.Сценарий = ЗНАЧЕНИЕ(Справочник.СценарииПланирования.БухгалтерскийФакт)
	|			ТОГДА ""БухФакт""
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК НаименованиеСценария,
	|	ВЫБОР
	|		КОГДА ОборотыБюджетовОбороты.СтатьяОборотов.абс_ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.абс_ТипыСтатейОборотовПоБюджетам.СтатьяРасходов)
	|			ТОГДА 0
	|		ИНАЧЕ ОборотыБюджетовОбороты.КоличествоОборот
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА ОборотыБюджетовОбороты.СтатьяОборотов.абс_ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.абс_ТипыСтатейОборотовПоБюджетам.СтатьяРасходов)
	|			ТОГДА -ОборотыБюджетовОбороты.СуммаУпрБезНДСОборот
	|		ИНАЧЕ ОборотыБюджетовОбороты.СуммаУпрБезНДСОборот
	|	КОНЕЦ КАК СуммаБезНДС
	|ИЗ
	|	РегистрНакопления.ОборотыБюджетов.Обороты(
	|			НАЧАЛОПЕРИОДА(&Период, ГОД),
	|			КОНЕЦПЕРИОДА(&Период, ГОД),
	|			Месяц,
	|			Сценарий В (ЗНАЧЕНИЕ(Справочник.СценарииПланирования.План), ЗНАЧЕНИЕ(Справочник.сценарииПланирования.ОперативныйФакт), ЗНАЧЕНИЕ(Справочник.СценарииПланирования.БухгалтерскийФакт))
	|				И Организация = &Организация
	|				И абс_Бюджет = ЗНАЧЕНИЕ(Справочник.Бюджеты.СводныйБДР)) КАК ОборотыБюджетовОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтатьиПоЦФО КАК ВТ_СтатьиПоЦФО
	|		ПО ОборотыБюджетовОбороты.СтатьяОборотов = ВТ_СтатьиПоЦФО.СтатьяОборотовПоБюджетам
	|ГДЕ
	|	НЕ ОборотыБюджетовОбороты.СтатьяОборотов.абс_ВидЗаполнения = ЗНАЧЕНИЕ(Перечисление.абс_ВидыЗаполненияСтатейОборотовПоБюджетам.Выручка)
	|	И НЕ ОборотыБюджетовОбороты.СтатьяОборотов.абс_ВидЗаполнения = ЗНАЧЕНИЕ(Перечисление.абс_ВидыЗаполненияСтатейОборотовПоБюджетам.Себестоимость)
	|ИТОГИ
	|	СУММА(Сумма),
	|	СУММА(Количество),
	|	СУММА(СуммаБезНДС)
	|ПО
	|	ЦФО,
	|	СтатьяОборотовПоБюджетам,
	|	Контрагент,
	|	Период,
	|	НаименованиеСценария";
	
	Запрос.УстановитьПараметр("Период", ДатаПланирования);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	ВыборкаЦФО=Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаЦФО.Следующий() Цикл
		
		ВыборкаСтатья=ВыборкаЦФО.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаСтатья.Следующий() Цикл
							
			ВыборкаКонтрагент=ВыборкаСтатья.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаКонтрагент.Следующий() Цикл
							
				СтрокаТЧ=Объект.СоставБюджета.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТЧ,ВыборкаКонтрагент);
				
				ВыборкаПериод=ВыборкаКонтрагент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаПериод.Следующий() Цикл
					
					ВыборкаНаименованиеСценария = ВыборкаПериод.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Пока ВыборкаНаименованиеСценария.Следующий() Цикл
						
						Если НЕ ЗначениеЗаполнено(ВыборкаНаименованиеСценария.НаименованиеСценария) Тогда
							Продолжить;
						КонецЕсли;
						
						СтрокаТЧ["" + ВыборкаНаименованиеСценария.НаименованиеСценария + "Количество"	+ Месяц(ВыборкаПериод.Период)]	= ВыборкаНаименованиеСценария.Количество;
						
						СтрокаТЧ["" + ВыборкаНаименованиеСценария.НаименованиеСценария + "Сумма"		+ Месяц(ВыборкаПериод.Период)]	= ВыборкаНаименованиеСценария.Сумма;
						СтрокаТЧ["" + ВыборкаНаименованиеСценария.НаименованиеСценария + "СуммаБезНДС"	+ Месяц(ВыборкаПериод.Период)]	= ВыборкаНаименованиеСценария.СуммаБезНДС;
						
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;	
	
КонецПроцедуры

Процедура ЗаполнитьДаннымиНаТекущийМесяц(Объект) Экспорт
	
	ТекМесяц 		= НачалоМесяца(Объект.ТекущийПериод);
	ИндексТекМесяца = Месяц(ТекМесяц);
	
	// Очистим данные по текущему месяцу
	Для Каждого СтрокаТЧ Из Объект.СоставБюджета Цикл
		
		СтрокаТЧ["ПланКоличество" 		+ ИндексТекМесяца] 	= 0;
		СтрокаТЧ["ПланСумма" 			+ ИндексТекМесяца] 	= 0;
		СтрокаТЧ["ПланСуммаБезНДС"		+ ИндексТекМесяца]	= 0;
		
		СтрокаТЧ["ОперФактКоличество" 	+ ИндексТекМесяца] 	= 0;
		СтрокаТЧ["ОперФактСумма" 		+ ИндексТекМесяца] 	= 0;
		СтрокаТЧ["ОперФактСуммаБезНДС"	+ ИндексТекМесяца]	= 0;
		
		СтрокаТЧ["БухФактКоличество" 	+ ИндексТекМесяца] 	= 0;
		СтрокаТЧ["БухФактСумма" 		+ ИндексТекМесяца] 	= 0;
		СтрокаТЧ["БухФактСуммаБезНДС"	+ ИндексТекМесяца]	= 0;		
		
	КонецЦикла;	
	
	// Заполним данные по данным плана	
	
	Запрос = Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	абс_СтатьиБюджетаПоЦФО.СтатьяОборотовПоБюджетам,
	|	абс_СтатьиБюджетаПоЦФО.ЦФО
	|ПОМЕСТИТЬ ВТ_СтатьиПоЦФО
	|ИЗ
	|	РегистрСведений.абс_СтатьиБюджетаПоЦФО КАК абс_СтатьиБюджетаПоЦФО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОборотыБюджетовОбороты.Период КАК Период,
	|	ЕСТЬNULL(ВТ_СтатьиПоЦФО.ЦФО, ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)) КАК ЦФО,
	|	ОборотыБюджетовОбороты.СтатьяОборотов КАК СтатьяОборотовПоБюджетам,
	|	ОборотыБюджетовОбороты.Контрагент КАК Контрагент,
	|	ВЫБОР
	|		КОГДА ОборотыБюджетовОбороты.Сценарий = ЗНАЧЕНИЕ(Справочник.СценарииПланирования.План)
	|			ТОГДА ""План""
	|		КОГДА ОборотыБюджетовОбороты.Сценарий = ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Оперативныйфакт)
	|			ТОГДА ""ОперФакт""
	|		КОГДА ОборотыБюджетовОбороты.Сценарий = ЗНАЧЕНИЕ(Справочник.СценарииПланирования.БухгалтерскийФакт)
	|			ТОГДА ""БухФакт""
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК НаименованиеСценария,
	|	ВЫБОР
	|		КОГДА ОборотыБюджетовОбороты.СтатьяОборотов.абс_ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.абс_ТипыСтатейОборотовПоБюджетам.СтатьяРасходов)
	|			ТОГДА -ОборотыБюджетовОбороты.СуммаУпрОборот
	|		ИНАЧЕ ОборотыБюджетовОбороты.СуммаУпрОборот
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА ОборотыБюджетовОбороты.СтатьяОборотов.абс_ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.абс_ТипыСтатейОборотовПоБюджетам.СтатьяРасходов)
	|			ТОГДА -ОборотыБюджетовОбороты.СуммаУпрБезНДСОборот
	|		ИНАЧЕ ОборотыБюджетовОбороты.СуммаУпрБезНДСОборот
	|	КОНЕЦ КАК СуммаБезНДС,
	|	ВЫБОР
	|		КОГДА ОборотыБюджетовОбороты.СтатьяОборотов.абс_ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.абс_ТипыСтатейОборотовПоБюджетам.СтатьяРасходов)
	|			ТОГДА 0
	|		ИНАЧЕ ОборотыБюджетовОбороты.КоличествоОборот
	|	КОНЕЦ КАК Количество
	|ИЗ
	|	РегистрНакопления.ОборотыБюджетов.Обороты(
	|			НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ),
	|			КОНЕЦПЕРИОДА(&Период, МЕСЯЦ),
	|			Месяц,
	|			Сценарий В (ЗНАЧЕНИЕ(Справочник.СценарииПланирования.План), ЗНАЧЕНИЕ(Справочник.сценарииПланирования.ОперативныйФакт), ЗНАЧЕНИЕ(Справочник.СценарииПланирования.БухгалтерскийФакт))
	|				И Организация = &Организация
	|				И абс_Бюджет = ЗНАЧЕНИЕ(Справочник.Бюджеты.СводныйБДР)) КАК ОборотыБюджетовОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтатьиПоЦФО КАК ВТ_СтатьиПоЦФО
	|		ПО ОборотыБюджетовОбороты.СтатьяОборотов = ВТ_СтатьиПоЦФО.СтатьяОборотовПоБюджетам
	|			И ОборотыБюджетовОбороты.ЦФО = ВТ_СтатьиПоЦФО.ЦФО
	|ГДЕ
	|	НЕ ОборотыБюджетовОбороты.СтатьяОборотов.абс_ВидЗаполнения = ЗНАЧЕНИЕ(Перечисление.абс_ВидыЗаполненияСтатейОборотовПоБюджетам.Выручка)
	|	И НЕ ОборотыБюджетовОбороты.СтатьяОборотов.абс_ВидЗаполнения = ЗНАЧЕНИЕ(Перечисление.абс_ВидыЗаполненияСтатейОборотовПоБюджетам.Себестоимость)
	|ИТОГИ
	|	СУММА(Сумма),
	|	СУММА(СуммаБезНДС)
	|ПО
	|	ЦФО,
	|	СтатьяОборотовПоБюджетам,
	|	Контрагент,
	|	Период,
	|	НаименованиеСценария";
	
	Запрос.УстановитьПараметр("Период", ТекМесяц);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	ВыборкаЦФО=Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаЦФО.Следующий() Цикл
		
		ВыборкаСтатья=ВыборкаЦФО.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаСтатья.Следующий() Цикл
							
			ВыборкаКонтрагент=ВыборкаСтатья.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаКонтрагент.Следующий() Цикл
								
				// Попробуем найти строку в табличной части, если строки нет - создадим новую
				//  если есть, заполним существующую
				СтруктураАналитик = Новый Структура("ЦФО,СтатьяОборотовПоБюджетам,Контрагент");
				ЗаполнитьЗначенияСвойств(СтруктураАналитик, ВыборкаКонтрагент);
				
				СтрокиТЧ = Объект.СоставБюджета.НайтиСтроки(СтруктураАналитик);
				
				Если СтрокиТЧ.Количество() = 0 Тогда
					СтрокаТЧ = Объект.СоставБюджета.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТЧ, ВыборкаКонтрагент);
				Иначе
					СтрокаТЧ = СтрокиТЧ[0];
				КонецЕсли;
				
				ВыборкаПериод=ВыборкаКонтрагент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаПериод.Следующий() Цикл
					
					ВыборкаНаименованиеСценария = ВыборкаПериод.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Пока ВыборкаНаименованиеСценария.Следующий() Цикл
						
						Если НЕ ЗначениеЗаполнено(ВыборкаНаименованиеСценария.НаименованиеСценария) Тогда
							Продолжить;
						КонецЕсли;
						
						СтрокаТЧ["" + ВыборкаНаименованиеСценария.НаименованиеСценария + "Количество"	+ Месяц(ВыборкаПериод.Период)]	= ВыборкаНаименованиеСценария.Количество;
						
						СтрокаТЧ["" + ВыборкаНаименованиеСценария.НаименованиеСценария + "Сумма"		+ Месяц(ВыборкаПериод.Период)]	= ВыборкаНаименованиеСценария.Сумма;
						СтрокаТЧ["" + ВыборкаНаименованиеСценария.НаименованиеСценария + "СуммаБезНДС"	+ Месяц(ВыборкаПериод.Период)]	= ВыборкаНаименованиеСценария.СуммаБезНДС;
						
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;		
	
КонецПроцедуры

Процедура СортироватьСоставБюджетаПоСтатье(Объект) Экспорт
	
	Объект.СоставБюджета.Сортировать("СтатьяОборотовПоБюджетам Возр, Контрагент Возр");
	
КонецПроцедуры

Процедура СортироватьСоставБюджетаПоКонтрагенту(Объект) Экспорт
	
	Объект.СоставБюджета.Сортировать("Контрагент Возр, СтатьяОборотовПоБюджетам Возр");
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаЗаполнения(ДанныеЗаполения)
	
	Ответственный 		= ПараметрыСеанса.ТекущийПользователь;
	ДатаПланирования 	= НачалоГода(ТекущаяДата());
	
	ТекущийПериод		= ДатаПланирования
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.ОборотыБюджетов.Записывать = Истина;
	Движения.ОборотыБюджетов.Очистить();
	
	Запрос=Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	абс_ПодтверждениеФактаСоставБюджета.Ссылка,
	|	абс_ПодтверждениеФактаСоставБюджета.ЦФО,
	|	абс_ПодтверждениеФактаСоставБюджета.СтатьяОборотовПоБюджетам КАК СтатьяОборотов,
	|	абс_ПодтверждениеФактаСоставБюджета.АналитикаБюджета КАК абс_АналитикаБюджета,
	|	ЗНАЧЕНИЕ(Справочник.Регионы.ПустаяСсылка) КАК Регион,
	|	абс_ПодтверждениеФактаСоставБюджета.Контрагент,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) КАК ДоговорКонтрагента,
	|	ВЫБОР
	|		КОГДА абс_ПодтверждениеФактаСоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20)
	|				ИЛИ абс_ПодтверждениеФактаСоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20_120)
	|			ТОГДА 20
	|		КОГДА абс_ПодтверждениеФактаСоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10)
	|				ИЛИ абс_ПодтверждениеФактаСоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10_110)
	|			ТОГДА 10
	|		КОГДА абс_ПодтверждениеФактаСоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18)
	|				ИЛИ абс_ПодтверждениеФактаСоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118)
	|			ТОГДА 18
	|		КОГДА абс_ПодтверждениеФактаСоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС5)
	|				ИЛИ абс_ПодтверждениеФактаСоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС5_105)
	|			ТОГДА 5
	|		КОГДА абс_ПодтверждениеФактаСоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС7)
	|				ИЛИ абс_ПодтверждениеФактаСоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС7_107)
	|			ТОГДА 7
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтавкаНДС,
	|	абс_ПодтверждениеФактаСоставБюджета.Ссылка.Организация,
	|	ВЫБОР
	|		КОГДА абс_ПодтверждениеФактаСоставБюджета.СтатьяОборотовПоБюджетам.абс_ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.абс_ТипыСтатейОборотовПоБюджетам.СтатьяРасходов)
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Коэффициент,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактСуммаБезНДС1,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактСуммаБезНДС2,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактСуммаБезНДС3,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактСуммаБезНДС4,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактСуммаБезНДС5,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактСуммаБезНДС6,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактСуммаБезНДС7,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактСуммаБезНДС8,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактСуммаБезНДС9,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактСуммаБезНДС10,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактСуммаБезНДС11,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактСуммаБезНДС12,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактСумма1,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактСумма2,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактСумма3,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактСумма4,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактСумма5,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактСумма6,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактСумма7,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактСумма8,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактСумма9,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактСумма10,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактСумма11,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактСумма12,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактКоличество1,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактКоличество2,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактКоличество3,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактКоличество4,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактКоличество5,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактКоличество6,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактКоличество7,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактКоличество8,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактКоличество9,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактКоличество10,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактКоличество11,
	|	абс_ПодтверждениеФактаСоставБюджета.ФактКоличество12
	|ИЗ
	|	Документ.абс_ПодтверждениеФакта.СоставБюджета КАК абс_ПодтверждениеФактаСоставБюджета
	|ГДЕ
	|	абс_ПодтверждениеФактаСоставБюджета.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Для сч = 1 по 12 Цикл
			
			//движения по сценарию Факт
			
			ФактКоличество	= Выборка["ФактКоличество"+СокрЛП(Сч)];
			
			ФактСумма		= Выборка["ФактСумма"+СокрЛП(Сч)];
			ФактСуммаБезНДС	= Выборка["ФактСуммаБезНДС"+СокрЛП(Сч)];
			//ОперФактКоличество	= Выборка["ОперФактКоличество"+СокрЛП(Сч)];
			
			Если ФактСумма<>0 Тогда
				
				Движение = Движения.ОборотыБюджетов.Добавить();
				
				ЗаполнитьЗначенияСвойств(Движение,Выборка);
				
				Движение.ЦФО 			= ?(Движение.СтатьяОборотов.абс_ПрямаяЗатрата, Выборка.ЦФО, Движение.СтатьяОборотов.ОсновноеЦФО);
				Движение.Период 		= ДобавитьМесяц(ДатаПланирования,Сч-1);
				Движение.СуммаУпр		= ?(ФактСумма=0,ФактСуммаБезНДС+ФактСуммаБезНДС*Выборка.СтавкаНДС/100,ФактСумма)*Выборка.Коэффициент;
				Движение.СуммаУпрБезНДС	= ФактСуммаБезНДС	* Выборка.Коэффициент;
				Движение.Количество		= ФактКоличество	* Выборка.Коэффициент;
				Движение.абс_Бюджет		= Справочники.Бюджеты.СводныйБДР;
				Движение.Сценарий		= Справочники.СценарииПланирования.Факт;
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры
