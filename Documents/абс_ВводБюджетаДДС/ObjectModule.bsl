////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

//заполняет табличную часть документа по регистру Обороты бюджета по выбранному сценарию
Процедура ЗаполнитьСтатьиДДС(Объект) Экспорт
	
	Запрос=Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ОборотыБюджетовОбороты.абс_ДоговорКонтрагента.КонтролироватьЧислоДнейЗадолженности, ЛОЖЬ) = ИСТИНА
	|			ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ОборотыБюджетовОбороты.Период, ДЕНЬ, ОборотыБюджетовОбороты.абс_ДоговорКонтрагента.ДопустимоеЧислоДнейЗадолженности), МЕСЯЦ)
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ОборотыБюджетовОбороты.Период, ДЕНЬ, ОборотыБюджетовОбороты.СтатьяОборотов.абс_ДопустимоеЧислоДнейЗадолженности), МЕСЯЦ)
	|	КОНЕЦ КАК Период,
	|	ОборотыБюджетовОбороты.ЦФО КАК ЦФО,
	|	ОборотыБюджетовОбороты.СтатьяОборотов КАК СтатьяОборотовПоБюджетам,
	|	ОборотыБюджетовОбороты.Контрагент КАК Контрагент,
	|	ОборотыБюджетовОбороты.абс_ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ВЫБОР
	|		КОГДА ОборотыБюджетовОбороты.СтатьяОборотов.абс_ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.абс_ТипыСтатейОборотовПоБюджетам.СтатьяРасходов)
	|			ТОГДА -ОборотыБюджетовОбороты.СуммаУпрОборот
	|		ИНАЧЕ ОборотыБюджетовОбороты.СуммаУпрОборот
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА ОборотыБюджетовОбороты.СтатьяОборотов.абс_ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.абс_ТипыСтатейОборотовПоБюджетам.СтатьяРасходов)
	|			ТОГДА -ОборотыБюджетовОбороты.СуммаУпрБезНДСОборот
	|		ИНАЧЕ ОборотыБюджетовОбороты.СуммаУпрБезНДСОборот
	|	КОНЕЦ КАК СуммаБезНДС,
	|	ЗНАЧЕНИЕ(Перечисление.абс_ПериодичностьНачисленияПремийКонтрагентов.Месяц) КАК ПериодичностьНачисления,
	|	ЛОЖЬ КАК ОплатаФиксированнойСуммой,
	|	0 КАК ДопустимоеЧислоДнейЗадолженности,
	|	0 КАК НомерМесяца,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиОборотовПоБюджетам.ПустаяСсылка) КАК СтатьяВыручки
	|ИЗ
	|	РегистрНакопления.ОборотыБюджетов.Обороты(
	|			НАЧАЛОПЕРИОДА(&Период, ГОД),
	|			КОНЕЦПЕРИОДА(&Период, ГОД),
	|			Месяц,
	|			Организация = &Организация
	|				И абс_Бюджет = ЗНАЧЕНИЕ(Справочник.Бюджеты.СводныйБДР)
	|				И Сценарий = &Сценарий
	|				И НЕ СтатьяОборотов.абс_ВидЗаполнения = ЗНАЧЕНИЕ(Перечисление.абс_ВидыЗаполненияСтатейОборотовПоБюджетам.Ретро)) КАК ОборотыБюджетовОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОборотыБюджетовОбороты.Период,
	|	ОборотыБюджетовОбороты.ЦФО,
	|	ОборотыБюджетовОбороты.СтатьяОборотов,
	|	ОборотыБюджетовОбороты.Контрагент,
	|	ОборотыБюджетовОбороты.абс_ДоговорКонтрагента,
	|	ВЫБОР
	|		КОГДА ОборотыБюджетовОбороты.СтатьяОборотов.абс_ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.абс_ТипыСтатейОборотовПоБюджетам.СтатьяРасходов)
	|			ТОГДА -ОборотыБюджетовОбороты.СуммаУпрОборот
	|		ИНАЧЕ ОборотыБюджетовОбороты.СуммаУпрОборот
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ОборотыБюджетовОбороты.СтатьяОборотов.абс_ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.абс_ТипыСтатейОборотовПоБюджетам.СтатьяРасходов)
	|			ТОГДА -ОборотыБюджетовОбороты.СуммаУпрБезНДСОборот
	|		ИНАЧЕ ОборотыБюджетовОбороты.СуммаУпрБезНДСОборот
	|	КОНЕЦ,
	|	ЕСТЬNULL(ОборотыБюджетовОбороты.абс_ДоговорКонтрагента.абс_ПериодичностьНачисленияПремииКонтрагента, ЗНАЧЕНИЕ(Перечисление.абс_ПериодичностьНачисленияПремийКонтрагентов.Месяц)),
	|	ЕСТЬNULL(ОборотыБюджетовОбороты.абс_ДоговорКонтрагента.абс_ОплатаПремииФиксированнойСуммой, ЛОЖЬ),
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ОборотыБюджетовОбороты.абс_ДоговорКонтрагента.КонтролироватьЧислоДнейЗадолженности, ЛОЖЬ) = ИСТИНА
	|			ТОГДА ЕСТЬNULL(ОборотыБюджетовОбороты.абс_ДоговорКонтрагента.ДопустимоеЧислоДнейЗадолженности, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	МЕСЯЦ(ОборотыБюджетовОбороты.Период),
	|	МАКСИМУМ(абс_СтатьиБюджетаПоЦФО.СтатьяОборотовПоБюджетам)
	|ИЗ
	|	РегистрНакопления.ОборотыБюджетов.Обороты(
	|			НАЧАЛОПЕРИОДА(&Период, ГОД),
	|			КОНЕЦПЕРИОДА(&Период, ГОД),
	|			Месяц,
	|			Организация = &Организация
	|				И абс_Бюджет = ЗНАЧЕНИЕ(Справочник.Бюджеты.СводныйБДР)
	|				И Сценарий = &Сценарий
	|				И СтатьяОборотов.абс_ВидЗаполнения = ЗНАЧЕНИЕ(Перечисление.абс_ВидыЗаполненияСтатейОборотовПоБюджетам.Ретро)) КАК ОборотыБюджетовОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.абс_СтатьиБюджетаПоЦФО КАК абс_СтатьиБюджетаПоЦФО
	|		ПО ОборотыБюджетовОбороты.ЦФО = абс_СтатьиБюджетаПоЦФО.ЦФО
	|ГДЕ
	|	абс_СтатьиБюджетаПоЦФО.СтатьяОборотовПоБюджетам.абс_ВидЗаполнения = ЗНАЧЕНИЕ(Перечисление.абс_ВидыЗаполненияСтатейОборотовПоБюджетам.Выручка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОборотыБюджетовОбороты.Период,
	|	ОборотыБюджетовОбороты.ЦФО,
	|	ОборотыБюджетовОбороты.СтатьяОборотов,
	|	ОборотыБюджетовОбороты.Контрагент,
	|	ОборотыБюджетовОбороты.абс_ДоговорКонтрагента,
	|	ЕСТЬNULL(ОборотыБюджетовОбороты.абс_ДоговорКонтрагента.абс_ПериодичностьНачисленияПремииКонтрагента, ЗНАЧЕНИЕ(Перечисление.абс_ПериодичностьНачисленияПремийКонтрагентов.Месяц)),
	|	ЕСТЬNULL(ОборотыБюджетовОбороты.абс_ДоговорКонтрагента.абс_ОплатаПремииФиксированнойСуммой, ЛОЖЬ),
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ОборотыБюджетовОбороты.абс_ДоговорКонтрагента.КонтролироватьЧислоДнейЗадолженности, ЛОЖЬ) = ИСТИНА
	|			ТОГДА ЕСТЬNULL(ОборотыБюджетовОбороты.абс_ДоговорКонтрагента.ДопустимоеЧислоДнейЗадолженности, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	МЕСЯЦ(ОборотыБюджетовОбороты.Период),
	|	ВЫБОР
	|		КОГДА ОборотыБюджетовОбороты.СтатьяОборотов.абс_ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.абс_ТипыСтатейОборотовПоБюджетам.СтатьяРасходов)
	|			ТОГДА -ОборотыБюджетовОбороты.СуммаУпрОборот
	|		ИНАЧЕ ОборотыБюджетовОбороты.СуммаУпрОборот
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ОборотыБюджетовОбороты.СтатьяОборотов.абс_ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.абс_ТипыСтатейОборотовПоБюджетам.СтатьяРасходов)
	|			ТОГДА -ОборотыБюджетовОбороты.СуммаУпрБезНДСОборот
	|		ИНАЧЕ ОборотыБюджетовОбороты.СуммаУпрБезНДСОборот
	|	КОНЕЦ
	|ИТОГИ
	|	СУММА(Сумма),
	|	СУММА(СуммаБезНДС),
	|	МАКСИМУМ(ПериодичностьНачисления),
	|	МАКСИМУМ(ОплатаФиксированнойСуммой),
	|	МАКСИМУМ(ДопустимоеЧислоДнейЗадолженности),
	|	МАКСИМУМ(НомерМесяца),
	|	МАКСИМУМ(СтатьяВыручки)
	|ПО
	|	СтатьяОборотовПоБюджетам,
	|	ЦФО,
	|	Контрагент,
	|	ДоговорКонтрагента,
	|	Период";
	Запрос.УстановитьПараметр("Период",ДатаПланирования);
	Запрос.УстановитьПараметр("Организация",Организация);
	Запрос.УстановитьПараметр("Сценарий", абс_Бюджетирование.ПолучитьСценарийПоВидуОперации(ВидОперации));
	
	ВыборкаСтатья=Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаСтатья.Следующий() Цикл
		
		ВыборкаЦФО=ВыборкаСтатья.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаЦФО.Следующий() Цикл
			
			ВыборкаКонтрагент=ВыборкаЦФО.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаКонтрагент.Следующий() Цикл
				
				ВыборкаДоговорКонтрагента=ВыборкаКонтрагент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаДоговорКонтрагента.Следующий() Цикл
					
					СтрокаТЧ=Объект.СоставБюджета.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТЧ,ВыборкаДоговорКонтрагента);
					
					Если ВыборкаДоговорКонтрагента.СтатьяОборотовПоБюджетам.абс_ВидЗаполнения=Перечисления.абс_ВидыЗаполненияСтатейОборотовПоБюджетам.Ретро Тогда
						
						ОплатаВзаимозачетом=Ложь;
						Если ВыборкаДоговорКонтрагента.ДоговорКонтрагента.абс_ВидОплатыУслугКонтрагента=Перечисления.абс_ВидыОплатУслугКонтрагентов.Взаимозачет Тогда
							СтрокаТЧ.СтатьяОборотовПоБюджетам=ВыборкаДоговорКонтрагента.СтатьяВыручки;
							ОплатаВзаимозачетом=Истина;
						КонецЕсли;
		
						ВыборкаПериод=ВыборкаДоговорКонтрагента.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						
						СуммаПремииОтПроцента=0;
						СуммаПремииОтПроцентаБезНДС=0;
						
						СуммаПремииФикс=0;
						СуммаПремииФиксБезНДС=0;
						
						СтруктураНомер=Новый Структура("НомерМесяца");
						
						Для сч=1 По 12 Цикл
							
							СтруктураНомер.НомерМесяца=Сч;
							ВыборкаПериод.Сбросить();
							Если ВыборкаПериод.НайтиСледующий(СтруктураНомер) Тогда
								СуммаПремииОтПроцента=СуммаПремииОтПроцента+ВыборкаПериод.Сумма;
								СуммаПремииОтПроцентаБезНДС=СуммаПремииОтПроцентаБезНДС+ВыборкаПериод.СуммаБезНДС;
								
								СуммаПремииФикс=ВыборкаПериод.Сумма;
								СуммаПремииФиксБезНДС=ВыборкаПериод.СуммаБезНДС;
							КонецЕсли;
							
							Если ВыборкаДоговорКонтрагента.ПериодичностьНачисления=Перечисления.абс_ПериодичностьНачисленияПремийКонтрагентов.Квартал Тогда
								Если Не Цел(Сч/3)=Сч/3 Тогда
									Продолжить;
								КонецЕсли;
							ИначеЕсли ВыборкаДоговорКонтрагента.ПериодичностьНачисления=Перечисления.абс_ПериодичностьНачисленияПремийКонтрагентов.Год Тогда
								Если Не Сч=12 Тогда
									Продолжить;
								КонецЕсли;
							КонецЕсли;
							
							Если СуммаПремииОтПроцента>0 Тогда
								ДатаПремии=НачалоМесяца(Добавитьмесяц(НачалоГода(Объект.ДатаПланирования),Сч-1)+ВыборкаДоговорКонтрагента.ДопустимоеЧислоДнейЗадолженности);
								Если Год(ДатаПремии)=Год(Объект.ДатаПланирования) Тогда
									МесяцПремии=Месяц(ДатаПремии);
									СтрокаТЧ["Сумма"+МесяцПремии]=?(ВыборкаДоговорКонтрагента.ОплатаФиксированнойСуммой, СуммаПремииФикс, СуммаПремииОтПроцента)*?(ОплатаВзаимозачетом,-1,1);
									СтрокаТЧ["СуммаБезНДС"+МесяцПремии]=?(ВыборкаДоговорКонтрагента.ОплатаФиксированнойСуммой, СуммаПремииФиксБезНДС,СуммаПремииОтПроцентаБезНДС)*?(ОплатаВзаимозачетом,-1,1);
								КонецЕсли;
								СуммаПремииОтПроцента=0;
								СуммаПремииОтПроцентаБезНДС=0;
							КонецЕсли;
						КонецЦикла;
					Иначе
						ВыборкаПериод=ВыборкаДоговорКонтрагента.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						Пока ВыборкаПериод.Следующий() Цикл
							СтрокаТЧ["Сумма"+Месяц(ВыборкаПериод.Период)]=ВыборкаПериод.Сумма;
							СтрокаТЧ["СуммаБезНДС"+Месяц(ВыборкаПериод.Период)]=ВыборкаПериод.СуммаБезНДС;
						КонецЦикла;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаЗаполнения(ДанныеЗаполнения,СтандартнаяОбработка)
	
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
	ВидОперации = Перечисления.абс_ВидыОперацийВводБюджета.Бюджет;
	ДатаПланирования = НачалоГода(ТекущаяДата());
КонецПроцедуры                                                     

Процедура ОбработкаПроведения(Отказ, Режим)
	
	// регистр ОборотыБюджетов 
	Движения.ОборотыБюджетов.Записывать = Истина;
	Движения.ОборотыБюджетов.Очистить();
	
	Запрос=Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	абс_ВводБюджетаДДССоставБюджета.СтатьяОборотовПоБюджетам КАК СтатьяОборотов,
	|	абс_ВводБюджетаДДССоставБюджета.СуммаБезНДС1,
	|	абс_ВводБюджетаДДССоставБюджета.СуммаБезНДС2,
	|	абс_ВводБюджетаДДССоставБюджета.СуммаБезНДС3,
	|	абс_ВводБюджетаДДССоставБюджета.СуммаБезНДС4,
	|	абс_ВводБюджетаДДССоставБюджета.СуммаБезНДС5,
	|	абс_ВводБюджетаДДССоставБюджета.СуммаБезНДС6,
	|	абс_ВводБюджетаДДССоставБюджета.СуммаБезНДС7,
	|	абс_ВводБюджетаДДССоставБюджета.СуммаБезНДС8,
	|	абс_ВводБюджетаДДССоставБюджета.СуммаБезНДС9,
	|	абс_ВводБюджетаДДССоставБюджета.СуммаБезНДС10,
	|	абс_ВводБюджетаДДССоставБюджета.СуммаБезНДС11,
	|	абс_ВводБюджетаДДССоставБюджета.СуммаБезНДС12,
	|	ВЫБОР
	|		КОГДА абс_ВводБюджетаДДССоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20)
	|				ИЛИ абс_ВводБюджетаДДССоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20_120)
	|			ТОГДА 20
	|		КОГДА абс_ВводБюджетаДДССоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10)
	|				ИЛИ абс_ВводБюджетаДДССоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10_110)
	|			ТОГДА 10
	|		КОГДА абс_ВводБюджетаДДССоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18)
	|				ИЛИ абс_ВводБюджетаДДССоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118)
	|			ТОГДА 18
	|		КОГДА абс_ВводБюджетаДДССоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС5)
	|				ИЛИ абс_ВводБюджетаДДССоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС5_105)
	|			ТОГДА 5
	|		КОГДА абс_ВводБюджетаДДССоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС7)
	|				ИЛИ абс_ВводБюджетаДДССоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС7_107)
	|			ТОГДА 7
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтавкаНДС,
	|	абс_ВводБюджетаДДССоставБюджета.Сумма1,
	|	абс_ВводБюджетаДДССоставБюджета.Сумма2,
	|	абс_ВводБюджетаДДССоставБюджета.Сумма3,
	|	абс_ВводБюджетаДДССоставБюджета.Сумма4,
	|	абс_ВводБюджетаДДССоставБюджета.Сумма5,
	|	абс_ВводБюджетаДДССоставБюджета.Сумма6,
	|	абс_ВводБюджетаДДССоставБюджета.Сумма7,
	|	абс_ВводБюджетаДДССоставБюджета.Сумма8,
	|	абс_ВводБюджетаДДССоставБюджета.Сумма9,
	|	абс_ВводБюджетаДДССоставБюджета.Сумма10,
	|	абс_ВводБюджетаДДССоставБюджета.Сумма11,
	|	абс_ВводБюджетаДДССоставБюджета.Сумма12,
	|	абс_ВводБюджетаДДССоставБюджета.Контрагент,
	|	абс_ВводБюджетаДДССоставБюджета.ЦФО,
	|	абс_ВводБюджетаДДССоставБюджета.Ссылка.Организация,
	|	ВЫБОР
	|		КОГДА абс_ВводБюджетаДДССоставБюджета.СтатьяОборотовПоБюджетам.абс_ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.абс_ТипыСтатейОборотовПоБюджетам.СтатьяРасходов)
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Коэффициент,	
	|	абс_ВводБюджетаДДССоставБюджета.ДоговорКонтрагента КАК абс_ДоговорКонтрагента
	|ИЗ
	|	Документ.абс_ВводБюджетаДДС.СоставБюджета КАК абс_ВводБюджетаДДССоставБюджета
	|ГДЕ
	|	абс_ВводБюджетаДДССоставБюджета.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
	Сценарий=абс_Бюджетирование.ПолучитьСценарийПоВидуОперации(ВидОперации);
	Выборка=Запрос.Выполнить().Выбрать();                            
	Пока Выборка.Следующий() Цикл
		Для сч=1 по 12 Цикл
			Сумма=Выборка["Сумма"+СокрЛП(Сч)];
			СуммаБезНДС=Выборка["СуммаБезНДС"+СокрЛП(Сч)];
			Если СуммаБезНДС<>0 Тогда
				Движение = Движения.ОборотыБюджетов.Добавить();
				ЗаполнитьЗначенияСвойств(Движение,Выборка);
				Движение.Период = ДобавитьМесяц(ДатаПланирования,Сч-1);
				Движение.СуммаУпр=?(Сумма=0,СуммаБезНДС+СуммаБезНДС*Выборка.СтавкаНДС/100,Сумма)*Выборка.Коэффициент;
				Движение.СуммаУпрБезНДС=СуммаБезНДС*Выборка.Коэффициент;
				Движение.абс_Бюджет=Справочники.Бюджеты.БДДС;
				Движение.Сценарий=Сценарий;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПередЗаписью()
	
	СуммаДокумента=0;
	Для сч=1 по 12 Цикл
		СуммаДокумента=СуммаДокумента+СоставБюджета.Итог("Сумма"+Сч);
	КонецЦикла;
	
КонецПроцедуры

