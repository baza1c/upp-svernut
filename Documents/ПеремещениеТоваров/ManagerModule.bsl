// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ОшибкиПечати          - Список значений  - Ошибки печати  (значение - ссылка на объект, представление - текст ошибки)
//   ОбъектыПечати         - Список значений  - Объекты печати (значение - ссылка на объект, представление - имя области в которой был выведен объект)
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Ведомость") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "Ведомость", "Перемещение товаров", ПечатьПеремещениеТоваров(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТОРГ13") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ТОРГ13", "ТОРГ-13 (Накладная на внутреннее перемещение)", ПечатьТОРГ13(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "М4") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "М4", "М-4 (Приходный ордер)", ПечатьМ4(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;

	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "М11") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "М11", "M-11 (Требование-накладная)", ПечатьМ11(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
	//АБС ВСТАВКА 38854  13.02.2014 11:07:42  Шамов
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "М15") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "М15", "M-15 (Отпуск материалов на сторону)", ПечатьМ15(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;

	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МХ18") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "МХ18", "MХ-18 (Передача готовой продукции в места хранения)", ПечатьМХ18(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	//АБС ВСТАВКА 38854 КОНЕЦ

КонецПроцедуры

// Функция формирует табличный документ с печатной формой накладной,
// разработанной методистами
//
// Возвращаемое значение:
//  Табличный документ - печатная форма накладной
//
Функция ПечатьПеремещениеТоваров(МассивОбъектов, ОбъектыПечати)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Номер,
	|	Дата,
	|	Организация,
	|	СкладОтправитель,
	|	СкладОтправитель.Представление КАК Поставщик,
	|	СкладПолучатель,
	|	СкладПолучатель.Представление  КАК Покупатель,
	|	Товары.(
	|		Номенклатура,
	|		Номенклатура.НаименованиеПолное КАК Товар,
	|		Номенклатура.Код                КАК Код,
	|		Номенклатура.Артикул            Как Артикул,
	|		КоличествоМест,
	|		Количество,
	|		ЕдиницаИзмерения.Представление     КАК ЕдиницаИзмерения,
	|		ЕдиницаИзмеренияМест.Представление КАК ЕдиницаИзмеренияМест,
	|		Цена,
	|		ХарактеристикаНоменклатуры КАК Характеристика,
	|		СерияНоменклатуры КАК Серия
	|	),
	|	ВозвратнаяТара.(
	|		Номенклатура,
	|		Номенклатура.НаименованиеПолное КАК Товар,
	|		Номенклатура.Код                КАК Код,
	|		Номенклатура.Артикул            КАК Артикул,
	|		0 КАК КоличествоМест,
	|		Количество,
	|		0 КАК Цена,
	|		Номенклатура.ЕдиницаХраненияОстатков.Представление КАК ЕдиницаИзмерения,
	|		NULL                                               КАК ЕдиницаИзмеренияМест
	|	)
	|ИЗ
	|	Документ.ПеремещениеТоваров КАК ПеремещениеТоваров
	|ГДЕ
	|	ПеремещениеТоваров.Ссылка = &ТекущийДокумент
	|
	|УПОРЯДОЧИТЬ ПО
	|	Товары.НомерСтроки,
	|	ВозвратнаяТара.НомерСтроки
	|";
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПеремещениеТоваров_Накладная";
	Макет       = ПолучитьМакет("Накладная");

	// Вывести табличную часть
	ДопКолонка = Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить();
	Если ДопКолонка = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул Тогда
		ВыводитьКоды = Истина;
		Колонка = "Артикул";
	ИначеЕсли ДопКолонка = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.Код Тогда
		ВыводитьКоды = Истина;
		Колонка = "Код";
	Иначе
		ВыводитьКоды = Ложь;
	КонецЕсли;

	ПервыйДокумент = Истина;
	
	Для Каждого Ссылка Из МассивОбъектов Цикл
		//Макет необходимо получать для каждого следующего документа,
		//	т.к. в процессе печати меняется ширина колонок исходного макета
		Макет       = ПолучитьМакет("Накладная");

		Если Не ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;

		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ТекущийДокумент", Ссылка);

		Запрос.Текст = ТекстЗапроса;

		Шапка = Запрос.Выполнить().Выбрать();
		Шапка.Следующий();
		ВыборкаСтрокТовары = Шапка.Товары.Выбрать();
		ВыборкаСтрокТара   = Шапка.ВозвратнаяТара.Выбрать();

		// Выводим шапку накладной
		ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
		ОбластьМакета.Параметры.ТекстЗаголовка = ОбщегоНазначения.СформироватьЗаголовокДокумента(Шапка, "Накладная на перемещение");
		ТабДокумент.Вывести(ОбластьМакета);

		ОбластьМакета = Макет.ПолучитьОбласть("Поставщик");
		ОбластьМакета.Параметры.ПредставлениеПоставщика = Шапка.Поставщик;
		ОбластьМакета.Параметры.Поставщик = Шапка.СкладОтправитель;
		ТабДокумент.Вывести(ОбластьМакета);

		ОбластьМакета = Макет.ПолучитьОбласть("Покупатель");
		ОбластьМакета.Параметры.ПредставлениеПолучателя = Шапка.Покупатель;
		ОбластьМакета.Параметры.Получатель = Шапка.СкладПолучатель;
		ТабДокумент.Вывести(ОбластьМакета);

        //АБС ИЗМЕНЕНИЕ   25.03.2014 12:51:58  Балашенко
		Если Шапка.СкладОтправитель.ВидСклада = Перечисления.ВидыСкладов.НТТ
		ИЛИ Шапка.СкладПолучатель.ВидСклада = Перечисления.ВидыСкладов.НТТ Тогда
			ВыводитьЦены = Истина;
			ИмяШапки  = "ШапкаТаблицыСЦенами";
			ИмяСтроки = "СтрокаСЦенами";
			ИмяИтога =  "Итог";
		Иначе
			Если ЗначениеЗаполнено(Ссылка.ТипЦен) Тогда
				ИмяШапки  = "ШапкаТаблицыСЦенами";
				ИмяСтроки = "СтрокаСЦенами";
				ИмяИтога =  "Итог";
			Иначе
				ИмяШапки  = "ШапкаТаблицы";
				ИмяСтроки = "Строка";
				ИмяИтога =  "Итог1";
			КонецЕсли;
		КонецЕсли;
		//АБС ИЗМЕНЕНИЕ  КОНЕЦ
		
		ОбластьНомера = Макет.ПолучитьОбласть(ИмяШапки + "|НомерСтроки");
		ОбластьКодов  = Макет.ПолучитьОбласть(ИмяШапки + "|КолонкаКодов");
		ОбластьДанных = Макет.ПолучитьОбласть(ИмяШапки + "|Данные");

		ТабДокумент.Вывести(ОбластьНомера);
		Если ВыводитьКоды Тогда
			ОбластьКодов.Параметры.ИмяКолонкиКодов = Колонка;
			ТабДокумент.Присоединить(ОбластьКодов);
		КонецЕсли;
		ТабДокумент.Присоединить(ОбластьДанных);
		ОбластьКолонкаТовар = Макет.Область("Товар");
		Если Не ВыводитьКоды Тогда
			ОбластьКолонкаТовар.ШиринаКолонки = ОбластьКолонкаТовар.ШиринаКолонки + 
												Макет.Область("КолонкаКодов").ШиринаКолонки;
		КонецЕсли;

		ОбластьНомера = Макет.ПолучитьОбласть(ИмяСтроки + "|НомерСтроки");
		ОбластьКодов  = Макет.ПолучитьОбласть(ИмяСтроки + "|КолонкаКодов");
		ОбластьДанных = Макет.ПолучитьОбласть(ИмяСтроки + "|Данные");
		
		// Инициализация итогов в документе
		ИтогоКоличествоМест = 0;
		ИтогоКоличество 	= 0;
		ИтогоСумма    		= 0;

		Ном = 0;
	    ФлЕстьКоличествоМест = ложь;
		// Выборка товаров
		Пока ВыборкаСтрокТовары.Следующий() Цикл

			Если НЕ ЗначениеЗаполнено(ВыборкаСтрокТовары.Номенклатура) Тогда
				Продолжить;
			КонецЕсли;

			Ном = Ном + 1;
			Если ВыборкаСтрокТовары.КоличествоМест<>0 Тогда 
				ФлЕстьКоличествоМест = истина;
			КонецЕсли;
			
			//ОбластьНомера.Параметры.Заполнить(ВыборкаСтрокТовары);
			ОбластьНомера.Параметры.НомерСтроки = Ном;
			ТабДокумент.Вывести(ОбластьНомера);

			Если ВыводитьКоды Тогда
				Если Колонка = "Артикул" Тогда
					ОбластьКодов.Параметры.Артикул = ВыборкаСтрокТовары.Артикул;
				Иначе
					ОбластьКодов.Параметры.Артикул = ВыборкаСтрокТовары.Код;
				КонецЕсли;
				ТабДокумент.Присоединить(ОбластьКодов);
			КонецЕсли;

			ОбластьДанных.Параметры.Заполнить(ВыборкаСтрокТовары);
			ОбластьДанных.Параметры.Товар = ВыборкаСтрокТовары.Товар + ФормированиеПечатныхФормСервер.ПредставлениеСерий(ВыборкаСтрокТовары);

			ТабДокумент.Присоединить(ОбластьДанных);
			// + Лаврухин 05.11.2014
			ИтогоКоличествоМест = ИтогоКоличествоМест + ВыборкаСтрокТовары.КоличествоМест;
			ИтогоКоличество     = ИтогоКоличество + ВыборкаСтрокТовары.Количество;
			ИтогоСумма          = ИтогоСумма + ВыборкаСтрокТовары.Цена * ВыборкаСтрокТовары.Количество; 
            // - Лаврухин 05.11.2014
		КонецЦикла;

		// Выборка возвратная тара
		Пока ВыборкаСтрокТара.Следующий() Цикл

			Если НЕ ЗначениеЗаполнено(ВыборкаСтрокТара.Номенклатура) Тогда
				Продолжить;
			КонецЕсли;
			Ном = Ном + 1;

			//ОбластьНомера.Параметры.Заполнить(ВыборкаСтрокТара);
			ОбластьНомера.Параметры.НомерСтроки = Ном;
			ТабДокумент.Вывести(ОбластьНомера);

			Если ВыводитьКоды Тогда
				Если Колонка = "Артикул" Тогда
					ОбластьКодов.Параметры.Артикул = ВыборкаСтрокТара.Артикул;
				Иначе
					ОбластьКодов.Параметры.Артикул = ВыборкаСтрокТара.Код;
				КонецЕсли;
				ТабДокумент.Присоединить(ОбластьКодов);
			КонецЕсли;
            
			ОбластьДанных.Параметры.Заполнить(ВыборкаСтрокТара);
			ОбластьДанных.Параметры.Товар = ВыборкаСтрокТара.Товар;
			ТабДокумент.Присоединить(ОбластьДанных);
           
		КонецЦикла;

		//// Вывести подвал
		//ОбластьНомера = Макет.ПолучитьОбласть("Подвал|НомерСтроки");
		//ОбластьКодов  = Макет.ПолучитьОбласть("Подвал|КолонкаКодов");
		//ОбластьДанных = Макет.ПолучитьОбласть("Подвал|Данные");
		
		// + Лаврухин 05.11.2014                                                 
		
		ОбластьНомера = Макет.ПолучитьОбласть(ИмяИтога + "|НомерСтроки");
		ОбластьКодов  = Макет.ПолучитьОбласть(ИмяИтога + "|КолонкаКодов");		
		ОбластьДанных = Макет.ПолучитьОбласть(ИмяИтога + "|Данные");
		ОбластьДанных.Параметры.ИтогоМест = ИтогоКоличествоМест;
		ОбластьДанных.Параметры.ЕдиницаИзмеренияМест = ВыборкаСтрокТовары.ЕдиницаИзмеренияМест;
		ОбластьДанных.Параметры.ИтогоКоличество = ИтогоКоличество;
		ОбластьДанных.Параметры.ЕдиницаИзмерения = ВыборкаСтрокТовары.ЕдиницаИзмерения;

		Если Шапка.СкладОтправитель.ВидСклада = Перечисления.ВидыСкладов.НТТ
		ИЛИ Шапка.СкладПолучатель.ВидСклада = Перечисления.ВидыСкладов.НТТ Тогда
   			ОбластьДанных.Параметры.Сумма = ИтогоСумма;
		Иначе
			Если ЗначениеЗаполнено(Ссылка.ТипЦен) Тогда
				ОбластьДанных.Параметры.Сумма = ИтогоСумма;			
			КонецЕсли;
		КонецЕсли;	

		// - Лаврухин 05.11.2014		

		ТабДокумент.Вывести(ОбластьНомера);
		Если ВыводитьКоды Тогда
			ТабДокумент.Присоединить(ОбластьКодов);
		КонецЕсли;
		ТабДокумент.Присоединить(ОбластьДанных);

		// Вывести подписи
		ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
		ОбластьМакета.Параметры.Заполнить(Шапка);
		ТабДокумент.Вывести(ОбластьМакета);

		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, НомерСтрокиНачало, ОбъектыПечати, Ссылка);

    КонецЦикла;
	
	Возврат ТабДокумент;

КонецФункции // ПечатьПеремещениеТоваров()

// Формирует табличный документ по унифицированной форме ТОРГ-13
// 
// Возвращаемое значение
//  Табличный документ - унифицированная форма ТОРГ-13
//
Функция ПечатьТОРГ13(МассивОбъектов, ОбъектыПечати)

	ДопКолонка = Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить();
	Если ДопКолонка = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул Тогда
		ТоварКод = "Артикул";
	Иначе
		ТоварКод = "Код";
	КонецЕсли;
	
	ТекстЗапроса = 	"ВЫБРАТЬ
	|	Номер КАК НомерДокумента,
	|	Дата  КАК ДатаДокумента,
	|	Организация,
	|	СкладОтправитель КАК ОтправительПодразделение,
	|	СкладПолучатель  КАК ПолучательПодразделение,
	|	СкладОтправитель.ВидСклада КАК ОтправительВидСклада,
	|	СкладПолучатель.ВидСклада  КАК ПолучательВидСклада,
    |   ИдентификаторГосКонтракта  КАК ИдентификаторГосКонтракта,
    |	ОтветственныеЛицаОтправитель.ФизическоеЛицо КАК ФизЛицоОтправитель,
	|	ОтветственныеЛицаПолучатель.ФизическоеЛицо  КАК ФизЛицоПолучатель,
	|	Товары.(
	|		Номенклатура,
	|		Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	|		Номенклатура." + ТоварКод + " КАК ТоварКод,
	|		Цена,
	|		Количество,
	|		КоличествоМест,
	|		Номенклатура.ЕдиницаХраненияОстатков.Коэффициент КАК ЕдиницаХраненияОстатковКоэффициент,
	|		ЕдиницаИзмерения.Коэффициент КАК ДокЕдиницаКоэффициент,
	|		ЕдиницаИзмерения.Представление               КАК ЕдиницаИзмеренияНаименование,
	|		ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код КАК ЕдиницаИзмеренияКодПоОКЕИ,
	|		ЕдиницаИзмеренияМест.Представление           КАК ЕдиницаИзмеренияМестНаименование,
	|		ВЫБОР
	|			КОГДА КоличествоМест > 0 ТОГДА ЕдиницаИзмеренияМест.Коэффициент / Коэффициент
	|			ИНАЧЕ NULL
	|		КОНЕЦ                                        КАК КоличествоВОдномМесте,
	|		ХарактеристикаНоменклатуры                   КАК Характеристика,
	|		СерияНоменклатуры                            КАК Серия,
	|		ВЫБОР
	|			КОГДА Количество > 0 И НЕ ЕдиницаИзмеренияМест.Вес ЕСТЬ NULL
	|				ТОГДА КоличествоМест * ЕдиницаИзмеренияМест.Вес
	|			ИНАЧЕ Количество * ЕдиницаИзмерения.Вес
	|		КОНЕЦ КАК МассаБрутто,
	|		Количество * ЕдиницаИзмерения.Коэффициент/Номенклатура.ЕдиницаХраненияОстатков.Коэффициент * Номенклатура.ЕдиницаХраненияОстатков.Вес КАК МассаНетто,
	|		Номенклатура.ВестиПартионныйУчетПоСериям     КАК ПартионныйУчетПоСериям
	|	),
	|	ВозвратнаяТара.(
	|		Номенклатура,
	|		Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	|		Номенклатура." + ТоварКод + " КАК ТоварКод,
	|		0 КАК Цена,
	|		0 КАК Сумма,
	|		0 КАК КоличествоВОдномМесте,
	|		Количество,
	|		Номенклатура.ЕдиницаХраненияОстатков.Представление               КАК ЕдиницаИзмеренияНаименование,
	|		Номенклатура.ЕдиницаХраненияОстатков.ЕдиницаПоКлассификатору.Код КАК ЕдиницаИзмеренияКодПоОКЕИ
	|	)
	|ИЗ
	|	Документ.ПеремещениеТоваров КАК ПеремещениеТоваров
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрСведений.ОтветственныеЛица.СрезПоследних(&Дата, СтруктурнаяЕдиница = &СкладОтправитель) КАК ОтветственныеЛицаОтправитель
	|ПО
	|	ИСТИНА
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрСведений.ОтветственныеЛица.СрезПоследних(&Дата, СтруктурнаяЕдиница = &СкладПолучатель)  КАК ОтветственныеЛицаПолучатель
	|ПО
	|	ИСТИНА
	|
	|ГДЕ
	|	ПеремещениеТоваров.Ссылка = &ТекущийДокумент
	|
	|УПОРЯДОЧИТЬ ПО
	|	Товары.НомерСтроки,
	|	ВозвратнаяТара.НомерСтроки
	|";
	
	Макет = ПолучитьОбщийМакет("ТОРГ13");

	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПеремещениеТоваров_ТОРГ13";

 	ВалютаПечати = глЗначениеПеременной("ВалютаРегламентированногоУчета");

   	ПервыйДокумент = Истина;
	
	Для Каждого Ссылка Из МассивОбъектов Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;

		Запрос       = Новый Запрос;
		Запрос.УстановитьПараметр("Дата",             Ссылка.Дата);
		Запрос.УстановитьПараметр("ТекущийДокумент",  Ссылка);
		Запрос.УстановитьПараметр("СкладОтправитель", Ссылка.СкладОтправитель);
		Запрос.УстановитьПараметр("СкладПолучатель",  Ссылка.СкладПолучатель);
	    Запрос.Текст = ТекстЗапроса;


		Шапка = Запрос.Выполнить().Выбрать();
		Шапка.Следующий();

		ВыборкаСтрокТовары = Шапка.Товары.Выбрать();
		ВыборкаСтрокТара   = Шапка.ВозвратнаяТара.Выбрать();

		ЭтоСкладНТТ = (Шапка.ОтправительВидСклада = Перечисления.ВидыСкладов.НТТ
		              ИЛИ Шапка.ПолучательВидСклада = Перечисления.ВидыСкладов.НТТ);

		Если НЕ ЭтоСкладНТТ Тогда
			ТаблицаСтоимости        = ПолучитьТаблицуСтоимостиТоваров(Ссылка);
			Если ТаблицаСтоимости = Неопределено Тогда
				Продолжить;
			КонецЕсли;	
			ВалютаУпрУчета          = глЗначениеПеременной("ВалютаУправленческогоУчета");
			Параметры               = МодульВалютногоУчета.ПолучитьКурсВалюты(ВалютаУпрУчета, Ссылка.Дата);
			КурсВалютыУпрУчета      = Параметры.Курс;
			КратностьВалютыУпрУчета = Параметры.Кратность;
		КонецЕсли;
		
		// Выводим общие реквизиты шапки
		СведенияОбОрганизации = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Шапка.Организация, Шапка.ДатаДокумента);
		ОбластьМакета         = Макет.ПолучитьОбласть("Шапка");
		ОбластьМакета.Параметры.Заполнить(Шапка);
		ОбластьМакета.Параметры.ПредставлениеОрганизации = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Шапка.Организация, Шапка.ДатаДокумента));
		ОбластьМакета.Параметры.НомерДокумента           = ОбщегоНазначения.ПолучитьНомерНаПечать(Ссылка);
		ОбластьМакета.Параметры.ДатаДокумента            = Шапка.ДатаДокумента;
		ОбластьМакета.Параметры.ОрганизацияПоОКПО        = СведенияОбОрганизации.КодПоОКПО;
		ТабДокумент.Вывести(ОбластьМакета);
		
		//{Лео Катанович
		//ИГК
		ИГК = Шапка.ИдентификаторГосКонтракта;	
		Если ЗначениеЗаполнено(ИГК) Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("ИдентификаторГосКонтракта");
			ШаблонЗаголовкаГосконтракт = НСтр("ru = 'Идентификатор государственного контракта, договора (соглашения) (при наличии): %1'");
			ОбластьМакета.Параметры.ИдентификаторГосКонтракта = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонЗаголовкаГосконтракт, ИГК);
			ТабДокумент.Вывести(ОбластьМакета);
		КонецЕсли;
		//Лео Катанович}

		НомерСтраницы   = 1;
		
		// Выводим заголовок таблицы
		ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
		ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы; 
		ТабДокумент.Вывести(ЗаголовокТаблицы);
		
		// инициализация итогов по странице
		ИтогКоличествоМестПоСтранице = 0;
		ИтогМассаБруттоПоСтранице    = 0;
		ИтогМассыНеттоПоСтранице     = 0;
		ИтогСуммыПоСтранице          = 0;

		// инициализация итогов по документу
		ИтогоКоличество  = 0;
		ИтогоМассаБрутто = 0;
		ИтогоМассаНетто  = 0;
		ИтогоСумма       = 0;
		
		ОбластьИтоговПоСтранице = Макет.ПолучитьОбласть("ИтогиПоСтранице");
		ОбластьМакета			= Макет.ПолучитьОбласть("Строка");
		ОбластьВсего			= Макет.ПолучитьОбласть("Всего");
		ОбластьПодвал			= Макет.ПолучитьОбласть("Подвал");
		
		// Выводим многострочную часть докмента
		ВыборкаСтрокТовары      = Шапка.Товары.Выбрать();
		
		Ном = 0;
		КоличествоСтрок = ВыборкаСтрокТовары.Количество() + ВыборкаСтрокТара.Количество();
		МассивВыводимыхОбластей = Новый Массив;
		
		// Выборка товаров
		Пока ВыборкаСтрокТовары.Следующий() Цикл
			
			Если НЕ ЗначениеЗаполнено(ВыборкаСтрокТовары.Номенклатура) Тогда
				Продолжить;
			КонецЕсли;
			
			Ном = Ном + 1;
			
			ОбластьМакета.Параметры.Заполнить(ВыборкаСтрокТовары);
			
			КоличествоМест = Формат(ВыборкаСтрокТовары.Количество, "ЧЦ=15; ЧДЦ=3");
			
			Если ВыборкаСтрокТовары.КоличествоМест <> 0 И ЗначениеЗаполнено(ВыборкаСтрокТовары.ЕдиницаИзмеренияМестНаименование) Тогда
				КоличествоМест = КоличествоМест 
				                 + Символы.ПС
				                 + " (" + Формат(ВыборкаСтрокТовары.КоличествоМест,"ЧЦ=15; ЧДЦ=0")
				                 + " "
				                 + ВыборкаСтрокТовары.ЕдиницаИзмеренияМестНаименование
				                 + ")";
			КонецЕсли;

			ОбластьМакета.Параметры.КоличествоМест    = КоличествоМест;
			ОбластьМакета.Параметры.ТоварНаименование = ВыборкаСтрокТовары.ТоварНаименование + ФормированиеПечатныхФормСервер.ПредставлениеСерий(ВыборкаСтрокТовары);

			Если ЭтоСкладНТТ Тогда
				Сумма = ВыборкаСтрокТовары.Количество * ВыборкаСтрокТовары.Цена;
			Иначе
				Если ВыборкаСтрокТовары.Количество = 0 Тогда
					Сумма = 0;
					Цена  = 0;
				Иначе
					Количество = 0;
					Сумма = ПолучитьСтоимостьТовара(ТаблицаСтоимости,
					                                ВыборкаСтрокТовары.Номенклатура,
					                                ВыборкаСтрокТовары.Характеристика,
					                                ВыборкаСтрокТовары.Серия,
					                                ВыборкаСтрокТовары.ПартионныйУчетПоСериям,
					                                Количество);
					Цена  = ?(Количество = 0, 0, Сумма / Количество);
				КонецЕсли;

				Если ВыборкаСтрокТовары.ДокЕдиницаКоэффициент <> 0 Тогда
					Количество = Количество * ВыборкаСтрокТовары.ЕдиницаХраненияОстатковКоэффициент / ВыборкаСтрокТовары.ДокЕдиницаКоэффициент;
				КонецЕсли;
				
				//АБС ИЗМЕНЕНИЕ   25.03.2014 13:59:57  Балашенко
				Если ЗначениеЗаполнено(Ссылка.ТипЦен) Тогда
					Цена = ВыборкаСтрокТовары.Цена;
				Иначе
					Цена = Окр(?(Количество > 0, Сумма / Количество, 0), 2);
				КонецЕсли;
				//АБС ИЗМЕНЕНИЕ  КОНЕЦ

				Сумма = Цена * ВыборкаСтрокТовары.Количество;
				ОбластьМакета.Параметры.Цена = Цена;
			КонецЕсли;
			
			ОбластьМакета.Параметры.Сумма = Сумма;
			
			МассивВыводимыхОбластей.Очистить();
			МассивВыводимыхОбластей.Добавить(ОбластьМакета);
			МассивВыводимыхОбластей.Добавить(ОбластьИтоговПоСтранице);
			Если Ном = КоличествоСтрок Тогда
				МассивВыводимыхОбластей.Добавить(ОбластьВсего);
				МассивВыводимыхОбластей.Добавить(ОбластьПодвал);
			КонецЕсли;
			
			Если НЕ ФормированиеПечатныхФормСервер.ПроверитьВыводТабличногоДокумента(ТабДокумент, МассивВыводимыхОбластей) Тогда
				
				ОбластьИтоговПоСтранице.Параметры.ИтогКоличествоМестПоСтранице = ИтогКоличествоМестПоСтранице;
				ОбластьИтоговПоСтранице.Параметры.ИтогМассаБруттоПоСтранице    = ИтогМассаБруттоПоСтранице;
				ОбластьИтоговПоСтранице.Параметры.ИтогМассыНеттоПоСтранице     = ИтогМассыНеттоПоСтранице;
				ОбластьИтоговПоСтранице.Параметры.ИтогСуммыПоСтранице          = ИтогСуммыПоСтранице;
				
				ТабДокумент.Вывести(ОбластьИтоговПоСтранице);
				
				// инициализация итогов по странице
				ИтогКоличествоМестПоСтранице = 0;
				ИтогМассаБруттоПоСтранице    = 0;
				ИтогМассыНеттоПоСтранице     = 0;
				ИтогСуммыПоСтранице          = 0;

				НомерСтраницы = НомерСтраницы + 1;
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
				ТабДокумент.Вывести(ЗаголовокТаблицы);
				
			КонецЕсли;
			
			ТабДокумент.Вывести(ОбластьМакета);

			// Обновим итоги по странице
			ИтогКоличествоМестПоСтранице = ИтогКоличествоМестПоСтранице + ВыборкаСтрокТовары.Количество;
			ИтогСуммыПоСтранице          = ИтогСуммыПоСтранице          + Сумма;
			ИтогМассаБруттоПоСтранице 	 = ИтогМассаБруттоПоСтранице    + ВыборкаСтрокТовары.МассаБрутто;
			ИтогМассыНеттоПоСтранице 	 = ИтогМассыНеттоПоСтранице     + ВыборкаСтрокТовары.МассаНетто;

			// Обновим итогов по документу
			ИтогоКоличество  = ИтогоКоличество  + ВыборкаСтрокТовары.Количество;
			ИтогоСумма       = ИтогоСумма       + Сумма;
            ИтогоМассаБрутто = ИтогоМассаБрутто + ВыборкаСтрокТовары.МассаБрутто;
			ИтогоМассаНетто  = ИтогоМассаНетто  + ВыборкаСтрокТовары.МассаНетто;

		КонецЦикла;

		// Выборка возвратная тара
		Пока ВыборкаСтрокТара.Следующий() Цикл

			Если НЕ ЗначениеЗаполнено(ВыборкаСтрокТара.Номенклатура) Тогда
				Продолжить;
			КонецЕсли;

			Ном = Ном + 1;

			ОбластьМакета.Параметры.Заполнить(ВыборкаСтрокТара);

			ОбластьМакета.Параметры.КоличествоМест    = Формат(ВыборкаСтрокТара.Количество, "ЧЦ=15; ЧДЦ=3");
			ОбластьМакета.Параметры.ТоварНаименование = ВыборкаСтрокТара.ТоварНаименование + " (возвратная тара)";
			
			МассивВыводимыхОбластей.Очистить();
			МассивВыводимыхОбластей.Добавить(ОбластьМакета);
			МассивВыводимыхОбластей.Добавить(ОбластьИтоговПоСтранице);
			Если Ном = КоличествоСтрок Тогда
				МассивВыводимыхОбластей.Добавить(ОбластьВсего);
				МассивВыводимыхОбластей.Добавить(ОбластьПодвал);
			КонецЕсли;
				
			Если НЕ ФормированиеПечатныхФормСервер.ПроверитьВыводТабличногоДокумента(ТабДокумент, МассивВыводимыхОбластей) Тогда
				
				ОбластьИтоговПоСтранице.Параметры.ИтогКоличествоМестПоСтранице = ИтогКоличествоМестПоСтранице;
				ОбластьИтоговПоСтранице.Параметры.ИтогМассаБруттоПоСтранице    = ИтогМассаБруттоПоСтранице;
				ОбластьИтоговПоСтранице.Параметры.ИтогМассыНеттоПоСтранице     = ИтогМассыНеттоПоСтранице;
				ОбластьИтоговПоСтранице.Параметры.ИтогСуммыПоСтранице          = ИтогСуммыПоСтранице;
				
				ТабДокумент.Вывести(ОбластьИтоговПоСтранице);
				
				// инициализация итогов по странице
				ИтогКоличествоМестПоСтранице = 0;
				ИтогМассаБруттоПоСтранице    = 0;
				ИтогМассыНеттоПоСтранице     = 0;
				ИтогСуммыПоСтранице          = 0;
				
				НомерСтраницы = НомерСтраницы + 1;
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
				ТабДокумент.Вывести(ЗаголовокТаблицы);
				
			КонецЕсли;

			ТабДокумент.Вывести(ОбластьМакета);

			// Обновим итоги по странице
			ИтогКоличествоМестПоСтранице = ИтогКоличествоМестПоСтранице + ВыборкаСтрокТара.Количество;

			// Обновим итогов по документу
			ИтогоКоличество  = ИтогоКоличество  + ВыборкаСтрокТара.Количество;

		КонецЦикла;

		ОбластьИтоговПоСтранице.Параметры.ИтогКоличествоМестПоСтранице = ИтогКоличествоМестПоСтранице;
		ОбластьИтоговПоСтранице.Параметры.ИтогМассаБруттоПоСтранице    = ИтогМассаБруттоПоСтранице;
		ОбластьИтоговПоСтранице.Параметры.ИтогМассыНеттоПоСтранице     = ИтогМассыНеттоПоСтранице;
		ОбластьИтоговПоСтранице.Параметры.ИтогСуммыПоСтранице          = ИтогСуммыПоСтранице;

		ТабДокумент.Вывести(ОбластьИтоговПоСтранице);

		// Выводим итоги по документу в целом
		ОбластьВсего.Параметры.ИтогоКоличествоМест = ИтогоКоличество;
		ОбластьВсего.Параметры.ИтогоМассаБрутто    = ИтогоМассаБрутто;
		ОбластьВсего.Параметры.ИтогоМассаНетто     = ИтогоМассаНетто;
		ОбластьВсего.Параметры.ИтогоСумма          = ИтогоСумма;

		ТабДокумент.Вывести(ОбластьВсего);

		// Выводим подвал документа
		ФИООтправителя = ФормированиеПечатныхФормСервер.ФамилияИмяОтчество(Шапка.ФизЛицоОтправитель, Шапка.ДатаДокумента);
		ФИОПолучателя  = ФормированиеПечатныхФормСервер.ФамилияИмяОтчество(Шапка.ФизЛицоПолучатель,  Шапка.ДатаДокумента);
		ОбластьПодвал.Параметры.Заполнить(Шапка);
		ОбластьПодвал.Параметры.ДолжностьОтправителя = ПолныеПрава.СведенияОСотруднике(Шапка.ФизЛицоОтправитель, Шапка.ДатаДокумента, Шапка.Организация).Должность;
		ОбластьПодвал.Параметры.ФИООтправителя       = ОбщегоНазначения.ФамилияИнициалыФизЛица(ФИООтправителя.Фамилия + " " + ФИООтправителя.Имя + " " + ФИООтправителя.Отчество);
		ОбластьПодвал.Параметры.ДолжностьПолучателя  = ПолныеПрава.СведенияОСотруднике(Шапка.ФизЛицоПолучатель,  Шапка.ДатаДокумента, Шапка.Организация).Должность;
		ОбластьПодвал.Параметры.ФИОПолучателя        = ОбщегоНазначения.ФамилияИнициалыФизЛица(ФИОПолучателя.Фамилия  + " " + ФИОПолучателя.Имя  + " " + ФИОПолучателя.Отчество);
		ПараметрыПрописиНаРусском = ВалютаПечати.ПараметрыПрописиНаРусском;
		ПараметрыПрописиНаРусском = СтрЗаменить(ПараметрыПрописиНаРусском, "1", "0");
		ПараметрыПрописиНаРусском = СтрЗаменить(ПараметрыПрописиНаРусском, "2", "0");
		ПараметрыПрописиНаРусском = СтрЗаменить(ПараметрыПрописиНаРусском, "3", "0");
		ОбластьПодвал.Параметры.ИтогоСуммаПрописью = ЧислоПрописью(Цел(ИтогоСумма), "L=ru_RU; НП=Ложь; НД=Ложь", ПараметрыПрописиНаРусском);
		ОбластьПодвал.Параметры.ИтогоСуммаКоп      = Формат(Цел((ИтогоСумма-Цел(ИтогоСумма))*100), "ЧЦ=2; ЧН=00");
		ТабДокумент.Вывести(ОбластьПодвал);

		// Зададим параметры макета
		ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, НомерСтрокиНачало, ОбъектыПечати, Ссылка);
    КонецЦикла;

	Возврат ТабДокумент;

КонецФункции // ПечатьТОРГ13()

// Возвращает таблицу стоимости по товарам
//
Функция ПолучитьТаблицуСтоимостиТоваров(Ссылка)

	СтруктраПараметров = Новый Структура();
	
	СтруктраПараметров.Вставить("Регистратор", Ссылка);

	ТекстЗапроса = "ВЫБРАТЬ
	               |	СписаниеТоваров.Номенклатура,
	               |	СписаниеТоваров.ХарактеристикаНоменклатуры,
	               |	СписаниеТоваров.СерияНоменклатуры,
	               |	СписаниеТоваров.СчетУчетаБУ КАК СчетУчета,
	               |	СписаниеТоваров.Качество,
	               |	СписаниеТоваров.Количество,
	               |	СписаниеТоваров.Количество * ЕСТЬNULL(ТаблицаСебестоимостиОбороты.Стоимость, 0) / ЕСТЬNULL(ВЫБОР
	               |			КОГДА ТаблицаСебестоимостиОбороты.Количество = 0
	               |				ТОГДА 1
	               |			ИНАЧЕ ТаблицаСебестоимостиОбороты.Количество
	               |		КОНЕЦ, 1) КАК Стоимость,
	               |	СписаниеТоваров.ХарактеристикаНоменклатуры КАК Характеристика,
	               |	СписаниеТоваров.СерияНоменклатуры КАК Серия,
	               |	СписаниеТоваров.НомерСтроки КАК НомерСтроки
	               |ИЗ
	               |	Документ.ПеремещениеТоваров.Товары КАК СписаниеТоваров
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСебестоимостиОбороты КАК ТаблицаСебестоимостиОбороты
	               |		ПО СписаниеТоваров.Номенклатура = ТаблицаСебестоимостиОбороты.Номенклатура
	               |			И СписаниеТоваров.Ссылка = ТаблицаСебестоимостиОбороты.Регистратор
	               |			И (СписаниеТоваров.ХарактеристикаНоменклатуры = ТаблицаСебестоимостиОбороты.ХарактеристикаНоменклатуры
	               |				ИЛИ ТаблицаСебестоимостиОбороты.ХарактеристикаНоменклатуры = ""Не используется"")
	               |			И (СписаниеТоваров.СерияНоменклатуры = ТаблицаСебестоимостиОбороты.СерияНоменклатуры
	               |				ИЛИ ТаблицаСебестоимостиОбороты.СерияНоменклатуры = ""Не используется"")
				   |			И ((СписаниеТоваров.СчетУчетаБУ = ТаблицаСебестоимостиОбороты.СчетУчета) 
				   |				ИЛИ (ТаблицаСебестоимостиОбороты.СчетУчета = ""Не используется""))"+
				   ?(Ссылка.ВидОперации=Перечисления.ВидыОперацийСписаниеТоваров.ТоварыПродукция,"
				   |			И ((СписаниеТоваров.Качество = ТаблицаСебестоимостиОбороты.Качество)
				   |				ИЛИ (ТаблицаСебестоимостиОбороты.Качество = ""Не используется""))","")+"
	               |ГДЕ
	               |	СписаниеТоваров.Ссылка = &Регистратор";
										
										
	Возврат УправлениеЗапасами.ПолучитьТаблицуЗатратДляПечатиИЗаполненияДокументов(Ссылка, ТекстЗапроса, СтруктраПараметров);

КонецФункции // ПолучитьТаблицуСтоимостиТоваров()

// Возвращает стоимость товара
//
Функция ПолучитьСтоимостьТовара(Таблица, Номенклатура, Характеристика = Неопределено, Серия = Неопределено, ПартионныйУчетПоСериям = Ложь, Количество)

	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Номенклатура",                   Номенклатура);
	Если ЗначениеЗаполнено(Характеристика) Тогда
		СтруктураПоиска.Вставить("ХарактеристикаНоменклатуры", Характеристика);
	КонецЕсли;

	Если ПартионныйУчетПоСериям
	   И ЗначениеЗаполнено(Серия) Тогда
		СтруктураПоиска.Вставить("СерияНоменклатуры",          Серия);
	КонецЕсли;

	Строки = Таблица.НайтиСтроки(СтруктураПоиска);

	Если Строки.Количество() > 0 Тогда
		Стоимость  = Строки[0].Стоимость;
		Количество = Строки[0].Количество;
	Иначе
		Стоимость  = 0;
		Количество = 0;
	КонецЕсли;

	Возврат Стоимость;

КонецФункции // ПолучитьСтоимостьПоПартиямТоваров()

// Функция формирует табличный документ унифицированной формы М-4
//
// Параметры: 
//  Нет.
//
// Возвращаемое значение:
//  Табличный документ по форме М-4 (приходный ордер).
//
Функция ПечатьМ4(МассивОбъектов, ОбъектыПечати)
	
	ТекстЗапросаШапка = "
	|ВЫБРАТЬ
	|	Ссылка.Номер КАК Номер,
	|	Ссылка.Дата  КАК ДатаСоставления,
	|	Ссылка.Организация,
	|	Ссылка.Организация             КАК ЮрФизЛицо,
	|	Ссылка.СкладПолучатель         КАК МестоПриемки,
	|	Ссылка.СкладПолучатель.Представление КАК СкладНаименование,
	|	Ссылка.//Подразделение КАК Подразделение,
	|	ПРЕДСТАВЛЕНИЕ(Ссылка.//Подразделение)  КАК ПредставлениеПодразделения
	|	//ПОЛЕ_КорСчет ,ВЫБОР КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ НовыйСчетУчетаБУ) = 1 ТОГДА
	|	//ПОЛЕ_КорСчет	МИНИМУМ(НовыйСчетУчетаБУ) 
	|	//ПОЛЕ_КорСчет ИНАЧЕ """" КОНЕЦ КАК СубСчет
	|ИЗ
	|	Документ.ПеремещениеТоваров КАК ПеремещениеТоваров
	|ГДЕ
	|	ПеремещениеТоваров.Ссылка = &ТекущийДокумент
	|//ПОЛЕ_КорСчет СГРУППИРОВАТЬ ПО Ссылка";

	ТекстЗапросаТовары = "
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Номенклатура,
	|	ВЫРАЗИТЬ(ВложенныйЗапрос.Номенклатура.НаименованиеПолное КАК Строка(1000)) КАК ТоварНаименование,
	|	ВложенныйЗапрос.Номенклатура.Код                КАК ТоварКод,
	|	ВложенныйЗапрос.ЕдиницаИзмерения.Представление  КАК ЕдиницаИзмеренияНаименование,
	|	ВложенныйЗапрос.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код КАК ЕдиницаИзмеренияКод,
	|	ВложенныйЗапрос.КоличествоПринято,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Серия,
	|	ВложенныйЗапрос.НомерСтроки КАК НомерСтроки,
	|	ВложенныйЗапрос.Метка       КАК Метка
	|
	|ИЗ
	|	(
	|	ВЫБРАТЬ
	|		Номенклатура,
	|		ЕдиницаИзмерения,
	|		СУММА(Количество)                    КАК КоличествоПринято,
	|		ХарактеристикаНоменклатуры           КАК Характеристика,
	|		СерияНоменклатуры                    КАК Серия,
	|		МИНИМУМ(НомерСтроки)                 КАК НомерСтроки,
	|		0                                    КАК Метка
	|	ИЗ
	|		Документ.ПеремещениеТоваров.Товары КАК ПеремещениеТоваров
	|	ГДЕ
	|		ПеремещениеТоваров.Ссылка = &ТекущийДокумент
	|
	|	СГРУППИРОВАТЬ ПО 
	|		Номенклатура,
	|		ЕдиницаИзмерения,
	|		ХарактеристикаНоменклатуры,
	|		СерияНоменклатуры
	|	) КАК ВложенныйЗапрос
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Номенклатура,
	|	ВЫРАЗИТЬ(Номенклатура.НаименованиеПолное КАК Строка(1000)),
	|	Номенклатура.Код,
	|	Номенклатура.ЕдиницаХраненияОстатков.Представление  КАК ЕдиницаИзмеренияНаименование,
	|	Номенклатура.ЕдиницаХраненияОстатков.ЕдиницаПоКлассификатору.Код КАК ЕдиницаИзмеренияКод,
	|	Количество,
	|	NULL,
	|	NULL,
	|	НомерСтроки КАК НомерСтроки,
	|	2           КАК Метка
	|ИЗ
	|	Документ.ПеремещениеТоваров.ВозвратнаяТара КАК ПеремещениеТоваров
	|ГДЕ
	|	ПеремещениеТоваров.Ссылка = &ТекущийДокумент
	|
	|УПОРЯДОЧИТЬ ПО Метка ВОЗР, НомерСтроки ВОЗР
	|
	|";
	
	Макет = ПолучитьОбщийМакет("М4");
	
	ТабДокумент = Новый ТабличныйДокумент;
	
	// Зададим параметры печатной формы по умолчанию
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	// Восстановим установленные пользователем параметры печатной формы
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПеремещениеТоваров_М4";
	
	ПервыйДокумент = Истина;
	
	Для Каждого Ссылка Из МассивОбъектов Цикл
		
		Если НЕ ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;

		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ТекущийДокумент",     Ссылка);
		Запрос.УстановитьПараметр("ВидПоступленияОрдер", Перечисления.ВидыПоступленияТоваров.ПоОрдеру);

		Запрос.Текст = ТекстЗапросаШапка;
		Если Ссылка.ОтражатьВБухгалтерскомУчете Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "Документ.ПеремещениеТоваров", "Документ.ПеремещениеТоваров.Товары");
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "//ПОЛЕ_КорСчет", "");
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "//Подразделение", "ПодразделениеОрганизации");
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст,"Ссылка.", "");
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "//Подразделение", "Подразделение");
		КонецЕсли;

		Шапка = Запрос.Выполнить().Выбрать();
		Шапка.Следующий();

		ЗапросПоТоварам = Новый Запрос();
		ЗапросПоТоварам.УстановитьПараметр("ТекущийДокумент", Ссылка);
		ЗапросПоТоварам.Текст = ТекстЗапросаТовары;	
		ВыборкаСтрокТовары = ЗапросПоТоварам.Выполнить().Выбрать();

		// Выводим общие реквизиты шапки
		СведенияОПокупателе = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Шапка.ЮрФизЛицо, Шапка.ДатаСоставления);

		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		ОбластьМакета.Параметры.Заполнить(Шапка);
		ОбластьМакета.Параметры.ПредставлениеОрганизации = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПокупателе);
		ОбластьМакета.Параметры.ОрганизацияПоОКПО        = СведенияОПокупателе.КодПоОКПО;
		ОбластьМакета.Параметры.НомерДокумента           = ОбщегоНазначения.ПолучитьНомерНаПечать(Шапка);
		ТабДокумент.Вывести(ОбластьМакета);

		// Выводим заголовок докмента
		ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокДокумента");
		ОбластьМакета.Параметры.Заполнить(Шапка);
		ОбластьМакета.Параметры.ДатаСоставления = Шапка.ДатаСоставления;
		ПредставлениеКонтрагента = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Шапка.Организация, Шапка.ДатаСоставления), "ПолноеНаименование,");
		ОбластьМакета.Параметры.ПоставщикНаименование = ПредставлениеКонтрагента;
		ТабДокумент.Вывести(ОбластьМакета);
		
		// Выводим заголовок таблицы
		ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
		ТабДокумент.Вывести(ЗаголовокТаблицы);

		НомерСтраницы   = 1;

		КоличествоСтрок = ВыборкаСтрокТовары.Количество();

		// Инициализация итогов в документе
		ИтогоКоличествоПринято = 0;
		Ном = 0;

		// Создаем массив для проверки вывода
		МассивВыводимыхОбластей = Новый Массив;	

		// Выводим многострочную часть докмента
		ОбластьМакета           = Макет.ПолучитьОбласть("Строка");
		ОбластьИтоговПоСтранице = Макет.ПолучитьОбласть("ПодвалСтрок");
		ОбластьПодвала          = Макет.ПолучитьОбласть("Подвал");
		Пока ВыборкаСтрокТовары.Следующий() Цикл

			Если НЕ ЗначениеЗаполнено(ВыборкаСтрокТовары.Номенклатура) Тогда
				Продолжить;
			КонецЕсли;

			Ном = Ном + 1;

			ОбластьМакета.Параметры.Заполнить(ВыборкаСтрокТовары);

			КоличествоПринято = ВыборкаСтрокТовары.КоличествоПринято;

			ОбластьМакета.Параметры.КоличествоПринято = КоличествоПринято;
			ОбластьМакета.Параметры.ТоварНаименование = СокрЛП(ВыборкаСтрокТовары.ТоварНаименование)
			                                          + ФормированиеПечатныхФормСервер.ПредставлениеСерий(ВыборкаСтрокТовары)
			                                          + ?(ВыборкаСтрокТовары.Метка = 2, " (возвратная тара)", "");
													  
			МассивВыводимыхОбластей.Очистить();
			МассивВыводимыхОбластей.Добавить(ОбластьМакета);
			МассивВыводимыхОбластей.Добавить(ОбластьИтоговПоСтранице);
			Если Ном = КоличествоСтрок Тогда
				МассивВыводимыхОбластей.Добавить(ОбластьПодвала);
			КонецЕсли;

			Если НЕ ФормированиеПечатныхФормСервер.ПроверитьВыводТабличногоДокумента(ТабДокумент, МассивВыводимыхОбластей) Тогда

				НомерСтраницы = НомерСтраницы + 1;
				ПодвалСтрок   = Макет.ПолучитьОбласть("ПодвалСтрок");
				ТабДокумент.Вывести(ПодвалСтрок);
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
				ТабДокумент.Вывести(ЗаголовокТаблицы);
				
			КонецЕсли;												  
													  
			ТабДокумент.Вывести(ОбластьМакета);

			ИтогоКоличествоПринято = ИтогоКоличествоПринято + КоличествоПринято;

		КонецЦикла;

		// Выводим итоги по документу
		ОбластьМакета = Макет.ПолучитьОбласть("Итого");

		ОбластьМакета.Параметры.ИтогоКоличествоПринято = ИтогоКоличествоПринято;
		ТабДокумент.Вывести(ОбластьМакета);

		// Выводим итоги по документу
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
		ОбластьМакета.Параметры.Заполнить(Шапка);
		ТабДокумент.Вывести(ОбластьМакета);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, НомерСтрокиНачало, ОбъектыПечати, Ссылка);
		
	КонецЦикла; 
	
	Возврат ТабДокумент;
	
КонецФункции // ПечатьМ4()

// Функция формирует печатную форму М-11
//
Функция ПечатьМ11(МассивОбъектов, ОбъектыПечати)
	
	ДопКолонка = Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить();
	Если ДопКолонка = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул Тогда
		ТоварКод = "Артикул";
	Иначе
		ТоварКод = "Код";
	КонецЕсли;
	
	ТекстЗапросаШапка = 
	"ВЫБРАТЬ
	|	Ссылка.Номер 	КАК НомерДокумента,
	|	Ссылка.Дата	КАК ДатаДокумента,
	|	Ссылка.Дата	КАК ДатаСоставления,
	|	Ссылка.Организация,
	|	Ссылка.СкладОтправитель КАК Склад,
	|	Ссылка.ИдентификаторГосКонтракта КАК ИдентификаторГосКонтракта,   
	|	Ссылка.СкладПолучатель КАК Подразделение
	|	//ПОЛЕ_КорСчет ,ВЫБОР КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ НовыйСчетУчетаБУ) = 1 ТОГДА
	|	//ПОЛЕ_КорСчет	МИНИМУМ(НовыйСчетУчетаБУ) 
	|	//ПОЛЕ_КорСчет ИНАЧЕ """" КОНЕЦ КАК КоррСчет
	|ИЗ
	|	Документ.ПеремещениеТоваров КАК ПеремещениеТоваров
	|
	|ГДЕ
	|	ПеремещениеТоваров.Ссылка = &ТекущийДокумент
	|//ПОЛЕ_КорСчет СГРУППИРОВАТЬ ПО Ссылка";
	
	ТекстЗапросаТовары = "
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Номенклатура                                  КАК Номенклатура,
	|	ВЫРАЗИТЬ(ВложенныйЗапрос.Номенклатура.НаименованиеПолное КАК Строка(1000)) КАК МатериалНаименование,
	|	ВложенныйЗапрос.Номенклатура." + ТоварКод + "                 КАК НоменклатурныйНомер,
	|	ВложенныйЗапрос.ЕдиницаИзмерения.Представление                КАК ЕдиницаИзмеренияНаименование,
	|	ВложенныйЗапрос.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код  КАК ЕдиницаИзмеренияКод,
	|	ВложенныйЗапрос.Характеристика       КАК Характеристика,
	|	ВложенныйЗапрос.Серия                КАК Серия,
	|	ВложенныйЗапрос.Количество           КАК Количество,
	|	ВложенныйЗапрос.Счет           		 КАК Счет,
	|	ВложенныйЗапрос.НомерСтроки          КАК НомерСтроки
	|ИЗ 
	|	(
	|	ВЫБРАТЬ
	|		Номенклатура,
	|		ЕдиницаИзмерения,
	|		СчетУчетаБУ КАК Счет,
	|		ХарактеристикаНоменклатуры	КАК Характеристика,
	|		СерияНоменклатуры           КАК Серия,
	|		СУММА(Количество)           КАК Количество,
	|		МИНИМУМ(НомерСтроки) 		КАК НомерСтроки
	|	ИЗ
	|		Документ.ПеремещениеТоваров.Товары КАК ПеремещениеТоваров
	|	ГДЕ
	|		ПеремещениеТоваров.Ссылка = &ТекущийДокумент
	|
	|	СГРУППИРОВАТЬ ПО
	|		Номенклатура,
	|		ЕдиницаИзмерения,
	|		ХарактеристикаНоменклатуры,
	|		СерияНоменклатуры,
	|		СчетУчетаБУ
	|
	|	) КАК ВложенныйЗапрос
	|
	|УПОРЯДОЧИТЬ ПО НомерСтроки ВОЗР
	|";
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПеремещениеТоваров_М11";
	
	// Вывод заголовка
	Макет = ПолучитьОбщийМакет("М11");

	ПервыйДокумент = Истина;
	
	Для Каждого Ссылка Из МассивОбъектов Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;

		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ТекущийДокумент", Ссылка);
		Запрос.Текст = ТекстЗапросаШапка;
		Если Ссылка.ОтражатьВБухгалтерскомУчете Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "Документ.ПеремещениеТоваров", "Документ.ПеремещениеТоваров.Товары");
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "//ПОЛЕ_КорСчет", "");
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст,"Ссылка.", "");
		КонецЕсли;

		Шапка = Запрос.Выполнить().Выбрать();
		Шапка.Следующий();
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ТекущийДокумент", Ссылка);

		Запрос.Текст = ТекстЗапросаТовары;

		ЗапросПоНоменклатуре = Запрос.Выполнить();
		
		Область = Макет.ПолучитьОбласть("Шапка");
		Область.Параметры.Заголовок     = "ТРЕБОВАНИЕ-НАКЛАДНАЯ № " + Строка(Шапка.НомерДокумента);
		Область.Параметры.Заполнить(Шапка);
		
		СведенияОбОрганизации = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Шапка.Организация, Шапка.ДатаДокумента);

		Область.Параметры.ПредставлениеОрганизации   = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОбОрганизации);
		Область.Параметры.ПредставлениеПодразделения = Шапка.Подразделение;
		Область.Параметры.КодОКПО                    = СведенияОбОрганизации.КодПоОКПО;
		//{Лео Катанович
		ИГК = Шапка.ИдентификаторГосКонтракта;	
		Если ЗначениеЗаполнено(ИГК) Тогда
			ШаблонЗаголовкаГосконтракт = НСтр("ru = 'Идентификатор государственного контракта, договора (соглашения) (при наличии): %1'");
			Область.Параметры.ИдентификаторГосКонтракта = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонЗаголовкаГосконтракт, ИГК);
		КонецЕсли;	 
		//Лео Катанович}

		ТабДокумент.Вывести(Область);
		
		ВыборкаПоСтрокам = ЗапросПоНоменклатуре.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоСтрокам.Следующий() Цикл

			Область = Макет.ПолучитьОбласть("Строка");
			Область.Параметры.Заполнить(ВыборкаПоСтрокам);
			Область.Параметры.МатериалНаименование = СокрЛП(ВыборкаПоСтрокам.МатериалНаименование) + ФормированиеПечатныхФормСервер.ПредставлениеСерий(ВыборкаПоСтрокам);
			
			ТабДокумент.Вывести(Область);

		КонецЦикла;
		
		Область = Макет.ПолучитьОбласть("Подвал");
		ТабДокумент.Вывести(Область);
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, НомерСтрокиНачало, ОбъектыПечати, Ссылка);
	КонецЦикла; 

	Возврат ТабДокумент;
	
КонецФункции // ПечатьМ11()

// В функции описано, какие данные следует сохранять в шаблоне
Функция СтруктураДополнительныхДанныхФормы() Экспорт
	
	Возврат ХранилищаНастроек.ДанныеФорм.СформироватьСтруктуруДополнительныхДанных("Товары,ВозвратнаяТара");
	
КонецФункции

//АБС ВСТАВКА 38854  13.02.2014 10:38:23  Шамов

// Функция формирует табличный документ формы М-15
//
// Параметры: 
//  Нет.
//
// Возвращаемое значение:
//  Табличный документ по форме М-15 (Отпуск материалов на сторону).
//
Функция ПечатьМ15(МассивОбъектов, ОбъектыПечати)
	
	мВалютаРегламентированногоУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");
	
	ДопКолонка = Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить();
	Если ДопКолонка = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул Тогда
		ТоварКод = "Артикул";
	Иначе
		ТоварКод = "Код";
	КонецЕсли;
	
	ТекстЗапросаШапка = "ВЫБРАТЬ
	                    |	ПеремещениеТоваров.Номер КАК Номер,
	                    |	ПеремещениеТоваров.Дата КАК ДатаСоставления,
	                    |	ПеремещениеТоваров.Дата КАК ДатаДокумента,
	                    |	ПеремещениеТоваров.Организация КАК Организация,
	                    |	ПеремещениеТоваров.Организация КАК ЮрФизЛицо,
	                    |	ПеремещениеТоваров.Организация КАК Руководители,
	                    |	ПеремещениеТоваров.СкладОтправитель КАК Склад,
	                    |	ПеремещениеТоваров.СкладОтправитель.Наименование КАК СкладНаименование,
	                    |	ПеремещениеТоваров.СкладПолучатель.абс_Контрагент.Код КАК КонтрагентКод,
	                    |	ПеремещениеТоваров.СкладПолучатель.абс_Контрагент КАК Контрагент,
	                    |	&ВалютаРегламентированногоУчета КАК ВалютаДокумента,
	                    |	1 КАК Курс,
	                    |	1 КАК Кратность,
	                    |	ИСТИНА КАК УчитыватьНДС,
	                    |	ПеремещениеТоваров.НДСвСтоимостиТоваров КАК СуммаВключаетНДС,
	                    |	ЕСТЬNULL(ПеремещениеТоваров.ТипЦен.ЦенаВключаетНДС, ЛОЖЬ) КАК ЦенаВключаетНДС,
	                    |	ПеремещениеТоваров.ТипЦен КАК ТипЦен,
	                    |	ОтветственныеЛица.ФизическоеЛицо КАК ОтветственноеЛицо,
	                    |	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК ОтпускРазрешил,
	                    |	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК ОтпускПроизвел,
	                    |	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК ГлавныйБухгалтер,
	                    |	"""" КАК ЗаГлавногоБухгалтераПоПриказу,
	                    |	"""" КАК ЗаРуководителяПоПриказу,
	                    |	ПеремещениеТоваров.СкладПолучатель.абс_Контрагент.ОсновнойДоговорКонтрагента КАК Основание,
	                    |	ПеремещениеТоваров.ИдентификаторГосКонтракта
	                    |ИЗ
	                    |	Документ.ПеремещениеТоваров КАК ПеремещениеТоваров
	                    |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтветственныеЛица.СрезПоследних(&ДатаСреза, СтруктурнаяЕдиница = &СтруктурнаяЕдиница) КАК ОтветственныеЛица
	                    |		ПО (ОтветственныеЛица.СтруктурнаяЕдиница = ПеремещениеТоваров.СкладОтправитель)
	                    |ГДЕ
	                    |	ПеремещениеТоваров.Ссылка = &ТекущийДокумент";
	
	ТекстЗапросаТовары = "
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.НомерТабличнойЧасти                          КАК НомерТабличнойЧасти,
	|	ВложенныйЗапрос.Номенклатура                                 КАК Номенклатура,
	|	ВЫРАЗИТЬ(ВложенныйЗапрос.Номенклатура.НаименованиеПолное КАК Строка(1000)) КАК ТоварНаименование,
	|	ВложенныйЗапрос.Номенклатура." + ТоварКод + "                КАК ТоварКод,
	|	ВложенныйЗапрос.ЕдиницаИзмерения.Представление               КАК ЕдиницаИзмеренияНаименование,
	|	ВложенныйЗапрос.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код КАК ЕдиницаИзмеренияКод,
	|	ВложенныйЗапрос.КоличествоПринято                            КАК КоличествоПринято,
	|	ВложенныйЗапрос.Цена                                         КАК Цена,
	|	ВложенныйЗапрос.Сумма                                        КАК Сумма,
	|	ВложенныйЗапрос.СуммаНДС                                     КАК СуммаНДС,
	|	ВложенныйЗапрос.СтавкаНДС                                    КАК СтавкаНДС,
	|	ВложенныйЗапрос.Характеристика                               КАК Характеристика,
	|	ВложенныйЗапрос.Номенклатура.ВестиПартионныйУчетПоСериям     КАК ПартионныйУчетПоСериям,
	|	ВложенныйЗапрос.Серия                                        КАК Серия
	|
	|ИЗ (
	|	ВЫБРАТЬ
	|		0.00                                        КАК НомерТабличнойЧасти,
	|		МАКСИМУМ(Товары.НомерСтроки)                КАК НомерСтроки,
	|		Товары.Номенклатура                         КАК Номенклатура,
	|		Товары.ЕдиницаИзмерения                     КАК ЕдиницаИзмерения,
	|		СУММА(Товары.Количество)                    КАК КоличествоПринято,
	|		Товары.Цена        						    КАК Цена,
	|		СУММА(0)    КАК Сумма,
	|		СУММА(0) КАК СуммаНДС,
	|		Товары.Номенклатура.СтавкаНДС				КАК СтавкаНДС,
	|		Товары.ХарактеристикаНоменклатуры           КАК Характеристика,
	|		Товары.СерияНоменклатуры                    КАК Серия
	|	ИЗ
	|		Документ.ПеремещениеТоваров.Товары КАК Товары
	|	ГДЕ
	|		Товары.Ссылка = &ТекущийДокумент
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Товары.Номенклатура,
	|		Товары.ЕдиницаИзмерения,
	|		Товары.ХарактеристикаНоменклатуры,
	|		Товары.Номенклатура.СтавкаНДС,
	|		Товары.СерияНоменклатуры,
	|		Товары.Цена
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		1.00                                                КАК НомерТабличнойЧасти,
	|		МАКСИМУМ(ВозвратнаяТара.НомерСтроки)                КАК НомерСтроки,
	|		ВозвратнаяТара.Номенклатура                         КАК Номенклатура,
	|		ВозвратнаяТара.Номенклатура.ЕдиницаХраненияОстатков КАК ЕдиницаИзмерения,
	|		СУММА(ВозвратнаяТара.Количество)                    КАК КоличествоПринято,
	|		0.00            КАК Цена,
	|		СУММА(0.00)    КАК Сумма,
	|		0.00                                                КАК СуммаНДС,
	|		""""												КАК СтавкаНДС,
	|		Неопределено                                        КАК Характеристика,
	|		Неопределено                                        КАК Серия
	|	ИЗ
	|		Документ.ПеремещениеТоваров.ВозвратнаяТара КАК ВозвратнаяТара
	|	ГДЕ
	|		ВозвратнаяТара.Ссылка = &ТекущийДокумент
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ВозвратнаяТара.Номенклатура,
	|		0.00
    |
	|	) КАК ВложенныйЗапрос
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерТабличнойЧасти ВОЗР,
	|	ВложенныйЗапрос.НомерСтроки ВОЗР
	|";

	Макет = ПолучитьОбщийМакет("М15");
	
	ТабДокумент = Новый ТабличныйДокумент;
	
	// Зададим параметры печатной формы по умолчанию
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	// Восстановим установленные пользователем параметры печатной формы
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_РеализацияТоваровУслуг_М15";

	ПервыйДокумент = Истина;
	
	Для Каждого Ссылка Из МассивОбъектов Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;

		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ДатаСреза",          Ссылка.Дата);
		Запрос.УстановитьПараметр("СтруктурнаяЕдиница", Ссылка.СкладОтправитель);
		Запрос.УстановитьПараметр("ТекущийДокумент",    Ссылка);
		Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета",    мВалютаРегламентированногоУчета);
		Запрос.Текст = ТекстЗапросаШапка;
		
		Шапка = Запрос.Выполнить().Выбрать();
		Шапка.Следующий();
		
		ЗапросПоТоварам = Новый Запрос();
		ЗапросПоТоварам.УстановитьПараметр("ТекущийДокумент", Ссылка);

		//ЗапросПоТоварам.УстановитьПараметр("Курс",      ЗаполнениеДокументов.КурсДокумента(Ссылка, мВалютаРегламентированногоУчета));
		//ЗапросПоТоварам.УстановитьПараметр("Кратность", ЗаполнениеДокументов.КратностьДокумента(Ссылка, мВалютаРегламентированногоУчета));
		ЗапросПоТоварам.Текст = ТекстЗапросаТовары;
		ЗапросТовары = ЗапросПоТоварам.Выполнить().Выгрузить();
		
		ТаблицаСтоимости        = ПолучитьТаблицуСтоимостиТоваров(Ссылка);	
		
		// Выводим общие реквизиты шапки
		СведенияОбОрганизации = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Шапка.ЮрФизЛицо, Шапка.ДатаСоставления);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		ОбластьМакета.Параметры.Заполнить(Шапка);
		ОбластьМакета.Параметры.ПредставлениеОрганизации = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОбОрганизации);
		ОбластьМакета.Параметры.ОрганизацияПоОКПО        = СведенияОбОрганизации.КодПоОКПО;
		ОбластьМакета.Параметры.НомерДокумента           = ОбщегоНазначения.ПолучитьНомерНаПечать(Шапка);
		
		ПредставлениеКонтрагента = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Шапка.Контрагент, Шапка.ДатаСоставления), "ПолноеНаименование,");
		ОбластьМакета.Параметры.КонтрагентНаименование = ПредставлениеКонтрагента;
		//{Лео Катанович
		ИГК = Шапка.ИдентификаторГосКонтракта;	
		Если ЗначениеЗаполнено(ИГК) Тогда
			ШаблонЗаголовкаГосконтракт = НСтр("ru = 'Идентификатор государственного контракта, договора (соглашения) (при наличии): %1'");
			ОбластьМакета.Параметры.ИдентификаторГосКонтракта = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонЗаголовкаГосконтракт, ИГК);
		КонецЕсли;	 
		//Лео Катанович}
		ТабДокумент.Вывести(ОбластьМакета);
	
		// Выводим заголовок таблицы
		ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
		ТабДокумент.Вывести(ЗаголовокТаблицы);
		
		НомерСтраницы   = 1;
		
		КоличествоСтрок = ЗапросТовары.Количество();
		
		Для Инд = 1 По КоличествоСтрок Цикл
			СтрокаТовара = ЗапросТовары[КоличествоСтрок - Инд];
			Если (СтрокаТовара.НомерТабличнойЧасти = 1) Тогда
				ЗапросТовары.Удалить(СтрокаТовара);
			КонецЕсли;
		КонецЦикла;
		
		КоличествоСтрок = ЗапросТовары.Количество();
		
		// Инициализация итогов в документе
		ИтогоКоличествоПринято = 0;
		ИтогоСуммаБезНДС = 0;
		ИтогоСуммаНДС    = 0;
		ИтогоВсегоСНДС   = 0;
		Ном              = 0;
		
		// Создаем массив для проверки вывода
		МассивВыводимыхОбластей = Новый Массив;
	
		// Выводим многострочную часть докмента
		ОбластьМакета  = Макет.ПолучитьОбласть("Строка");
		ОбластьПодвала = Макет.ПолучитьОбласть("Подвал");
		
		Для каждого ВыборкаСтрокТовары Из ЗапросТовары Цикл
			
			Если НЕ ЗначениеЗаполнено(ВыборкаСтрокТовары.Номенклатура) Тогда
				Продолжить;
			КонецЕсли;
			
			Ном = Ном + 1;
			
			МассивВыводимыхОбластей.Очистить();
			МассивВыводимыхОбластей.Добавить(ОбластьМакета);
			Если Ном = КоличествоСтрок Тогда
				МассивВыводимыхОбластей.Добавить(ОбластьПодвала);
			КонецЕсли;		
			
			Если НЕ ФормированиеПечатныхФормСервер.ПроверитьВыводТабличногоДокумента(ТабДокумент, МассивВыводимыхОбластей) Тогда
				НомерСтраницы	= НомерСтраницы + 1;
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
				ТабДокумент.Вывести(ЗаголовокТаблицы);
			КонецЕсли;
			
			ОбластьМакета.Параметры.Заполнить(ВыборкаСтрокТовары);
			
			Кратность = ?(Шапка.Кратность = 0, 1, Шапка.Кратность);
			//СуммаСНДС   = (ВыборкаСтрокТовары.Сумма + ?(Шапка.СуммаВключаетНДС, 0, ВыборкаСтрокТовары.СуммаНДС));
			//СуммаНДС    = ВыборкаСтрокТовары.СуммаНДС;
			//СуммаБезНДС = СуммаСНДС  - СуммаНДС;
			//
			//Цена            = СуммаБезНДС / Количество;
			
			Количество 			= ВыборкаСтрокТовары.КоличествоПринято;
			//СуммаБезНДС 		= ПолучитьСтоимостьТовара(ТаблицаСтоимости,
			//								ВыборкаСтрокТовары.Номенклатура,
			//								ВыборкаСтрокТовары.Характеристика,
			//								ВыборкаСтрокТовары.Серия,
			//								ВыборкаСтрокТовары.ПартионныйУчетПоСериям,
			//								Количество);
			ЗначениеСтавкиНДС 	= УчетНДС.ПолучитьСтавкуНДС(ВыборкаСтрокТовары.СтавкаНДС);
			//СуммаНДС 			= ?(ЗначениеСтавкиНДС = 0, 0, Окр(СуммаБезНДС * ЗначениеСтавкиНДС/(100+ЗначениеСтавкиНДС),2));
			//СуммаСНДС 			= СуммаБезНДС - СуммаНДС;
			//Цена            	= СуммаБезНДС / Количество;
			
			Цена = ВыборкаСтрокТовары.Цена;
			Если Шапка.ЦенаВключаетНДС Тогда
				СуммаСНДС = Цена * Количество;
				СуммаБезНДС = ?(ЗначениеСтавкиНДС = 0, СуммаСНДС, Окр(СуммаСНДС/(1+ЗначениеСтавкиНДС/100), 2))
			Иначе
				СуммаБезНДС = Цена * Количество;
				СуммаСНДС = ?(ЗначениеСтавкиНДС = 0, СуммаБезНДС, Окр(СуммаБезНДС * (1 + ЗначениеСтавкиНДС/100), 2));
			КонецЕсли;
			СуммаНДС = СуммаСНДС - СуммаБезНДС;
			
			ОбластьМакета.Параметры.Количество 		  = Количество;
			ОбластьМакета.Параметры.СуммаСНДС         = СуммаСНДС;
			ОбластьМакета.Параметры.СуммаБезНДС       = СуммаБезНДС;
			ОбластьМакета.Параметры.СуммаНДС          = СуммаНДС;
			ОбластьМакета.Параметры.Цена              = Цена;
			ОбластьМакета.Параметры.ТоварНаименование = СокрЛП(ВыборкаСтрокТовары.ТоварНаименование) 
				+ ФормированиеПечатныхФормСервер.ПредставлениеСерий(ВыборкаСтрокТовары)
				+ ?(ВыборкаСтрокТовары.НомерТабличнойЧасти = 2, " (возвратная тара)", "");
			
			ТабДокумент.Вывести(ОбластьМакета);
			
			ИтогоКоличествоПринято = ИтогоКоличествоПринято + Количество;
			ИтогоСуммаБезНДС       = ИтогоСуммаБезНДС       + СуммаБезНДС;
			ИтогоВсегоСНДС         = ИтогоВсегоСНДС         + СуммаСНДС;
			
		КонецЦикла;
	
		// Выводим итоги по документу
		ОбластьПодвала.Параметры.Заполнить(Шапка);
		
		Руководители = РегламентированнаяОтчетность.ОтветственныеЛицаОрганизаций(Шапка.Руководители, Шапка.ДатаДокумента,);
		Руководитель = Руководители.Руководитель;
		Бухгалтер    = Руководители.ГлавныйБухгалтер;
		
		// Главный бухгалтер
		Если НЕ ЗначениеЗаполнено(Шапка.ГлавныйБухгалтер) Тогда
			ФИОБухгалтер 					 = Бухгалтер;	
		Иначе
			ФамилияИмяОтчествоФизЛица        = ФормированиеПечатныхФормСервер.ФамилияИмяОтчество(Шапка.ГлавныйБухгалтер, Шапка.ДатаДокумента);
			ФамилияИмяОтчествоБухгалтер 	 = ФамилияИмяОтчествоФизЛица.Фамилия + " " + ФамилияИмяОтчествоФизЛица.Имя + " " + ФамилияИмяОтчествоФизЛица.Отчество;
			ФИОБухгалтер                	 = ОбщегоНазначения.ФамилияИнициалыФизЛица(ФамилияИмяОтчествоБухгалтер);
			ФИОБухгалтер					 = ФИОБухгалтер + " " + Шапка.ЗаГлавногоБухгалтераПоПриказу;
		КонецЕсли;
		ОбластьПодвала.Параметры.ФИОГлавБухгалтера = ФИОБухгалтер;
		
		// Отпуск товара разрешил
		Если НЕ ЗначениеЗаполнено(Шапка.ОтпускРазрешил) Тогда
			ФИООтпускРазрешил       = Руководитель;
			ДолжностьОтпускРазрешил = Руководители.РуководительДолжность;
		Иначе
			ФамилияИмяОтчествоФизЛица        = ФормированиеПечатныхФормСервер.ФамилияИмяОтчество(Шапка.ОтпускРазрешил, Шапка.ДатаДокумента);
			ПодразделениеДолжностьФизЛица    = ПолныеПрава.СведенияОСотруднике(Шапка.ОтпускРазрешил, Шапка.ДатаДокумента, Шапка.Организация);
			ФамилияИмяОтчествоОтпускРазрешил = ФамилияИмяОтчествоФизЛица.Фамилия + " " + ФамилияИмяОтчествоФизЛица.Имя + " " + ФамилияИмяОтчествоФизЛица.Отчество;
			ФИООтпускРазрешил                = ОбщегоНазначения.ФамилияИнициалыФизЛица(ФамилияИмяОтчествоОтпускРазрешил);
			ДолжностьОтпускРазрешил          = ПодразделениеДолжностьФизЛица.Должность;
			ФИООтпускРазрешил			 	 = ФИООтпускРазрешил  + " " + Шапка.ЗаРуководителяПоПриказу;
		КонецЕсли;
		
		ОбластьПодвала.Параметры.ФИОРуководителя       = ФИООтпускРазрешил;
		ОбластьПодвала.Параметры.ДолжностьРуководителя = ДолжностьОтпускРазрешил;
		
		// Отпуск товара произвел
		Если НЕ ЗначениеЗаполнено(Шапка.ОтпускПроизвел) Тогда
			ФамилияИмяОтчествоФизЛица     = ФормированиеПечатныхФормСервер.ФамилияИмяОтчество(Шапка.ОтветственноеЛицо, Шапка.ДатаДокумента);
			ПодразделениеДолжностьФизЛица = ПолныеПрава.СведенияОСотруднике(Шапка.ОтветственноеЛицо, Шапка.ДатаДокумента, Шапка.Организация);
		Иначе
			ФамилияИмяОтчествоФизЛица     = ФормированиеПечатныхФормСервер.ФамилияИмяОтчество(Шапка.ОтпускПроизвел, Шапка.ДатаДокумента);
			ПодразделениеДолжностьФизЛица = ПолныеПрава.СведенияОСотруднике(Шапка.ОтпускПроизвел, Шапка.ДатаДокумента, Шапка.Организация);
		КонецЕсли;
		
		ФамилияИмяОтчествоОтпускПроизвел = ФамилияИмяОтчествоФизЛица.Фамилия + " " + ФамилияИмяОтчествоФизЛица.Имя + " " + ФамилияИмяОтчествоФизЛица.Отчество;
		ФИООтпускПроизвел                = ОбщегоНазначения.ФамилияИнициалыФизЛица(ФамилияИмяОтчествоОтпускПроизвел);
		ДолжностьОтпускПроизвел          = ПодразделениеДолжностьФизЛица.Должность;
		
		ОбластьПодвала.Параметры.ФИОКладовщика       = ФИООтпускПроизвел;
		ОбластьПодвала.Параметры.ДолжностьКладовщика = ДолжностьОтпускПроизвел;
		
		ОбластьПодвала.Параметры.КоличествоПорядковыхНомеровЗаписейПрописью = ЧислоПрописью(КоличествоСтрок, ,",,,,,,,,0");
		ОбластьПодвала.Параметры.СуммаПрописью = ОбщегоНазначения.СформироватьСуммуПрописью(ИтогоВсегоСНДС, мВалютаРегламентированногоУчета);
		ОбластьПодвала.Параметры.ИтогНДС       = ИтогоВсегоСНДС - ИтогоСуммаБезНДС;
		
		ТабДокумент.Вывести(ОбластьПодвала);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, НомерСтрокиНачало, ОбъектыПечати, Ссылка);
		
	КонецЦикла;
	
	Возврат ТабДокумент;
	
КонецФункции

// Функция формирует табличный документ формы МХ-18
//
// Параметры: 
//  Нет.
//
// Возвращаемое значение:
//  Табличный документ по форме MХ-18 (Передача готовой продукции в места хранения).
//
Функция ПечатьМХ18(МассивОбъектов, ОбъектыПечати)
	
	Макет = ПолучитьОбщийМакет("МХ18");
	
	ТабДокумент  = Новый ТабличныйДокумент;
	
	// Зададим параметры печатной формы по умолчанию
	ТабДокумент.РазмерКолонтитулаСверху = 0;
	ТабДокумент.РазмерКолонтитулаСнизу  = 0;
	ТабДокумент.АвтоМасштаб             = Истина;
	ТабДокумент.ОриентацияСтраницы      = ОриентацияСтраницы.Ландшафт;
	
	// Восстановим установленные пользователем параметры печатной формы
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ВыпускПродукции_МХ18";
	
	// Определяем, что будем выводить - код или артикул
	ВыводитьАртикул      = (глЗначениеПеременной("ДополнительнаяКолонкаПечатныхФормДокументов") = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул);
	ЕдиницаИзмеренияВеса = глЗначениеПеременной("ЕдиницаИзмеренияВеса");
	
	ПервыйДокумент = Истина;
	
	Для Каждого Ссылка Из МассивОбъектов Цикл
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Регистратор", Ссылка);
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВыпускПродукции.Номер,
		|	ВыпускПродукции.Дата КАК ДатаДокумента,
		|	ВыпускПродукции.Организация,
		|	ВыпускПродукции.СкладПолучатель КАК Получатель,
		|	ВыпускПродукции.Подразделение.Представление КАК ПредставлениеПодразделения,
		|	"""" КАК КоррСчет,
		|	ВыпускПродукции.ИдентификаторГосКонтракта
		|ИЗ
		|	Документ.ПеремещениеТоваров КАК ВыпускПродукции
		|ГДЕ
		|	ВыпускПродукции.Ссылка = &Регистратор";
		Шапка = Запрос.Выполнить().Выбрать();
		Шапка.Следующий();
		
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	Продукция.Номенклатура 																КАК Номенклатура,
		|	ВЫРАЗИТЬ(Продукция.Номенклатура.НаименованиеПолное КАК СТРОКА(1000)) 				КАК ТоварНаименование,
		|	Продукция.Номенклатура.Код 															КАК ТоварКод,
		|	Продукция.Номенклатура.Артикул 														КАК ТоварАртикул,
		|	Продукция.ЕдиницаИзмерения.Представление 											КАК БазоваяЕдиницаНаименование,
		|	Продукция.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код 								КАК БазоваяЕдиницаКодПоОКЕИ,
		|	Продукция.ЕдиницаИзмерения 															КАК ЕдиницаИзмерения,
		|	Продукция.ЕдиницаИзмерения.Наименование 											КАК ЕдиницаИзмеренияНаименование,
		|	Продукция.Номенклатура.БазоваяЕдиницаИзмерения.Код 									КАК ЕдиницаИзмеренияКод,
		|	Продукция.ЕдиницаИзмеренияМест.Представление 										КАК ВидУпаковки,
		|	Продукция.ЕдиницаИзмеренияМест.Коэффициент / Продукция.Коэффициент 					КАК КоличествоВОдномМесте,
		|	ВЫБОР
		|		КОГДА Продукция.КоличествоМест > 0 И НЕ Продукция.ЕдиницаИзмеренияМест.Вес ЕСТЬ NULL
		|			ТОГДА Продукция.КоличествоМест * Продукция.ЕдиницаИзмеренияМест.Вес
		|		ИНАЧЕ Продукция.Количество * Продукция.ЕдиницаИзмерения.Вес
		|	КОНЕЦ 																				КАК МассаБрутто,
		|	Продукция.ХарактеристикаНоменклатуры 												КАК Характеристика,
		|	Продукция.СерияНоменклатуры 														КАК Серия,
		|	Продукция.Количество 																КАК Количество,
		|	Продукция.КоличествоМест 															КАК КоличествоМест,
		|	Продукция.НомерСтроки 																КАК НомерСтроки,
		|	Продукция.Цена																		КАК Цена,
		|	ВЫРАЗИТЬ(ЕСТЬNULL(ТаблицаСебестоимостиОбороты.Стоимость, 0) 
		|			 * Продукция.Количество * Продукция.Коэффициент 
		|			 / ЕСТЬNULL(Продукция.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент, 1)
		|			 / ЕСТЬNULL(ТаблицаСебестоимостиОбороты.Количество, 1) КАК ЧИСЛО(15, 2)) 	КАК Сумма
		|ИЗ 
		|	Документ.ПеремещениеТоваров.Товары КАК Продукция
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСебестоимостиОбороты КАК ТаблицаСебестоимостиОбороты
		|		ПО Продукция.Номенклатура = ТаблицаСебестоимостиОбороты.Номенклатура
		|			И (ТаблицаСебестоимостиОбороты.Регистратор = &Регистратор)
		|			И (Продукция.ХарактеристикаНоменклатуры = ТаблицаСебестоимостиОбороты.ХарактеристикаНоменклатуры
		|				ИЛИ ТаблицаСебестоимостиОбороты.ХарактеристикаНоменклатуры = ""Не используется"")
		|			И (Продукция.СерияНоменклатуры = ТаблицаСебестоимостиОбороты.СерияНоменклатуры
		|				ИЛИ ТаблицаСебестоимостиОбороты.СерияНоменклатуры = ""Не используется"")
		|			И (Продукция.СчетУчетаБУ = ТаблицаСебестоимостиОбороты.СчетУчета
		|				ИЛИ ТаблицаСебестоимостиОбороты.СчетУчета = ""Не используется"")
		|ГДЕ
		|	Продукция.Ссылка = &Регистратор
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
		
		СтруктраПараметров = Новый Структура();
		СтруктраПараметров.Вставить("Регистратор", 						Ссылка);
		СтруктраПараметров.Вставить("НачГраница", 						Новый Граница(Шапка.ДатаДокумента, ВидГраницы.Включая));
		
		ТаблицаПродукции = УправлениеЗапасами.ПолучитьТаблицуЗатратДляПечатиИЗаполненияДокументов(Ссылка, ТекстЗапроса, СтруктраПараметров);
		
		Если ТаблицаПродукции = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;
		
		// Выводим общие реквизиты шапки
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		ОбластьМакета.Параметры.Заполнить(Шапка);
		ОбластьМакета.Параметры.НомерДокумента = ОбщегоНазначения.ПолучитьНомерНаПечать(Шапка);
		
		СведенияОбОрганизации = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Шапка.Организация, Шапка.ДатаДокумента);
		ОбластьМакета.Параметры.ПредставлениеОрганизации 	= ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОбОрганизации);
		ОбластьМакета.Параметры.ОрганизацияПоОКПО			= СведенияОбОрганизации.КодПоОКПО;
		
		//{Лео Катанович
		ИГК = Шапка.ИдентификаторГосКонтракта;	
		Если ЗначениеЗаполнено(ИГК) Тогда
			ШаблонЗаголовкаГосконтракт = НСтр("ru = 'Идентификатор государственного контракта, договора (соглашения) (при наличии): %1'");
			ОбластьМакета.Параметры.ИдентификаторГосКонтракта = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонЗаголовкаГосконтракт, ИГК);
		КонецЕсли;	 
		//Лео Катанович}
		
		ТабДокумент.Вывести(ОбластьМакета);

		НомерСтраницы   = 1;

		// Выводим заголовок таблицы
		ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
		ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы; 
		ТабДокумент.Вывести(ЗаголовокТаблицы);

		КоличествоСтрок = ТаблицаПродукции.Количество();
		
		// инициализация итогов по странице
		ИтогоМассаБруттоНаСтранице = 0;
		ИтогоМестНаСтранице        = 0;
		ИтогоКоличествоНаСтранице  = 0;
		ИтогоСуммаНаСтранице  	   = 0;

		// инициализация итогов по документу
		ИтогоМассаБрутто = 0;
		ИтогоМест        = 0;
		ИтогоКоличество  = 0;
		ИтогоСумма  	 = 0;
		Ном              = 0;

		ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
		ОбластьИтого  = Макет.ПолучитьОбласть("Итого");
		ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
		
		// Выводим многострочную часть докмента
		ОбластьМакета = Макет.ПолучитьОбласть("Строка");
		Для Каждого СтрокаПродукции Из ТаблицаПродукции Цикл
			
			Если НЕ ЗначениеЗаполнено(СтрокаПродукции.Номенклатура) Тогда
				Продолжить;
			КонецЕсли;
			
			Ном = Ном + 1;
			
			// Проверим, помещается ли строка с данными и последняя строка (или итоги) на странице.
			МассивВыводимыхОбластей = Новый Массив;
			МассивВыводимыхОбластей.Добавить(ОбластьСтрока);
			Если Ном = КоличествоСтрок Тогда
				МассивВыводимыхОбластей.Добавить(ОбластьИтого);
				МассивВыводимыхОбластей.Добавить(ОбластьПодвал);
			КонецЕсли;
			
			Если НЕ ФормированиеПечатныхФормСервер.ПроверитьВыводТабличногоДокумента(ТабДокумент, МассивВыводимыхОбластей) Тогда
			
				// очистим итоги по странице
				ИтогоМассаБруттоНаСтранице = 0;
				ИтогоМестНаСтранице        = 0;
				ИтогоКоличествоНаСтранице  = 0;
				ИтогоСуммаНаСтранице  	   = 0;

				НомерСтраницы = НомерСтраницы + 1;
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
				ТабДокумент.Вывести(ЗаголовокТаблицы);

			КонецЕсли;
			
			ОбластьМакета.Параметры.Заполнить(СтрокаПродукции);
			ОбластьМакета.Параметры.ТоварНаименование = СокрЛП(СтрокаПродукции.ТоварНаименование) + ФормированиеПечатныхФормСервер.ПредставлениеСерий(СтрокаПродукции);
			//АБС ИЗМЕНЕНИЕ   25.03.2014 14:05:56  Балашенко
			Если ЗначениеЗаполнено(Ссылка.ТипЦен) Тогда
				ОбластьМакета.Параметры.Сумма = СтрокаПродукции.Цена * СтрокаПродукции.Количество;
				СтрокаПродукции.Сумма = СтрокаПродукции.Цена * СтрокаПродукции.Количество;
			Иначе
				ОбластьМакета.Параметры.Цена               = ?(СтрокаПродукции.Количество = 0, СтрокаПродукции.Сумма, Окр(СтрокаПродукции.Сумма / СтрокаПродукции.Количество, 2));
			КонецЕсли;
			//АБС ИЗМЕНЕНИЕ  КОНЕЦ
			Если ВыводитьАртикул Тогда
				ОбластьМакета.Параметры.ТоварКод      = СтрокаПродукции.ТоварАртикул;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(ЕдиницаИзмеренияВеса) Тогда
				МассаБрутто = 0;
			Иначе
				МассаБрутто = СтрокаПродукции.МассаБрутто;
			КонецЕсли;
			
			ОбластьМакета.Параметры.МассаБрутто = МассаБрутто;
			
			ТабДокумент.Вывести(ОбластьМакета);
			
			// увеличим итоги по странице
			ИтогоМассаБруттоНаСтранице = ИтогоМассаБруттоНаСтранице + МассаБрутто;
			ИтогоМестНаСтранице        = ИтогоМестНаСтранице        + СтрокаПродукции.КоличествоМест;
			ИтогоКоличествоНаСтранице  = ИтогоКоличествоНаСтранице  + СтрокаПродукции.Количество;
			ИтогоСуммаНаСтранице  	   = ИтогоСуммаНаСтранице		+ СтрокаПродукции.Сумма;
			
			// увеличим итоги по дукументу
			ИтогоМассаБрутто = ИтогоМассаБрутто + МассаБрутто;
			ИтогоМест        = ИтогоМест        + СтрокаПродукции.КоличествоМест;
			ИтогоКоличество  = ИтогоКоличество  + СтрокаПродукции.Количество;
			ИтогоСумма  	 = ИтогоСумма		+ СтрокаПродукции.Сумма;
			
		КонецЦикла;
		
		// Выводим итоги по документу в целом
		ОбластьИтого.Параметры.ИтогМассаБрутто = ИтогоМассаБрутто;
		ОбластьИтого.Параметры.ИтогМест        = ИтогоМест;
		ОбластьИтого.Параметры.ИтогКоличество  = ИтогоКоличество;
		ОбластьИтого.Параметры.ИтогСумма 	   = ИтогоСумма;
		
		ТабДокумент.Вывести(ОбластьИтого);
		
		// Выводим подвал документа
		ОбластьПодвал.Параметры.КоличествоПорядковыхНомеровЗаписейПрописью = ЧислоПрописью(КоличествоСтрок, ,",,,,,,,,0");
		ТабДокумент.Вывести(ОбластьПодвал);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, НомерСтрокиНачало, ОбъектыПечати, Ссылка);
		
	КонецЦикла; 
		
	Возврат ТабДокумент;
	
КонецФункции

//АБС ВСТАВКА 38854 КОНЕЦ