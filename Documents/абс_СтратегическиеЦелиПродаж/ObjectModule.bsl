Процедура ЗаполнитьТЧДокументаПоТаблице(Объект, ТабДок) Экспорт
	
	ТаблицаЗагрузки = Новый ТаблицаЗначений;
	
	ТаблицаЗагрузки.Колонки.Добавить("Период");
	
	ТаблицаЗагрузки.Колонки.Добавить("Регион");
	ТаблицаЗагрузки.Колонки.Добавить("Территория");
	ТаблицаЗагрузки.Колонки.Добавить("Город");
	ТаблицаЗагрузки.Колонки.Добавить("Канал");
	
	ТаблицаЗагрузки.Колонки.Добавить("ЦельПоЧисленнойДистрибуции");
	ТаблицаЗагрузки.Колонки.Добавить("ЦельПоДистрибуцииФА1");
	ТаблицаЗагрузки.Колонки.Добавить("ЦельПоДистрибуцииФА2");
	ТаблицаЗагрузки.Колонки.Добавить("ЦельПоДистрибуцииФА3");
	ТаблицаЗагрузки.Колонки.Добавить("ЦельПоДистрибуцииФА4");
	
	//рассчитаем количество строк
	СчСтрок = 3;
	СчКолонок = 1;
	
	ТекДанные = ТабДок.Область(СчСтрок, СчКолонок).Текст;
	     
	Пока ЗначениеЗаполнено(ТекДанные) Цикл
		
		СчСтрок = СчСтрок + 1;
		
		ТекДанные = ТабДок.Область(СчСтрок, СчКолонок).Текст;		
		
	КонецЦикла;
	
	КоличествоСтрок = СчСтрок-1;
	
	
	//рассчитаем количество строк
	СчСтрок = 2;
	СчКолонок = 2;
	
	ТекДанные = ТабДок.Область(СчСтрок, СчКолонок).Текст;
	     
	Пока ЗначениеЗаполнено(ТекДанные) Цикл
		
		СчКолонок = СчКолонок + 1;
		
		ТекДанные = ТабДок.Область(СчСтрок, СчКолонок).Текст;		
		
	КонецЦикла;
	
	КоличествоКолонок = СчКолонок-1;
		
	//считаем данные
	Для СчСтрок = 3 По КоличествоСтрок Цикл
		
		сТекРегион = ТабДок.Область(СчСтрок, 1).Текст;
		ссТекРегион = Справочники.Регионы.НайтиПоНаименованию(сТекРегион, Истина);
		Если ссТекРегион.Пустая() ТОгда
			Сообщить("Не найден регион """ + сТекРегион + """!");
			//Продолжить;
		КонецЕсли;
		
		сТекТерритория = ТабДок.Область(СчСтрок, 2).Текст;
		ссТекТерритория = Справочники.Регионы.НайтиПоНаименованию(сТекТерритория, Истина);
		Если ссТекТерритория.Пустая() ТОгда
			Сообщить("Не найдена территория """ + сТекТерритория + """!");
			//Продолжить;
		КонецЕсли;
		
		сТекГород = ТабДок.Область(СчСтрок, 3).Текст;
		ссТекГород = Справочники.Регионы.НайтиПоНаименованию(сТекГород, Истина);
		Если ссТекГород.Пустая() ТОгда
			Сообщить("Не найден город """ + сТекГород + """!");
			//Продолжить;
		КонецЕсли;		
		
		сТекКанал = ТабДок.Область(СчСтрок, 4).Текст;
		ссТекКанал = Справочники.ЗначенияСвойствОбъектов.НайтиПоНаименованию(сТекКанал, Истина,, ПланыВидовХарактеристик.СвойстваОбъектов.абс_Канал);
		Если ссТекКанал.Пустая() ТОгда
			ссТекКаналОбъект = Справочники.ЗначенияСвойствОбъектов.СоздатьЭлемент();
			ссТекКаналОбъект.Наименование = сТекКанал;
			ссТекКаналОбъект.Владелец = ПланыВидовХарактеристик.СвойстваОбъектов.абс_Канал;
			ссТекКаналОбъект.Записать();
			ссТекКанал = ссТекКаналОбъект.Ссылка;	
		КонецЕсли;				
		
		
		Для СчКолонок = 5 По КоличествоКолонок Цикл
			
			сТекПериод = СокрЛП(ТабДок.Область(1, СчКолонок).Текст);
			Если Не сТекПериод="" Тогда
				дТекПериод = Дата(Сред(сТекПериод,7,4)+Сред(сТекПериод,4,2)+Лев(сТекПериод,2));
			КонецЕсли;
			Если (СчКолонок - 4) % 5 = 1 ТОгда
				СтрокаТЧ = ТаблицаЗагрузки.Добавить();
				СтрокаТЧ.Период 	= дТекПериод;
				СтрокаТЧ.Регион 	= ссТекРегион;
				СтрокаТЧ.Территория = ссТекТерритория;
				СтрокаТЧ.Город 		= ссТекГород;
				СтрокаТЧ.Канал 		= ссТекКанал;
			КонецЕсли;
			
			сЗначениеВЯчейке = СокрЛП(ТабДок.Область(СчСтрок, СчКолонок).Текст);
			Попытка
				чЗначениеВЯчейке = Число(сЗначениеВЯчейке);
			Исключение
				чЗначениеВЯчейке = 0;
			КонецПопытки;
			
			Если (СчКолонок-4) % 5 = 1 Тогда
				СтрокаТЧ.ЦельПоЧисленнойДистрибуции = чЗначениеВЯчейке;
			ИначеЕсли (СчКолонок-4) % 5 = 2 Тогда
				СтрокаТЧ.ЦельПоДистрибуцииФА1 = чЗначениеВЯчейке;
			ИначеЕсли (СчКолонок-4) % 5 = 3 Тогда
				СтрокаТЧ.ЦельПоДистрибуцииФА2 = чЗначениеВЯчейке;
			ИначеЕсли (СчКолонок-4) % 5 = 4 Тогда
				СтрокаТЧ.ЦельПоДистрибуцииФА3 = чЗначениеВЯчейке;
			Иначе
				СтрокаТЧ.ЦельПоДистрибуцииФА4 = чЗначениеВЯчейке;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	КоличествоСтрок = ТаблицаЗагрузки.Количество();
	Для Сч = 1 По КоличествоСтрок Цикл
		ТекСтрока = ТаблицаЗагрузки[КоличествоСтрок-Сч];
		Если ТекСтрока.ЦельПоЧисленнойДистрибуции = 0
			И ТекСтрока.ЦельПоДистрибуцииФА1 = 0
			И ТекСтрока.ЦельПоДистрибуцииФА2 = 0
			И ТекСтрока.ЦельПоДистрибуцииФА3 = 0
			И ТекСтрока.ЦельПоДистрибуцииФА4 = 0 Тогда
			ТаблицаЗагрузки.Удалить(ТекСтрока);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ТекСтрока из ТаблицаЗагрузки Цикл
		НоваяСтрока = Объект.СоставПлана.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,ТекСтрока);
	КонецЦикла;
	
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения,СтандартнаяОбработка)
	Ответственный=ПараметрыСеанса.ТекущийПользователь;
	Состояние=Перечисления.СостоянияОбъектов.Подготовлен;
	ДатаПланирования = НачалоГода(ТекущаяДата());
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.абс_ЦелиПродаж.Записывать = Истина;
	Движения.абс_ЦелиПродаж.Очистить();
	
	Движения.абс_СтратегическиеЦелиПродаж.Записывать = Истина;
	Движения.абс_СтратегическиеЦелиПродаж.Очистить();

	// проверка соотвествия дат в табличной части и даты планирования в шапке документа.
	Запрос=Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	абс_СтратегическиеЦелиПродажСоставПлана.НомерСтроки
	|ИЗ
	|	Документ.абс_СтратегическиеЦелиПродаж.СоставПлана КАК абс_СтратегическиеЦелиПродажСоставПлана
	|ГДЕ
	|	абс_СтратегическиеЦелиПродажСоставПлана.Ссылка = &Ссылка
	|	И НЕ НАЧАЛОПЕРИОДА(абс_СтратегическиеЦелиПродажСоставПлана.Период, ГОД) = НАЧАЛОПЕРИОДА(абс_СтратегическиеЦелиПродажСоставПлана.Ссылка.ДатаПланирования, ГОД)";
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Результат=Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка=Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			Сообщение=Новый СообщениеПользователю;
			Сообщение.Текст="В строке "+Выборка.НомерСтроки+" не верно указан период. Период должен принадлежать году "+Год(ДатаПланирования);
			Сообщение.Сообщить();
			Отказ=Истина;
		КонецЦикла;
	КонецЕсли;
	
	Если Не Отказ Тогда
		Для Каждого ТекСтрокаСоставПлана Из СоставПлана Цикл
			//регистр абс_СтратегическиеЦелиПродаж
			Движение = Движения.абс_СтратегическиеЦелиПродаж.Добавить();
			Движение.ПериодПлана = ТекСтрокаСоставПлана.Период;
			Движение.Сценарий = Сценарий;
			Движение.Регион = ТекСтрокаСоставПлана.Регион;
			Движение.Территория = ТекСтрокаСоставПлана.Территория;
			Движение.Город = ТекСтрокаСоставПлана.Город;
			Движение.Канал = ТекСтрокаСоставПлана.Канал;
			
			// регистр абс_ЦелиПродаж 
			Движение = Движения.абс_ЦелиПродаж.Добавить();
			Движение.Период = ТекСтрокаСоставПлана.Период;
			Движение.Регион = ТекСтрокаСоставПлана.Регион;
			Движение.Территория = ТекСтрокаСоставПлана.Территория;
			Движение.Город = ТекСтрокаСоставПлана.Город;
			Движение.Канал = ТекСтрокаСоставПлана.Канал;
			Движение.Сценарий = Сценарий;
			Движение.КоличествоТочекПродаж = ТекСтрокаСоставПлана.КоличествоТочекПродаж;
			Движение.АКБ = ТекСтрокаСоставПлана.АКБ;
			Движение.ЦельПоЧисленнойДистрибуции = ТекСтрокаСоставПлана.ЦельПоЧисленнойДистрибуции;
			Движение.КоличествоАртикулов = ТекСтрокаСоставПлана.КоличествоАртикулов;
			Движение.ЦельПоДистрибуцииФА1 = ТекСтрокаСоставПлана.ЦельПоДистрибуцииФА1;
			Движение.ЦельПоДистрибуцииФА2 = ТекСтрокаСоставПлана.ЦельПоДистрибуцииФА2;
			Движение.ЦельПоДистрибуцииФА3 = ТекСтрокаСоставПлана.ЦельПоДистрибуцииФА3;
			Движение.ЦельПоДистрибуцииФА4 = ТекСтрокаСоставПлана.ЦельПоДистрибуцииФА4;
		КонецЦикла;
	КонецЕсли;
	
	//Если используется механизм согласования документов Стратегические цели продаж, и документ еще не прошел по маршуту утверждения 
	//	- добавим запись в регистр СостоянияСогласования
	НаборЗаписейСостояниеСогласования = РегистрыСведений.абс_СостоянияСогласованияДокументов.СоздатьНаборЗаписей();
	НаборЗаписейСостояниеСогласования.Отбор.ДокументДляСогласования.Установить(Ссылка);
	НаборЗаписейСостояниеСогласования.Прочитать();
	Если абс_СогласованиеДокументов.ИспользуетсяСогласованиеДокументов(Организация,Перечисления.абс_ТипыДокументовДляСогласования.СтратегическиеЦелиПродаж,Дата) Тогда
		Если Состояние=Перечисления.СостоянияОбъектов.Подготовлен Тогда
			Запрос=Новый Запрос;
			Запрос.Текст=
			"ВЫБРАТЬ
			|	абс_СостоянияСогласованияДокументовСрезПоследних.Состояние
			|ИЗ
			|	РегистрСведений.абс_СостоянияСогласованияДокументов.СрезПоследних(, ДокументДляСогласования = &Ссылка) КАК абс_СостоянияСогласованияДокументовСрезПоследних";
			Запрос.УстановитьПараметр("Ссылка",Ссылка);
			Выборка=Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				Если Выборка.Состояние=Перечисления.СостоянияОбъектов.Отклонен Тогда
					МаршрутСогласования = абс_СогласованиеДокументов.ОпределитьМаршрутСогласования(Неопределено,Перечисления.абс_ТипыДокументовДляСогласования.СтратегическиеЦелиПродаж);
					Если НЕ ЗначениеЗаполнено(МаршрутСогласования) Тогда
						Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);
						ОбщегоНазначения.СообщитьОбОшибке("Не удалось определить маршрут согласования документа", Отказ, Заголовок);
						Возврат;
					КонецЕсли;
					
					НоваяЗапись = НаборЗаписейСостояниеСогласования.Добавить();
					
					НоваяЗапись.Период = ТекущаяДата();
					НоваяЗапись.Активность = Истина;
					НоваяЗапись.ДокументДляСогласования = Ссылка;
					НоваяЗапись.Пользователь = Ответственный;
					НоваяЗапись.Этап = МаршрутСогласования;
					НоваяЗапись.Уровень = МаршрутСогласования.Уровень() + 1;
					НоваяЗапись.Состояние = Перечисления.СостоянияОбъектов.Подготовлен;
					
					НаборЗаписейСостояниеСогласования.Записать();
					Возврат;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Если НаборЗаписейСостояниеСогласования.Количество() > 1 Тогда
			//Документ уже пошел по маршруту согласования
			Возврат;
		КонецЕсли;
		
		МаршрутСогласования = абс_СогласованиеДокументов.ОпределитьМаршрутСогласования(Неопределено,Перечисления.абс_ТипыДокументовДляСогласования.СтратегическиеЦелиПродаж);
		Если НЕ ЗначениеЗаполнено(МаршрутСогласования) Тогда
			Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);
			ОбщегоНазначения.СообщитьОбОшибке("Не удалось определить маршрут согласования документа", Отказ, Заголовок);
			Возврат;
		КонецЕсли;
		//Уже есть запись с правильным маршрутом согласования
		Если НаборЗаписейСостояниеСогласования.Количество() = 1 И
			НаборЗаписейСостояниеСогласования[0].Этап = МаршрутСогласования Тогда
			Возврат;
		КонецЕсли;
		НаборЗаписейСостояниеСогласования.Очистить();
		НоваяЗапись = НаборЗаписейСостояниеСогласования.Добавить();
		
		НоваяЗапись.Период = ТекущаяДата();
		НоваяЗапись.Активность = Истина;
		НоваяЗапись.ДокументДляСогласования = Ссылка;
		НоваяЗапись.Пользователь = Ответственный;
		НоваяЗапись.Этап = МаршрутСогласования;
		НоваяЗапись.Уровень = МаршрутСогласования.Уровень() + 1;
		НоваяЗапись.Состояние = Перечисления.СостоянияОбъектов.Подготовлен;
		
		НаборЗаписейСостояниеСогласования.Записать();
	ИначеЕсли НаборЗаписейСостояниеСогласования.Количество() > 0 Тогда
		//Если есть записи по этому документу, а согласование не используется - очистим
		НаборЗаписейСостояниеСогласования.Очистить();
		НаборЗаписейСостояниеСогласования.Записать();
	КонецЕсли;

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Состояние=Перечисления.СостоянияОбъектов.Подготовлен;
	ЭтапСогласования=Справочники.абс_МаршрутыСогласованияДокументов.ПустаяСсылка();
	УровеньСогласования=0;
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	// Удаление движений по регистру абс_СостоянияСогласованияДокументов, если документ был подготовлен
	НаборЗаписейСостояниеСогласования = РегистрыСведений.абс_СостоянияСогласованияДокументов.СоздатьНаборЗаписей();
	НаборЗаписейСостояниеСогласования.Отбор.ДокументДляСогласования.Установить(Ссылка);
	НаборЗаписейСостояниеСогласования.Прочитать();
	Если НаборЗаписейСостояниеСогласования.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	Если абс_СогласованиеДокументов.ИспользуетсяСогласованиеДокументов(Организация,Перечисления.абс_ТипыДокументовДляСогласования.СтратегическиеЦелиПродаж,Дата) Тогда
		Если НаборЗаписейСостояниеСогласования.Количество() > 1 Тогда
			//Документ уже пошел по маршруту согласования, не следует ничего менять
			Возврат;
		КонецЕсли;
		//Если в наборе записей только одна запись - значит документ только подготовлен.
		//Очистим набор записей
		НаборЗаписейСостояниеСогласования.Очистить();
		НаборЗаписейСостояниеСогласования.Записать();
	ИначеЕсли НаборЗаписейСостояниеСогласования.Количество() > 0 Тогда
		//Если есть записи по этому документу, а согласование не используется - очистим
		НаборЗаписейСостояниеСогласования.Очистить();
		НаборЗаписейСостояниеСогласования.Записать();
	КонецЕсли;
КонецПроцедуры