////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

Процедура ЗаполнитьСтатьиБюджета(Объект) Экспорт
	
	Запрос=Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	ОборотыБюджетовОбороты.Период КАК Период,
	|	ОборотыБюджетовОбороты.ЦФО КАК ЦФО,
	|	ОборотыБюджетовОбороты.СтатьяОборотов КАК СтатьяОборотовПоБюджетам,
	|	ОборотыБюджетовОбороты.Контрагент КАК Контрагент,
	|	ОборотыБюджетовОбороты.Номенклатура КАК Номенклатура,
	|	ОборотыБюджетовОбороты.абс_АналитикаБюджета КАК АналитикаБюджета,
	|	ОборотыБюджетовОбороты.абс_ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ОборотыБюджетовОбороты.абс_Регион КАК Регион,
	|	ВЫБОР
	|		КОГДА ОборотыБюджетовОбороты.СтатьяОборотов.абс_ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.абс_ТипыСтатейОборотовПоБюджетам.СтатьяРасходов)
	|			ТОГДА -ОборотыБюджетовОбороты.КоличествоОборот
	|		ИНАЧЕ ОборотыБюджетовОбороты.КоличествоОборот
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА ОборотыБюджетовОбороты.СтатьяОборотов.абс_ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.абс_ТипыСтатейОборотовПоБюджетам.СтатьяРасходов)
	|			ТОГДА -ОборотыБюджетовОбороты.СуммаУпрОборот
	|		ИНАЧЕ ОборотыБюджетовОбороты.СуммаУпрОборот
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА ОборотыБюджетовОбороты.СтатьяОборотов.абс_ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.абс_ТипыСтатейОборотовПоБюджетам.СтатьяРасходов)
	|			ТОГДА -ОборотыБюджетовОбороты.СуммаУпрБезНДСОборот
	|		ИНАЧЕ ОборотыБюджетовОбороты.СуммаУпрБезНДСОборот
	|	КОНЕЦ КАК СуммаБезНДС
	|ИЗ
	|	РегистрНакопления.ОборотыБюджетов.Обороты(
	|			НАЧАЛОПЕРИОДА(&Период, ГОД),
	|			КОНЕЦПЕРИОДА(&Период, ГОД),
	|			Месяц,
	|			Сценарий = ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Бюджет)
	|				И Организация = &Организация
	|				И ЦФО = &ЦФО
	|				И абс_Бюджет = ЗНАЧЕНИЕ(Справочник.Бюджеты.СводныйБДР)) КАК ОборотыБюджетовОбороты
	|ИТОГИ
	|	СУММА(Количество),
	|	СУММА(Сумма),
	|	СУММА(СуммаБезНДС)
	|ПО
	|	ЦФО,
	|	СтатьяОборотовПоБюджетам,
	|	АналитикаБюджета,
	|	Регион,
	|	Контрагент,
	|	ДоговорКонтрагента,
	|	Номенклатура,
	|	Период";
	
	Запрос.УстановитьПараметр("Период", ДатаПланирования);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ЦФО", ЦФО);
	
	ВыборкаЦФО=Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаЦФО.Следующий() Цикл
		
		ВыборкаСтатья=ВыборкаЦФО.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаСтатья.Следующий() Цикл
			
			ВыборкаАналитикаБюджета=ВыборкаСтатья.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаАналитикаБюджета.Следующий() Цикл
				
				ВыборкаРегион=ВыборкаАналитикаБюджета.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаРегион.Следующий() Цикл
					
					ВыборкаКонтрагент=ВыборкаРегион.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Пока ВыборкаКонтрагент.Следующий() Цикл
						
						ВыборкаДоговорКонтрагента=ВыборкаКонтрагент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						Пока ВыборкаДоговорКонтрагента.Следующий() Цикл
							
							ВыборкаНоменклатура=ВыборкаДоговорКонтрагента.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
							Пока ВыборкаНоменклатура.Следующий() Цикл
								
								СтрокаТЧ=Объект.СоставБюджета.Добавить();
								ЗаполнитьЗначенияСвойств(СтрокаТЧ,ВыборкаНоменклатура);
								
								ВыборкаПериод=ВыборкаНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
								Пока ВыборкаПериод.Следующий() Цикл
									СтрокаТЧ["БюджетСумма"+Месяц(ВыборкаПериод.Период)]=ВыборкаПериод.Сумма;
									СтрокаТЧ["БюджетКоличество"+Месяц(ВыборкаПериод.Период)]=ВыборкаПериод.Количество;
									СтрокаТЧ["БюджетСуммаБезНДС"+Месяц(ВыборкаПериод.Период)]=ВыборкаПериод.СуммаБезНДС;
									
									СтрокаТЧ["ПланСумма"+Месяц(ВыборкаПериод.Период)]=ВыборкаПериод.Сумма;
									СтрокаТЧ["ПланКоличество"+Месяц(ВыборкаПериод.Период)]=ВыборкаПериод.Количество;
									СтрокаТЧ["ПланСуммаБезНДС"+Месяц(ВыборкаПериод.Период)]=ВыборкаПериод.СуммаБезНДС;
								КонецЦикла;
							КонецЦикла;
						КонецЦикла;
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновитьДанныеФакта(Объект) Экспорт
	
	Запрос=Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	СУММА(ВЫБОР
	|			КОГДА ОборотыБюджетовОборотыФакт.СтатьяОборотов.абс_ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.абс_ТипыСтатейОборотовПоБюджетам.СтатьяРасходов)
	|				ТОГДА -ОборотыБюджетовОборотыФакт.КоличествоОборот
	|			ИНАЧЕ ОборотыБюджетовОборотыФакт.КоличествоОборот
	|		КОНЕЦ) КАК Количество,
	|	СУММА(ВЫБОР
	|			КОГДА ОборотыБюджетовОборотыФакт.СтатьяОборотов.абс_ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.абс_ТипыСтатейОборотовПоБюджетам.СтатьяРасходов)
	|				ТОГДА -ОборотыБюджетовОборотыФакт.СуммаУпрОборот
	|			ИНАЧЕ ОборотыБюджетовОборотыФакт.СуммаУпрОборот
	|		КОНЕЦ) КАК Сумма,
	|	СУММА(ВЫБОР
	|			КОГДА ОборотыБюджетовОборотыФакт.СтатьяОборотов.абс_ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.абс_ТипыСтатейОборотовПоБюджетам.СтатьяРасходов)
	|				ТОГДА -ОборотыБюджетовОборотыФакт.СуммаУпрБезНДСОборот
	|			ИНАЧЕ ОборотыБюджетовОборотыФакт.СуммаУпрБезНДСОборот
	|		КОНЕЦ) КАК СуммаБезНДС,
	|	ОборотыБюджетовБюджет.Период,
	|	ОборотыБюджетовБюджет.СтатьяОборотов,
	|	ОборотыБюджетовБюджет.ЦФО,
	|	ОборотыБюджетовБюджет.Контрагент,
	|	ОборотыБюджетовБюджет.Номенклатура,
	|	ОборотыБюджетовБюджет.абс_АналитикаБюджета КАК АналитикаБюджета,
	|	ОборотыБюджетовБюджет.абс_ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ОборотыБюджетовБюджет.абс_Регион КАК Регион
	|ИЗ
	|	РегистрНакопления.ОборотыБюджетов.Обороты(
	|			НАЧАЛОПЕРИОДА(&ДатаПланирования, ГОД),
	|			КОНЕЦПЕРИОДА(&ДатаПланирования, ГОД),
	|			Месяц,
	|			Сценарий = ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Бюджет)
	|				И абс_Бюджет = ЗНАЧЕНИЕ(Справочник.Бюджеты.СводныйБДР)
	|				И Организация = &Организация
	|				И ЦФО = &ЦФО) КАК ОборотыБюджетовБюджет
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ОборотыБюджетов.Обороты(
	|				НАЧАЛОПЕРИОДА(&ДатаПланирования, ГОД),
	|				КОНЕЦПЕРИОДА(&ДатаПланирования, ГОД),
	|				Месяц,
	|				Сценарий = ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Факт)
	|					И абс_Бюджет = ЗНАЧЕНИЕ(Справочник.Бюджеты.СводныйБДР)
	|					И Организация = &Организация
	|					И ЦФО = &ЦФО) КАК ОборотыБюджетовОборотыФакт
	|		ПО ОборотыБюджетовБюджет.ЦФО = ОборотыБюджетовОборотыФакт.ЦФО
	|			И ОборотыБюджетовБюджет.СтатьяОборотов = ОборотыБюджетовОборотыФакт.СтатьяОборотов
	|			И ОборотыБюджетовБюджет.Контрагент = ОборотыБюджетовОборотыФакт.Контрагент
	|			И ОборотыБюджетовБюджет.Номенклатура = ОборотыБюджетовОборотыФакт.Номенклатура
	|			И ОборотыБюджетовБюджет.Организация = ОборотыБюджетовОборотыФакт.Организация
	|			И ОборотыБюджетовБюджет.абс_АналитикаБюджета = ОборотыБюджетовОборотыФакт.абс_АналитикаБюджета
	|			И ОборотыБюджетовБюджет.абс_ДоговорКонтрагента = ОборотыБюджетовОборотыФакт.абс_ДоговорКонтрагента
	|			И ОборотыБюджетовБюджет.абс_Регион = ОборотыБюджетовОборотыФакт.абс_Регион
	|			И ОборотыБюджетовБюджет.Период = ОборотыБюджетовОборотыФакт.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ОборотыБюджетовБюджет.Период,
	|	ОборотыБюджетовБюджет.СтатьяОборотов,
	|	ОборотыБюджетовБюджет.ЦФО,
	|	ОборотыБюджетовБюджет.Контрагент,
	|	ОборотыБюджетовБюджет.Номенклатура,
	|	ОборотыБюджетовБюджет.абс_АналитикаБюджета,
	|	ОборотыБюджетовБюджет.абс_ДоговорКонтрагента,
	|	ОборотыБюджетовБюджет.абс_Регион";
	
	Запрос.УстановитьПараметр("Ссылка",Объект.Ссылка);
	Запрос.УстановитьПараметр("ДатаПланирования",Объект.ДатаПланирования);
	Запрос.УстановитьПараметр("Организация",Объект.Организация);
	Запрос.УстановитьПараметр("ЦФО",Объект.ЦФО);

	Выборка=Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтруктураПоиска=Новый Структура;
		СтруктураПоиска.Вставить("ЦФО",Выборка.ЦФО);
		СтруктураПоиска.Вставить("СтатьяОборотовПоБюджетам",Выборка.СтатьяОборотов);
		СтруктураПоиска.Вставить("АналитикаБюджета",Выборка.АналитикаБюджета);
		СтруктураПоиска.Вставить("Регион",Выборка.Регион);
		СтруктураПоиска.Вставить("Контрагент",Выборка.Контрагент);
		СтруктураПоиска.Вставить("ДоговорКонтрагента",Выборка.ДоговорКонтрагента);
		СтруктураПоиска.Вставить("Номенклатура",Выборка.Номенклатура);
		
		МассивСтрок=Объект.СоставБюджета.НайтиСтроки(СтруктураПоиска);
		
		Для каждого СтрокаТЧ из МассивСтрок Цикл
			СтрокаТЧ["ФактСумма"+Месяц(Выборка.Период)]=Выборка.Сумма;
			СтрокаТЧ["ФактКоличество"+Месяц(Выборка.Период)]=Выборка.Количество;
			СтрокаТЧ["ФактСуммаБезНДС"+Месяц(Выборка.Период)]=Выборка.СуммаБезНДС;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаЗаполнения(ДанныеЗаполения)
	Ответственный=ПараметрыСеанса.ТекущийПользователь;
	ДатаПланирования = НачалоГода(ТекущаяДата());
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.ОборотыБюджетов.Записывать = Истина;
	Движения.ОборотыБюджетов.Очистить();
	
	Запрос=Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	абс_ОперативныйБюджетСоставБюджета.Ссылка,
	|	абс_ОперативныйБюджетСоставБюджета.ЦФО,
	|	абс_ОперативныйБюджетСоставБюджета.СтатьяОборотовПоБюджетам КАК СтатьяОборотов,
	|	абс_ОперативныйБюджетСоставБюджета.АналитикаБюджета КАК абс_АналитикаБюджета,
	|	абс_ОперативныйБюджетСоставБюджета.Номенклатура,
	|	абс_ОперативныйБюджетСоставБюджета.Контрагент,
	|	абс_ОперативныйБюджетСоставБюджета.ДоговорКонтрагента КАК абс_ДоговорКонтрагента,
	|	абс_ОперативныйБюджетСоставБюджета.Регион КАК абс_Регион,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетСуммаБезНДС1,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетСуммаБезНДС2,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетСуммаБезНДС3,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетСуммаБезНДС4,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетСуммаБезНДС5,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетСуммаБезНДС6,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетСуммаБезНДС7,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетСуммаБезНДС8,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетСуммаБезНДС9,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетСуммаБезНДС10,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетСуммаБезНДС11,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетСуммаБезНДС12,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетСумма1,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетСумма2,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетСумма3,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетСумма4,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетСумма5,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетСумма6,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетСумма7,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетСумма8,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетСумма9,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетСумма10,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетСумма11,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетСумма12,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетКоличество1,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетКоличество2,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетКоличество3,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетКоличество4,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетКоличество5,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетКоличество6,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетКоличество7,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетКоличество8,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетКоличество9,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетКоличество10,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетКоличество11,
	|	абс_ОперативныйБюджетСоставБюджета.БюджетКоличество12,
	|	абс_ОперативныйБюджетСоставБюджета.ПланСуммаБезНДС1,
	|	абс_ОперативныйБюджетСоставБюджета.ПланСуммаБезНДС2,
	|	абс_ОперативныйБюджетСоставБюджета.ПланСуммаБезНДС3,
	|	абс_ОперативныйБюджетСоставБюджета.ПланСуммаБезНДС4,
	|	абс_ОперативныйБюджетСоставБюджета.ПланСуммаБезНДС5,
	|	абс_ОперативныйБюджетСоставБюджета.ПланСуммаБезНДС6,
	|	абс_ОперативныйБюджетСоставБюджета.ПланСуммаБезНДС7,
	|	абс_ОперативныйБюджетСоставБюджета.ПланСуммаБезНДС8,
	|	абс_ОперативныйБюджетСоставБюджета.ПланСуммаБезНДС9,
	|	абс_ОперативныйБюджетСоставБюджета.ПланСуммаБезНДС10,
	|	абс_ОперативныйБюджетСоставБюджета.ПланСуммаБезНДС11,
	|	абс_ОперативныйБюджетСоставБюджета.ПланСуммаБезНДС12,
	|	абс_ОперативныйБюджетСоставБюджета.ПланСумма1,
	|	абс_ОперативныйБюджетСоставБюджета.ПланСумма2,
	|	абс_ОперативныйБюджетСоставБюджета.ПланСумма3,
	|	абс_ОперативныйБюджетСоставБюджета.ПланСумма4,
	|	абс_ОперативныйБюджетСоставБюджета.ПланСумма5,
	|	абс_ОперативныйБюджетСоставБюджета.ПланСумма6,
	|	абс_ОперативныйБюджетСоставБюджета.ПланСумма7,
	|	абс_ОперативныйБюджетСоставБюджета.ПланСумма8,
	|	абс_ОперативныйБюджетСоставБюджета.ПланСумма9,
	|	абс_ОперативныйБюджетСоставБюджета.ПланСумма10,
	|	абс_ОперативныйБюджетСоставБюджета.ПланСумма11,
	|	абс_ОперативныйБюджетСоставБюджета.ПланСумма12,
	|	абс_ОперативныйБюджетСоставБюджета.ПланКоличество1,
	|	абс_ОперативныйБюджетСоставБюджета.ПланКоличество2,
	|	абс_ОперативныйБюджетСоставБюджета.ПланКоличество3,
	|	абс_ОперативныйБюджетСоставБюджета.ПланКоличество4,
	|	абс_ОперативныйБюджетСоставБюджета.ПланКоличество5,
	|	абс_ОперативныйБюджетСоставБюджета.ПланКоличество6,
	|	абс_ОперативныйБюджетСоставБюджета.ПланКоличество7,
	|	абс_ОперативныйБюджетСоставБюджета.ПланКоличество8,
	|	абс_ОперативныйБюджетСоставБюджета.ПланКоличество9,
	|	абс_ОперативныйБюджетСоставБюджета.ПланКоличество10,
	|	абс_ОперативныйБюджетСоставБюджета.ПланКоличество11,
	|	абс_ОперативныйБюджетСоставБюджета.ПланКоличество12,
	|	абс_ОперативныйБюджетСоставБюджета.ФактСуммаБезНДС1,
	|	абс_ОперативныйБюджетСоставБюджета.ФактСуммаБезНДС2,
	|	абс_ОперативныйБюджетСоставБюджета.ФактСуммаБезНДС3,
	|	абс_ОперативныйБюджетСоставБюджета.ФактСуммаБезНДС4,
	|	абс_ОперативныйБюджетСоставБюджета.ФактСуммаБезНДС5,
	|	абс_ОперативныйБюджетСоставБюджета.ФактСуммаБезНДС6,
	|	абс_ОперативныйБюджетСоставБюджета.ФактСуммаБезНДС7,
	|	абс_ОперативныйБюджетСоставБюджета.ФактСуммаБезНДС8,
	|	абс_ОперативныйБюджетСоставБюджета.ФактСуммаБезНДС9,
	|	абс_ОперативныйБюджетСоставБюджета.ФактСуммаБезНДС10,
	|	абс_ОперативныйБюджетСоставБюджета.ФактСуммаБезНДС11,
	|	абс_ОперативныйБюджетСоставБюджета.ФактСуммаБезНДС12,
	|	абс_ОперативныйБюджетСоставБюджета.ФактСумма1,
	|	абс_ОперативныйБюджетСоставБюджета.ФактСумма2,
	|	абс_ОперативныйБюджетСоставБюджета.ФактСумма3,
	|	абс_ОперативныйБюджетСоставБюджета.ФактСумма4,
	|	абс_ОперативныйБюджетСоставБюджета.ФактСумма5,
	|	абс_ОперативныйБюджетСоставБюджета.ФактСумма6,
	|	абс_ОперативныйБюджетСоставБюджета.ФактСумма7,
	|	абс_ОперативныйБюджетСоставБюджета.ФактСумма8,
	|	абс_ОперативныйБюджетСоставБюджета.ФактСумма9,
	|	абс_ОперативныйБюджетСоставБюджета.ФактСумма10,
	|	абс_ОперативныйБюджетСоставБюджета.ФактСумма11,
	|	абс_ОперативныйБюджетСоставБюджета.ФактСумма12,
	|	абс_ОперативныйБюджетСоставБюджета.ФактКоличество1,
	|	абс_ОперативныйБюджетСоставБюджета.ФактКоличество2,
	|	абс_ОперативныйБюджетСоставБюджета.ФактКоличество3,
	|	абс_ОперативныйБюджетСоставБюджета.ФактКоличество4,
	|	абс_ОперативныйБюджетСоставБюджета.ФактКоличество5,
	|	абс_ОперативныйБюджетСоставБюджета.ФактКоличество6,
	|	абс_ОперативныйБюджетСоставБюджета.ФактКоличество7,
	|	абс_ОперативныйБюджетСоставБюджета.ФактКоличество8,
	|	абс_ОперативныйБюджетСоставБюджета.ФактКоличество9,
	|	абс_ОперативныйБюджетСоставБюджета.ФактКоличество10,
	|	абс_ОперативныйБюджетСоставБюджета.ФактКоличество11,
	|	абс_ОперативныйБюджетСоставБюджета.ФактКоличество12,
	|	ВЫБОР
	|		КОГДА абс_ОперативныйБюджетСоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20)
	|				ИЛИ абс_ОперативныйБюджетСоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20_120)
	|			ТОГДА 20
	|		КОГДА абс_ОперативныйБюджетСоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10)
	|				ИЛИ абс_ОперативныйБюджетСоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10_110)
	|			ТОГДА 10
	|		КОГДА абс_ОперативныйБюджетСоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18)
	|				ИЛИ абс_ОперативныйБюджетСоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118)
	|			ТОГДА 18    
	|		КОГДА абс_ОперативныйБюджетСоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС5)
	|				ИЛИ абс_ОперативныйБюджетСоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС5_105)
	|			ТОГДА 5
	|		КОГДА абс_ОперативныйБюджетСоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС7)
	|				ИЛИ абс_ОперативныйБюджетСоставБюджета.СтатьяОборотовПоБюджетам.абс_СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС7_107)
	|			ТОГДА 7
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтавкаНДС,
	|	абс_ОперативныйБюджетСоставБюджета.Ссылка.Организация,
	|	ВЫБОР
	|		КОГДА абс_ОперативныйБюджетСоставБюджета.СтатьяОборотовПоБюджетам.абс_ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.абс_ТипыСтатейОборотовПоБюджетам.СтатьяРасходов)
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Коэффициент
	|ИЗ
	|	Документ.абс_ОперативныйБюджет.СоставБюджета КАК абс_ОперативныйБюджетСоставБюджета
	|ГДЕ
	|	абс_ОперативныйБюджетСоставБюджета.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	
	Выборка=Запрос.Выполнить().Выбрать();                            
	Пока Выборка.Следующий() Цикл
		Для сч=1 по 12 Цикл
			
			//движения по сценарию План
			ПланСумма=Выборка["ПланСумма"+СокрЛП(Сч)];
			ПланСуммаБезНДС=Выборка["ПланСуммаБезНДС"+СокрЛП(Сч)];
			ПланКоличество=Выборка["ПланКоличество"+СокрЛП(Сч)];
			Если ПланСуммаБезНДС<>0 Тогда
				Движение = Движения.ОборотыБюджетов.Добавить();
				ЗаполнитьЗначенияСвойств(Движение,Выборка);
				Движение.Период = ДобавитьМесяц(ДатаПланирования,Сч-1);
				Движение.СуммаУпр=?(ПланСумма=0,ПланСуммаБезНДС+ПланСуммаБезНДС*Выборка.СтавкаНДС/100,ПланСумма)*Выборка.Коэффициент;
				Движение.СуммаУпрБезНДС=ПланСуммаБезНДС*Выборка.Коэффициент;
				Движение.Количество=ПланКоличество*Выборка.Коэффициент;
				Движение.абс_Бюджет=Справочники.Бюджеты.СводныйБДР;
				Движение.Сценарий=Справочники.СценарииПланирования.План;
			КонецЕсли;
			
			//движения по сценарию Предварительный факт
			ФактСумма=Выборка["ФактСумма"+СокрЛП(Сч)];
			ФактСуммаБезНДС=Выборка["ФактСуммаБезНДС"+СокрЛП(Сч)];
			ФактКоличество=Выборка["ФактКоличество"+СокрЛП(Сч)];
			Если ФактСуммаБезНДС<>0 Тогда
				Движение = Движения.ОборотыБюджетов.Добавить();
				ЗаполнитьЗначенияСвойств(Движение,Выборка);
				Движение.Период = ДобавитьМесяц(ДатаПланирования,Сч-1);
				Движение.СуммаУпр=?(ФактСумма=0,ФактСуммаБезНДС+ФактСуммаБезНДС*Выборка.СтавкаНДС/100,ФактСумма)*Выборка.Коэффициент;
				Движение.СуммаУпрБезНДС=ФактСуммаБезНДС*Выборка.Коэффициент;
				Движение.Количество=ФактКоличество*Выборка.Коэффициент;
				Движение.абс_Бюджет=Справочники.Бюджеты.СводныйБДР;
				Движение.Сценарий=Справочники.СценарииПланирования.ОперативныйФакт;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры
