Процедура ОбработкаЗаполнения(ДанныеЗаполнения,СтандартнаяОбработка)
	Ответственный=ПараметрыСеанса.ТекущийПользователь;
	ВидОперации=Перечисления.абс_ВидыОперацийЗаявкиНаБюджетТМ.БесплатнаяОтгрузка;
	ВидОплаты=Перечисления.абс_ВидыОплатУслугКонтрагентов.БесплатнаяОтгрузка;
	Состояние=Перечисления.СостоянияОбъектов.Подготовлен;
КонецПроцедуры

Процедура ПередЗаписью(Отказ,РежимЗаписи,РежимПроведения)
	Если ВидОперации=Перечисления.абс_ВидыОперацийЗаявкиНаБюджетТМ.БесплатнаяОтгрузка Тогда
		СуммаДокумента=Товары.Итог("Сумма");
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.абс_БюджетОтделаТМ.Записывать = Истина;
	Движения.абс_БюджетОтделаТМ.Очистить();
	
	Если Состояние = Перечисления.СостоянияОбъектов.Утвержден Тогда
		// регистр абс_БюджетОтделаТМ Приход
		Движение = Движения.абс_БюджетОтделаТМ.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.ЗаявкаНаБюджетОтделаТМ = Ссылка;
		Движение.Сумма = СуммаДокумента;
		
		Если ВидОперации = Перечисления.абс_ВидыОперацийЗаявкиНаБюджетТМ.Взаимозачет 
			И Закрыта Тогда
			Движение = Движения.абс_БюджетОтделаТМ.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = Дата;
			Движение.ЗаявкаНаБюджетОтделаТМ = Ссылка;
			Движение.Сумма = СуммаДокумента;
		КонецЕсли;
			
	КонецЕсли;
	
	//Если используется механизм согласования документов ВводБюджета, и документ еще не прошел по маршуту утверждения 
	//	- добавим запись в регистр СостоянияСогласования
	НаборЗаписейСостояниеСогласования = РегистрыСведений.абс_СостоянияСогласованияДокументов.СоздатьНаборЗаписей();
	НаборЗаписейСостояниеСогласования.Отбор.ДокументДляСогласования.Установить(Ссылка);
	НаборЗаписейСостояниеСогласования.Прочитать();
	Если абс_СогласованиеДокументов.ИспользуетсяСогласованиеДокументов(Организация,Перечисления.абс_ТипыДокументовДляСогласования.ЗаявкаНаБюджетОтделаТМ,Дата) Тогда
		Если Состояние=Перечисления.СостоянияОбъектов.Подготовлен Тогда
			Запрос=Новый Запрос;
			Запрос.Текст=
			"ВЫБРАТЬ
			|	абс_СостоянияСогласованияДокументовСрезПоследних.Состояние
			|ИЗ
			|	РегистрСведений.абс_СостоянияСогласованияДокументов.СрезПоследних(, ДокументДляСогласования = &Ссылка) КАК абс_СостоянияСогласованияДокументовСрезПоследних";
			Запрос.УстановитьПараметр("Ссылка",Ссылка);
			Выборка=Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				Если Выборка.Состояние=Перечисления.СостоянияОбъектов.Отклонен Тогда
					МаршрутСогласования = абс_СогласованиеДокументов.ОпределитьМаршрутСогласования(ЦФО,Перечисления.абс_ТипыДокументовДляСогласования.ЗаявкаНаБюджетОтделаТМ);
					Если НЕ ЗначениеЗаполнено(МаршрутСогласования) Тогда
						Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);
						ОбщегоНазначения.СообщитьОбОшибке("Не удалось определить маршрут согласования документа", Отказ, Заголовок);
						Возврат;
					КонецЕсли;
					
					НоваяЗапись = НаборЗаписейСостояниеСогласования.Добавить();
					
					НоваяЗапись.Период = ТекущаяДата();
					НоваяЗапись.Активность = Истина;
					НоваяЗапись.ДокументДляСогласования = Ссылка;
					НоваяЗапись.Пользователь = Ответственный;
					НоваяЗапись.Этап = МаршрутСогласования;
					НоваяЗапись.Уровень = МаршрутСогласования.Уровень() + 1;
					НоваяЗапись.Состояние = Перечисления.СостоянияОбъектов.Подготовлен;
					
					НаборЗаписейСостояниеСогласования.Записать();
					Возврат;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Если НаборЗаписейСостояниеСогласования.Количество() > 1 Тогда
			//Документ уже пошел по маршруту согласования
			Возврат;
		КонецЕсли;
		МаршрутСогласования = абс_СогласованиеДокументов.ОпределитьМаршрутСогласования(ЦФО,Перечисления.абс_ТипыДокументовДляСогласования.ЗаявкаНаБюджетОтделаТМ);
		Если НЕ ЗначениеЗаполнено(МаршрутСогласования) Тогда
			Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);
			ОбщегоНазначения.СообщитьОбОшибке("Не удалось определить маршрут согласования документа", Отказ, Заголовок);
			Возврат;
		КонецЕсли;
		//Уже есть запись с правильным маршрутом согласования
		Если НаборЗаписейСостояниеСогласования.Количество() = 1 И
			НаборЗаписейСостояниеСогласования[0].Этап = МаршрутСогласования Тогда
			Возврат;
		КонецЕсли;
		НаборЗаписейСостояниеСогласования.Очистить();
		НоваяЗапись = НаборЗаписейСостояниеСогласования.Добавить();
		
		НоваяЗапись.Период = ТекущаяДата();
		НоваяЗапись.Активность = Истина;
		НоваяЗапись.ДокументДляСогласования = Ссылка;
		НоваяЗапись.Пользователь = Ответственный;
		НоваяЗапись.Этап = МаршрутСогласования;
		НоваяЗапись.Уровень = МаршрутСогласования.Уровень() + 1;
		НоваяЗапись.Состояние = Перечисления.СостоянияОбъектов.Подготовлен;
		
		НаборЗаписейСостояниеСогласования.Записать();
	ИначеЕсли НаборЗаписейСостояниеСогласования.Количество() > 0 Тогда
		//Если есть записи по этому документу, а согласование не используется - очистим
		НаборЗаписейСостояниеСогласования.Очистить();
		НаборЗаписейСостояниеСогласования.Записать();
	КонецЕсли;

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Состояние=Перечисления.СостоянияОбъектов.Подготовлен;
	ЭтапСогласования=Справочники.абс_МаршрутыСогласованияДокументов.ПустаяСсылка();
	УровеньСогласования=0;
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	// Удаление движений по регистру абс_СостоянияСогласованияДокументов, если документ был подготовлен
	НаборЗаписейСостояниеСогласования = РегистрыСведений.абс_СостоянияСогласованияДокументов.СоздатьНаборЗаписей();
	НаборЗаписейСостояниеСогласования.Отбор.ДокументДляСогласования.Установить(Ссылка);
	НаборЗаписейСостояниеСогласования.Прочитать();
	Если НаборЗаписейСостояниеСогласования.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	Если абс_СогласованиеДокументов.ИспользуетсяСогласованиеДокументов(Организация,Перечисления.абс_ТипыДокументовДляСогласования.ЗаявкаНаБюджетОтделаТМ,Дата) Тогда
		Если НаборЗаписейСостояниеСогласования.Количество() > 1 Тогда
			//Документ уже пошел по маршруту согласования, не следует ничего менять
			Возврат;
		КонецЕсли;
		//Если в наборе записей только одна запись - значит документ только подготовлен.
		//Очистим набор записей
		НаборЗаписейСостояниеСогласования.Очистить();
		НаборЗаписейСостояниеСогласования.Записать();
	ИначеЕсли НаборЗаписейСостояниеСогласования.Количество() > 0 Тогда
		//Если есть записи по этому документу, а согласование не используется - очистим
		НаборЗаписейСостояниеСогласования.Очистить();
		НаборЗаписейСостояниеСогласования.Записать();
	КонецЕсли;
КонецПроцедуры