
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроверитьЗаполнениеДокумента(Отказ);
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеДокумента(Отказ)
	
	Для каждого СтрокаТЧ из Маршруты Цикл
		Если Не ЗначениеЗаполнено(СтрокаТЧ.ТранспортныйМаршрут) Тогда
			ОбщегоНазначения.СообщитьОбОшибке("В строке "+СтрокаТЧ.НомерСтроки+" не указан транспортный маршрут");
			Отказ = Истина;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СтрокаТЧ.ЗаявкаНаТранспорт) Тогда
			ОбщегоНазначения.СообщитьОбОшибке("В строке "+СтрокаТЧ.НомерСтроки+" не указана заявка на транспорт");
			Отказ = Истина;
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	ЗаполнитьТранспортныеМаршруты(Отказ);	
	
КонецПроцедуры

//Процедура проверяет табличные части документов Транспортный маршрут,
//если в Сводной заявке для Транспортного маршрута добавились Заявки на транспорт
//эти заявки добавляются в соответствующие Транспортные маршруты 
Процедура ЗаполнитьТранспортныеМаршруты(Отказ = Ложь)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	абс_СводнаяЗаявкаНаТранспортМаршруты.ТранспортныйМаршрут,
	|	абс_СводнаяЗаявкаНаТранспортМаршруты.ЗаявкаНаТранспорт,
	|	абс_ТранспортныйМаршрутЗадания.ЗаявкаНаТранспорт КАК ЗаявкаИзМаршрута
	|ПОМЕСТИТЬ Маршруты
	|ИЗ
	|	Документ.абс_СводнаяЗаявкаНаТранспорт.Маршруты КАК абс_СводнаяЗаявкаНаТранспортМаршруты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.абс_ТранспортныйМаршрут.Задания КАК абс_ТранспортныйМаршрутЗадания
	|		ПО абс_СводнаяЗаявкаНаТранспортМаршруты.ТранспортныйМаршрут = абс_ТранспортныйМаршрутЗадания.Ссылка
	|			И абс_СводнаяЗаявкаНаТранспортМаршруты.ЗаявкаНаТранспорт = абс_ТранспортныйМаршрутЗадания.ЗаявкаНаТранспорт
	|ГДЕ
	|	абс_СводнаяЗаявкаНаТранспортМаршруты.Ссылка = &Ссылка
	|	И НЕ абс_СводнаяЗаявкаНаТранспортМаршруты.ТранспортныйМаршрут = ЗНАЧЕНИЕ(Документ.абс_ТранспортныйМаршрут.ПустаяСсылка)
	|	И НЕ абс_СводнаяЗаявкаНаТранспортМаршруты.ЗаявкаНаТранспорт = ЗНАЧЕНИЕ(Документ.абс_ЗаявкаНаТранспорт.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Маршруты.ТранспортныйМаршрут КАК ТранспортныйМаршрут,
	|	Маршруты.ЗаявкаНаТранспорт КАК ЗаявкаНаТранспорт
	|ИЗ
	|	Маршруты КАК Маршруты
	|ГДЕ
	|	Маршруты.ЗаявкаИзМаршрута ЕСТЬ NULL 
	|ИТОГИ ПО
	|	ТранспортныйМаршрут,
	|	ЗаявкаНаТранспорт";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	ВыборкаМаршрут = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаМаршрут.Следующий() Цикл
		
		ТранспортныйМаршрут = ВыборкаМаршрут.ТранспортныйМаршрут.ПолучитьОбъект();
		
		ВыборкаЗаявка = ВыборкаМаршрут.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаЗаявка.Следующий() Цикл
			НоваяСтрока = ТранспортныйМаршрут.Задания.Добавить();
			НоваяСтрока.ЗаявкаНаТранспорт = ВыборкаЗаявка.ЗаявкаНаТранспорт;
		КонецЦикла;
		
		Попытка
			ТранспортныйМаршрут.Записать(РежимЗаписиДокумента.Запись);
		Исключение
			Отказ = Истина;
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры
