
&НаКлиенте
Процедура ЗаполнитьПоЗаявкам(Команда)
	
	Если Объект.Маршруты.Количество() > 0 
		И Вопрос("Заполнить табличную часть по заявкам на транспорт?" + Символы.ПС + 
		"Существующие строки будут очищены.",РежимДиалогаВопрос.ДаНет) <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьМаршрутыПоЗаявкам_Сервер();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьМаршрутыПоЗаявкам_Сервер()
	
	Объект.Маршруты.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	абс_ЗаявкаНаТранспорт.Ссылка КАК ЗаявкаНаТранспорт,
	               |	абс_ЗаявкаНаТранспорт.Ссылка.Контрагент КАК Контрагент,
	               |	абс_ЗаявкаНаТранспорт.Ссылка.ОтветственныйМенеджер КАК Менеджер,
	               |	абс_ЗаявкаНаТранспорт.Ссылка.КонтактыОтветственногоМенеджера КАК КонтактыМенеджера,
	               |	абс_ЗаявкаНаТранспорт.Ссылка.Склад КАК Склад,
	               |	абс_ЗаявкаНаТранспорт.Ссылка.ВсегоОбъем КАК ОбъемПоЗаявке,
	               |	абс_ЗаявкаНаТранспорт.Ссылка.ВсегоВес КАК ВесПоЗаявке,
	               |	абс_ЗаявкаНаТранспорт.Ссылка.ВсегоКоличествоПалет КАК КоличествоПалетПоЗаявке,
	               |	СУММА(абс_ЗаявкаНаТранспорт.Сумма) КАК СуммаПоЗаявке,
	               |	ВЫРАЗИТЬ(абс_ЗаявкаНаТранспорт.Ссылка.Комментарий КАК СТРОКА(300)) КАК Комментарий
	               |ИЗ
	               |	Документ.абс_ЗаявкаНаТранспорт.Товары КАК абс_ЗаявкаНаТранспорт
	               |ГДЕ
	               |	абс_ЗаявкаНаТранспорт.Ссылка.ПланируемаяДатаОтгрузки <= &ПланируемаяДатаОтгрузки
	               |	И абс_ЗаявкаНаТранспорт.Ссылка.Статус = &Статус
	               |	И абс_ЗаявкаНаТранспорт.Ссылка.Проведен
	               |	И ВЫБОР
	               |			КОГДА НЕ ЕСТЬNULL(абс_ЗаявкаНаТранспорт.Ссылка.ЗаказПокупателя.ДоговорКонтрагента.ВидВзаиморасчетов, ЗНАЧЕНИЕ(Справочник.ВидыВзаиморасчетов.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.ВидыВзаиморасчетов.ПустаяСсылка)
	               |				ТОГДА НЕ абс_ЗаявкаНаТранспорт.Ссылка.ЗаказПокупателя.ДоговорКонтрагента.ВидВзаиморасчетов.абс_НеЯвляетсяПродажей
	               |			ИНАЧЕ ИСТИНА
	               |		КОНЕЦ
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	абс_ЗаявкаНаТранспорт.Ссылка,
	               |	абс_ЗаявкаНаТранспорт.Ссылка.Контрагент,
	               |	абс_ЗаявкаНаТранспорт.Ссылка.ОтветственныйМенеджер,
	               |	абс_ЗаявкаНаТранспорт.Ссылка.КонтактыОтветственногоМенеджера,
	               |	абс_ЗаявкаНаТранспорт.Ссылка.Склад,
	               |	абс_ЗаявкаНаТранспорт.Ссылка.ВсегоОбъем,
	               |	абс_ЗаявкаНаТранспорт.Ссылка.ВсегоВес,
	               |	абс_ЗаявкаНаТранспорт.Ссылка.ВсегоКоличествоПалет,
	               |	ВЫРАЗИТЬ(абс_ЗаявкаНаТранспорт.Ссылка.Комментарий КАК СТРОКА(300))
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	абс_ЗаявкаНаТранспорт.Ссылка.Склад,
	               |	абс_ЗаявкаНаТранспорт.Ссылка.Организация,
	               |	абс_ЗаявкаНаТранспорт.Ссылка.АдресПромежуточный,
	               |	абс_ЗаявкаНаТранспорт.Ссылка.АдресКонтрагента";
	
	Запрос.УстановитьПараметр("ПланируемаяДатаОтгрузки",КонецДня(Объект.ДатаИсполнения));
	Запрос.УстановитьПараметр("Статус",Перечисления.абс_СтатусыЗаявокНаТранспорт.КИсполнению);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Объект.Маршруты.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьМаршрут(Команда)
	
	Если Элементы.Маршруты.ВыделенныеСтроки.Количество()=0 Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не выделено ни одной заявки";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если Вопрос("Перед формированием маршрута документ будет записан. Продолжить?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
			Записать(Новый Структура("РежимЗаписи,РежимПроведения", РежимЗаписиДокумента.Запись, РежимПроведенияДокумента.Неоперативный));
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Вопрос("Сформировать маршрут по выделенным заявкам?",РежимДиалогаВопрос.ДаНет) <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	СформироватьТранспортныйМаршрут_Сервер();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьТранспортныйМаршрут_Сервер()
	
//	Записать(Новый Структура("РежимЗаписи,РежимПроведения", РежимЗаписиДокумента.Запись, РежимПроведенияДокумента.Неоперативный));
	
	Отказ = Ложь;
	
	Для Каждого ЭлМассива ИЗ Элементы.Маршруты.ВыделенныеСтроки Цикл
		ТекСтрока = Объект.Маршруты.НайтиПоИдентификатору(ЭлМассива);
		Если ЗначениеЗаполнено(ТекСтрока.ТранспортныйМаршрут) Тогда
			Сообщить("В строке " + Строка(ЭлМассива+1) + " уже указан маршрут!");
			Отказ = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ДокМаршрут = Документы.абс_ТранспортныйМаршрут.СоздатьДокумент();
	ДокМаршрут.Дата                     	= Объект.Дата;
	ДокМаршрут.СводнаяЗаявкаНаТранспорт 	= Объект.Ссылка;
	ДокМаршрут.Статус 						= Перечисления.абс_СтатусыЗаявокНаТранспорт.КИсполнению;
	ДокМаршрут.Ответственный 				= ПараметрыСеанса.ТекущийПользователь;
	
	//{Лео Катанович
	//Если Объект.Маршруты.Количество()>0 Тогда
	//	ДокМаршрут.Организация 				= Объект.Маршруты[0].ЗаявкаНаТранспорт.Организация;
	//Иначе
	//	ДокМаршрут.Организация 				= УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(ДокМаршрут.Ответственный, "ОсновнаяОрганизация");
	//КонецЕсли;
	//Лео Катанович}
	
	//нужна согласованная запись транспортного маршрута и заявок на транспорт
	НачатьТранзакцию();
	
	Для Каждого ЭлМассива ИЗ Элементы.Маршруты.ВыделенныеСтроки Цикл
		ТекСтрока = Объект.Маршруты.НайтиПоИдентификатору(ЭлМассива);
		НоваяСтрока = ДокМаршрут.Задания.Добавить();
		НоваяСтрока.ЗаявкаНаТранспорт = ТекСтрока.ЗаявкаНаТранспорт;
		
		//обновить статус в заявке на транспорт
		ДокЗаявкаОбъект = ТекСтрока.ЗаявкаНаТранспорт.ПолучитьОбъект();
		ДокЗаявкаОбъект.Статус = Перечисления.абс_СтатусыЗаявокНаТранспорт.Исполнение;
		
		//{Лео Катанович
		ДокМаршрут.Организация = ДокЗаявкаОбъект.Организация;
		
		//Лео Катанович}
		
		Попытка
			ДокЗаявкаОбъект.Записать();
		Исключение
			Сообщить(Строка(ДокЗаявкаОбъект) + " не записан! " + ОписаниеОшибки());
			Отказ = Истина;
		КонецПопытки;
		
	КонецЦикла;
	
	Если Отказ Тогда
		ОтменитьТранзакцию();
		Возврат;
	КонецЕсли;
	
	Попытка
		ДокМаршрут.Записать();
	Исключение
		Сообщить("По сводной заявке '" + СокрЛП(Объект.Номер) + " от " + Формат(Объект.Дата,"ДДММГГГГ") + " транспортный маршрут не сформирован! "
		+ ОписаниеОшибки(),СтатусСообщения.Важное);
		Отказ = Истина;
	КонецПопытки;
	
	Если Не Отказ Тогда
		//в текущей сводной заявке пропишем сформированные маршруты
		Для Каждого ЭлМассива ИЗ Элементы.Маршруты.ВыделенныеСтроки Цикл
			ТекСтрока = Объект.Маршруты.НайтиПоИдентификатору(ЭлМассива);
			ТекСтрока.ТранспортныйМаршрут = ДокМаршрут.Ссылка;
		КонецЦикла;
	КонецЕсли;
	
	Записать(Новый Структура("РежимЗаписи,РежимПроведения", РежимЗаписиДокумента.Запись, РежимПроведенияДокумента.Неоперативный));
	
	Если Отказ Тогда
		ОтменитьТранзакцию();
	Иначе
		ЗафиксироватьТранзакцию();		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ ЗначениеЗаполнено(Объект.ДатаИсполнения) Тогда
		Объект.ДатаИсполнения = Объект.Дата;
	КонецЕсли;
	
	УстановитьУсловноеОформлениеТЧ();
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
	ЭтотОбъект = ДанныеФормыВЗначение(Объект, Тип("ДокументОбъект.абс_СводнаяЗаявкаНаТранспорт")); 
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента( , ЭтотОбъект, ЭтаФорма);
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ГрупповаяПечать(Команда)
	П=Новый Структура("СводнаяЗаявкаНаТранспорт",Объект.Ссылка);
	Форма=ПолучитьФорму("Обработка.абс_ГрупповаяПечатьДокументов.Форма.Форма",П,ЭтаФорма);
	Если Форма.Открыта() Тогда
		Форма.Активизировать();
	Иначе
		Форма.Открыть();
	КонецЕсли;
КонецПроцедуры

&НаСервере 
Процедура УстановитьУсловноеОформлениеТЧ()
	
	СписокТМ = Новый СписокЗначений;
	Для каждого СтрокаТЧ из Объект.Маршруты Цикл
		Если ЗначениеЗаполнено(СтрокаТЧ.ТранспортныйМаршрут.ТранспортноеСредство) Тогда
			СписокТМ.Добавить(СтрокаТЧ.ТранспортныйМаршрут);
		КонецЕсли;
	КонецЦикла;
	
	УсловноеОформление.Элементы.Очистить();
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеОформления = ЭлементОформления.Поля.Элементы.Добавить();
	ПолеОформления.Поле = Новый ПолеКомпоновкиДанных("Маршруты");

	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Маршруты.ТранспортныйМаршрут");
	ЭлементОтбора.ПравоеЗначение = СписокТМ;
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона",WebЦвета.БледноЗеленый);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
	ЭтотОбъект = ДанныеФормыВЗначение(Объект, Тип("ДокументОбъект.абс_СводнаяЗаявкаНаТранспорт")); 
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента( , ЭтотОбъект, ЭтаФорма);
	#КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДокумент(Команда)
	ОбновитьДокументСервер();
КонецПроцедуры

&НаСервере 
Процедура ОбновитьДокументСервер()
	
	ЭтотОбъект = РеквизитФормыВЗначение("Объект");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	абс_ЗаявкаНаТранспорт.Ссылка КАК ЗаявкаНаТранспорт,
	|	ЕСТЬNULL(абс_ТранспортныйМаршрутЗадания.Ссылка, ЗНАЧЕНИЕ(Документ.абс_ТранспортныйМаршрут.ПустаяСсылка)) КАК ТранспортныйМаршрут
	|ИЗ
	|	Документ.абс_ЗаявкаНаТранспорт.Товары КАК абс_ЗаявкаНаТранспорт
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.абс_ТранспортныйМаршрут.Задания КАК абс_ТранспортныйМаршрутЗадания
	|		ПО абс_ЗаявкаНаТранспорт.Ссылка = абс_ТранспортныйМаршрутЗадания.ЗаявкаНаТранспорт
	|ГДЕ
	|	абс_ЗаявкаНаТранспорт.Ссылка.ПланируемаяДатаОтгрузки <= &ПланируемаяДатаОтгрузки
	|	И абс_ЗаявкаНаТранспорт.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.абс_СтатусыЗаявокНаТранспорт.КИсполнению)
	|	И абс_ЗаявкаНаТранспорт.Ссылка.Проведен
	|	И ВЫБОР
	|			КОГДА НЕ ЕСТЬNULL(абс_ЗаявкаНаТранспорт.Ссылка.ЗаказПокупателя.ДоговорКонтрагента.ВидВзаиморасчетов, ЗНАЧЕНИЕ(Справочник.ВидыВзаиморасчетов.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.ВидыВзаиморасчетов.ПустаяСсылка)
	|				ТОГДА НЕ абс_ЗаявкаНаТранспорт.Ссылка.ЗаказПокупателя.ДоговорКонтрагента.ВидВзаиморасчетов.абс_НеЯвляетсяПродажей
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|СГРУППИРОВАТЬ ПО
	|	абс_ЗаявкаНаТранспорт.Ссылка,
	|	ЕСТЬNULL(абс_ТранспортныйМаршрутЗадания.Ссылка, ЗНАЧЕНИЕ(Документ.абс_ТранспортныйМаршрут.ПустаяСсылка))";	
	
	Запрос.УстановитьПараметр("ПланируемаяДатаОтгрузки", КонецДня(ЭтотОбъект.ДатаИсполнения));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СтрокаТабличнойЧасти = ЭтотОбъект.Маршруты.Найти(Выборка.ЗаявкаНаТранспорт, "ЗаявкаНаТранспорт");
		Если СтрокаТабличнойЧасти = Неопределено Тогда
			НоваяСтрока = ЭтотОбъект.Маршруты.Добавить();
			НоваяСтрока.ЗаявкаНаТранспорт = Выборка.ЗаявкаНаТранспорт;
			НоваяСтрока.Комментарий = Выборка.ЗаявкаНаТранспорт.Комментарий;
		Иначе
			СтрокаТабличнойЧасти.ТранспортныйМаршрут = Выборка.ТранспортныйМаршрут;
		КонецЕсли;
	КонецЦикла;
	МассивСтрок = Новый Массив;	
	Для каждого СтрокаТЧ Из ЭтотОбъект.Маршруты Цикл
		Если СтрокаТЧ.ЗаявкаНаТранспорт.ПланируемаяДатаОтгрузки>=КонецДня(ЭтотОбъект.ДатаИсполнения) И Не ЗначениеЗаполнено(СтрокаТЧ.ТранспортныйМаршрут) Тогда
			МассивСтрок.Добавить(СтрокаТЧ);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаТЧ из МассивСтрок Цикл
		ЭтотОбъект.Маршруты.Удалить(СтрокаТЧ);
	КонецЦикла;
	
	ЭтотОбъект.Маршруты.Сортировать("ТранспортныйМаршрут, ЗаявкаНаТранспорт");
	
	ЗначениеВРеквизитФормы(ЭтотОбъект, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура МаршрутыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура МаршрутыТранспортныйМаршрутНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ФормаВыбора = ПолучитьФорму("Документ.абс_ТранспортныйМаршрут.ФормаВыбора",,Элемент);
	
	ЭлементОтбора = ФормаВыбора.Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Дата");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
	ЭлементОтбора.ПравоеЗначение = НачалоДня(Объект.ДатаИсполнения);
	ЭлементОтбора.Использование = Истина;
	
	ЭлементОтбора = ФормаВыбора.Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Дата");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.МеньшеИлиРавно;
	ЭлементОтбора.ПравоеЗначение = КонецДня(Объект.ДатаИсполнения);
	ЭлементОтбора.Использование = Истина;
	
	ФормаВыбора.Открыть();
	
КонецПроцедуры

