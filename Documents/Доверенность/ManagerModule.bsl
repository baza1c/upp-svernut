//абс вставка 38913
Функция ПечатьДоверенности(ДоверенностьСсылка) Экспорт

	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ТекущийДокумент",  ДоверенностьСсылка);
	Запрос.УстановитьПараметр("ДатаСреза",        ДоверенностьСсылка.Дата);
	Запрос.УстановитьПараметр("ФизЛицо",          ДоверенностьСсылка.ФизЛицо.Ссылка);

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Доверенность.Номер КАК НомерДокумента,
	|	Доверенность.Дата КАК ДатаДокумента,
	|	Доверенность.Организация КАК Руководители,
	|	Доверенность.Организация,
	|	Доверенность.ФизЛицо,
	|	Доверенность.Контрагент КАК Поставщик,
	|	Доверенность.БанковскийСчетОрганизации КАК БанковскийСчет,
	|	ВЫБОР
	|		КОГДА Доверенность.НаПолучениеОт = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА Доверенность.Контрагент.НаименованиеПолное
	|		ИНАЧЕ ВЫБОР
	|				КОГДА (ВЫРАЗИТЬ(Доверенность.НаПолучениеОт.НаименованиеПолное КАК СТРОКА(100))) = """"
	|					ТОГДА Доверенность.НаПолучениеОт.Наименование
	|				ИНАЧЕ Доверенность.НаПолучениеОт.НаименованиеПолное
	|			КОНЕЦ
	|	КОНЕЦ КАК ПоставщикПредставление,
	|	Доверенность.ДатаДействия КАК СрокДействия,
	|	Доверенность.ПоДокументу КАК РеквизитыДокументаНаПолучение,
	|	Доверенность.Товары.(
	|		НомерСтроки КАК Номер,
	|		НаименованиеТовара КАК Ценнности,
	|		НаименованиеТовара КАК ЦеннностиПредставление,
	|		ЕдиницаПоКлассификатору КАК ЕдиницаИзмерения,
	|		ЕдиницаПоКлассификатору.Представление КАК ЕдиницаИзмеренияПредставление,
	|		Количество
	|	),
	|	Доверенность.абс_ДокументОснование.ДокументОснование КАК ДокументОснование
	|ИЗ
	|	Документ.Доверенность КАК Доверенность
	|ГДЕ
	|	Доверенность.Ссылка = &ТекущийДокумент
	|
	|УПОРЯДОЧИТЬ ПО
	|	Доверенность.Товары.НомерСтроки";

	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();
	ВыборкаСтрокТовары = Шапка.Товары.Выбрать();

	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Доверенность_М2";

	Макет = ПолучитьОбщийМакет("М2");

	ФамилияИмяОтчествоФизЛица     = ФормированиеПечатныхФормСервер.ФамилияИмяОтчество(Шапка.ФизЛицо, Шапка.ДатаДокумента);
	ПодразделениеДолжностьФизЛица = ПолныеПрава.СведенияОСотруднике(Шапка.ФизЛицо, Шапка.ДатаДокумента, Шапка.Организация);

	ФамилияИмяОтчествоДоверенного = ФамилияИмяОтчествоФизЛица.Фамилия + " " + ФамилияИмяОтчествоФизЛица.Имя + " " + ФамилияИмяОтчествоФизЛица.Отчество;
	ДолжностьДоверенного          = ?(НЕ ЗначениеЗаполнено(ПодразделениеДолжностьФизЛица.Должность),"",ПодразделениеДолжностьФизЛица.Должность);;
	
	ОбластьМакета = Макет.ПолучитьОбласть("Отрез");
	ОбластьМакета.Параметры.Заполнить(Шапка);
	// {Лео Катанович
	Если ЗначениеЗаполнено(Шапка.ДокументОснование) Тогда
		Если ТипЗнч(Шапка.ДокументОснование) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
			 ОбластьМакета.Параметры.ПоставщикПредставление = Шапка.Поставщик.НаименованиеПолное;
		КонецЕсли;
	КонецЕсли;	
	// Лео Катанович}
	ОбластьМакета.Параметры.НомерДокумента = ОбщегоНазначения.ПолучитьНомерНаПечать(ДоверенностьСсылка);
	ОбластьМакета.Параметры.ФИОДоверенного = ДолжностьДоверенного + " " + ОбщегоНазначения.ФамилияИнициалыФизЛица(ФамилияИмяОтчествоДоверенного);
	ТабДокумент.Вывести(ОбластьМакета);
	НазваниеФормы = "Типовая межотраслевая форма № М-2";
	
	Руководители = РегламентированнаяОтчетность.ОтветственныеЛицаОрганизаций(Шапка.Руководители, Шапка.ДатаДокумента);
	Руководитель = Руководители.Руководитель;
	Бухгалтер    = Руководители.ГлавныйБухгалтер;

	ПаспортФизЛица        = ОбщегоНазначения.ПаспортныеДанные(Шапка.ФизЛицо, Шапка.ДатаДокумента);
	СведенияОбОрганизации = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Шапка.Организация, Шапка.ДатаДокумента,, Шапка.БанковскийСчет);

	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(Шапка);
	ОбластьМакета.Параметры.НомерДокумента                = ОбщегоНазначения.ПолучитьНомерНаПечать(ДоверенностьСсылка);
	ОбластьМакета.Параметры.НазваниеФормы                 = НазваниеФормы;
	ОбластьМакета.Параметры.ОрганизацияПредставление      = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование") + Символы.ПС + ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОбОрганизации, "ИНН,КПП,ЮридическийАдрес,Телефоны,");
	ОбластьМакета.Параметры.РеквизитыСчета                = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОбОрганизации, "НомерСчета,Банк,БИК,КоррСчет,");
	ОбластьМакета.Параметры.РеквизитыПотребителя          = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование,ИНН,КПП,ЮридическийАдрес,Телефоны,");
	ОбластьМакета.Параметры.ОрганизацияКодПоОКПО          = СведенияОбОрганизации.КодПоОКПО;
	ОбластьМакета.Параметры.ДолжностьДоверенного          = ДолжностьДоверенного;
	ОбластьМакета.Параметры.ФамилияИмяОтчествоДоверенного = ФамилияИмяОтчествоДоверенного;
	ОбластьМакета.Параметры.ПаспортСерия                  = ПаспортФизЛица.Серия;
	ОбластьМакета.Параметры.ПаспортНомер                  = ПаспортФизЛица.Номер;
	ОбластьМакета.Параметры.ПаспортВыдан                  = ПаспортФизЛица.Выдан;
	ОбластьМакета.Параметры.ПаспортДатаВыдачи             = ПаспортФизЛица.ДатаВыдачи;
	ОбластьМакета.Параметры.ДатаДокумента                 = Формат(Шапка.ДатаДокумента,"ДФ = ""дд ММММ гггг """"г.""");                      
	ОбластьМакета.Параметры.СрокДействия                  = Формат(Шапка.СрокДействия,"ДФ = ""дд ММММ гггг """"г.""");
	
	Если ДоверенностьСсылка.абс_ВидДоверенности=Перечисления.абс_ВидыДоверенностей.СдачаТМЦ Тогда
		ОбластьМакета.Параметры.НазначениеДоверенности    = "на сдачу";
	Иначе
		ОбластьМакета.Параметры.НазначениеДоверенности    = "на получение от";
	КонецЕсли;
	
		// {Лео Катанович
	Если ЗначениеЗаполнено(Шапка.ДокументОснование) Тогда
		Если ТипЗнч(Шапка.ДокументОснование) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
			 ОбластьМакета.Параметры.ПоставщикПредставление = Шапка.Поставщик.НаименованиеПолное;
		     ОбластьМакета.Параметры.РеквизитыДокументаНаПолучение = "";	 
		КонецЕсли;
	КонецЕсли;	
	// Лео Катанович}

	
	ТабДокумент.Вывести(ОбластьМакета);

	ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ТабДокумент.Вывести(ОбластьМакета);
	
	Если ДоверенностьСсылка.абс_ВыводитьТабличнуюЧастьСтрокой Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("Строка");
		ОбластьМакета.Параметры.Номер="1";
		Если ДоверенностьСсылка.абс_ВидДоверенности=Перечисления.абс_ВидыДоверенностей.СдачаТМЦ Тогда
			ОбластьМакета.Параметры.ЦеннностиПредставление="Сдача груза товаров и услуг";
		Иначе
			Дополнение = ДоверенностьСсылка.ПоДокументу;
			// {Лео Катанович
			Если ЗначениеЗаполнено(Шапка.ДокументОснование) Тогда
				Если ТипЗнч(Шапка.ДокументОснование) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
					Дополнение = ""; 
				КонецЕсли;
			КонецЕсли;	
			// Лео Катанович}

			ОбластьМакета.Параметры.ЦеннностиПредставление="ТМЦ согласно товарной накладной " + Дополнение;
		КонецЕсли;
		ОбластьМакета.Параметры.ЕдиницаИзмеренияПредставление="";
		ОбластьМакета.Параметры.КоличествоПрописью="";
		ТабДокумент.Вывести(ОбластьМакета)
	Иначе
		Пока ВыборкаСтрокТовары.Следующий() Цикл
			ОбластьМакета = Макет.ПолучитьОбласть("Строка");
			ОбластьМакета.Параметры.Заполнить(ВыборкаСтрокТовары);
			ОбластьМакета.Параметры.КоличествоПрописью = ?(ВыборкаСтрокТовары.Количество = 0, "",
			Строка(ВыборкаСтрокТовары.Количество) + " (" + 
			ФормированиеПечатныхФормСервер.КоличествоПрописью(ВыборкаСтрокТовары.Количество) + ")");
			ТабДокумент.Вывести(ОбластьМакета);
		КонецЦикла;
	КонецЕсли;	

	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.Заполнить(Шапка);
	
	//<< Горбунов, по просьбе Марчук Д.С. добавил в макет людей, имеющих право подписи.
	Если Шапка.Организация = Справочники.Организации.НайтиПоКоду("О00000001") Тогда//организация Леотон
		ОбластьМакета.Параметры.ПриказФамилия01 = "По приказу №8-п от 01.07.12 Петрушин Д.Н.";
		ОбластьМакета.Параметры.ПриказФамилия02 = "По приказу №8-п от 01.07.12 Глухова С.Л.";
	ИначеЕсли Шапка.Организация = Справочники.Организации.НайтиПоКоду("О00000003") Тогда//организация Леовит
		ОбластьМакета.Параметры.ПриказФамилия01 = "По приказу №11-п от 05.02.14 Рангелова С.С.";
	//	ОбластьМакета.Параметры.ПриказФамилия02 = "По приказу №11-п от 05.02.14 Марчук Д.С.";
		ОбластьМакета.Параметры.ПриказФамилия01 = "По приказу №96-п от 08.12.21 Петрушин Д.Н.";
		ОбластьМакета.Параметры.ПриказФамилия02 = "";
		
	КонецЕсли;
	//>> Горбунов

	Если ЗначениеЗаполнено(Руководитель) Тогда
		ОбластьМакета.Параметры.ФИОРуководителя       = Руководитель;
		ОбластьМакета.Параметры.Руководитель          = Руководитель;
	КонецЕсли;

	Если ЗначениеЗаполнено(Бухгалтер) Тогда
		ОбластьМакета.Параметры.ФИОГлавногоБухгалтера = Бухгалтер;
		ОбластьМакета.Параметры.ГлавныйБухгалтер      = Бухгалтер;
	КонецЕсли;

	ТабДокумент.Вывести(ОбластьМакета);

	Возврат ТабДокумент;

КонецФункции // ПечатьДоверенности()
//абс вставка 38913 конец
