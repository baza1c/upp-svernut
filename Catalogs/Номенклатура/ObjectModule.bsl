Перем мСписокВозможныхРеквизитов Экспорт;
Перем мРеквизитыКонтрольУникальности Экспорт;
Перем ПрошлыйИзмененныйРодительОбъектаДоступа;

Перем мЭтоНеНовый Экспорт;

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если мЭтоНеНовый = Неопределено Тогда
		мЭтоНеНовый = Не ЭтоНовый();
	КонецЕсли;
	
	Если НЕ ЭтоГруппа Тогда
		//Для элемента требуется проверять заполнение реквизита ВидНоменклатуры
		ПроверяемыеРеквизиты.Добавить("ВидНоменклатуры");
	КонецЕсли;
	
	Если (ВидНоменклатуры.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга) Тогда
		
		// Базовая единица может быть не заполнена
		НомерУдаляемогоЭлемента = ПроверяемыеРеквизиты.Найти("БазоваяЕдиницаИзмерения");
		Если НомерУдаляемогоЭлемента <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(НомерУдаляемогоЭлемента);
		КонецЕсли;
		
	КонецЕсли;
	
	// Надо проверить владельца единицы хранения остатков.
	Если ЗначениеЗаполнено(ЕдиницаХраненияОстатков)
		И ЕдиницаХраненияОстатков.Владелец <> Ссылка Тогда
		ТекстСообщения = НСтр("ru = 'Неверно указан владелец единицы хранения остатков!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ЕдиницаХраненияОстатков",, Отказ);
	КонецЕсли;
	
	// Надо проверить владельца единицы для отчетов.
	Если ЗначениеЗаполнено(ЕдиницаДляОтчетов)
		И ЕдиницаДляОтчетов.Владелец <> Ссылка Тогда
		ТекстСообщения = НСтр("ru = 'Неверно указан владелец единицы для отчетов!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ЕдиницаДляОтчетов",, Отказ);
	КонецЕсли;
	
	Если мЭтоНеНовый И НЕ ЭтоГруппа Тогда
		СуществуютСсылки = Неопределено;
		
		Если (ВидНоменклатуры.ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.Услуга)
			И Ссылка.ЕдиницаХраненияОстатков <> ЕдиницаХраненияОстатков 
			И ПолныеПрава.Номенклатура_СуществуютСсылки(Ссылка, СуществуютСсылки) Тогда
			
			ТекстСообщения = НСтр("ru = 'Номенклатура уже участвует в товародвижении. 
			|Изменить единицу хранения остатков уже нельзя!'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ЕдиницаХраненияОстатков",, Отказ);
		КонецЕсли;
		
		Если ВидНоменклатуры.ТипНоменклатуры <> Ссылка.ВидНоменклатуры.ТипНоменклатуры
			И ПолныеПрава.Номенклатура_СуществуютСсылки(Ссылка, СуществуютСсылки) Тогда
			
			ТекстСообщения = НСтр("ru = 'Номенклатура уже участвует в товародвижении. 
			|Тип номенклатуры не может быть изменен'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ВидНоменклатуры",, Отказ);
		КонецЕсли;
		
		Если Не ВестиУчетПоСериям И Ссылка.ВестиУчетПоСериям И ПолныеПрава.Номенклатура_СуществуютСсылкиНаСерииВРегистрахНакопления(Ссылка) Тогда
			ТекстСообщения = НСтр("ru = 'Номенклатура уже участвует в товародвижении. 
			|Признак ""Вести учет по сериям"" не может быть изменен!'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ВестиУчетПоСериям",, Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
	
КонецПроцедуры

// Обработчик события элемента ПриКопировании.
//
Процедура ПриКопировании(ОбъектКопирования)
	
	Если Не ЭтоГруппа Тогда
		ЕдиницаХраненияОстатков = Неопределено;
		ЕдиницаДляОтчетов       = Неопределено;
		ЕдиницаИзмеренияМест    = Неопределено;
		ОсновноеИзображение     = Неопределено;
		НазначениеИспользования = Неопределено;
		лео_ИмяСпец             = Неопределено;
		Лео_ОКК_ТУ              = Неопределено;
		Лео_Аллерген	        = Неопределено;
		Лео_АдресХранения       = Неопределено;
	КонецЕсли;
	
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда
		ПрошлыйИзмененныйРодительОбъектаДоступа = ?(Не ЭтоНовый() и Не Ссылка.Родитель = Родитель, Ссылка.Родитель, Неопределено);
		НастройкаПравДоступа.ПередЗаписьюНовогоОбъектаСПравамиДоступаПользователей(ЭтотОбъект, Отказ, Родитель);
		
	Иначе
		
		Если НЕ ЗначениеЗаполнено(ЕдиницаДляОтчетов) Тогда
			ЕдиницаДляОтчетов = ЕдиницаХраненияОстатков;
		КонецЕсли;
		
		Услуга   = (ВидНоменклатуры.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга);
		Набор    = (ВидНоменклатуры.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Набор);
		Комплект = (ВидНоменклатуры.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Комплект);
		
		Если Услуга ИЛИ Набор Тогда
			ВестиУчетПоХарактеристикам = Ложь;
		КонецЕсли;
		
		Если ВидНоменклатуры.ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.Товар Тогда
			
			ВестиУчетПоСериям 				 = Ложь;
			ВестиПартионныйУчетПоСериям 	 = Ложь;
			Весовой 						 = Ложь;
			ВестиОперативныйУчетОстатковНЗП  = Ложь;
			ВестиУчетПоСериямВНЗП 			 = Ложь;
			ВестиСерийныеНомера 			 = Ложь;
			
			НомерГТД                         = Неопределено;
			СтранаПроисхождения              = Неопределено;
			НазначениеИспользования          = Неопределено;
			ПорядокПрисвоенияСерийногоНомера = Неопределено;
			НаправлениеВыпуска               = Неопределено;
			
		КонецЕсли;
		
	КонецЕсли;
	
	//<< Горбунов Добавил исключение группы
	Если Не ЭтоГруппа Тогда
		//АБС 26.05.2014 44000
		Если ТребуетсяВнешняяСертификация и Не ЭтоНовый()  Тогда
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ абс_ТоварыСертификатовСрезПоследних.Регистратор.ВидСертификата) КАК КоличествоВидовСертификата
			|ИЗ
			|	РегистрСведений.абс_ТоварыСертификатов.СрезПоследних(
			|			&ДатаОстатков,
			|			Номенклатура = &Номенклатура
			|				И Регистратор.ВидСертификата В (&СписокВидыСертификатов)
			|				И (Регистратор.ДатаОкончанияДействияСертификата = ДАТАВРЕМЯ(1, 1, 1)
			|					ИЛИ Регистратор.ДатаОкончанияДействияСертификата >= &ДатаОстатков)
			|				И НЕ Регистратор.Неактуальный) КАК абс_ТоварыСертификатовСрезПоследних";
			
			
			СписокВидыСертификатов = Новый СписокЗначений;
			СписокВидыСертификатов.Добавить(Перечисления.абс_ВидыСертификатовНаТовары.ДекларацияСоответствия);
			СписокВидыСертификатов.Добавить(Перечисления.абс_ВидыСертификатовНаТовары.ДекларацияТаможенногоСоюза);
			СписокВидыСертификатов.Добавить(Перечисления.абс_ВидыСертификатовНаТовары.СвидетельствоОГосрегистрации);
			СписокВидыСертификатов.Добавить(Перечисления.абс_ВидыСертификатовНаТовары.СертификатСоответствия);
			Запрос.УстановитьПараметр("СписокВидыСертификатов", СписокВидыСертификатов);
			Запрос.УстановитьПараметр("Номенклатура", Ссылка);
			Запрос.УстановитьПараметр("ДатаОстатков", абс_СерверныеФункции.ПолучитьДатуСервера());
			
			Результат = Запрос.Выполнить();
			Выборка = Результат.Выбрать();
			
			Если Выборка.Следующий() Тогда
				Если Выборка.КоличествоВидовСертификата < 4 Тогда
					Сообщить("По данной номенклатуре отсутствует полный комплект сертификатов:
					|""Свидетельство соответствия"", ""Декларация о соответствии"", ""Декларация таможенного союза"" и ""Свидетельство о госрегистрации"".");
					
					Отказ = Истина;
				КонецЕсли;
			Иначе
				Сообщить("По данной номенклатуре отсутствует полный комплект сертификатов:
				|""Свидетельство соответствия"", ""Декларация о соответствии"", ""Декларация таможенного союза"" и ""Свидетельство о госрегистрации"".");
				
				Отказ = Истина;
			КонецЕсли;
			
			
		КонецЕсли;
		//АБС 26.05.2014 44000	
	КонецЕсли;
	//<< Горбунов Добавил исключение группы
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ ОбменДанными.Загрузка Тогда
		НастройкаПравДоступа.ОбновитьПраваДоступаКИерархическимОбъектамПриНеобходимости(Ссылка,ПрошлыйИзмененныйРодительОбъектаДоступа, Отказ);
	КонецЕсли;	
КонецПроцедуры
