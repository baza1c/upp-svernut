Перем мОснование;

// Обработчик события ПриКопировании
//
Процедура ПриКопировании(ОбъектКопирования)

	Если НЕ ЭтотОбъект.ЭтоГруппа Тогда
		ЭтотОбъект.ОсновнойДоговорКонтрагента = Неопределено;
		ЭтотОбъект.ОсновнойБанковскийСчет     = Неопределено;
		//( + Леовит роман 23.03.15
		ЭтотОбъект.абс_ОсновнойСпособДоставки     = Неопределено;
		//+)
		Если ОбъектКопирования.ГоловнойКонтрагент = ОбъектКопирования.Ссылка Тогда
			ЭтотОбъект.ГоловнойКонтрагент     = Неопределено;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Функция возвращает результат запроса по справочнику контрагентов с заданным головным контрагентом
//
// Параметры:
//  ГоловнойКонтрагент - заданный головной контрагент
//
// Возвращаемое значение:
//  Результат - результат работы запроса
// 
Функция ПолучитьКонтрагентовПоЗаданномуГоловномуКонтрагенту(ГоловнойКонтрагент) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ГоловнойКонтрагент", ГоловнойКонтрагент);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Контрагенты.Ссылка КАК Контрагент,
	|	Контрагенты.ОбособленноеПодразделение КАК ОбособленноеПодразделение
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.ГоловнойКонтрагент = &ГоловнойКонтрагент
	|	И Контрагенты.ГоловнойКонтрагент <> Контрагенты.Ссылка
	|	И НЕ Контрагенты.ПометкаУдаления
	|	И НЕ Контрагенты.ЭтоГруппа
	|
	|УПОРЯДОЧИТЬ ПО
	|	Контрагент";
	
	Результат = Запрос.Выполнить();
	
	Возврат Результат;
	
КонецФункции // ПолучитьКонтрагентовПоЗаданномуГоловномуКонтрагенту()

// Процедура - обработчик события "ОбработкаЗаполнения".
//
Процедура ОбработкаЗаполнения(Основание)

	Если ТипЗнч(Основание) = Тип("СправочникСсылка.Организации") Тогда
		
		Наименование           = Основание.Наименование;
		ЮрФизЛицо              = Основание.ЮрФизЛицо;
		НаименованиеПолное     = Основание.НаименованиеПолное;
		ОсновнойБанковскийСчет = Основание.ОсновнойБанковскийСчет;
		ИНН                    = Основание.ИНН;
		КПП                    = Основание.КПП;
		КодПоОКПО              = Основание.КодПоОКПО;
		мОснование             = Основание;
		Если НЕ Основание.ГоловнаяОрганизация.Пустая() Тогда
			ОбособленноеПодразделение = Истина;	
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
		
	// Проверим основной вид деятельности контрагента
	Если ЗначениеЗаполнено(ОсновнойВидДеятельности) И ВидыДеятельности.Найти(ОсновнойВидДеятельности, "ВидДеятельности") = Неопределено Тогда
		ОсновнойВидДеятельности = Справочники.ВидыДеятельностиКонтрагентов.ПустаяСсылка();
	КонецЕсли;
	
	// установим головного контрагента если он не заполнен
	Если НЕ ЭтоГруппа И НЕ ЗначениеЗаполнено(ГоловнойКонтрагент) И НЕ ОбособленноеПодразделение Тогда
		ГоловнойКонтрагент = Ссылка;
	КонецЕсли;
	
	//АБС ВСТАВКА   18.03.2014 10:53:01  Балашенко
	
	//заполнение основного способа доставки
	Если Не ЭтоГруппа И Не ЭтоНовый() Тогда
		абс_Дистрибуция.ПроверитьОсновныеРеквизитыКонтрагента(ЭтотОбъект, Отказ);
	КонецЕсли;
	
	//АБС ВСТАВКА  КОНЕЦ
	
	//АБС ВСТАВКА   06.05.2014 11:49:07  Балашенко
	Если Не ЭтоГруппа Тогда
		абс_Город 			= ПолучитьБизнесРегионПоУровню(3);
		абс_Территория 		= ПолучитьБизнесРегионПоУровню(2);
		абс_БизнесРегион 	= ПолучитьБизнесРегионПоУровню(1);
		абс_Отдел 			= ПолучитьБизнесРегионПоУровню(0);
	КонецЕсли;
	//АБС ВСТАВКА  КОНЕЦ
	
	//АБС ВСТАВКА   15.06.2014 21:08:57  Фролов
	Если Не ЭтоГруппа Тогда
		абс_ЗаполнитьМенеджеровПоБизнесРегиону();
	КонецЕсли;
	//АБС ВСТАВКА  КОНЕЦ 
	
	//АБС ВСТАВКА 18.06.2014 Фролов
	Если НЕ ЭтоГруппа Тогда
		абс_ЗаполнитьОсновнойАдресКонтрагента();
	КонецЕсли;
	//АБС ВСТАВКА  КОНЕЦ  
	
	#Если Клиент Тогда  
		Если (ЗначениеЗаполнено(ИНН) и НеЯвляетсяРезидентом) ИЛИ (НЕ ЗначениеЗаполнено(ИНН) и НЕ НеЯвляетсяРезидентом) Тогда
			Предупреждение("Не корректное заполнение Контрагента, небходимо либо заполнить ИНН контрагента, либо установить галочку НЕ РЕЗИДЕНТ",20);
			Отказ	=	Истина;
		КонецЕсли;
	#КонецЕсли
КонецПроцедуры

//АБС ВСТАВКА   06.05.2014 11:49:07  Балашенко
Функция ПолучитьБизнесРегионПоУровню(Уровень) Экспорт
	
	Если Регион.Пустая() Тогда
		Возврат Справочники.Регионы.ПустаяСсылка();
	КонецЕсли;
	ТекущийУровень = Регион.Уровень();
	Если ТекущийУровень<Уровень Тогда
		Возврат Справочники.Регионы.ПустаяСсылка();
	КонецЕсли;
	БизнесРегион = Регион;
	Сч = ТекущийУровень - Уровень;
	Пока Сч>0 Цикл
		БизнесРегион = БизнесРегион.Родитель;
		Сч = Сч - 1;
	КонецЦикла;
	
	Возврат БизнесРегион;
	
КонецФункции
//АБС ВСТАВКА  КОНЕЦ

//АБС ВСТАВКА   15.06.2014 21:01:22  Фролов
Процедура абс_ЗаполнитьМенеджеровПоБизнесРегиону() Экспорт
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("абс_РучнойВвод", Ложь);
	
	МассивСтрок = ЭтотОбъект.МенеджерыПокупателя.НайтиСтроки(СтруктураОтбора);
	
	Для Каждого СтрокаТЧ Из МассивСтрок Цикл
		ЭтотОбъект.МенеджерыПокупателя.Удалить(СтрокаТЧ);
	КонецЦикла;
	
	//ЭтотОбъект.МенеджерыПокупателя.Очистить();
	
	ЭтотОбъект.МенеджерыПокупателя.Добавить().МенеджерПокупателя = ЭтотОбъект.абс_БизнесРегион.абс_Менеджер;
	
	ЭтотОбъект.ОсновнойМенеджерПокупателя = ЭтотОбъект.абс_БизнесРегион.абс_Менеджер;
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		
		НаборСвойства = РегистрыСведений.ЗначенияСвойствОбъектов.СоздатьНаборЗаписей();
		
		НаборСвойства.Отбор.Объект.Установить(Ссылка);
		НаборСвойства.Отбор.Свойство.Установить(ПланыВидовХарактеристик.СвойстваОбъектов.абс_ВторойМенеджер);
		
		НаборСвойства.Прочитать();
		
		НаборСвойства.Очистить();
		
		ЗаписьСвойство = НаборСвойства.Добавить();
		
		ЗаписьСвойство.Объект 			= Ссылка;
		ЗаписьСвойство.Свойство         = ПланыВидовХарактеристик.СвойстваОбъектов.абс_ВторойМенеджер;
		ЗаписьСвойство.Значение			= ЭтотОбъект.абс_Территория.абс_Менеджер;
		
		НаборСвойства.Записать();
		
	КонецЕсли;
	
КонецПроцедуры
//АБС ВСТАВКА  КОНЕЦ 

//АБС ВСТАВКА 18.06.2014 Фролов
Процедура абс_ЗаполнитьОсновнойАдресКонтрагента() Экспорт
	
	Если ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КонтактнаяИнформация.Представление КАК абс_ОсновнойАдресКонтрагента
	|ИЗ
	|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Объект = &Объект
	|	И КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес)
	|	И КонтактнаяИнформация.ЗначениеПоУмолчанию");
	
	Запрос.УстановитьПараметр("Объект", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		абс_ОсновнойАдресКонтрагента = Выборка.абс_ОсновнойАдресКонтрагента;
		
	КонецЕсли;	
	
КонецПроцедуры
//АБС ВСТАВКА КОНЕЦ

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
		
	Если ЗначениеЗаполнено(ГоловнойКонтрагент) И ГоловнойКонтрагент <> Ссылка Тогда
		
		Если ЗначениеЗаполнено(ГоловнойКонтрагент.ГоловнойКонтрагент) И ГоловнойКонтрагент.ГоловнойКонтрагент <> ГоловнойКонтрагент Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Контрагент "+СокрЛП(ГоловнойКонтрагент)+" не может быть выбран головным, 
							|так как для него уже был назначен головной контрагент "+СокрЛП(ГоловнойКонтрагент.ГоловнойКонтрагент)+"!");
			Отказ = Истина;
			Возврат;
		Иначе
			
			// надо проверить, что если указываем головного контрагента, то этот элемент уже не был установлен
			// в качестве головного у другого контрагента.
			ВыборкаПоГоловномуКонтрагенту = ПолучитьКонтрагентовПоЗаданномуГоловномуКонтрагенту(Ссылка).Выбрать();
			Если ВыборкаПоГоловномуКонтрагенту.Количество() <> 0 Тогда
				
				СообщениеОНевозможностиЗаписи = "Контрагент "+СокрЛП(ЭтотОбъект)+" не может иметь головного контрагента!
												|Этот контрагент уже установлен головным для: ";
				Пока ВыборкаПоГоловномуКонтрагенту.Следующий() Цикл
					СообщениеОНевозможностиЗаписи = СообщениеОНевозможностиЗаписи + Символы.ПС + СокрЛП(ВыборкаПоГоловномуКонтрагенту.Контрагент);
				КонецЦикла;
				
				ОбщегоНазначения.СообщитьОбОшибке(СообщениеОНевозможностиЗаписи);
				Отказ = Истина;
				Возврат;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// проверим, что контрагент - физ. лицо уже не был установлен головным контрагентом для обособленных подразделений
	Если НЕ ЭтоНовый() И ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
		
		ЕстьОбособленныеПодразделения = Ложь;
		СообщениеОНевозможностиЗаписи = "Контрагент " + СокрЛП(ЭтотОбъект) + " не может быть физическим лицом!
			|Этот контрагент уже установлен головным для: ";
			
		ВыборкаПоГоловномуКонтрагенту = ПолучитьКонтрагентовПоЗаданномуГоловномуКонтрагенту(Ссылка).Выбрать();			
		Если ВыборкаПоГоловномуКонтрагенту.Количество() <> 0 Тогда
			Пока ВыборкаПоГоловномуКонтрагенту.Следующий() Цикл
				Если ВыборкаПоГоловномуКонтрагенту.ОбособленноеПодразделение Тогда
					ЕстьОбособленныеПодразделения = Истина;
					СообщениеОНевозможностиЗаписи = СообщениеОНевозможностиЗаписи + Символы.ПС + СокрЛП(ВыборкаПоГоловномуКонтрагенту.Контрагент);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;	
		
		Если ЕстьОбособленныеПодразделения Тогда
			ОбщегоНазначения.СообщитьОбОшибке(СообщениеОНевозможностиЗаписи, Отказ);
			Возврат;
		КонецЕсли;
		
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(мОснование) Тогда
		НаборЗаписей = РегистрыСведений.СобственныеКонтрагенты.СоздатьНаборЗаписей();
		ЗаписьРегистра = НаборЗаписей.Добавить();
		ЗаписьРегистра.Контрагент = Ссылка;
		ЗаписьРегистра.ВидСвязи   = Перечисления.ВидыСобственныхКонтрагентов.Организация;
		ЗаписьРегистра.Объект     = мОснование;
		НаборЗаписей.Записать(Ложь);
		мОснование = "";
	КонецЕсли;
	
	// при изменении ИНН перепишем ИНН у обособленных подразделений контрагента
	Если НЕ ЭтоНовый() И НЕ ОбособленноеПодразделение И ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
		
		ВыборкаПоГоловномуКонтрагенту = ПолучитьКонтрагентовПоЗаданномуГоловномуКонтрагенту(Ссылка).Выбрать();
		Если ВыборкаПоГоловномуКонтрагенту.Количество() <> 0 Тогда
			
			Пока ВыборкаПоГоловномуКонтрагенту.Следующий() Цикл
				
				Если ИНН = ВыборкаПоГоловномуКонтрагенту.Контрагент.ИНН
					ИЛИ НЕ ВыборкаПоГоловномуКонтрагенту.ОбособленноеПодразделение Тогда
					Продолжить;
				КонецЕсли;
				
				КонтрагентДляИзменения = ВыборкаПоГоловномуКонтрагенту.Контрагент.ПолучитьОбъект();
				КонтрагентДляИзменения.ИНН = ИНН;
				Попытка
					КонтрагентДляИзменения.Записать();
				Исключение
					ОписаниеОшибки = "Ошибка при записи обособленных подразделений контрагента.
					|Не удалось изменить ИНН у обособленного подразделения " + КонтрагентДляИзменения.Наименование;
					ОбщегоНазначения.СообщитьОбОшибке(ОписаниеОшибки);
				КонецПопытки;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

