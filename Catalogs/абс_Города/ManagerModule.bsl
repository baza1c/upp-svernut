Процедура ЗаполнитьПоКлассификаторуАдресов() Экспорт

	Запрос = Новый Запрос( 
	"ВЫБРАТЬ
	|	АдресныйКлассификатор.КодРегионаВКоде КАК КодРегиона,
	|	АдресныйКлассификатор.Наименование КАК НаименованиеРегиона,
	|	АдресныйКлассификатор.КодРегионаВКоде КАК абс_КодРегионаВКоде,
	|	Регионы.Ссылка КАК РегионСсылка
	|ПОМЕСТИТЬ ВТ_Регионы
	|ИЗ
	|	РегистрСведений.АдресныйКлассификатор КАК АдресныйКлассификатор
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.абс_Регионы КАК Регионы
	|		ПО АдресныйКлассификатор.КодРегионаВКоде = Регионы.абс_КодРегионаВКоде
	|ГДЕ
	|	АдресныйКлассификатор.КодРайонаВКоде = 0
	|	И АдресныйКлассификатор.КодГородаВКоде = 0
	|	И АдресныйКлассификатор.КодНаселенногоПунктаВКоде = 0
	|	И АдресныйКлассификатор.КодУлицыВКоде = 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АдресныйКлассификатор.КодГородаВКоде,
	|	АдресныйКлассификатор.Наименование,
	|	АдресныйКлассификатор.Сокращение,
	|	АдресныйКлассификатор.Наименование + "" "" + АдресныйКлассификатор.Сокращение КАК НаименованиеПолное,
	|	ВТ_Регионы.КодРегиона,
	|	ВТ_Регионы.НаименованиеРегиона,
	|	ВТ_Регионы.абс_КодРегионаВКоде,
	|	ВТ_Регионы.РегионСсылка
	|ПОМЕСТИТЬ ВТ_КладрРегионы
	|ИЗ
	|	РегистрСведений.АдресныйКлассификатор КАК АдресныйКлассификатор
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Регионы КАК ВТ_Регионы
	|		ПО АдресныйКлассификатор.КодРегионаВКоде = ВТ_Регионы.абс_КодРегионаВКоде
	|ГДЕ
	|	АдресныйКлассификатор.КодУлицыВКоде = 0
	|	И АдресныйКлассификатор.КодГородаВКоде <> 0
	|	И АдресныйКлассификатор.Сокращение = ""г""
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_КладрРегионы.КодГородаВКоде,
	|	ВТ_КладрРегионы.Наименование,
	|	ВТ_КладрРегионы.Сокращение,
	|	ВТ_КладрРегионы.НаименованиеПолное,
	|	ВТ_КладрРегионы.КодРегиона,
	|	ВТ_КладрРегионы.НаименованиеРегиона,
	|	ВТ_КладрРегионы.абс_КодРегионаВКоде,
	|	ВТ_КладрРегионы.РегионСсылка,
	|	абс_Города.Ссылка КАК ГородСсылка
	|ИЗ
	|	ВТ_КладрРегионы КАК ВТ_КладрРегионы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.абс_Города КАК абс_Города
	|		ПО ВТ_КладрРегионы.РегионСсылка = абс_Города.Владелец
	|			И ВТ_КладрРегионы.КодГородаВКоде = абс_Города.КодГородаВКоде");
	
	ТЗГородов = Запрос.Выполнить().Выгрузить();
	
	СоответствиеРегионов = Новый Соответствие();
	
	Для Каждого СтрПоГороду Из ТЗГородов Цикл
		
		Если НЕ ЗначениеЗаполнено(СтрПоГороду.РегионСсылка) Тогда
			
			СтрПоГороду.РегионСсылка = СоответствиеРегионов.Получить(СтрПоГороду.абс_КодРегионаВКоде);
			
			Если НЕ ЗначениеЗаполнено(СтрПоГороду.РегионСсылка) Тогда
				СтрПоГороду.РегионСсылка = Справочники.абс_Регионы.СоздатьОбновитьРегион(Новый Структура("КодРегиона,Наименование,абс_КодРегионаВКоде,РегионСсылка", СтрПоГороду.КодРегиона, СтрПоГороду.НаименованиеРегиона, СтрПоГороду.абс_КодРегионаВКоде, СтрПоГороду.РегионСсылка));
				
				СоответствиеРегионов.Вставить(СтрПоГороду.абс_КодРегионаВКоде, СтрПоГороду.РегионСсылка);
			КонецЕсли;
		КонецЕсли;
		
		СоздатьОбновитьГород(СтрПоГороду);
		
	КонецЦикла;
	
КонецПроцедуры

Функция СоздатьОбновитьГород(ДанныеПоГороду) Экспорт
	
	ГородОбъект = Неопределено;
	
	Если ЗначениеЗаполнено(ДанныеПоГороду.ГородСсылка) Тогда		
		ГородОбъект = ДанныеПоГороду.ГородСсылка.ПолучитьОбъект();
	Иначе
		ГородОбъект = Справочники.абс_Города.СоздатьЭлемент();
	КонецЕсли;
		
	ЗаполнитьЗначенияСвойств(ГородОбъект, ДанныеПоГороду);	
	
	ГородОбъект.Владелец = ДанныеПоГороду.РегионСсылка;
	
	ГородОбъект.ОбменДанными = Истина;
	
	ГородОбъект.Записать();
	
	ОбщегоНазначения.СообщитьИнформациюПользователю("Город """ + ДанныеПоГороду.Наименование + """ " + ?(ЗначениеЗаполнено(ДанныеПоГороду.ГородСсылка), "обновлен", "создан") + "!");
	
	Возврат ГородОбъект.Ссылка;		
	
КонецФункции	