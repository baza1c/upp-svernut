Перем ТабИнтервалы;

#Если Клиент Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ НАЧАЛЬНОЙ НАСТРОЙКИ ОТЧЕТА

Процедура УстановитьТекстЗапроса()
	
	КолИнтервалов = ТабИнтервалы.Количество();
	
	ТекстПоказателей = "";
	Для сч = 1 по КолИнтервалов Цикл
		ТекстПоказателей = ТекстПоказателей +
		",
		|	СтопЛист"+Сч+".СуммаОбщейЗадолженности КАК СуммаОбщейЗадолженности"+Сч+",
		|	СтопЛист"+Сч+".СуммаПросроченнойЗадолженности КАК СуммаПросроченнойЗадолженности"+Сч+",
		|	ВЫБОР
		|		КОГДА СтопЛист"+Сч+".СуммаОбщейЗадолженности = 0
		|			ТОГДА 0
		|		ИНАЧЕ СтопЛист"+Сч+".СуммаПросроченнойЗадолженности / СтопЛист"+Сч+".СуммаОбщейЗадолженности * 100
		|	КОНЕЦ КАК ПроцентПросроченнойЗадолженности"+Сч+",
		|	СтопЛист"+Сч+".КоличествоДнейПросроченнойЗадолженности КАК КоличествоДнейПросроченнойЗадолженности"+Сч;
	КонецЦикла;
	
	ТекстПсевдонимов = "";
	Для сч = 1 по КолИнтервалов Цикл
		ТекстПсевдонимов = ТекстПсевдонимов + 
		",
		|	СуммаОбщейЗадолженности"+Сч+",
		|	СуммаПросроченнойЗадолженности"+Сч+",
		|	ПроцентПросроченнойЗадолженности"+Сч+",
		|	КоличествоДнейПросроченнойЗадолженности"+Сч;
	КонецЦикла;
	
	ТекстСоединения = "";
	Для сч = 1 по КолИнтервалов Цикл
		ТекстСоединения = ТекстСоединения + "
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.абс_СтопЛистПоКонтрагентам.СрезПоследних(&Период"+Сч+", ) КАК СтопЛист"+Сч+"
		|		ПО ТаблицаКонтрагентов.Контрагент = СтопЛист"+Сч+".Контрагент";
	КонецЦикла;
	
	ТекстИтогов = "";
	Для сч = 1 по КолИнтервалов-1 Цикл
		ТекстИтогов = ТекстИтогов + 
		"
		|	СУММА(СуммаОбщейЗадолженности"+Сч+"),
		|	СУММА(СуммаПросроченнойЗадолженности"+Сч+"),
		|	МАКСИМУМ(ПроцентПросроченнойЗадолженности"+Сч+"),
		|	МАКСИМУМ(КоличествоДнейПросроченнойЗадолженности"+Сч+"),";
	КонецЦикла;
	Если КолИнтервалов>0 Тогда
		ТекстИтогов = ТекстИтогов + 
		"
		|	СУММА(СуммаОбщейЗадолженности"+Сч+"),
		|	СУММА(СуммаПросроченнойЗадолженности"+Сч+"),
		|	МАКСИМУМ(ПроцентПросроченнойЗадолженности"+Сч+"),
		|	МАКСИМУМ(КоличествоДнейПросроченнойЗадолженности"+Сч+")";
	КонецЕсли;
	
	ТекстЗапроса =  
    "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	абс_СтопЛистПоКонтрагентамСрезПоследних.Контрагент
	|ПОМЕСТИТЬ ТаблицаКонтрагентов
	|ИЗ
	|	РегистрСведений.абс_СтопЛистПоКонтрагентам.СрезПоследних(&КонецПериода, ) КАК абс_СтопЛистПоКонтрагентамСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаКонтрагентов.Контрагент"
	+ ТекстПоказателей + "
	|	//ПОЛЯ_СВОЙСТВА
	|	//ПОЛЯ_КАТЕГОРИИ
	|{ВЫБРАТЬ
	|	Контрагент.*"
	+ ТекстПсевдонимов + "
	|	//ПСЕВДОНИМЫ_СВОЙСТВА
	|	//ПСЕВДОНИМЫ_КАТЕГОРИИ
	|}
	|ИЗ
	|	ТаблицаКонтрагентов КАК ТаблицаКонтрагентов"
	+ ТекстСоединения + "
	|	//СОЕДИНЕНИЯ
	|{ГДЕ
	|	ТаблицаКонтрагентов.Контрагент.*"
	+ ТекстПоказателей + "
	|	//УСЛОВИЯ_СВОЙСТВА
	|	//УСЛОВИЯ_КАТЕГОРИИ
	|}
	|{УПОРЯДОЧИТЬ ПО
	|	Контрагент.*"
	+ ТекстПсевдонимов + "
	|	//ПСЕВДОНИМЫ_СВОЙСТВА
	|	//ПСЕВДОНИМЫ_КАТЕГОРИИ
	|}
	|ИТОГИ"
	+ ТекстИтогов + "
	|	//ИТОГИ_СВОЙСТВА
	|	//ИТОГИ_КАТЕГОРИИ
	|ПО
	|	ОБЩИЕ
	|{ИТОГИ ПО
	|	Контрагент.*
	|	//ПСЕВДОНИМЫ_СВОЙСТВА
	|	//ПСЕВДОНИМЫ_КАТЕГОРИИ
	|}";
	
	// В универсальном отчете включен флаг использования свойств и категорий.
	Если УниверсальныйОтчет.ИспользоватьСвойстваИКатегории Тогда
		
		// Добавление свойств и категорий поля запроса в таблицу полей.
		// Необходимо вызывать для каждого поля запроса, предоставляющего возможность использования свойств и категорий.
		
		// УниверсальныйОтчет.ДобавитьСвойстваИКатегорииДляПоля(<ПсевдонимТаблицы>.<Поле> , <ПсевдонимПоля>, <Представление>, <Назначение>);
		УниверсальныйОтчет.ДобавитьСвойстваИКатегорииДляПоля("ТаблицаКонтрагентов.Контрагент", "Контрагент", "Контрагент", ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_Контрагенты);

		УниверсальныйОтчет.ДобавитьВТекстЗапросаСвойстваИКатегории(ТекстЗапроса);
		
	КонецЕсли;
		
	// Инициализация текста запроса построителя отчета
	УниверсальныйОтчет.ПостроительОтчета.Текст = ТекстЗапроса;
	
КонецПроцедуры

Процедура УстановитьИнтервалыЗапроса()

	ТабИнтервалы = Новый ТаблицаЗначений;
	ТабИнтервалы.Колонки.Добавить("ИмяИнтервала");
	ТабИнтервалы.Колонки.Добавить("НомерИнтервала");
	ТабИнтервалы.Колонки.Добавить("Период");
	
	КоличествоДней = 0;
	
	Если ЗначениеЗаполнено(УниверсальныйОтчет.ДатаНач) И ЗначениеЗаполнено(УниверсальныйОтчет.ДатаКон) Тогда
		
		НачалоПериода = УниверсальныйОтчет.ДатаНач;
		КонецПериода =УниверсальныйОтчет.ДатаКон;
	
		КоличествоДнейВПериоде = (НачалоДня(КонецПериода) - НачалоПериода)/86400 + 1;
	
		МассивПериодов = Новый Массив;
	
		Для Сч = 0 По КоличествоДнейВПериоде-1 Цикл
			Период = НачалоПериода + Сч * 86400;
			УправлениеПланированием.ВыровнятьДатуПоКонцуПериода(Период, Периодичность);
			Если МассивПериодов.Найти(Период) = Неопределено Тогда
				МассивПериодов.Добавить(Период);
			КонецЕсли;
		КонецЦикла;
		сч=1;
		Для каждого Период из МассивПериодов Цикл
			НоваяСтрока = ТабИнтервалы.Добавить();
			НоваяСтрока.ИмяИнтервала    = Формат(Период,"ДФ = дд.ММ.гггг");
			НоваяСтрока.НомерИнтервала  = сч;
			НоваяСтрока.Период = Период;
			сч = сч+1;
		КонецЦикла;
	КонецЕсли;
	
	ПараметрыПостроителя = УниверсальныйОтчет.ПостроительОтчета.Параметры;
	ПараметрыПостроителя.Вставить("КонецПериода", КонецДня(УниверсальныйОтчет.ДатаКон));
	
	Для каждого  СтрокаИнтервала из ТабИнтервалы Цикл
		ПараметрыПостроителя.Вставить("Период" + СтрокаИнтервала.НомерИнтервала, СтрокаИнтервала.Период);
	КонецЦикла;
	
КонецПроцедуры

// Процедура установки начальных настроек отчета с использованием текста запроса
//
Процедура УстановитьНачальныеНастройки(ДополнительныеПараметры = Неопределено) Экспорт
	
	// Настройка общих параметров универсального отчета
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		
		УправлениеОтчетами.ВосстановитьРеквизитыОтчета(ЭтотОбъект, ДополнительныеПараметры);
		
	КонецЕсли;
	
	// Содержит название отчета, которое будет выводиться в шапке.
	// Тип: Строка.
	// Пример:
	// УниверсальныйОтчет.мНазваниеОтчета = "Название отчета";
	УниверсальныйОтчет.мНазваниеОтчета = СокрЛП(ЭтотОбъект.Метаданные().Синоним);
	
	// Содержит признак необходимости отображения надписи и поля выбора раздела учета в форме настройки.
	// Тип: Булево.
	// Значение по умолчанию: Истина.
	// Пример:
	// УниверсальныйОтчет.мВыбиратьИмяРегистра = Ложь;
	УниверсальныйОтчет.мВыбиратьИмяРегистра = Ложь;
	
	// Содержит имя регистра, по метаданным которого будет выполняться заполнение настроек отчета.
	// Тип: Строка.
	// Пример:
	// УниверсальныйОтчет.ИмяРегистра = "ТоварыНаСкладах";
	
	// Содержит признак необходимости вывода отрицательных значений показателей красным цветом.
	// Тип: Булево.
	// Значение по умолчанию: Ложь.
	// Пример:
	// УниверсальныйОтчет.ОтрицательноеКрасным = Истина;
	
	// Содержит признак необходимости вывода в отчет общих итогов.
	// Тип: Булево.
	// Значение по умолчанию: Истина.
	// Пример:
	// УниверсальныйОтчет.ВыводитьОбщиеИтоги = Ложь;
	
	// Содержит признак необходимости вывода детальных записей в отчет.
	// Тип: Булево.
	// Значение по умолчанию: Ложь.
	// Пример:
	// УниверсальныйОтчет.ВыводитьДетальныеЗаписи = Истина;
	
	// Содержит признак необходимости отображения флага использования свойств и категорий в форме настройки.
	// Тип: Булево.
	// Значение по умолчанию: Истина.
	// Пример:
	// УниверсальныйОтчет.мВыбиратьИспользованиеСвойств = Ложь;
	
	// Содержит признак использования свойств и категорий при заполнении настроек отчета.
	// Тип: Булево.
	// Значение по умолчанию: Ложь.
	// Пример:
	// УниверсальныйОтчет.ИспользоватьСвойстваИКатегории = Истина;
	
	// Содержит признак использования простой формы настроек отчета без группировок колонок.
	// Тип: Булево.
	// Значение по умолчанию: Ложь.
	// Пример:
	// УниверсальныйОтчет.мРежимФормыНастройкиБезГруппировокКолонок = Истина;
	УниверсальныйОтчет.мРежимФормыНастройкиБезГруппировокКолонок = Истина;
	
	// Дополнительные параметры, переданные из отчета, вызвавшего расшифровку.
	// Информация, передаваемая в переменной ДополнительныеПараметры, может быть использована
	// для реализации специфичных для данного отчета параметрических настроек.
	
	// Описание исходного текста запроса.
	
	УниверсальныйОтчет.ДатаНач = ?(ЗначениеЗаполнено(УниверсальныйОтчет.ДатаНач), УниверсальныйОтчет.ДатаНач, НачалоМесяца(РабочаяДата));
	УниверсальныйОтчет.ДатаКон = ?(ЗначениеЗаполнено(УниверсальныйОтчет.ДатаКон), УниверсальныйОтчет.ДатаКон, РабочаяДата);
	
 	УниверсальныйОтчет.Показатели.Строки.Очистить();
	
	УстановитьИнтервалыЗапроса();
	
	УстановитьТекстЗапроса();
	
	// Представления полей отчета.
	// Необходимо вызывать для каждого поля запроса.
	// УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить(<ИмяПоля>, <ПредставлениеПоля>);

	// Представления полей отчета.
	// Необходимо вызывать для каждого поля запроса.
	// УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить(<ИмяПоля>, <ПредставлениеПоля>);
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("Контрагент", "Контрагент");
            
	
	Для каждого СтрокаИнтервала из ТабИнтервалы Цикл
		Ном = СтрокаИнтервала.НомерИнтервала;
		УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("Период" + Ном, СтрокаИнтервала.ИмяИнтервала);
	КонецЦикла;
	
	// Добавление показателей
	// Необходимо вызывать для каждого добавляемого показателя.
	// УниверсальныйОтчет.ДобавитьПоказатель(<ИмяПоказателя>, <ПредставлениеПоказателя>, <ВключенПоУмолчанию>, <Формат>, <ИмяГруппы>, <ПредставлениеГруппы>);

	Для каждого СтрокаИнтервала из ТабИнтервалы Цикл
		Ном = СтрокаИнтервала.НомерИнтервала;
		
		УниверсальныйОтчет.ДобавитьПоказатель("СуммаПросроченнойЗадолженности" + Ном, "Просроченная задолженость", Истина, "ЧЦ=15; ЧДЦ=2", СтрокаИнтервала.ИмяИнтервала, СтрокаИнтервала.ИмяИнтервала);
		УниверсальныйОтчет.ДобавитьПоказатель("КоличествоДнейПросроченнойЗадолженности" + Ном, "Дней просроченной задолженности", Истина, "ЧЦ=0; ЧДЦ=0", СтрокаИнтервала.ИмяИнтервала, СтрокаИнтервала.ИмяИнтервала);
		УниверсальныйОтчет.ДобавитьПоказатель("ПроцентПросроченнойЗадолженности" + Ном, "% просроченной задолженности", Истина, "ЧЦ=15; ЧДЦ=1", СтрокаИнтервала.ИмяИнтервала, СтрокаИнтервала.ИмяИнтервала);
		УниверсальныйОтчет.ДобавитьПоказатель("СуммаОбщейЗадолженности" + Ном, "Общая задолженность", Истина, "ЧЦ=15; ЧДЦ=2", СтрокаИнтервала.ИмяИнтервала, СтрокаИнтервала.ИмяИнтервала);
	КонецЦикла;
	
	// Добавление предопределенных группировок строк отчета.
	// Необходимо вызывать для каждой добавляемой группировки строки.
	// УниверсальныйОтчет.ДобавитьИзмерениеСтроки(<ПутьКДанным>);
	УниверсальныйОтчет.ДобавитьИзмерениеСтроки("Контрагент");
	

	// Добавление предопределенных группировок колонок отчета.
	// Необходимо вызывать для каждой добавляемой группировки колонки.
	// УниверсальныйОтчет.ДобавитьИзмерениеКолонки(<ПутьКДанным>);
	
	// Добавление предопределенных отборов отчета.
	// Необходимо вызывать для каждого добавляемого отбора.
	// УниверсальныйОтчет.ДобавитьОтбор(<ПутьКДанным>);
	УниверсальныйОтчет.ДобавитьОтбор("Контрагент");
	
	// Добавление предопределенных полей порядка отчета.
	// Необходимо вызывать для каждого добавляемого поля порядка.
	// УниверсальныйОтчет.ДобавитьПорядок(<ПутьКДанным>);
	
	// Установка связи подчиненных и родительских полей
	// УниверсальныйОтчет.УстановитьСвязьПолей(<ПутьКДанным>, <ПутьКДаннымРодитель>);
	
	// Установка связи полей и измерений
	// УниверсальныйОтчет.УстановитьСвязьПоляИИзмерения(<ИмяПоля>, <ИмяИзмерения>);
	
	// Установка представлений полей
	УниверсальныйОтчет.УстановитьПредставленияПолей(УниверсальныйОтчет.мСтруктураПредставлениеПолей, УниверсальныйОтчет.ПостроительОтчета);
	
	// Установка типов значений свойств в отборах отчета
	УниверсальныйОтчет.УстановитьТипыЗначенийСвойствДляОтбора();
	
	// Заполнение начальных настроек универсального отчета
	УниверсальныйОтчет.УстановитьНачальныеНастройки(Ложь);
	
	УниверсальныйОтчет.ВыводитьОбщиеИтоги = Ложь;

	// Добавление дополнительных полей
	// Необходимо вызывать для каждого добавляемого дополнительного поля.
	// УниверсальныйОтчет.ДобавитьДополнительноеПоле(<ПутьКДанным>, <Размещение>, <Положение>);

КонецПроцедуры // УстановитьНачальныеНастройки()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ФОРМИРОВАНИЯ ОТЧЕТА 
	
// Процедура формирования отчета
//
Процедура СформироватьОтчет(ТабличныйДокумент) Экспорт
	
	Если Не ЗначениеЗаполнено(УниверсальныйОтчет.ДатаНач) Или Не ЗначениеЗаполнено(УниверсальныйОтчет.ДатаКон) Тогда
		Сообщить("Не заполнен период", СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Периодичность) Тогда
		Сообщить("Не выбрана периодичность отчета", СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	// Перед формированием отчета можно установить необходимые параметры универсального отчета.
	
	УстановитьИнтервалыЗапроса();
	
	УниверсальныйОтчет.СформироватьОтчет(ТабличныйДокумент,,, ЭтотОбъект);

КонецПроцедуры // СформироватьОтчет()

Функция ПолучитьТекстСправкиФормы() Экспорт
	
	Возврат "";
	
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Процедура обработки расшифровки
//
Процедура ОбработкаРасшифровки(Расшифровка, Объект) Экспорт
	
	// Дополнительные параметры в расшифровывающий отчет можно передать
	// посредством инициализации переменной "ДополнительныеПараметры".
	
	ДополнительныеПараметры = УправлениеОтчетами.СохранитьРеквизитыОтчета(ЭтотОбъект);
	УниверсальныйОтчет.ОбработкаРасшифровкиУниверсальногоОтчета(Расшифровка, Объект, ДополнительныеПараметры);
	
КонецПроцедуры // ОбработкаРасшифровки()

// Формирует структуру для сохранения настроек отчета
//
Процедура СформироватьСтруктуруДляСохраненияНастроек(СтруктураСНастройками) Экспорт
	
	УниверсальныйОтчет.СформироватьСтруктуруДляСохраненияНастроек(СтруктураСНастройками);
	УправлениеОтчетами.СохранитьРеквизитыОтчета(ЭтотОбъект, СтруктураСНастройками);
	
КонецПроцедуры // СформироватьСтруктуруДляСохраненияНастроек()

// Заполняет настройки отчета из структуры сохраненных настроек
//
Функция ВосстановитьНастройкиИзСтруктуры(СтруктураСНастройками) Экспорт
	
	УправлениеОтчетами.ВосстановитьРеквизитыОтчета(ЭтотОбъект, СтруктураСНастройками);
	Возврат УниверсальныйОтчет.ВосстановитьНастройкиИзСтруктуры(СтруктураСНастройками, ЭтотОбъект);
	
КонецФункции // ВосстановитьНастройкиИзСтруктуры()

// Содержит значение используемого режима ввода периода.
// Тип: Число.
// Возможные значения: // (-1) - не выбирать период, 0 - произвольный период, 1 - на дату, 2 - неделя, 3 - декада, 4 - месяц, 5 - квартал, 6 - полугодие, 7 - год
// Значение по умолчанию: 0

УниверсальныйОтчет.мРежимВводаПериода = 1;

#КонецЕсли
