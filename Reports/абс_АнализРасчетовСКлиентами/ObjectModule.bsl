Перем ТабИнтервалы;

#Если Клиент Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ НАЧАЛЬНОЙ НАСТРОЙКИ ОТЧЕТА

Процедура УстановитьТекстЗапроса() Экспорт
	
	КолИнтервалов = ТабИнтервалы.Количество();
	
	ТекстПоказателей="
	|   ТаблицаЗадолженностиПоДням.ОстатокДолга КАК ОстатокДолга"; 
	Для Ном = 1 По КолИнтервалов Цикл
		ТекстПоказателей=ТекстПоказателей+",
		|ВЫБОР
		|КОГДА ТаблицаЗадолженностиПоДням.ДатаОплаты МЕЖДУ &НачалоИнтервала"+Ном+" И &КонецИнтервала"+Ном+"
		|ТОГДА ТаблицаЗадолженностиПоДням.ОстатокДолга
		|ИНАЧЕ 0
		|КОНЕЦ КАК ОстатокПериода" + Ном;
	КонецЦикла;
	
	УсловиеПоказателей = "";
	Для Ном = 1 по КолИнтервалов Цикл
		УсловиеПоказателей = УсловиеПоказателей + "
		|ВЫБОР
		|КОГДА ТаблицаЗадолженностиПоДням.ДатаОплаты МЕЖДУ &НачалоИнтервала"+Ном+" И &КонецИнтервала"+Ном+"
		|ТОГДА ТаблицаЗадолженностиПоДням.ОстатокДолга
		|ИНАЧЕ 0
		|КОНЕЦ>0 ИЛИ ";
	КонецЦикла;
	Если СтрДлина(УсловиеПоказателей)>0 Тогда
		УсловиеПоказателей=Сред(УсловиеПоказателей,1,СтрДлина(УсловиеПоказателей)-4);
	КонецЕсли;

	ТекстЗапроса=
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДоговорКонтрагента,
	|	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.Сделка,
	|	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДокументРасчетовСКонтрагентом,
	|	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.Организация,
	|	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.Контрагент,
	|	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.Контрагент.ОсновнойМенеджерПокупателя КАК ОсновнойМенеджер,
	|	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.Контрагент.ОсновнойМенеджерПокупателя.абс_Подразделение КАК Подразделение,
	|	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.Контрагент.Регион КАК Регион,
	|	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.СуммаВзаиморасчетовОстаток КАК ОстатокДолга,
	|	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДоговорКонтрагента.ДопустимоеЧислоДнейЗадолженности КАК ДопустимоеЧислоДнейЗадолженности,
	|	ВЫРАЗИТЬ(ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДокументРасчетовСКонтрагентом КАК Документ.РеализацияТоваровУслуг).абс_ДатаОплаты КАК ДатаОплаты
	|ПОМЕСТИТЬ ТаблицаЗадолженности
	|ИЗ
	|	РегистрНакопления.ВзаиморасчетыСКонтрагентамиПоДокументамРасчетов.Остатки(&ДатаОстатков, ) КАК ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки
	|ГДЕ
	|	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.СуммаВзаиморасчетовОстаток > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаЗадолженности.Организация КАК Организация,
	|	ТаблицаЗадолженности.Контрагент КАК Контрагент,
	|	ТаблицаЗадолженности.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	СУММА(ВЫБОР
	|	КОГДА ТаблицаЗадолженности.ДатаОплаты<&ДатаОстатков
	|	ТОГДА ТаблицаЗадолженности.ОстатокДолга
	|	ИНАЧЕ 0
	|	КОНЕЦ) КАК ПросроченнаяЗадолженность
	|ПОМЕСТИТЬ ТаблицаЗадолженностиИтог
	|ИЗ ТаблицаЗадолженности КАК ТаблицаЗадолженности
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗадолженности.Организация,
	|	ТаблицаЗадолженности.Контрагент,
	|	ТаблицаЗадолженности.ДоговорКонтрагента
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаЗадолженностиПоДням.Организация КАК Организация,
	|	ТаблицаЗадолженностиПоДням.Организация.Представление КАК ОрганизацияПредставление,
	|	ТаблицаЗадолженностиПоДням.Контрагент КАК Контрагент,
	|	ТаблицаЗадолженностиПоДням.Контрагент.Представление КАК КонтрагентПредставление,
	|	ТаблицаЗадолженностиПоДням.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ТаблицаЗадолженностиПоДням.ОсновнойМенеджер КАК ОсновнойМенеджер,
	|	ТаблицаЗадолженностиПоДням.ОсновнойМенеджер.Представление КАК ОсновнойМенеджерПредставление,
	|	ТаблицаЗадолженностиПоДням.Регион КАК Регион,
	|	ТаблицаЗадолженностиПоДням.Регион.Представление КАК РегионПредставление,
	|	ТаблицаЗадолженностиПоДням.Подразделение КАК Подразделение,
	|	ТаблицаЗадолженностиПоДням.Подразделение.Представление КАК ПодразделениеПредставление,
	|	ТаблицаЗадолженностиПоДням.ДоговорКонтрагента.Представление КАК ДоговорКонтрагентаПредставление,
	|	ТаблицаЗадолженностиПоДням.ДоговорКонтрагента.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ТаблицаЗадолженностиПоДням.ДоговорКонтрагента.ВалютаВзаиморасчетов.Представление КАК ВалютаВзаиморасчетовПредставление,
	|	ТаблицаЗадолженностиПоДням.Сделка КАК Сделка,
	|	ТаблицаЗадолженностиПоДням.Сделка.Представление КАК СделкаПредставление,
	|	ТаблицаЗадолженностиПоДням.ДокументРасчетовСКонтрагентом КАК ДокументРасчетовСКонтрагентом,
	|	ТаблицаЗадолженностиПоДням.ДокументРасчетовСКонтрагентом.Представление КАК ДокументРасчетовСКонтрагентомПредставление,
	|	ТаблицаЗадолженностиПоДням.ДатаОплаты КАК ДатаОплаты,
	|	ТаблицаЗадолженностиПоДням.ДопустимоеЧислоДнейЗадолженности КАК ДопустимоеЧислоДнейЗадолженности,
	|	ТаблицаЗадолженностиИтог.ПросроченнаяЗадолженность КАК ПросроченнаяЗадолженность,"
	+ ТекстПоказателей + "
	|	//ПОЛЯ_СВОЙСТВА
	|	//ПОЛЯ_КАТЕГОРИИ
	|{ВЫБРАТЬ
	|	Организация.*,
	|	Контрагент.*,
	|	ДоговорКонтрагента.*,
	|	ОсновнойМенеджер.*,
	|	Регион.*,
	|	Подразделение.*,
	|	ВалютаВзаиморасчетов.*,
	|	Сделка.*,
	|	ДокументРасчетовСКонтрагентом.*,
	|	ДатаОплаты,
	|	ДопустимоеЧислоДнейЗадолженности,
	|	ПросроченнаяЗадолженность,"
	+ ТекстПоказателей + "
	|	//ПСЕВДОНИМЫ_СВОЙСТВА
	|	//ПСЕВДОНИМЫ_КАТЕГОРИИ
	|}
	|ИЗ ТаблицаЗадолженности КАК ТаблицаЗадолженностиПоДням
	|	ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаЗадолженностиИтог КАК ТаблицаЗадолженностиИтог
	|	ПО ТаблицаЗадолженностиПоДням.Организация = ТаблицаЗадолженностиИтог.Организация
	|	И ТаблицаЗадолженностиПоДням.Контрагент = ТаблицаЗадолженностиИтог.Контрагент
	//|	И ТаблицаЗадолженностиПоДням.ОсновнойМенеджер = ТаблицаЗадолженностиИтог.ОсновнойМенеджер
	//|	И ТаблицаЗадолженностиПоДням.Регион = ТаблицаЗадолженностиИтог.Регион
	|	И ТаблицаЗадолженностиПоДням.ДоговорКонтрагента = ТаблицаЗадолженностиИтог.ДоговорКонтрагента
	|	
	|	//СОЕДИНЕНИЯ
	|"+?(СтрДлина(УсловиеПоказателей)>0,"ГДЕ "+УсловиеПоказателей,"")+"
	|{ГДЕ"
	+ ТекстПоказателей + "
	|	//УСЛОВИЯ_СВОЙСТВА
	|	//УСЛОВИЯ_КАТЕГОРИИ
	|}
	|{УПОРЯДОЧИТЬ ПО
	|	Организация.*,
	|	Контрагент.*,
	|	ДоговорКонтрагента.*,
	|	Сделка.*,
	|	ДокументРасчетовСКонтрагентом.*,
	|	ОсновнойМенеджер.*,
	|	Регион.*,
	|	Подразделение.*,
	|	ВалютаВзаиморасчетов.*,
	|	ДатаОплаты,
	|	ДопустимоеЧислоДнейЗадолженности,
	|	ДопустимоеЧислоДнейЗадолженности,
	|	ПросроченнаяЗадолженность,
	|	ОстатокДолга";
	Для Ном = 1 По КолИнтервалов Цикл
		ТекстЗапроса = ТекстЗапроса + ",
		|	ОстатокПериода" + Ном;
	КонецЦикла;
	ТекстЗапроса = ТекстЗапроса + "
	|	//ПСЕВДОНИМЫ_СВОЙСТВА
	|	//ПСЕВДОНИМЫ_КАТЕГОРИИ
	|}
	|ИТОГИ
	|	МАКСИМУМ(ПросроченнаяЗадолженность),
	|	МИНИМУМ(ДатаОплаты),
	|	МАКСИМУМ(ДопустимоеЧислоДнейЗадолженности),
	|	СУММА(ОстатокДолга)";
	Для Ном = 1 По КолИнтервалов Цикл
		ТекстЗапроса = ТекстЗапроса + ",
		|	СУММА(ОстатокПериода" + Ном + ")";
	КонецЦикла;
	ТекстЗапроса = ТекстЗапроса + "
	|	//ИТОГИ_СВОЙСТВА
	|	//ИТОГИ_КАТЕГОРИИ
	|ПО
	|	ОБЩИЕ
	|{ИТОГИ ПО
	|	ВалютаВзаиморасчетов.*,
	|	Организация.*,
	|	Контрагент.*,
	|	ДоговорКонтрагента.*,
	|	Контрагент.*,
	|	ОсновнойМенеджер.*,
	|	Регион.*,
	|	Подразделение.*,
	|	Сделка.*,
	|	ДокументРасчетовСКонтрагентом.*,
	|	ВалютаВзаиморасчетов.*
	|	//ПСЕВДОНИМЫ_СВОЙСТВА
	|	//ПСЕВДОНИМЫ_КАТЕГОРИИ
	|}";
	
	// В универсальном отчете включен флаг использования свойств и категорий.
	Если УниверсальныйОтчет.ИспользоватьСвойстваИКатегории Тогда
		
		// Добавление свойств и категорий поля запроса в таблицу полей.
		// Необходимо вызывать для каждого поля запроса, предоставляющего возможность использования свойств и категорий.
		
		// УниверсальныйОтчет.ДобавитьСвойстваИКатегорииДляПоля(<ПсевдонимТаблицы>.<Поле> , <ПсевдонимПоля>, <Представление>, <Назначение>);
		УниверсальныйОтчет.ДобавитьСвойстваИКатегорииДляПоля("ТаблицаЗадолженности.Контрагент", "Контрагент", "Контрагент", ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_Контрагенты);
		УниверсальныйОтчет.ДобавитьСвойстваИКатегорииДляПоля("ТаблицаЗадолженности.ДоговорКонтрагента", "ДоговорКонтрагента", "Договор контрагента", ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_ДоговорыКонтрагентов);
		УниверсальныйОтчет.ДобавитьСвойстваИКатегорииДляПоля("ТаблицаЗадолженности.Организация", "Организация", "Организация", ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_Организации);

		УниверсальныйОтчет.ДобавитьВТекстЗапросаСвойстваИКатегории(ТекстЗапроса);
		
	КонецЕсли;
		
	// Инициализация текста запроса построителя отчета
	УниверсальныйОтчет.ПостроительОтчета.Текст = ТекстЗапроса;
	
КонецПроцедуры

Процедура УстановитьИнтервалыЗапроса() Экспорт

	ТабИнтервалы = Новый ТаблицаЗначений;
	ТабИнтервалы.Колонки.Добавить("ИмяИнтервала");
	ТабИнтервалы.Колонки.Добавить("НомерИнтервала");
	ТабИнтервалы.Колонки.Добавить("НачалоИнтервала");
	ТабИнтервалы.Колонки.Добавить("КонецИнтервала");
	
	КоличествоДней = 0;
	
	Если ЗначениеЗаполнено(УниверсальныйОтчет.ДатаНач) И ЗначениеЗаполнено(УниверсальныйОтчет.ДатаКон) Тогда
		КоличествоДней = (НачалоДня(УниверсальныйОтчет.ДатаКон) - НачалоДня(УниверсальныйОтчет.ДатаНач))/86400;
		Если КоличествоДней>0 Тогда
			Для сч=1 По КоличествоДней+1 Цикл
				ТекущийДень = УниверсальныйОтчет.ДатаНач + (сч-1)*86400;
				
				НоваяСтрока = ТабИнтервалы.Добавить();
				НоваяСтрока.ИмяИнтервала    = Формат(ТекущийДень,"ДФ = дд.ММ.гггг");
				НоваяСтрока.НомерИнтервала  = сч;
				НоваяСтрока.НачалоИнтервала = НачалоДня(ТекущийДень);
				НоваяСтрока.КонецИнтервала  = КонецДня(ТекущийДень);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыПостроителя = УниверсальныйОтчет.ПостроительОтчета.Параметры;
	ПараметрыПостроителя.Вставить("ДатаОстатков", КонецДня(УниверсальныйОтчет.ДатаНач) + 1);
	
	Для каждого  СтрокаИнтервала из ТабИнтервалы Цикл
		ПараметрыПостроителя.Вставить("НачалоИнтервала" + СтрокаИнтервала.НомерИнтервала, СтрокаИнтервала.НачалоИнтервала);
		ПараметрыПостроителя.Вставить("КонецИнтервала"  + СтрокаИнтервала.НомерИнтервала, СтрокаИнтервала.КонецИнтервала);
	КонецЦикла;
	
КонецПроцедуры

// Процедура установки начальных настроек отчета с использованием текста запроса
//
Процедура УстановитьНачальныеНастройки(ДополнительныеПараметры = Неопределено) Экспорт
	
	// Настройка общих параметров универсального отчета
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		
		УправлениеОтчетами.ВосстановитьРеквизитыОтчета(ЭтотОбъект, ДополнительныеПараметры);
		
	КонецЕсли;
	
	// Содержит название отчета, которое будет выводиться в шапке.
	// Тип: Строка.
	// Пример:
	// УниверсальныйОтчет.мНазваниеОтчета = "Название отчета";
	УниверсальныйОтчет.мНазваниеОтчета = СокрЛП(ЭтотОбъект.Метаданные().Синоним);
	
	// Содержит признак необходимости отображения надписи и поля выбора раздела учета в форме настройки.
	// Тип: Булево.
	// Значение по умолчанию: Истина.
	// Пример:
	// УниверсальныйОтчет.мВыбиратьИмяРегистра = Ложь;
	УниверсальныйОтчет.мВыбиратьИмяРегистра = Ложь;
	
	// Содержит имя регистра, по метаданным которого будет выполняться заполнение настроек отчета.
	// Тип: Строка.
	// Пример:
	// УниверсальныйОтчет.ИмяРегистра = "ТоварыНаСкладах";
	
	// Содержит признак необходимости вывода отрицательных значений показателей красным цветом.
	// Тип: Булево.
	// Значение по умолчанию: Ложь.
	// Пример:
	// УниверсальныйОтчет.ОтрицательноеКрасным = Истина;
	
	// Содержит признак необходимости вывода в отчет общих итогов.
	// Тип: Булево.
	// Значение по умолчанию: Истина.
	// Пример:
	// УниверсальныйОтчет.ВыводитьОбщиеИтоги = Ложь;
	
	// Содержит признак необходимости вывода детальных записей в отчет.
	// Тип: Булево.
	// Значение по умолчанию: Ложь.
	// Пример:
	// УниверсальныйОтчет.ВыводитьДетальныеЗаписи = Истина;
	
	// Содержит признак необходимости отображения флага использования свойств и категорий в форме настройки.
	// Тип: Булево.
	// Значение по умолчанию: Истина.
	// Пример:
	// УниверсальныйОтчет.мВыбиратьИспользованиеСвойств = Ложь;
	
	// Содержит признак использования свойств и категорий при заполнении настроек отчета.
	// Тип: Булево.
	// Значение по умолчанию: Ложь.
	// Пример:
	// УниверсальныйОтчет.ИспользоватьСвойстваИКатегории = Истина;
	
	// Содержит признак использования простой формы настроек отчета без группировок колонок.
	// Тип: Булево.
	// Значение по умолчанию: Ложь.
	// Пример:
	// УниверсальныйОтчет.мРежимФормыНастройкиБезГруппировокКолонок = Истина;
	УниверсальныйОтчет.мРежимФормыНастройкиБезГруппировокКолонок = Истина;
	
	// Дополнительные параметры, переданные из отчета, вызвавшего расшифровку.
	// Информация, передаваемая в переменной ДополнительныеПараметры, может быть использована
	// для реализации специфичных для данного отчета параметрических настроек.
	
	// Описание исходного текста запроса.
	
	УниверсальныйОтчет.ДатаНач = ?(ЗначениеЗаполнено(УниверсальныйОтчет.ДатаНач), УниверсальныйОтчет.ДатаНач, КонецДня(РабочаяДата));
	УниверсальныйОтчет.ДатаКон = ?(ЗначениеЗаполнено(УниверсальныйОтчет.ДатаКон), УниверсальныйОтчет.ДатаКон,УниверсальныйОтчет.ДатаНач + 8*86400);
	
	УстановитьИнтервалыЗапроса();
	
	УстановитьТекстЗапроса();
	
	// Представления полей отчета.
	// Необходимо вызывать для каждого поля запроса.
	// УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить(<ИмяПоля>, <ПредставлениеПоля>);

	// Представления полей отчета.
	// Необходимо вызывать для каждого поля запроса.
	// УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить(<ИмяПоля>, <ПредставлениеПоля>);
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("ДоговорКонтрагента", "Договор контрагента");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("ДокументРасчетовСКонтрагентом", "Документ расчетов с контрагентом");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("ВалютаВзаиморасчетов", "Валюта взаиморасчетов");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("ПросроченнаяЗадолженность", "ПросроченнаяЗадолженность");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("ОсновнойМенеджер", "Основной менеджер");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("Регион", "Бизнес-регион");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("Подразделение", "Подразделение");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("ДопустимоеЧислоДнейЗадолженности", "Число дней задолженности не более");	
            
	
	Для каждого СтрокаИнтервала из ТабИнтервалы Цикл
		Ном = СтрокаИнтервала.НомерИнтервала;
		УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("ОстатокПериода" + Ном, СтрокаИнтервала.ИмяИнтервала);
	КонецЦикла;
	
	УстановитьПоказатели();
	
	// Добавление предопределенных группировок строк отчета.
	// Необходимо вызывать для каждой добавляемой группировки строки.
	// УниверсальныйОтчет.ДобавитьИзмерениеСтроки(<ПутьКДанным>);
	УниверсальныйОтчет.ДобавитьИзмерениеСтроки("ВалютаВзаиморасчетов");
	УниверсальныйОтчет.ДобавитьИзмерениеСтроки("Организация");
	УниверсальныйОтчет.ДобавитьИзмерениеСтроки("Подразделение");
	УниверсальныйОтчет.ДобавитьИзмерениеСтроки("Регион");
	УниверсальныйОтчет.ДобавитьИзмерениеСтроки("ОсновнойМенеджер");
	УниверсальныйОтчет.ДобавитьИзмерениеСтроки("Контрагент");
	УниверсальныйОтчет.ДобавитьИзмерениеСтроки("ДоговорКонтрагента");
	УниверсальныйОтчет.ДобавитьИзмерениеСтроки("ДокументРасчетовСКонтрагентом");
	
	// Добавление предопределенных группировок колонок отчета.
	// Необходимо вызывать для каждой добавляемой группировки колонки.
	// УниверсальныйОтчет.ДобавитьИзмерениеКолонки(<ПутьКДанным>);
	
	// Добавление предопределенных отборов отчета.
	// Необходимо вызывать для каждого добавляемого отбора.
	// УниверсальныйОтчет.ДобавитьОтбор(<ПутьКДанным>);
	УниверсальныйОтчет.ДобавитьОтбор("Организация");
	УниверсальныйОтчет.ДобавитьОтбор("Контрагент");
	УниверсальныйОтчет.ДобавитьОтбор("ДоговорКонтрагента");
	УниверсальныйОтчет.ДобавитьОтбор("ОстатокДолга", , ВидСравнения.Больше, 0, , , Ложь);
	
	// Добавление предопределенных полей порядка отчета.
	// Необходимо вызывать для каждого добавляемого поля порядка.
	// УниверсальныйОтчет.ДобавитьПорядок(<ПутьКДанным>);
	
	// Установка связи подчиненных и родительских полей
	// УниверсальныйОтчет.УстановитьСвязьПолей(<ПутьКДанным>, <ПутьКДаннымРодитель>);
	УниверсальныйОтчет.УстановитьСвязьПолей("ВалютаВзаиморасчетов", "ДоговорКонтрагента");
	
	// Установка связи полей и измерений
	// УниверсальныйОтчет.УстановитьСвязьПоляИИзмерения(<ИмяПоля>, <ИмяИзмерения>);
	
	// Установка представлений полей
	УниверсальныйОтчет.УстановитьПредставленияПолей(УниверсальныйОтчет.мСтруктураПредставлениеПолей, УниверсальныйОтчет.ПостроительОтчета);
	
	// Установка типов значений свойств в отборах отчета
	УниверсальныйОтчет.УстановитьТипыЗначенийСвойствДляОтбора();
	
	// Заполнение начальных настроек универсального отчета
	УниверсальныйОтчет.УстановитьНачальныеНастройки(Ложь);
	
	УниверсальныйОтчет.ВыводитьОбщиеИтоги = Ложь;

	// Добавление дополнительных полей
	// Необходимо вызывать для каждого добавляемого дополнительного поля.
	// УниверсальныйОтчет.ДобавитьДополнительноеПоле(<ПутьКДанным>, <Размещение>, <Положение>);
	УниверсальныйОтчет.ДобавитьДополнительноеПоле("ВалютаВзаиморасчетов");

КонецПроцедуры // УстановитьНачальныеНастройки()

Процедура УстановитьПоказатели() Экспорт
	
	// Добавление показателей
	// Необходимо вызывать для каждого добавляемого показателя.
	// УниверсальныйОтчет.ДобавитьПоказатель(<ИмяПоказателя>, <ПредставлениеПоказателя>, <ВключенПоУмолчанию>, <Формат>, <ИмяГруппы>, <ПредставлениеГруппы>);
	УниверсальныйОтчет.ДобавитьПоказатель("ПросроченнаяЗадолженность", "Просроченная задолженность", Истина, "ЧЦ=15; ЧДЦ=2", , );
	УниверсальныйОтчет.ДобавитьПоказатель("ДопустимоеЧислоДнейЗадолженности", "Число дней задолженности не более", Истина, "ЧДЦ=0", , );
	
	Для каждого СтрокаИнтервала из ТабИнтервалы Цикл
		Ном = СтрокаИнтервала.НомерИнтервала;
		УниверсальныйОтчет.ДобавитьПоказатель("ОстатокПериода" + Ном, СтрокаИнтервала.ИмяИнтервала, Истина, "ЧЦ=15; ЧДЦ=2", "ПоПериодам", "Долг контрагента, подлежащий оплате не позднее:");
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ФОРМИРОВАНИЯ ОТЧЕТА 
	
// Процедура формирования отчета
//
Процедура СформироватьОтчет(ТабличныйДокумент) Экспорт
	
	Если Не ЗначениеЗаполнено(УниверсальныйОтчет.ДатаНач) Или Не ЗначениеЗаполнено(УниверсальныйОтчет.ДатаКон) Тогда
		Сообщить("Не заполнен период", СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	// Перед формированием отчета можно установить необходимые параметры универсального отчета.
	
	//УстановитьИнтервалыЗапроса();
	
	УниверсальныйОтчет.СформироватьОтчет(ТабличныйДокумент,,, ЭтотОбъект);

КонецПроцедуры // СформироватьОтчет()

Функция ПолучитьТекстСправкиФормы() Экспорт
	
	Возврат "";
	
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Процедура обработки расшифровки
//
Процедура ОбработкаРасшифровки(Расшифровка, Объект) Экспорт
	
	// Дополнительные параметры в расшифровывающий отчет можно передать
	// посредством инициализации переменной "ДополнительныеПараметры".
	
	ДополнительныеПараметры = УправлениеОтчетами.СохранитьРеквизитыОтчета(ЭтотОбъект);
	УниверсальныйОтчет.ОбработкаРасшифровкиУниверсальногоОтчета(Расшифровка, Объект, ДополнительныеПараметры);
	
КонецПроцедуры // ОбработкаРасшифровки()

// Формирует структуру для сохранения настроек отчета
//
Процедура СформироватьСтруктуруДляСохраненияНастроек(СтруктураСНастройками) Экспорт
	
	УниверсальныйОтчет.СформироватьСтруктуруДляСохраненияНастроек(СтруктураСНастройками);
	УправлениеОтчетами.СохранитьРеквизитыОтчета(ЭтотОбъект, СтруктураСНастройками);
	
КонецПроцедуры // СформироватьСтруктуруДляСохраненияНастроек()

// Заполняет настройки отчета из структуры сохраненных настроек
//
Функция ВосстановитьНастройкиИзСтруктуры(СтруктураСНастройками) Экспорт
	
	УправлениеОтчетами.ВосстановитьРеквизитыОтчета(ЭтотОбъект, СтруктураСНастройками);
	Возврат УниверсальныйОтчет.ВосстановитьНастройкиИзСтруктуры(СтруктураСНастройками, ЭтотОбъект);
	
КонецФункции // ВосстановитьНастройкиИзСтруктуры()

// Содержит значение используемого режима ввода периода.
// Тип: Число.
// Возможные значения: // (-1) - не выбирать период, 0 - произвольный период, 1 - на дату, 2 - неделя, 3 - декада, 4 - месяц, 5 - квартал, 6 - полугодие, 7 - год
// Значение по умолчанию: 0

УниверсальныйОтчет.мРежимВводаПериода = 1;

#КонецЕсли
