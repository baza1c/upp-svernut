Процедура СформироватьОтчет() Экспорт
	
	ЕстьОшибки = Ложь;
	Если Не ЗначениеЗаполнено(ДатаОтчета) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана дата отчета");
		ЕстьОшибки = Истина;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Сценарий) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указан сценарий");
		ЕстьОшибки = Истина;
	КонецЕсли;
	
	Если ЕстьОшибки Тогда
		Возврат;
	КонецЕсли;
	
	Результат.Очистить();
	
	УстановитьПривилегированныйРежим(Истина); 
	
	Макет = ЭтотОбъект.ПолучитьМакет("Макет");
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	Если ВыводитьКоличество тогда
		ОбластьЗаголовок.Параметры.Заголовок = "Отчет бюджет-Факт отгрузки на " + Формат(ДатаОтчета, "ДЛФ=DD")+" (в шт.)" + "    Сценарий плана: " + Сценарий + " (в шт.)";
	Иначе
		ОбластьЗаголовок.Параметры.Заголовок = "Отчет бюджет-Факт отгрузки на " + Формат(ДатаОтчета, "ДЛФ=DD")+ " (без НДС)" + "    Сценарий плана: " + Сценарий ;
	КонецЕсли;
	Результат.Вывести(ОбластьЗаголовок);
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	
	ДатаМесяцаПрошлогоГода = Дата(Год(ДатаОтчета)-1,Месяц(ДатаОтчета),1);
	
	Месяц = Формат(ДатаОтчета, "ДФ = ММММ");
	Квартал = Формат(ДатаОтчета, "ДФ =""к 'квартал'"" "); 
	Год = Формат(ДатаОтчета, "ДФ = гггг");
	ПрошлыйГод = Формат(ДатаМесяцаПрошлогоГода, "ДФ = гггг");
	ПрошлыйМесяц = Формат(НачалоМесяца(ДатаОтчета)-1, "ДФ = ММММ");
	
	РабочихДнейМесяц = ЗаполнениеДокументов.ЧислоРабочихДней(НачалоМесяца(ДатаОтчета), КонецМесяца(ДатаОтчета));
	РабочихДнейКвартал = ЗаполнениеДокументов.ЧислоРабочихДней(НачалоКвартала(ДатаОтчета), КонецКвартала(ДатаОтчета));
	РабочихДнейГод = ЗаполнениеДокументов.ЧислоРабочихДней(НачалоГода(ДатаОтчета), КонецГода(ДатаОтчета))-1;
	РабочихДнейПрошлыйГод = ЗаполнениеДокументов.ЧислоРабочихДней(НачалоГода(ДатаМесяцаПрошлогоГода), КонецГода(ДатаМесяцаПрошлогоГода));
	РабочихДнейМесяцПрошлогоГода = ЗаполнениеДокументов.ЧислоРабочихДней(НачалоМесяца(ДатаМесяцаПрошлогоГода),КонецМесяца(ДатаМесяцаПрошлогоГода));
	РабочихДнейКварталПрошлогоГода = ЗаполнениеДокументов.ЧислоРабочихДней(НачалоКвартала(ДатаМесяцаПрошлогоГода), КонецКвартала(ДатаМесяцаПрошлогоГода));
	РабочихДнейПрошлыйМесяц = ЗаполнениеДокументов.ЧислоРабочихДней(НачалоМесяца(НачалоМесяца(ДатаОтчета)-1), КонецМесяца(НачалоМесяца(ДатаОтчета)-1));
	ПрошлоРабочихДнейМесяц = ЗаполнениеДокументов.ЧислоРабочихДней(НачалоМесяца(ДатаОтчета), ДатаОтчета);
	ПрошлоРабочихДнейКвартал = ЗаполнениеДокументов.ЧислоРабочихДней(НачалоКвартала(ДатаОтчета), ДатаОтчета);
	ПрошлоРабочихДнейГод = ЗаполнениеДокументов.ЧислоРабочихДней(НачалоГода(ДатаОтчета), ДатаОтчета);
	
	Если ПрошлоРабочихДнейГод>РабочихДнейПрошлыйГод Тогда
		ДатаСопоставленияГод = КонецГода(ДатаМесяцаПрошлогоГода);
		ПрошлоРабочихДнейПрошлыйГод = РабочихДнейПрошлыйГод;
	ИначеЕсли КонецГода(ДатаОтчета) = КонецДня(ДатаОтчета) Тогда
		ДатаСопоставленияГод = КонецГода(ДатаМесяцаПрошлогоГода);
		ПрошлоРабочихДнейПрошлыйГод = РабочихДнейПрошлыйГод;
	Иначе
		ДатаСопоставленияГод = ЗаполнениеДокументов.ОпределитьДату(НачалоГода(ДатаМесяцаПрошлогоГода), ПрошлоРабочихДнейГод);
		ПрошлоРабочихДнейПрошлыйГод = ПрошлоРабочихДнейГод; 
	КонецЕсли;
	
	Если ПрошлоРабочихДнейКвартал>РабочихДнейКвартал Тогда
		ДатаСопоставленияКвартал = КонецКвартала(ДатаМесяцаПрошлогоГода);
		ПрошлоРабочихДнейКварталПрошлогоГода = РабочихДнейКварталПрошлогоГода;
	ИначеЕсли КонецКвартала(ДатаОтчета) = КонецДня(ДатаОтчета) Тогда
		ДатаСопоставленияКвартал = КонецКвартала(ДатаМесяцаПрошлогоГода);
		ПрошлоРабочихДнейКварталПрошлогоГода = РабочихДнейКварталПрошлогоГода;
	Иначе
		ДатаСопоставленияКвартал = ЗаполнениеДокументов.ОпределитьДату(НачалоКвартала(ДатаМесяцаПрошлогоГода), ПрошлоРабочихДнейКвартал);
		ПрошлоРабочихДнейКварталПрошлогоГода = ПрошлоРабочихДнейКвартал;
	КонецЕсли;
	
	//ПрошлоРабочихДнейКварталПрошлогоГода = РабочихДнейКварталПрошлогоГода;	
	
	ПроцентРабочихДнейМесяцЧисло = Окр(ПрошлоРабочихДнейМесяц/РабочихДнейМесяц*100,1);
	ПроцентРабочихДнейКварталЧисло = Окр(ПрошлоРабочихДнейКвартал/РабочихДнейКвартал*100,1);
	ПроцентРабочихДнейГодЧисло = Окр(ПрошлоРабочихДнейГод/РабочихДнейГод*100,1);
	
	ПроцентРабочихДнейМесяц = "" + ?(Цел(ПроцентРабочихДнейМесяцЧисло)=ПроцентРабочихДнейМесяцЧисло,Цел(ПроцентРабочихДнейМесяцЧисло),ПроцентРабочихДнейМесяцЧисло) + "%";
	ПроцентРабочихДнейКвартал = "" + ?(Цел(ПроцентРабочихДнейКварталЧисло)=ПроцентРабочихДнейКварталЧисло,Цел(ПроцентРабочихДнейКварталЧисло),ПроцентРабочихДнейКварталЧисло) + "%";
	ПроцентРабочихДнейГод = "" + ?(Цел(ПроцентРабочихДнейГодЧисло)=ПроцентРабочихДнейГодЧисло,Цел(ПроцентРабочихДнейГодЧисло),ПроцентРабочихДнейГодЧисло) + "%";
	

	ОбластьШапка.Параметры.Месяц = Месяц;
	ОбластьШапка.Параметры.Квартал = Квартал;
	ОбластьШапка.Параметры.Год = Год;
	ОбластьШапка.Параметры.ПрошлыйГод = ПрошлыйГод;
	ОбластьШапка.Параметры.ПрошлыйМесяц = ПрошлыйМесяц;
	ОбластьШапка.Параметры.РабочихДнейМесяц = РабочихДнейМесяц;
	ОбластьШапка.Параметры.РабочихДнейКвартал = РабочихДнейКвартал;
	ОбластьШапка.Параметры.РабочихДнейГод = РабочихДнейГод;
	//ОбластьШапка.Параметры.РабочихДнейПрошлыйГод = РабочихДнейПрошлыйГод;
	ОбластьШапка.Параметры.РабочихДнейМесяцПрошлогоГода = РабочихДнейМесяцПрошлогоГода;
	//ОбластьШапка.Параметры.РабочихДнейКварталПрошлогоГода = РабочихДнейКварталПрошлогоГода;
	ОбластьШапка.Параметры.РабочихДнейПрошлыйМесяц = РабочихДнейПрошлыйМесяц;
	ОбластьШапка.Параметры.ПрошлоРабочихДнейМесяц = ПрошлоРабочихДнейМесяц;
	ОбластьШапка.Параметры.ПрошлоРабочихДнейКвартал = ПрошлоРабочихДнейКвартал;
	ОбластьШапка.Параметры.ПрошлоРабочихДнейГод = ПрошлоРабочихДнейГод;
	ОбластьШапка.Параметры.ПроцентРабочихДнейМесяц = ПроцентРабочихДнейМесяц;
	ОбластьШапка.Параметры.ПроцентРабочихДнейКвартал = ПроцентРабочихДнейКвартал;
	ОбластьШапка.Параметры.ПроцентРабочихДнейГод = ПроцентРабочихДнейГод;
	ОбластьШапка.Параметры.ПрошлоРабочихДнейПрошлыйГод = ПрошлоРабочихДнейПрошлыйГод;
	ОбластьШапка.Параметры.ПрошлоРабочихДнейКварталПрошлогоГода = ПрошлоРабочихДнейКварталПрошлогоГода;
	
	Если Месяц = "Январь" Тогда
		ОбластьШапка.Параметры.ГодПрошлыйТекущий = ПрошлыйГод;		
	Иначе
		ОбластьШапка.Параметры.ГодПрошлыйТекущий = Год;		
	КонецЕсли;		
	
	Результат.Вывести(ОбластьШапка);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.ДатаКалендаря,
	|	ВложенныйЗапрос.Номенклатура,
	|	МАКСИМУМ(ВложенныйЗапрос.Цена) КАК Цена
	|ПОМЕСТИТЬ ЦенаПоДням
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВложенныйЗапрос.ДатаКалендаря КАК ДатаКалендаря,
	|		ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|		ЦеныНоменклатуры.Цена КАК Цена
	|	ИЗ
	|		(ВЫБРАТЬ
	|			РегламентированныйПроизводственныйКалендарь.ДатаКалендаря КАК ДатаКалендаря,
	|			ЦеныНоменклатуры.Номенклатура КАК Номенклатура,
	|			МАКСИМУМ(ЦеныНоменклатуры.Период) КАК Период,
	|			ЦеныНоменклатуры.ТипЦен КАК ТипЦен
	|		ИЗ
	|			РегистрСведений.РегламентированныйПроизводственныйКалендарь КАК РегламентированныйПроизводственныйКалендарь
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
	|				ПО РегламентированныйПроизводственныйКалендарь.ДатаКалендаря >= ЦеныНоменклатуры.Период
	|					И (ВЫБОР
	|						КОГДА &ТипЦен = ЗНАЧЕНИЕ(Справочник.ТипыЦенНоменклатуры.Пустаяссылка)
	|							ТОГДА ЛОЖЬ
	|						ИНАЧЕ ЦеныНоменклатуры.ТипЦен = &ТипЦен
	|					КОНЕЦ)
	|		ГДЕ
	|			РегламентированныйПроизводственныйКалендарь.ДатаКалендаря МЕЖДУ НАЧАЛОПЕРИОДА(&ДатаМесяцаПрошлогоГода, ГОД) И КОНЕЦПЕРИОДА(&ДатаОтчета, ГОД)
	|			И ВЫБОР
	|					КОГДА &ТипЦен = ЗНАЧЕНИЕ(Справочник.ТипыЦенНоменклатуры.Пустаяссылка)
	|						ТОГДА ЛОЖЬ
	|					ИНАЧЕ ЦеныНоменклатуры.ТипЦен = &ТипЦен
	|				КОНЕЦ
	|		
	|		СГРУППИРОВАТЬ ПО
	|			РегламентированныйПроизводственныйКалендарь.ДатаКалендаря,
	|			ЦеныНоменклатуры.Номенклатура,
	|			ЦеныНоменклатуры.ТипЦен) КАК ВложенныйЗапрос
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
	|			ПО ВложенныйЗапрос.Номенклатура = ЦеныНоменклатуры.Номенклатура
	|				И ВложенныйЗапрос.ТипЦен = ЦеныНоменклатуры.ТипЦен
	|				И ВложенныйЗапрос.Период = ЦеныНоменклатуры.Период) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.ДатаКалендаря,
	|	ВложенныйЗапрос.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПродажиОбороты.Период КАК Период,
	|	ПродажиОбороты.Контрагент.абс_Отдел КАК Подразделение,
	|	ПродажиОбороты.Номенклатура КАК Номенклатура,
	|	ПродажиОбороты.Контрагент КАК Контрагент,
	|	0 КАК СуммаПлан,
	|	ВЫБОР
	|		КОГДА &ТипЦен = ЗНАЧЕНИЕ(Справочник.ТипыЦенНоменклатуры.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(ПродажиОбороты.СтоимостьОборот, 0) - ЕСТЬNULL(ПродажиОбороты.НДСОборот, 0)
	|		ИНАЧЕ ВЫБОР
	|				КОГДА &ДинамическиеЦены
	|					ТОГДА ВЫБОР
	|							КОГДА ЦенаПоДням.Цена ЕСТЬ NULL 
	|								ТОГДА ЕСТЬNULL(ПродажиОбороты.СтоимостьОборот, 0) - ЕСТЬNULL(ПродажиОбороты.НДСОборот, 0)
	|							ИНАЧЕ ЕСТЬNULL(ПродажиОбороты.КоличествоОборот, 0) * ЕСТЬNULL(ЦенаПоДням.Цена, 0)
	|						КОНЕЦ
	|				ИНАЧЕ ВЫБОР
	|						КОГДА ЦеныНоменклатурыСрезПоследних.Цена ЕСТЬ NULL 
	|							ТОГДА ЕСТЬNULL(ПродажиОбороты.СтоимостьОборот, 0) - ЕСТЬNULL(ПродажиОбороты.НДСОборот, 0)
	|						ИНАЧЕ ЕСТЬNULL(ПродажиОбороты.КоличествоОборот, 0) * ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0)
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ КАК СуммаФакт,
	|	ПродажиОбороты.Контрагент.абс_БизнесРегион КАК БизнесРегион,
	|	ПродажиОбороты.Контрагент.абс_Территория КАК Территория,
	|	ПродажиОбороты.Контрагент.абс_Город КАК Город,
	|	ПродажиОбороты.КоличествоОборот КАК КоличествоФакт,
	|	0 КАК КоличествоПлан
	|ПОМЕСТИТЬ ПланФактПолный
	|ИЗ
	|	РегистрНакопления.Продажи.Обороты(
	|			НАЧАЛОПЕРИОДА(&ДатаМесяцаПрошлогоГода, ГОД),
	|			&КонецПериодаПрошлогоГода,
	|			День,
	|			НЕ Контрагент.абс_Отдел = ЗНАЧЕНИЕ(Справочник.Регионы.ПустаяСсылка)
	|				И Номенклатура.ВидНоменклатуры в (&ВидНоменклатуры)) КАК ПродажиОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЦенаПоДням КАК ЦенаПоДням
	|		ПО (НАЧАЛОПЕРИОДА(ПродажиОбороты.Период, ДЕНЬ) = НАЧАЛОПЕРИОДА(ЦенаПоДням.ДатаКалендаря, ДЕНЬ))
	|			И ПродажиОбороты.Номенклатура = ЦенаПоДням.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	|				,
	|				ВЫБОР
	|					КОГДА &ТипЦен = ЗНАЧЕНИЕ(Справочник.ТипыЦенНоменклатуры.Пустаяссылка)
	|						ТОГДА ЛОЖЬ
	|					ИНАЧЕ ТипЦен = &ТипЦен
	|				КОНЕЦ) КАК ЦеныНоменклатурыСрезПоследних
	|		ПО ПродажиОбороты.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПродажиОбороты.Период,
	|	ПродажиОбороты.Контрагент.абс_Отдел,
	|	ПродажиОбороты.Номенклатура,
	|	ПродажиОбороты.Контрагент,
	|	0,
	|	ВЫБОР
	|		КОГДА &ТипЦен = ЗНАЧЕНИЕ(Справочник.ТипыЦенНоменклатуры.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(ПродажиОбороты.СтоимостьОборот, 0) - ЕСТЬNULL(ПродажиОбороты.НДСОборот, 0)
	|		ИНАЧЕ ВЫБОР
	|				КОГДА &ДинамическиеЦены
	|					ТОГДА ВЫБОР
	|							КОГДА ЦенаПоДням.Цена ЕСТЬ NULL 
	|								ТОГДА ЕСТЬNULL(ПродажиОбороты.СтоимостьОборот, 0) - ЕСТЬNULL(ПродажиОбороты.НДСОборот, 0)
	|							ИНАЧЕ ЕСТЬNULL(ПродажиОбороты.КоличествоОборот, 0) * ЕСТЬNULL(ЦенаПоДням.Цена, 0)
	|						КОНЕЦ
	|				ИНАЧЕ ВЫБОР
	|						КОГДА ЦеныНоменклатурыСрезПоследних.Цена ЕСТЬ NULL 
	|							ТОГДА ЕСТЬNULL(ПродажиОбороты.СтоимостьОборот, 0) - ЕСТЬNULL(ПродажиОбороты.НДСОборот, 0)
	|						ИНАЧЕ ЕСТЬNULL(ПродажиОбороты.КоличествоОборот, 0) * ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0)
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ПродажиОбороты.Контрагент.абс_БизнесРегион,
	|	ПродажиОбороты.Контрагент.абс_Территория,
	|	ПродажиОбороты.Контрагент.абс_Город,
	|	ПродажиОбороты.КоличествоОборот,
	|	0
	|ИЗ
	|	РегистрНакопления.Продажи.Обороты(
	|			&НачалоПериода,
	|			КОНЕЦПЕРИОДА(&ДатаОтчета, ДЕНЬ),
	|			День,
	|			НЕ Контрагент.абс_Отдел = ЗНАЧЕНИЕ(Справочник.Регионы.ПустаяСсылка)
	|				И Номенклатура.ВидНоменклатуры в (&ВидНоменклатуры)) КАК ПродажиОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЦенаПоДням КАК ЦенаПоДням
	|		ПО (НАЧАЛОПЕРИОДА(ПродажиОбороты.Период, ДЕНЬ) = НАЧАЛОПЕРИОДА(ЦенаПоДням.ДатаКалендаря, ДЕНЬ))
	|			И ПродажиОбороты.Номенклатура = ЦенаПоДням.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	|				,
	|				ВЫБОР
	|					КОГДА &ТипЦен = ЗНАЧЕНИЕ(Справочник.ТипыЦенНоменклатуры.Пустаяссылка)
	|						ТОГДА ЛОЖЬ
	|					ИНАЧЕ ТипЦен = &ТипЦен
	|				КОНЕЦ) КАК ЦеныНоменклатурыСрезПоследних
	|		ПО ПродажиОбороты.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПланыПродажОбороты.Период,
	|	ПланыПродажОбороты.Контрагент.абс_Отдел,
	|	ПланыПродажОбороты.Номенклатура,
	|	ПланыПродажОбороты.Контрагент,
	|	ПланыПродажОбороты.СтоимостьОборот,
	|	0,
	|	ПланыПродажОбороты.Контрагент.абс_БизнесРегион,
	|	ПланыПродажОбороты.Контрагент.абс_Территория,
	|	ПланыПродажОбороты.Контрагент.абс_Город,
	|	0,
	|	ПланыПродажОбороты.КоличествоОборот
	|ИЗ
	|	РегистрНакопления.ПланыПродаж.Обороты(
	|			НАЧАЛОПЕРИОДА(&ДатаОтчета, ГОД),
	|			КОНЕЦПЕРИОДА(&ДатаОтчета, ГОД),
	|			Месяц,
	|			Сценарий = &Сценарий
	|				И НЕ Контрагент.абс_Отдел = ЗНАЧЕНИЕ(Справочник.Регионы.ПустаяСсылка)
	|				И ВЫБОР
	|					КОГДА Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ Номенклатура.ВидНоменклатуры в (&ВидНоменклатуры)
	|				КОНЕЦ) КАК ПланыПродажОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПланФактПолный.Подразделение КАК Подразделение,
	|	ПланФактПолный.Номенклатура КАК Номенклатура,
	|	ПланФактПолный.Контрагент КАК Контрагент,
	|	ПланФактПолный.БизнесРегион КАК БизнесРегион,
	|	ПланФактПолный.Территория КАК Территория,
	|	ПланФактПолный.Город КАК Город,
	|	СУММА(ВЫБОР
	|			КОГДА ПланФактПолный.Период МЕЖДУ НАЧАЛОПЕРИОДА(&ДатаОтчета, МЕСЯЦ) И &ДатаОтчета
	|				ТОГДА ВЫБОР
	|						КОГДА &ВыводитьКоличество
	|							ТОГДА ПланФактПолный.КоличествоФакт
	|						ИНАЧЕ ПланФактПолный.СуммаФакт
	|					КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаФактМесяц,
	|	СУММА(ВЫБОР
	|			КОГДА ПланФактПолный.Период МЕЖДУ НАЧАЛОПЕРИОДА(&ДатаОтчета, КВАРТАЛ) И &ДатаОтчета
	|				ТОГДА ВЫБОР
	|						КОГДА &ВыводитьКоличество
	|							ТОГДА ПланФактПолный.КоличествоФакт
	|						ИНАЧЕ ПланФактПолный.СуммаФакт
	|					КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаФактКвартал,
	|	СУММА(ВЫБОР
	|			КОГДА ПланФактПолный.Период МЕЖДУ НАЧАЛОПЕРИОДА(&ДатаОтчета, ГОД) И &ДатаОтчета
	|				ТОГДА ВЫБОР
	|						КОГДА &ВыводитьКоличество
	|							ТОГДА ПланФактПолный.КоличествоФакт
	|						ИНАЧЕ ПланФактПолный.СуммаФакт
	|					КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаФактГод,
	|	СУММА(ВЫБОР
	|			КОГДА ПланФактПолный.Период МЕЖДУ НАЧАЛОПЕРИОДА(&ДатаМесяцаПрошлогоГода, МЕСЯЦ) И КОНЕЦПЕРИОДА(&ДатаМесяцаПрошлогоГода, МЕСЯЦ)
	|				ТОГДА ВЫБОР
	|						КОГДА &ВыводитьКоличество
	|							ТОГДА ПланФактПолный.КоличествоФакт
	|						ИНАЧЕ ПланФактПолный.СуммаФакт
	|					КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаМесяцПрошлогоГода,
	|	СУММА(ВЫБОР
	|			КОГДА ПланФактПолный.Период МЕЖДУ НАЧАЛОПЕРИОДА(&ДатаМесяцаПрошлогоГода, КВАРТАЛ) И &ДатаСопоставленияКвартал
	|				ТОГДА ВЫБОР
	|						КОГДА &ВыводитьКоличество
	|							ТОГДА ПланФактПолный.КоличествоФакт
	|						ИНАЧЕ ПланФактПолный.СуммаФакт
	|					КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаКварталПрошлогоГода,
	|	СУММА(ВЫБОР
	|			КОГДА ПланФактПолный.Период МЕЖДУ НАЧАЛОПЕРИОДА(&ДатаМесяцаПрошлогоГода, ГОД) И &ДатаСопоставленияГод
	|				ТОГДА ВЫБОР
	|						КОГДА &ВыводитьКоличество
	|							ТОГДА ПланФактПолный.КоличествоФакт
	|						ИНАЧЕ ПланФактПолный.СуммаФакт
	|					КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаПрошлыйГод,
	|	СУММА(ВЫБОР
	|			КОГДА ПланФактПолный.Период МЕЖДУ НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&ДатаОтчета, МЕСЯЦ), МЕСЯЦ, -1), МЕСЯЦ) И КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(КОНЕЦПЕРИОДА(&ДатаОтчета, МЕСЯЦ), МЕСЯЦ, -1), МЕСЯЦ)
	|				ТОГДА ВЫБОР
	|						КОГДА &ВыводитьКоличество
	|							ТОГДА ПланФактПолный.КоличествоФакт
	|						ИНАЧЕ ПланФактПолный.СуммаФакт
	|					КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаПрошлыйМесяц,
	|	СУММА(ВЫБОР
	|			КОГДА ПланФактПолный.Период = НАЧАЛОПЕРИОДА(&ДатаОтчета, МЕСЯЦ)
	|				ТОГДА ВЫБОР
	|						КОГДА &ВыводитьКоличество
	|							ТОГДА ПланФактПолный.КоличествоПлан
	|						ИНАЧЕ ПланФактПолный.СуммаПлан
	|					КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаПланМесяц,
	|	СУММА(ВЫБОР
	|			КОГДА ПланФактПолный.Период МЕЖДУ НАЧАЛОПЕРИОДА(&ДАтаОтчета, КВАРТАЛ) И КОНЕЦПЕРИОДА(&ДатаОтчета, КВАРТАЛ)
	|				ТОГДА ВЫБОР
	|						КОГДА &ВыводитьКоличество
	|							ТОГДА ПланФактПолный.КоличествоПлан
	|						ИНАЧЕ ПланФактПолный.СуммаПлан
	|					КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаПланКвартал,
	|	СУММА(ВЫБОР
	|			КОГДА ПланФактПолный.Период МЕЖДУ НАЧАЛОПЕРИОДА(&ДАтаОтчета, ГОД) И КОНЕЦПЕРИОДА(&ДатаОтчета, ГОД)
	|				ТОГДА ВЫБОР
	|						КОГДА &ВыводитьКоличество
	|							ТОГДА ПланФактПолный.КоличествоПлан
	|						ИНАЧЕ ПланФактПолный.СуммаПлан
	|					КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаПланГод,
	|	ПланФактПолный.БизнесРегион.абс_ЦентрРегиона КАК ЦентрРегиона,
	|	ПланФактПолный.БизнесРегион.абс_Менеджер КАК ОтветственныйМенеджер,
	|	ПланФактПолный.Территория.абс_ЦентрРегиона КАК ЦентрТерритории,
	|	ПланФактПолный.Территория.абс_Менеджер КАК ТерриториальныйМенеджер,
	|	ЕСТЬNULL(ЗначенияСвойствОбъектов.Значение, """") КАК Канал,
	|	ПланФактПолный.Подразделение.абс_Менеджер КАК РуководительПодразделения
	|ПОМЕСТИТЬ ПланФакт
	|ИЗ
	|	ПланФактПолный КАК ПланФактПолный
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|		ПО ПланФактПолный.Контрагент = ЗначенияСвойствОбъектов.Объект
	|			И (ЗначенияСвойствОбъектов.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СвойстваОбъектов.абс_Канал))
	|
	|СГРУППИРОВАТЬ ПО
	|	ПланФактПолный.Территория,
	|	ПланФактПолный.БизнесРегион,
	|	ЕСТЬNULL(ЗначенияСвойствОбъектов.Значение, """"),
	|	ПланФактПолный.Подразделение,
	|	ПланФактПолный.Город,
	|	ПланФактПолный.Контрагент,
	|	ПланФактПолный.Номенклатура,
	|	ПланФактПолный.БизнесРегион.абс_ЦентрРегиона,
	|	ПланФактПолный.БизнесРегион.абс_Менеджер,
	|	ПланФактПолный.Территория.абс_ЦентрРегиона,
	|	ПланФактПолный.Территория.абс_Менеджер,
	|	ПланФактПолный.Подразделение.абс_Менеджер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПланФакт.Контрагент,
	|	СУММА(ПланФакт.СуммаФактГод) КАК СуммаФактГод,
	|	СУММА(ПланФакт.СуммаПрошлыйГод) КАК СуммаПрошлыйГод
	|ПОМЕСТИТЬ Контрагенты
	|ИЗ
	|	ПланФакт КАК ПланФакт
	|
	|СГРУППИРОВАТЬ ПО
	|	ПланФакт.Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Контрагенты.Контрагент,
	|	ВЫБОР
	|		КОГДА Контрагенты.СуммаФактГод = 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Порядок,
	|	ВЫБОР
	|		КОГДА Контрагенты.СуммаФактГод = 0
	|			ТОГДА Контрагенты.СуммаПрошлыйГод
	|		ИНАЧЕ Контрагенты.СуммаФактГод
	|	КОНЕЦ КАК Сумма
	|ПОМЕСТИТЬ ПорядокКонтрагентов
	|ИЗ
	|	Контрагенты КАК Контрагенты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПланФакт.Номенклатура,
	|	ПланФакт.Контрагент,
	|	СУММА(ПланФакт.СуммаФактГод) КАК СуммаФактГод,
	|	СУММА(ПланФакт.СуммаПрошлыйГод) КАК СуммаПрошлыйГод
	|ПОМЕСТИТЬ КонтрагентНоменклатура
	|ИЗ
	|	ПланФакт КАК ПланФакт
	|
	|СГРУППИРОВАТЬ ПО
	|	ПланФакт.Номенклатура,
	|	ПланФакт.Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КонтрагентНоменклатура.Контрагент,
	|	ВЫБОР
	|		КОГДА КонтрагентНоменклатура.СуммаФактГод = 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Порядок,
	|	КонтрагентНоменклатура.Номенклатура,
	|	ВЫБОР
	|		КОГДА КонтрагентНоменклатура.СуммаФактГод = 0
	|			ТОГДА КонтрагентНоменклатура.СуммаПрошлыйГод
	|		ИНАЧЕ КонтрагентНоменклатура.СуммаФактГод
	|	КОНЕЦ КАК Сумма
	|ПОМЕСТИТЬ ПорядокНоменклатуры
	|ИЗ
	|	КонтрагентНоменклатура КАК КонтрагентНоменклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПланФакт.Подразделение КАК Подразделение,
	|	ПланФакт.Номенклатура КАК Номенклатура,
	|	ПланФакт.Контрагент КАК Контрагент,
	|	ПланФакт.БизнесРегион КАК БизнесРегион,
	|	ПланФакт.Территория КАК Территория,
	|	ПланФакт.Город КАК Город,
	|	ПланФакт.СуммаФактМесяц КАК СуммаФактМесяц,
	|	ПланФакт.СуммаФактКвартал КАК СуммаФактКвартал,
	|	ПланФакт.СуммаФактГод КАК СуммаФактГод,
	|	ПланФакт.СуммаМесяцПрошлогоГода КАК СуммаМесяцПрошлогоГода,
	|	ПланФакт.СуммаКварталПрошлогоГода КАК СуммаКварталПрошлогоГода,
	|	ПланФакт.СуммаПрошлыйГод КАК СуммаПрошлыйГод,
	|	ПланФакт.СуммаПрошлыйМесяц КАК СуммаПрошлыйМесяц,
	|	ПланФакт.СуммаПланМесяц КАК СуммаПланМесяц,
	|	ПланФакт.СуммаПланКвартал КАК СуммаПланКвартал,
	|	ПланФакт.СуммаПланГод КАК СуммаПланГод,
	|	ПланФакт.ЦентрРегиона КАК ЦентрРегиона,
	|	ПланФакт.ОтветственныйМенеджер КАК ОтветственныйМенеджер,
	|	ПланФакт.ЦентрТерритории КАК ЦентрТерритории,
	|	ПланФакт.ТерриториальныйМенеджер КАК ТерриториальныйМенеджер,
	|	ПланФакт.Канал КАК Канал,
	|	ПланФакт.РуководительПодразделения КАК РуководительПодразделения,
	|	ПорядокКонтрагентов.Порядок КАК ПорядокКонтрагентов,
	|	ПорядокНоменклатуры.Порядок КАК ПорядокНоменклатуры,
	|	ПорядокНоменклатуры.Сумма КАК СуммаСортировкиНоменклтуры,
	|	ПорядокКонтрагентов.Сумма КАК СуммаСортировкиКонтрагентов
	|ИЗ
	|	ПланФакт КАК ПланФакт
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокКонтрагентов КАК ПорядокКонтрагентов
	|		ПО ПланФакт.Контрагент = ПорядокКонтрагентов.Контрагент
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокНоменклатуры КАК ПорядокНоменклатуры
	|		ПО ПланФакт.Контрагент = ПорядокНоменклатуры.Контрагент
	|			И ПланФакт.Номенклатура = ПорядокНоменклатуры.Номенклатура
	|ГДЕ
	|	НЕ ПланФакт.Подразделение В (&ПодразделениеКУдалению)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПланФакт.Подразделение.Порядок,
	|	ПланФакт.БизнесРегион.Порядок,
	|	ПланФакт.Территория.Порядок,
	|	ПланФакт.Город.Порядок,
	|	ПорядокКонтрагентов,
	|	СуммаСортировкиКонтрагентов УБЫВ,
	|	ПорядокНоменклатуры,
	|	СуммаСортировкиНоменклтуры УБЫВ
	|ИТОГИ
	|	ВЫРАЗИТЬ(СУММА(СуммаФактМесяц) КАК ЧИСЛО(15, 2)) КАК СуммаФактМесяц,
	|	ВЫРАЗИТЬ(СУММА(СуммаФактКвартал) КАК ЧИСЛО(15, 2)) КАК СуммаФактКвартал,
	|	ВЫРАЗИТЬ(СУММА(СуммаФактГод) КАК ЧИСЛО(15, 2)) КАК СуммаФактГод,
	|	ВЫРАЗИТЬ(СУММА(СуммаМесяцПрошлогоГода) КАК ЧИСЛО(15, 2)) КАК СуммаМесяцПрошлогоГода,
	|	ВЫРАЗИТЬ(СУММА(СуммаКварталПрошлогоГода) КАК ЧИСЛО(15, 2)) КАК СуммаКварталПрошлогоГода,
	|	ВЫРАЗИТЬ(СУММА(СуммаПрошлыйГод) КАК ЧИСЛО(15, 2)) КАК СуммаПрошлыйГод,
	|	ВЫРАЗИТЬ(СУММА(СуммаПрошлыйМесяц) КАК ЧИСЛО(15, 2)) КАК СуммаПрошлыйМесяц,
	|	ВЫРАЗИТЬ(СУММА(СуммаПланМесяц) КАК ЧИСЛО(15, 2)) КАК СуммаПланМесяц,
	|	ВЫРАЗИТЬ(СУММА(СуммаПланКвартал) КАК ЧИСЛО(15, 2)) КАК СуммаПланКвартал,
	|	ВЫРАЗИТЬ(СУММА(СуммаПланГод) КАК ЧИСЛО(15, 2)) КАК СуммаПланГод,
	|	МАКСИМУМ(ЦентрРегиона),
	|	МАКСИМУМ(ОтветственныйМенеджер),
	|	МАКСИМУМ(ЦентрТерритории),
	|	МАКСИМУМ(ТерриториальныйМенеджер),
	|	МАКСИМУМ(Канал),
	|	МАКСИМУМ(РуководительПодразделения),
	|	МАКСИМУМ(СуммаСортировкиНоменклтуры),
	|	МАКСИМУМ(СуммаСортировкиКонтрагентов)
	|ПО
	|	ОБЩИЕ,
	|	Подразделение,
	|	БизнесРегион,
	|	Территория,
	|	Город,
	|	ПорядокКонтрагентов,
	|	Контрагент,
	|	ПорядокНоменклатуры,
	|	Номенклатура";
	
	//ВидНоменклатуры = Справочники.ВидыНоменклатуры.НайтиПоНаименованию("Товар (б/х)");
	
		
	ВидНоменклатуры = Новый Массив();
	
	ВидНоменклатуры.Добавить(Справочники.ВидыНоменклатуры.НайтиПоНаименованию("Товар (б/х)"));
	ВидНоменклатуры.Добавить(Справочники.ВидыНоменклатуры.НайтиПоНаименованию("Конечная продукция"));
	//ВидНоменклатуры.Добавить(Справочники.ВидыНоменклатуры.НайтиПоНаименованию("Полуфабрикат"));

	
	НачалоПрошлогоМесяца = НачалоМесяца(НачалоМесяца(ДатаОтчета)-1);
	НачалоПериода = Мин(НачалоПрошлогоМесяца, НачалоГода(ДатаОтчета));
	
	КонецПериодаПрошлогоГода = Макс(ДатаСопоставленияКвартал, КонецМесяца(ДатаМесяцаПрошлогоГода));
	
	ПодразделениеКУдалению = Новый Массив();	
	ПодразделениеКУдалению.Добавить(Справочники.Регионы.НайтиПоНаименованию("ВНУТРЕННИЕ"));
	ПодразделениеКУдалению.Добавить(Справочники.Регионы.НайтиПоНаименованию("ПОСТАВЩИКИ"));
	
	Запрос.УстановитьПараметр("ПодразделениеКУдалению", ПодразделениеКУдалению);
	Запрос.УстановитьПараметр("ДатаОтчета", КонецДня(ДатаОтчета));
	Запрос.УстановитьПараметр("ДатаМесяцаПрошлогоГода", ДатаМесяцаПрошлогоГода);
	Запрос.УстановитьПараметр("Сценарий", Сценарий);
	Запрос.УстановитьПараметр("КоммерческаяСлужба", Константы.абс_КоммерческаяСлужба.Получить());
	Запрос.УстановитьПараметр("ДатаСопоставленияГод", КонецДня(ДатаСопоставленияГод));
	Запрос.УстановитьПараметр("ДатаСопоставленияКвартал", КонецДня(ДатаСопоставленияКвартал));
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериодаПрошлогоГода", КонецПериодаПрошлогоГода);
	Запрос.УстановитьПараметр("ТипЦен", ТипЦен);
	Запрос.УстановитьПараметр("ДинамическиеЦены", ДинамическиеЦены);
	Запрос.УстановитьПараметр("ВыводитьКоличество", ВыводитьКоличество);
	Запрос.УстановитьПараметр("ВидНоменклатуры",ВидНоменклатуры);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаКС = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Если ВыборкаКС.Следующий() Тогда
		
		ОбластьСтрокаКС = Макет.ПолучитьОбласть("СтрокаКоммерческаяСлужба");
		
		ЗаполнитьЗначенияСвойств(ОбластьСтрокаКС.Параметры, ВыборкаКС);
		
		ЗаполнитьПроценты(ОбластьСтрокаКС,ВыборкаКС, ПроцентРабочихДнейМесяцЧисло, ПроцентРабочихДнейКварталЧисло, ПроцентРабочихДнейГодЧисло);
		
		Результат.Вывести(ОбластьСтрокаКС);
		
		Результат.НачатьАвтогруппировкуСтрок();
		
		ВыборкаПодразделение = ВыборкаКС.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПодразделение.Следующий() Цикл
			
			ОбластьСтрокаПодразделение = Макет.ПолучитьОбласть("СтрокаПодразделение");
			
			ЗаполнитьЗначенияСвойств(ОбластьСтрокаПодразделение.Параметры, ВыборкаПодразделение);
			
			ЗаполнитьПроценты(ОбластьСтрокаПодразделение,ВыборкаПодразделение, ПроцентРабочихДнейМесяцЧисло, ПроцентРабочихДнейКварталЧисло, ПроцентРабочихДнейГодЧисло);
			
			Результат.Вывести(ОбластьСтрокаПодразделение,1);
			
			ВыборкаБизнесРегион = ВыборкаПодразделение.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаБизнесРегион.Следующий() Цикл
				
				ОбластьСтрокаБизнесРегион = Макет.ПолучитьОбласть("СтрокаБизнесРегион");
				
				ЗаполнитьЗначенияСвойств(ОбластьСтрокаБизнесРегион.Параметры, ВыборкаБизнесРегион);
				
				ЗаполнитьПроценты(ОбластьСтрокаБизнесРегион,ВыборкаБизнесРегион, ПроцентРабочихДнейМесяцЧисло, ПроцентРабочихДнейКварталЧисло, ПроцентРабочихДнейГодЧисло);
				
				Результат.Вывести(ОбластьСтрокаБизнесРегион, 2);
				
				ВыборкаТерритория = ВыборкаБизнесРегион.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаТерритория.Следующий() Цикл
					
					УровеньГорода = 3;
					Если ЗначениеЗаполнено(ВыборкаТерритория.Территория) Тогда
						
						ОбластьСтрокаТеритория = Макет.ПолучитьОбласть("СтрокаТерритория");
						
						ЗаполнитьЗначенияСвойств(ОбластьСтрокаТеритория.Параметры, ВыборкаТерритория);
						
						ЗаполнитьПроценты(ОбластьСтрокаТеритория,ВыборкаТерритория, ПроцентРабочихДнейМесяцЧисло, ПроцентРабочихДнейКварталЧисло, ПроцентРабочихДнейГодЧисло);
						
						Результат.Вывести(ОбластьСтрокаТеритория, 3);
						
						УровеньГорода = 4;
						
					КонецЕсли;
					
					ВыборкаГород = ВыборкаТерритория.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Пока ВыборкаГород.Следующий() Цикл
						
						УровеньКонтрагента = УровеньГорода;
						
						Если ЗначениеЗаполнено(ВыборкаГород.Город) Тогда
						
							ОбластьСтрокаГород = Макет.ПолучитьОбласть("СтрокаГород");
							
							ЗаполнитьЗначенияСвойств(ОбластьСтрокаГород.Параметры, ВыборкаГород);
							
							ЗаполнитьПроценты(ОбластьСтрокаГород,ВыборкаГород, ПроцентРабочихДнейМесяцЧисло, ПроцентРабочихДнейКварталЧисло, ПроцентРабочихДнейГодЧисло);
							
							Результат.Вывести(ОбластьСтрокаГород, УровеньГорода);
							
							УровеньКонтрагента = УровеньКонтрагента + 1;
							
						КонецЕсли;
						
						УровеньНоменклатуры = УровеньКонтрагента + 1;
						
						СтруктураПрочиеКонтрагенты = Новый Структура;
						
						ЗаполнитьСтруктуруПрочих(СтруктураПрочиеКонтрагенты);
						
						ВыводитьПрочихКонтрагентов = Ложь;
						
						ВыборкаПорядок = ВыборкаГород.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						Пока ВыборкаПорядок.Следующий() Цикл
							
							ВыборкаКонтрагент = ВыборкаПорядок.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
							Пока ВыборкаКонтрагент.Следующий() Цикл
								
								ОбластьСтрокаКонтрагент = Макет.ПолучитьОбласть("СтрокаКонтрагент");
								
								ЗаполнитьЗначенияСвойств(ОбластьСтрокаКонтрагент.Параметры, ВыборкаКонтрагент);
								
								ЗаполнитьПроценты(ОбластьСтрокаКонтрагент,ВыборкаКонтрагент, ПроцентРабочихДнейМесяцЧисло, ПроцентРабочихДнейКварталЧисло, ПроцентРабочихДнейГодЧисло);
								
								Результат.Вывести(ОбластьСтрокаКонтрагент, УровеньКонтрагента);
								
								Если ВыводитьНоменклатуру Тогда
									
									СтруктураПрочаяНоменклатура = Новый Структура;
									ЗаполнитьСтруктуруПрочих(СтруктураПрочаяНоменклатура);
									ВыводитьПрочуюНоменклатуру = Ложь;
									
									ВыборкаПорядокНоменклатуры = ВыборкаКонтрагент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 
									Пока ВыборкаПорядокНоменклатуры.Следующий() Цикл
										
										ВыборкаНоменклатура = ВыборкаПорядокНоменклатуры.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
										Пока ВыборкаНоменклатура.Следующий() Цикл
											
											ОбластьСтрокаНоменклатура = Макет.ПолучитьОбласть("СтрокаНоменклатура");
											
											ЗаполнитьЗначенияСвойств(ОбластьСтрокаНоменклатура.Параметры, ВыборкаНоменклатура);
											
											ЗаполнитьПроценты(ОбластьСтрокаНоменклатура,ВыборкаНоменклатура, ПроцентРабочихДнейМесяцЧисло, ПроцентРабочихДнейКварталЧисло, ПроцентРабочихДнейГодЧисло);
											
											Результат.Вывести(ОбластьСтрокаНоменклатура, УровеньНоменклатуры);
										КонецЦикла;
									КонецЦикла;
								КонецЕсли;
							КонецЦикла;
						КонецЦикла;
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Результат.ЗакончитьАвтогруппировкуСтрок();
	
	Результат.ОтображатьЗаголовки = Истина;
	
	Результат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Результат.ФиксацияСверху = 14;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ЗаполнитьПроценты(ОбластьМакета, ВыборкаЗапроса, ПроцентРабочихДнейМесяцЧисло, ПроцентРабочихДнейКварталЧисло, ПроцентРабочихДнейГодЧисло)
	
	
	ПроцентМесяц =Окр(?(ВыборкаЗапроса.СуммаПланМесяц=0,0,ВыборкаЗапроса.СуммаФактМесяц/ВыборкаЗапроса.СуммаПланМесяц*100),1);
	ОбластьМакета.Параметры.ПроцентМесяц = "" + Окр(ПроцентМесяц) + "%";
	Если ПроцентМесяц<ПроцентРабочихДнейМесяцЧисло Тогда
		ОбластьМакета.Область(1,11).ЦветТекста = WebЦвета.Красный;
	КонецЕсли;
	
	ПроцентКвартал = Окр(?(ВыборкаЗапроса.СуммаПланКвартал=0,0,ВыборкаЗапроса.СуммаФактКвартал/ВыборкаЗапроса.СуммаПланКвартал*100),1);
	ОбластьМакета.Параметры.ПроцентКвартал = "" + Окр(ПроцентКвартал) + "%";
	Если ПроцентКвартал<ПроцентРабочихДнейКварталЧисло Тогда
		ОбластьМакета.Область(1,14).ЦветТекста = WebЦвета.Красный;
	КонецЕсли;
	
	ПроцентГод = Окр(?(ВыборкаЗапроса.СуммаПланГод=0,0,ВыборкаЗапроса.СуммаФактГод/ВыборкаЗапроса.СуммаПланГод*100),1);
	ОбластьМакета.Параметры.ПроцентГод = "" + Окр(ПроцентГод) + "%";
	Если ПроцентГод<ПроцентРабочихДнейГодЧисло Тогда
		ОбластьМакета.Область(1,17).ЦветТекста = WebЦвета.Красный;
	КонецЕсли;
	
	ПроцентМесяцПрошлогоГода = Окр(?(ВыборкаЗапроса.СуммаМесяцПрошлогоГода=0,0,(ВыборкаЗапроса.СуммаФактМесяц-ВыборкаЗапроса.СуммаМесяцПрошлогоГода)/ВыборкаЗапроса.СуммаМесяцПрошлогоГода*100));
	ОбластьМакета.Параметры.ПроцентМесяцПрошлогоГода = "" + ПроцентМесяцПрошлогоГода +"%";
	Если ПроцентМесяцПрошлогоГода<5 Тогда
		ОбластьМакета.Область(1,7).ЦветТекста = WebЦвета.Красный;
	ИначеЕсли ПроцентМесяцПрошлогоГода<10 Тогда
		ОбластьМакета.Область(1,7).ЦветТекста = WebЦвета.Синий;
	КонецЕсли;
	
	ПроцентКварталПрошлогоГода = Окр(?(ВыборкаЗапроса.СуммаКварталПрошлогоГода=0,0,(ВыборкаЗапроса.СуммаФактКвартал-ВыборкаЗапроса.СуммаКварталПрошлогоГода)/ВыборкаЗапроса.СуммаКварталПрошлогоГода*100));
	ОбластьМакета.Параметры.ПроцентКварталПрошлогоГода = "" + ПроцентКварталПрошлогоГода + "%";
	Если ПроцентКварталПрошлогоГода<5 Тогда
		ОбластьМакета.Область(1,23).ЦветТекста = WebЦвета.Красный;
 	ИначеЕсли ПроцентКварталПрошлогоГода<10 Тогда
		ОбластьМакета.Область(1,23).ЦветТекста = WebЦвета.Синий;
	КонецЕсли;
	
	ПроцентПрошлыйГод = Окр(?(ВыборкаЗапроса.СуммаПрошлыйГод=0,0,(ВыборкаЗапроса.СуммаФактГод-ВыборкаЗапроса.СуммаПрошлыйГод)/ВыборкаЗапроса.СуммаПрошлыйГод*100));
	ОбластьМакета.Параметры.ПроцентПрошлыйГод = "" + ПроцентПрошлыйГод + "%";
	Если ПроцентПрошлыйГод<5 Тогда
		ОбластьМакета.Область(1,25).ЦветТекста = WebЦвета.Красный;
	ИначеЕсли ПроцентПрошлыйГод<10 Тогда
		ОбластьМакета.Область(1,25).ЦветТекста = WebЦвета.Синий;
	КонецЕсли;
	
	ПроцентПрошлыйМесяц = Окр(?(ВыборкаЗапроса.СуммаПрошлыйМесяц=0,0,(ВыборкаЗапроса.СуммаФактМесяц-ВыборкаЗапроса.СуммаПрошлыйМесяц)/ВыборкаЗапроса.СуммаПрошлыйМесяц*100));
	ОбластьМакета.Параметры.ПроцентПрошлыйМесяц = "" + ПроцентПрошлыйМесяц + "%";
	Если ПроцентПрошлыйМесяц<5 Тогда
		ОбластьМакета.Область(1,20).ЦветТекста = WebЦвета.Красный;
	ИначеЕсли ПроцентПрошлыйМесяц<10 Тогда
		ОбластьМакета.Область(1,20).ЦветТекста = WebЦвета.Синий;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСтруктуруПрочих(СтруктураЗначений)
	
	СтруктураЗначений.Вставить("СуммаФактМесяц", 0);
	СтруктураЗначений.Вставить("СуммаФактКвартал", 0);
	СтруктураЗначений.Вставить("СуммаФактГод", 0);
	СтруктураЗначений.Вставить("СуммаПрошлыйМесяц", 0);
	СтруктураЗначений.Вставить("СуммаПрошлыйГод", 0);
	СтруктураЗначений.Вставить("СуммаМесяцПрошлогоГода", 0);
	СтруктураЗначений.Вставить("СуммаКварталПрошлогоГода",0);
	СтруктураЗначений.Вставить("СуммаПланМесяц",0);
	СтруктураЗначений.Вставить("СуммаПланКвартал",0);
	СтруктураЗначений.Вставить("СуммаПланГод",0);
	
КонецПроцедуры

Процедура ДобавитьСуммыВСтруктуруПрочих(СтруктураЗначений, ВыборкаЗапроса)
	
	СтруктураЗначений.СуммаФактМесяц = СтруктураЗначений.СуммаФактМесяц + ВыборкаЗапроса.СуммаФактМесяц;
	СтруктураЗначений.СуммаФактКвартал = СтруктураЗначений.СуммаФактКвартал + ВыборкаЗапроса.СуммаФактКвартал;
	СтруктураЗначений.СуммаФактГод = СтруктураЗначений.СуммаФактГод + ВыборкаЗапроса.СуммаФактГод;
	СтруктураЗначений.СуммаПрошлыйМесяц = СтруктураЗначений.СуммаПрошлыйМесяц + ВыборкаЗапроса.СуммаПрошлыйМесяц;
	СтруктураЗначений.СуммаПрошлыйГод = СтруктураЗначений.СуммаПрошлыйГод + ВыборкаЗапроса.СуммаПрошлыйГод;
	СтруктураЗначений.СуммаМесяцПрошлогоГода = СтруктураЗначений.СуммаМесяцПрошлогоГода + ВыборкаЗапроса.СуммаМесяцПрошлогоГода;
	СтруктураЗначений.СуммаКварталПрошлогоГода = СтруктураЗначений.СуммаКварталПрошлогоГода + ВыборкаЗапроса.СуммаКварталПрошлогоГода;
	
КонецПроцедуры