Перем мПериод          Экспорт; // Период движений
Перем мТаблицаДвижений Экспорт; // Таблица движений

// Выполняет приход по регистру.
//
// Параметры:
//  Нет.
//
Процедура ВыполнитьПриход() Экспорт

	ОбщегоНазначения.ВыполнитьДвижениеПоРегистру(ЭтотОбъект, ВидДвиженияНакопления.Приход);

КонецПроцедуры // ВыполнитьПриход()

// Выполняет расход по регистру.
//
// Параметры:
//  Нет.
//
Процедура ВыполнитьРасход() Экспорт

	ОбщегоНазначения.ВыполнитьДвижениеПоРегистру(ЭтотОбъект, ВидДвиженияНакопления.Расход);

КонецПроцедуры // ВыполнитьРасход()

// Выполняет движения по регистру.
//
// Параметры:
//  Нет.
//
Процедура ВыполнитьДвижения() Экспорт

	Загрузить(мТаблицаДвижений);

КонецПроцедуры // ВыполнитьДвижения()

Процедура ПередЗаписью(Отказ, Замещение)
		
	Если НЕ ОбменДанными.Загрузка Тогда
		
		//АБС ВСТАВКА 38771  14.02.2014 12:56:27  Шамов
		//движения по реальному складу подменяются на движения по виртуальному
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЗначенияСвойствОбъектов.Объект КАК РеальныйСклад,
			|	ЗначенияСвойствОбъектов.Значение КАК ВиртуальныйСклад
			|ИЗ
			|	РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
			|ГДЕ
			|	ЗначенияСвойствОбъектов.Объект В(&мСклады)
			|	И ЗначенияСвойствОбъектов.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СвойстваОбъектов.абс_ВиртуальныйСкладДляРасчетаРезерва)";

		мСклады = ЭтотОбъект.ВыгрузитьКолонку("Склад");
		Запрос.УстановитьПараметр("мСклады", мСклады);

		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			тзПодмены = Результат.Выгрузить();
			тзПодмены.Индексы.Добавить("РеальныйСклад");
            //подменяем
			Для Каждого Запись Из ЭтотОбъект Цикл
				СтрокаПодмены = тзПодмены.Найти(Запись.Склад, "РеальныйСклад");
				Если СтрокаПодмены <> Неопределено Тогда
					Запись.Склад = СтрокаПодмены.ВиртуальныйСклад;	
				КонецЕсли;
			КонецЦикла;			
		КонецЕсли;		
		//АБС ВСТАВКА 38771 КОНЕЦ
		КорректировкаСерийПриРезервировании();
		
		ОбщегоНазначения.ВыполнитьДвиженияПоРегиструСвободныеОстатки(ЭтотОбъект, Отбор.Регистратор.Значение, Замещение, Перечисления.ВидыРегистровОснованийРегистраСвободныеОстатки.ТоварыВРезервеНаСкладах, Отказ);

	КонецЕсли;
	
КонецПроцедуры

Процедура КорректировкаСерийПриРезервировании()
	
	// Если в таблице движений нет серий, то модифицировать ничего не нужно.
	Если Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПустаяСерия = Справочники.СерииНоменклатуры.ПустаяСсылка();

	// Если серии при резервировании не используются, тогда очистим колонку "СерииНоменклатуры".
	Если Не (глЗначениеПеременной("ИспользоватьУказаниеСерийНоменклатурыПриРезервировании") И глЗначениеПеременной("ИспользоватьСерииНоменклатуры")) Тогда
	
		Для Каждого СтрокаДвижения Из ЭтотОбъект Цикл
			СтрокаДвижения.СерияНоменклатуры = ПустаяСерия;
		КонецЦикла;	
	
	Иначе
	
		// Для каждого заказа определим ведется ли обособленный учет, если нет, очистим серию.
		Для Каждого СтрокаДвижения Из ЭтотОбъект Цикл
			
			Если ТипЗнч(СтрокаДвижения.ДокументРезерва) <> Тип("ДокументСсылка.ЗаказПокупателя") Тогда
				СтрокаДвижения.СерияНоменклатуры = ПустаяСерия;
				Продолжить;
			КонецЕсли;	
			
			ОбособленныйУчетПоЗаказу = СтрокаДвижения.ДокументРезерва.ДоговорКонтрагента.ОбособленныйУчетТоваровПоЗаказамПокупателей;
			
			Если Не(ОбособленныйУчетПоЗаказу) Тогда
				СтрокаДвижения.СерияНоменклатуры = ПустаяСерия;
			КонецЕсли;	
			
		КонецЦикла;	
	
	КонецЕсли;
	
КонецПроцедуры	

