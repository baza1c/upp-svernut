Перем мПериод          Экспорт; // Период движений
Перем мТаблицаДвижений Экспорт; // Таблица движений

// Выполняет приход по регистру.
//
// Параметры:
//  Нет.
//
Процедура ВыполнитьПриход() Экспорт

	ОбщегоНазначения.ВыполнитьДвижениеПоРегистру(ЭтотОбъект, ВидДвиженияНакопления.Приход);

КонецПроцедуры // ВыполнитьПриход()

// Выполняет расход по регистру.
//
// Параметры:
//  Нет.
//
Процедура ВыполнитьРасход() Экспорт

	ОбщегоНазначения.ВыполнитьДвижениеПоРегистру(ЭтотОбъект, ВидДвиженияНакопления.Расход);

КонецПроцедуры // ВыполнитьРасход()

// Выполняет движения по регистру.
//
// Параметры:
//  Нет.
//
Процедура ВыполнитьДвижения() Экспорт

	Загрузить(мТаблицаДвижений);

КонецПроцедуры // ВыполнитьДвижения()

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если НЕ ОбменДанными.Загрузка Тогда
		//АБС ВСТАВКА 38771  14.02.2014 12:56:27  Шамов
		//контроль движений по виртуальному складу
		тзСоответствия = абс_Дистрибуция.ПолучитьТаблицуСоответствияРеальныхВиртуальныхСкладов();
		//таблица с добавляемыми строками
		тзДобавка = ЭтотОбъект.ВыгрузитьКолонки();

		Для Каждого Запись Из ЭтотОбъект Цикл
			Если Запись.ВидДвижения = ВидДвиженияНакопления.Приход ТОгда
			//приход по виртуальному складу разрешено делать только документом "Оприходование товаров"
				СтрокаСоответствия = тзСоответствия.Найти(Запись.Склад, "ВиртуальныйСклад");
				Если СтрокаСоответствия <> Неопределено И ТипЗнч(Отбор.Регистратор.Значение) <> Тип("ДокументСсылка.ОприходованиеТоваров") Тогда
					ОбщегоНазначения.СообщитьОбОшибке("Приход по виртуальному складу выполняются только документами оприходования!", Отказ); 
					Возврат;
				КонецЕсли;		
			Иначе
			//расход по реальному складу должен дублироваться расходом по виртуальному - для нормальной работы системы
			//учета резервов
				СтрокаСоответствия = тзСоответствия.Найти(Запись.Склад, "РеальныйСклад");
				Если СтрокаСоответствия <> Неопределено Тогда
					НоваяСтрока = тзДобавка.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Запись);
					НоваяСтрока.Склад = СтрокаСоответствия.ВиртуальныйСклад;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Для Каждого СтрокаТЗ Из тзДобавка Цикл
			Запись = ЭтотОбъект.ДобавитьРасход();
			ЗаполнитьЗначенияСвойств(Запись, СтрокаТЗ);
		КонецЦикла;
		//АБС ВСТАВКА 38771 КОНЕЦ
		
		ОбщегоНазначения.ВыполнитьДвиженияПоРегиструСвободныеОстатки(ЭтотОбъект, Отбор.Регистратор.Значение, Замещение, Перечисления.ВидыРегистровОснованийРегистраСвободныеОстатки.ТоварыНаСкладах, Отказ);
		
	КонецЕсли;

КонецПроцедуры

