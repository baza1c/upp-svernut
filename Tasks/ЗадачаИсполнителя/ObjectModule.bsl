////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// По описателям состояния задачи (реквизитам адресации) формирует текст инфо-строки и важность сообщения
//
// Параметры
//	Исполнитель, Роль, Организация, Выполнена - реквизиты, влияющие на состояние задачи
//
// Возвращаемое значение:
//	Структура из двух строковых значений - ТекстСообщения и ВажностьСообщения
//
Функция ПолучитьОписаниеОбъекта(Исполнитель, Роль, Организация, Выполнена) Экспорт
	
	Если Выполнена Тогда
		ТекстСообщения		= "Задача выполнена";
	Иначе
		Если ЗначениеЗаполнено(Исполнитель) Тогда
			ТекстСообщения		= "Задачу может выполнить только " + Исполнитель;
		Иначе
			ОписаниеРоли = "";
			Если ЗначениеЗаполнено(Роль) Тогда
				ОписаниеРоли = НРег(Строка(Роль));
			Иначе
				ОписаниеРоли = "кадровик или расчетчик";
			КонецЕсли;
			
			ОписаниеОрганизации = "";
			Если ЗначениеЗаполнено(Организация) Тогда
				ОписаниеОрганизации = " " + ОбщегоНазначенияЗК.ПреобразоватьСтрокуИнтерфейса("организации") + " " + Строка(Организация);
			КонецЕсли;
			
			ТекстСообщения		= "Задачу может выполнить любой " + ОписаниеРоли + ОписаниеОрганизации;
			
			РеквизитыАдресации = Новый Структура;
			Для Каждого Реквизит Из Метаданные().РеквизитыАдресации Цикл
				РеквизитыАдресации.Вставить(Реквизит.Имя, ЭтотОбъект[Реквизит.Имя]);
			КонецЦикла;
			УправлениеЗадачамиПереопределяемый.ДополнитьТекстОписанияЗадачи(ТекстСообщения, РеквизитыАдресации); 
			
		КонецЕсли;
	КонецЕсли;
	
	ВажностьСообщения	= "";
	
	Возврат Новый Структура("ТекстСообщения,ВажностьСообщения", ТекстСообщения, ВажностьСообщения)
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПередВыполнением()
	
	// Исполнитель берется из параметра сеанса
	Если Исполнитель.Пустая() Тогда
		Исполнитель = глЗначениеПеременной("глТекущийПользователь");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриВыполнении(Отказ)
	
	УправлениеЗадачами.ОтменитьЗадачу(ВидЗадачи, ОбъектЗадачи);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если  Выполнена = Истина Тогда
		Запрос = Новый Запрос();
		Запрос.Параметры.Вставить("ОбъектЗадачи", ОбъектЗадачи);
		Запрос.Параметры.Вставить("Уровень", Уровень);
		//{Лео Катанович
		//Только однотипные задачи 
		Запрос.Параметры.Вставить("ВидЗадачи", ВидЗадачи);
		
		//{Лео Катанович}

		
		Запрос.Текст = "ВЫБРАТЬ
		|	ЗадачаИсполнителя.Ссылка
		|ИЗ
		|	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
		|ГДЕ
		|	ЗадачаИсполнителя.Выполнена = Ложь И ЗадачаИсполнителя.ОбъектЗадачи = &ОбъектЗадачи
		|	И ЗадачаИсполнителя.Уровень = &Уровень И ЗадачаИсполнителя.ВидЗадачи = &ВидЗадачи";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗадачаОбъект = Выборка.Ссылка.ПолучитьОбъект();
			Если ЗадачаОбъект.ПометкаУдаления Тогда
				Продолжить;
			КонецЕсли;
			Если ЗадачаОбъект.Уровень = 0 Тогда
				 Если ЗадачаОбъект.ВидЗадачи <> Справочники.ВидыЗадачПользователей.Согласование И ЗадачаОбъект.ВидЗадачи <> Справочники.ВидыЗадачПользователей.Исполнение Тогда
				      Продолжить;
				 КонецЕсли;
			КонецЕсли;

			ЗадачаОбъект.Выполнена = Истина;
			ЗадачаОбъект.Записать();
		КонецЦикла;	
		
		
	КонецЕсли;
	// Вставить содержимое обработчика.
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
